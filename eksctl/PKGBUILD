# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: David Birks <david@birks.dev>
# Contributor: Mike Williamson <mike at korora dot ca>

pkgname=eksctl
pkgver=0.180.0
pkgrel=1
pkgdesc='Command line tool for creating clusters on Amazon EKS'
url='https://github.com/weaveworks/eksctl'
arch=('loong64' 'x86_64')
license=('Apache-2.0')
depends=('kubectl' 'glibc')
makedepends=('go')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('71223665c11d6b8285dc0ebe76596a3ecb752bbee1ee820d925122959b2b4b4c')
b2sums=('c67a084dafede6d582fa3ef62d2338195b477d4ca196e15d7febd4827d54bdad630cbf8f90b7a931fcf60ef251b5223394ba4d305b55f7473cae600ddfc94c78')

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  go build -v \
    -ldflags "-linkmode=external -extldflags '${LDFLAGS}' -X github.com/weaveworks/eksctl/pkg/version.gitTag=${pkgver}" \
    ./cmd/eksctl
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 ${pkgname} -t "$pkgdir/usr/bin"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  # completions
  "${pkgdir}/usr/bin/${pkgname}" completion bash | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion zsh | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion fish | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/fish/completions/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
