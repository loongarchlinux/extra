# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Philippe Proulx <pproulx@efficios.com>
# Contributor: Manuel Mendez <mmendez534 at gmail dot com>

pkgbase='lttng-ust'
pkgname=('lttng-ust' 'python-lttngust')
pkgver=2.13.8
pkgrel=1
arch=('loong64' 'x86_64')
url="https://lttng.org/"
license=('LGPL2.1' 'GPL2' 'MIT')
makedepends=('asciidoc' 'xmlto' 'libxml2' 'python' 'liburcu>=0.7.2' 'util-linux' 'numactl' 'python-setuptools')
source=("https://lttng.org/files/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2"{,.asc})
sha512sums=('f61f7e1633051a4d515ba28d4945b96a84eab8a8143ef698bbb6857cbaad1acbe238041e43f04d9818bac43f59023be3f3c2154ecf75f180d8233c9d7f65c555'
            'SKIP')
validpgpkeys=('2A0B4ED915F2D3FA45F5B16217280A9781186ACF')

prepare() {
    cd $pkgname-$pkgver
    sed -e '/SETUPTOOLS_USE_DISTUTILS/d' -i src/python-lttngust/Makefile.{in,am} # Fix broken Python version detection
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # build lttng-ust
    ./configure \
        --prefix='/usr' \
        --enable-man-pages
    make
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make check
}

package_lttng-ust() {
    pkgdesc="LTTng user space tracing libraries for LTTng"
    depends=('liburcu>=0.7.2' 'util-linux' 'python' 'numactl')
    optdepends=('babeltrace2: trace viewer')
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-lttngust() {
    pkgdesc='LTTng-UST Python Bindings'
    depends=('lttng-ust' 'python')
    cd "${srcdir}/${pkgbase}-${pkgver}/src/python-lttngust"
    make DESTDIR="${pkgdir}" install

    cd "${srcdir}/${pkgbase}-${pkgver}"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
