# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=archinfo
pkgname=python-${_pyname}
pkgver=9.2.102
pkgrel=1
pkgdesc='Classes with architecture-specific information useful to other projects'
url='https://github.com/angr/archinfo'
arch=('any')
license=('custom:BSD2')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
optdepends=(
  'python-capstone'
  'python-unicorn'
  'python-keystone'
  'python-pyvex'
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('171b236e64ab11b26a229f0f241337fadf57ec0f26a45e2001fa3972ca7912955ba951c4344312ee868de8f09c0872adbe8fa543cd9b505c985c52aabdc404a8')
b2sums=('73541dd9b3bf2b56bcdc5d7b2dbdbe2867d14fd589ac8a176a39ac92c1f1be5190417244975a4ad0131e275885466c73d53a5bd69151bc0d1f327c6d9b793e34')

build() {
  cd "${_pyname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
