# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libimagequant
pkgver=4.3.3
pkgrel=1
pkgdesc="Library for high-quality conversion of RGBA images to 8-bit indexed-color (palette) images"
arch=('loong64' 'x86_64')
url="https://pngquant.org/lib/"
license=('BSD')
depends=('gcc-libs')
makedepends=('cargo' 'cargo-c')
source=("https://github.com/ImageOptim/libimagequant/archive/$pkgver/libimagequant-$pkgver.tar.gz")
sha256sums=('c50a59003a4c4ce53c76314e62f1e86d86d882bc09addb13daa0faa9260b9614')

build() {
    cd "$srcdir/$pkgname-$pkgver/imagequant-sys"
    cargo cbuild --release --prefix=/usr
}

check() {
    cd "$srcdir/$pkgname-$pkgver/imagequant-sys"
    cargo ctest --release
}

package() {
    cd "$srcdir/$pkgname-$pkgver/imagequant-sys"
    cargo cinstall --destdir="$pkgdir" --prefix=/usr --release
    install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
