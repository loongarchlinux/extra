# Maintainer: David Runge <dvzrv@archlinux.org>

_name=warning
pkgname=ruby-warning
pkgver=1.3.0
pkgrel=2
pkgdesc="Add custom processing for warnings"
arch=(any)
url="https://github.com/jeremyevans/ruby-warning"
license=(MIT)
depends=(ruby)
makedepends=(
  ruby-rake
  ruby-rdoc
)
checkdepends=(
  ruby-minitest-global_expectations
)
options=(!emptydirs)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('a6d1ef0a70f024d9527dbbe2236805a32c6aa9d00def92f1a345f4f0bc8757d23d6933784c96e9282fba038c276999b85f2d0c1bd581a2c1994efd6440adc39f')
b2sums=('661e2af457805542e07db2eacdbc867ffda66a574a981b6834063fc497eacfa565f01ad21a0c6b0c57185a8019e0fd2d80ae14b77f902dc3378f7f655ee07000')

prepare() {
  cd $pkgname-$pkgver

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed -i -e 's|~>|>=|g' $_name.gemspec
}

build() {
  local gemdir="$(gem env gemdir)"
  local gem_install_options=(
    --local
    --verbose
    --ignore-dependencies
    --no-user-install
    --install-dir tmp_install/$gemdir
    --bindir tmp_install/usr/bin
    $_name-$pkgver.gem
  )
  local unrepro_files=(
    tmp_install/$gemdir/cache/
    tmp_install/$gemdir/gems/$_name-$pkgver/vendor/
    tmp_install/$gemdir/doc/$_name-$pkgver/ri/ext/
  )

  cd $pkgname-$pkgver

  gem build $_name.gemspec
  gem install "${gem_install_options[@]}"

  # remove unrepreducible files
  rm -frv "${unrepro_files[@]}"

  find tmp_install/$gemdir/gems/ \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find tmp_install/$gemdir/extensions/ \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  local gemdir="$(gem env gemdir)"

  cd $pkgname-$pkgver

  GEM_HOME="tmp_install/$gemdir" rake test
}

package() {
  cd $pkgname-$pkgver

  mv -v tmp_install/* "$pkgdir/"

  install -vDm 644 MIT-LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGELOG,README.rdoc} -t "$pkgdir/usr/share/doc/$pkgname/"
}
