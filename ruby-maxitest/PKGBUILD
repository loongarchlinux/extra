# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-maxitest
pkgver=5.4.0
_commit=6dc300bc82f2035649fcf481f845eff909e21b59
pkgrel=1
pkgdesc='Minitest + all the features you always wanted'
arch=(any)
url='https://github.com/grosser/maxitest'
license=(MIT)
depends=(ruby ruby-minitest)
makedepends=(git ruby-rake ruby-rspec)
checkdepends=(procps-ng ruby-bump ruby-byebug)
options=(!emptydirs)
source=("git+$url.git?signed#commit=$_commit")
sha256sums=('c0745e5d749221552cd9caa7d746ce41ca54b77376b2d0bbfa891b52f95cf1d1')
# curl https://github.com/grosser.gpg | gpg --import
validpgpkeys=('72170F9E193D0A5A31DB742FB19DB1587D698ED7') # Michael Grosser <michael@grosser.it>

prepare() {
  cd maxitest
  git rm Gemfile.lock
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd maxitest
  gem build maxitest.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    maxitest-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r "tmp_install/$_gemdir/cache"
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd maxitest
  PATH="tmp_install/usr/bin:$PATH" GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd maxitest
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 MIT-LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
