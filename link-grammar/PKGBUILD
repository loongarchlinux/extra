# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=link-grammar
pkgver=5.12.5
pkgrel=1
pkgdesc="A Grammar Checking library"
arch=('loong64' 'x86_64')
url="https://opencog.github.io/link-grammar-website/"
license=('LGPL-2.1-or-later')
depends=('hunspell' 'sqlite' 'libedit')
makedepends=('python-setuptools' 'swig' 'apache-ant' 'java-environment=11')
options=('!makeflags')
source=(https://www.gnucash.org/$pkgname/downloads/$pkgver/${pkgname}-$pkgver.tar.gz{,.asc})
validpgpkeys=('6407453C98BECC19ADB03D82EB6AA534E0C0651C'
              '8305252160000B5E89843F5464A99A8201045933') # Linas Vepstas <linasvepstas@gmail.com>
sha256sums=('adf7e60a40248e261dd159b948ad6c6b8cd4f99142525888081dca140395da34'
            'SKIP')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
