# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Stas Rudakou <stas at garage22 dot net>

pkgname=cobalt
pkgver=0.19.2
pkgrel=1
pkgdesc='Static site generator written in Rust'
url='https://cobalt-org.github.io/'
arch=('loong64' 'x86_64')
license=('MIT')
depends=('openssl' 'libsass' 'glibc' 'gcc-libs')
makedepends=('rust' 'cargo')
source=(https://github.com/cobalt-org/cobalt.rs/archive/v${pkgver}/${pkgname}.rs-v${pkgver}.tar.gz)
sha512sums=('c5a1667bf22130fcaf7d5f6e0efb122065c322652dbe1c5a8fd8235522652f6bedac6a68bf910a5f93284749d2bc1e5f378ac48518abad29faf12d22f70e490a')
b2sums=('6ba716fb1db352ec3caed1043878673301cece412bc4aabcf0a7b2686f12e6cc1fdcac43c7df89ab85cc8fa9564ea5305323adbbac9c139c4643cf9b282645f6')

build() {
  cd ${pkgname}.rs-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}.rs-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}.rs-${pkgver}
  install -Dm 755 target/release/cobalt -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
