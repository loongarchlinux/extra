# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: prg <prg _at_ xannode _dot_ com>
# Contributor: Nico <d3sox at protonmail dot com>
# Contributor: Caltlgin Stsodaat <contact@fossdaily.xyz>

pkgname=haruna
pkgver=1.1.0
pkgrel=1
pkgdesc='Video player built with Qt/QML on top of libmpv'
arch=(loong64 x86_64)
url='https://apps.kde.org/haruna/'
license=(GPL-3.0-or-later)
depends=(ffmpeg
         gcc-libs
         glibc
         kcolorscheme
         kconfig
         kcoreaddons
         kdeclarative
         kfilemetadata
         ki18n
         kio
         kirigami
         kwindowsystem
         mpvqt
         qt6-5compat
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=('yt-dlp: YouTube support')
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('29382f561ca39d9d00843c1ea3b7d862fd2193a818d4d49c179c69a453c79a4d'
            'SKIP')
validpgpkeys=(4E421C6554B89766DF9B7A37E12AB207C8755905) # George Florea Bănuș <georgefb899@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
