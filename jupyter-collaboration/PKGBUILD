# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=jupyter-collaboration
pkgver=2.1.1
pkgrel=1
pkgdesc='A Jupyter Server Extension Providing Support for Y Documents'
arch=(any)
url='https://jupyterlab-realtime-collaboration.readthedocs.io/en/latest/'
license=(custom)
depends=(jupyter-server
         jupyter-server-fileid
         python
         python-jupyter-events
         python-jupyter-ydoc
         python-pycrdt-websocket
         python-tornado
         python-traitlets)
makedepends=(git
             jupyterlab
             nodejs
             python-build
             python-hatch-jupyter-builder
             python-hatch-nodejs-version
             python-hatchling
             python-installer)
checkdepends=(python-jupyter-server-terminals
              python-pytest
              python-pytest-asyncio
              python-pytest-jupyter
              python-pytest-timeout
              python-websockets)
source=(git+https://github.com/jupyterlab/jupyter-collaboration#tag=v$pkgver)
sha256sums=('2e3f21307363e06fd4a0957779c83115c202d081f994155aaf7ca4d1b79b3a64')

build() {
  cd $pkgname
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $pkgname
  pytest -v # -k 'not test_room_sequential_opening' # https://github.com/jupyterlab/jupyter-collaboration/issues/252
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir"{/usr,}/etc

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
