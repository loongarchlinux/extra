# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=jupyter-collaboration
pkgver=2.1.2
pkgrel=1
pkgdesc='A Jupyter Server Extension Providing Support for Y Documents'
arch=(any)
url='https://jupyterlab-realtime-collaboration.readthedocs.io/en/latest/'
license=(custom)
depends=(jupyter-server
         jupyter-server-fileid
         python
         python-jupyter-events
         python-jupyter-ydoc
         python-pycrdt-websocket
         python-tornado
         python-traitlets)
makedepends=(git
             jupyterlab
             nodejs
             python-build
             python-hatch-jupyter-builder
             python-hatch-nodejs-version
             python-hatchling
             python-installer)
checkdepends=(python-jupyter-server-terminals
              python-pytest
              python-pytest-asyncio
              python-pytest-jupyter
              python-pytest-timeout
              python-websockets)
source=(git+https://github.com/jupyterlab/jupyter-collaboration#tag=v$pkgver)
sha256sums=('0479c6e2503b18d3ac8e060cff40e5fd3a4fa9a7c99ee8e8d98a26168d075f5d')

build() {
  cd $pkgname
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $pkgname
  pytest -v # -k 'not test_room_sequential_opening' # https://github.com/jupyterlab/jupyter-collaboration/issues/252
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir"{/usr,}/etc

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
