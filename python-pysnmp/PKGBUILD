# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Karol Babioch <karol@babioch.de>

pkgname=python-pysnmp
_pyname=pysnmp
pkgver=6.1.2
pkgrel=1
pkgdesc='Open source and free implementation of v1/v2c/v3 SNMP engine'
url='https://www.pysnmp.com/'
arch=('any')
license=('BSD')
depends=('python' 'python-pysmi' 'python-pyasn1' 'python-pycryptodomex')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-poetry-core')
checkdepends=('python-pytest' 'python-pytest-asyncio')
source=(https://github.com/lextudio/pysnmp/archive/v${pkgver}/${_pyname}-v${pkgver}.tar.gz)
sha256sums=('72a83040496dd58a4b3ec671a883648f27cfb53e5fafd4b124c6123282796c53')
sha512sums=('894526c71fb417d7b8c6b3d45cc8595877d001a645c588b59df9f03448b6e20816f920ab3a9bbeee3a62b39789e0582cb8e0f5f08110f7d17c1a17be817f108f')

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  # TODO: get the tests running
  #PYTHONPATH=. pytest -v .
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples -t "${pkgdir}/usr/share/doc/${pkgname}"

  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
