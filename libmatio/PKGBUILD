# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Camille Monière <draslorus at draslorus dot fr>
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Stefan Husmann <stefan-husmann at t-online dot de>
# Contributor: Alexander Rødseth <rodseth at gmail dot com>
# Contributor: William Rea <sillywilly at gmail dot com>

pkgname=libmatio
pkgver=1.5.27
pkgrel=2
pkgdesc='C library with a fortran 90/95 module interface for reading/writing MATLAB MAT-files'
arch=(loong64 x86_64)
license=(BSD-2-Clause)
url='https://sourceforge.net/projects/matio'
depends=(glibc
         hdf5
         zlib)
makedepends=(git)
source=(git+https://github.com/tbeu/matio#tag=v$pkgver)
sha256sums=('86adb65dc3f40040a5a72eda4c2dc5243c8a0e1fa76ab2a709e0ec0cc6f26678')

prepare() {
  cd matio
  ./autogen.sh
}

build() {
  cd matio
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --with-hdf5
  make
}

package() {
  cd matio
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
