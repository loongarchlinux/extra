# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kalzium
pkgver=24.02.2
pkgrel=1
pkgdesc='Periodic Table of Elements'
url='https://apps.kde.org/kalzium/'
arch=(loong64 x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(avogadrolibs
         avogadrolibs-qt5
         gcc-libs
         glibc
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         ki18n5
         kio5
         kitemviews5
         knewstuff5
         kplotting5
         ktextwidgets5
         kwidgetsaddons5
         kxmlgui5
         kunitconversion5
         openbabel
         qt5-base
         qt5-script
         qt5-svg)
makedepends=(eigen
             extra-cmake-modules
             facile
             kdoctools5
             ocaml
             python)
groups=(kde-applications
        kde-education)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('4c6c17ec5847db714c11a249de4599d41dc89fc04ba38139dc90a7fe15ff16ab'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
