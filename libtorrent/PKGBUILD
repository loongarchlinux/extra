# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: sh__

pkgname=libtorrent
pkgver=0.13.8
pkgrel=4
pkgdesc='BitTorrent library with a focus on high performance and good code'
url='https://rakshasa.github.io/rtorrent/'
arch=('loong64' 'x86_64')
license=('GPL')
makedepends=('git')
depends=('openssl')
source=("git+https://github.com/rakshasa/libtorrent.git#tag=v${pkgver}")
sha256sums=('71e09b1b878ba463b3698968afd94412623b793b95a6c06967b405c1b7f584f3')

prepare() {
	cd ${pkgname}
	./autogen.sh
}

build() {
	cd ${pkgname}
	export CXXFLAGS+=' -fno-strict-aliasing'
	./configure \
		--prefix=/usr \
		--disable-debug \

	make
}

package() {
	cd ${pkgname}
	make DESTDIR="${pkgdir}" install
}
