# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Constantine Fedotov <zenflak@gmail.com>
# Contributor: Vlad Frolov <frolvlad@gmail.com>

pkgname=cargo-hack
pkgver=0.6.31
_commit=c0b517b9eefa27cdaf27cca5f1b186c00ef1af47
pkgrel=1
pkgdesc="A cargo subcommand to provide various options useful for testing and CI"
arch=('loong64' 'x86_64')
url="https://github.com/taiki-e/cargo-hack"
license=('MIT' 'Apache-2.0')
depends=('gcc-libs')
makedepends=('cargo' 'git')
source=("$pkgname::git+$url.git#commit=$_commit"
        "Cargo.lock")
sha512sums=('b593b2adc459c5a6d7d1340c351397bffb28fb30885a3be85f5753858324c2025863480dbb0098f2d7a8559f4a9119fcb77eb09cc79a016ef828032cee181557'
            'ed3f81a23616ea70cf635f8d9959c1adcb645f012ae0c991292b18f254d15993ca250f084fb4bca38e0dbbe6e33f17a0f037db3ca6280d66a6edcec0c985a1b9')

prepare() {
  cd "$pkgname"
  cp "$srcdir/Cargo.lock" .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"
  cargo build --release --frozen
}

check() {
  cd "$pkgname"
  cargo test --frozen -- \
    --skip "multi_target" \
    --skip "version_range" \
    --skip "version_range_failure"
}

package() {
  cd "$pkgname"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
  depends+=(rustup)
}

# vim: ts=2 sw=2 et:
