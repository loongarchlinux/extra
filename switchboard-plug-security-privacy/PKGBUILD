# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-security-privacy
pkgver=7.1.0.r100.66928c1e
pkgrel=1
pkgdesc='Switchboard Security & Privacy Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-security-privacy
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  bash
  dconf
  glib2
  glibc
  gtk4
  libgee
  libgranite-7.so
  libswitchboard-3.so
  light-locker
  polkit
  ufw
  zeitgeist
)
makedepends=(
  git
  meson
  vala
)
_commit=66928c1e8913a5eb5c96972ea7f1a80ce3bc893e
source=(git+https://github.com/elementary/switchboard-plug-security-privacy.git#commit=${_commit})
b2sums=(SKIP)

pkgver() {
  cd switchboard-plug-security-privacy
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-security-privacy build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
