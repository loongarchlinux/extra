# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=oxyromon
pkgver=0.19.0
pkgrel=1
pkgdesc='Rusty ROM OrgaNizer'
arch=(loong64 x86_64)
url=https://github.com/alucryd/oxyromon
license=(GPL3)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  bchunk
  cmake
  ctrtool
  dolphin-emu-tool
  git
  mame-tools
  maxcso
  p7zip
  rust
  wit
  yarn
)
optdepends=(
  'bchunk: BIN to ISO support'
  'cdrtools: IRD support'
  'ctrtool: CIA support'
  'dolphin-emu-tool: RVZ support'
  'mame-tools: CHD support'
  'maxcso: CSO support'
  'p7zip: 7Z and ZIP support'
  'wit: WBFS support'
)
_tag=3435f4e3f83ccf583fde12ddebba6b555fa058db
source=(git+https://github.com/alucryd/oxyromon.git#tag=${_tag})
validpgpkeys=(95220BE99CE6FF778AE0DC670F65C7D881506130) # Maxime Gauduin <alucryd@archlinux.org>
b2sums=('830494114f30087de4dc67caf020c7bf72a00471df4627e8c84e64e4b6b885ecfc73cb2bba594d4706df21e75ec61f9f4a4a96889439eddf1b4d2c7b58b40499')
options=(!lto)

prepare() {
  cd oxyromon
  yarn install \
    --frozen-lockfile
  cargo fetch \
    --locked
}

pkgver() {
  cd oxyromon
  git describe --tags
}

build() {
  cd oxyromon
  yarn build
  cargo build \
    --features server \
    --release \
    --frozen
}

check() {
  cargo test \
    --features server \
    --release \
    --frozen \
    --manifest-path oxyromon/Cargo.toml
}

package() {
  cargo install \
    --features server \
    --frozen \
    --offline \
    --no-track \
    --path oxyromon \
    --root "${pkgdir}"/usr
}

# vim: ts=2 sw=2 et:
