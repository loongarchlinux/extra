# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@phare.normalesup.org>

pkgname=givaro
pkgver=4.2.0
pkgrel=2
pkgdesc='C++ library for arithmetic and algebraic computations'
arch=(loong64 x86_64)
url='http://givaro.forge.imag.fr/'
license=(CECILL-B)
depends=(gcc-libs
         glibc
         gmp
         sh)
makedepends=(git)
source=(git+https://github.com/linbox-team/givaro#tag=v$pkgver)
sha256sums=('bb3da8e6b00e4a906ef452e70920cdd045c06b022d48fa977c2f431677bbad8a')

prepare() {
  cd $pkgname
  git cherry-pick -n c7744bb133496cd7ac04688f345646d505e1bf52 # Fix build with GCC 13
  git cherry-pick -n b0cf33e1d4437530c7e4b3db90b6c80057a7f2f3 # Fix build with GCC 14
  ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-shared
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 Licence_CeCILL-B_V1-en.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
