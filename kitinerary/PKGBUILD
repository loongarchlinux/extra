# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kitinerary
pkgver=24.02.2
pkgrel=2
pkgdesc='Data model and extraction system for travel reservation information'
arch=(loong64 x86_64)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive
         kcalendarcore
         kcodecs
         kcontacts
         ki18n
         kmime
         kpkpass
         libphonenumber
         libxml2
         openssl
         poppler
         qt6-base
         qt6-declarative
         zlib
         zxing-cpp)
makedepends=(doxygen
             extra-cmake-modules
             qt6-doc
             qt6-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/pim/kitinerary/-/commit/6a0a4f45.patch)
sha256sums=('716e6e62175ca2b577e7df188c9143a52e2994c75007347c0d38ac01049e2af8'
            'SKIP'
            '71d6e3118f0634136b78d51b39b3de1451afa57cfb8f1e4cde9c0c7ca27f133f')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 6a0a4f45.patch # Fix build with poppler 24.05
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
