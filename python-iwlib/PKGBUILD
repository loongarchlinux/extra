# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-iwlib
pkgver=1.7.0
pkgrel=6
pkgdesc='Python module to interface with iwlib'
arch=(loong64 x86_64)
url="https://github.com/nathan-hoad/python-iwlib"
license=(GPL-2.0-only)
depends=(
  glibc
  python
  python-cffi
  wireless_tools
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('f88776702e6e26c3f682eed618917ff559b3678628ac8c149f12cad12144b208c79d3ee4c027f31a20fa3811bc89eb92db1cd7cc181c0b52002d4bde2ad98fdf')
b2sums=('616cc4788eb7098547c2cc0feed5a16add2d8bd418fd1145c04e8138b6fa0988d7e407dacb64fd11295b388c6c5b8db3652fa9cf8f9e0f89f9132671d20a46e3')

prepare() {
  cd $pkgname-$pkgver
  # fixing include discovery: https://github.com/nhoad/python-iwlib/issues/11
  rm -v test/__init__.py
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  local _py3_ver=$(python3 -c "import sys; print(str(sys.version_info.major)+str(sys.version_info.minor))")
  export PYTHONPATH="build/lib.linux-$CARCH-cpython-$_py3_ver/:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {AUTHORS,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
}
