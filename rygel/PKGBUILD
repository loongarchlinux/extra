# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=rygel
pkgver=0.42.6
pkgrel=1
epoch=1
pkgdesc="UPnP AV MediaServer and MediaRenderer"
url="https://wiki.gnome.org/Projects/Rygel"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gdk-pixbuf2
  glib2
  glibc
  gssdp
  gst-editing-services
  gst-plugins-base-libs
  gstreamer
  gtk3
  gupnp
  gupnp-av
  gupnp-dlna
  hicolor-icon-theme
  libgee
  libmediaart
  libsoup3
  libunistring
  libx11
  libxml2
  sqlite
  tracker3
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-base: Extra media codecs'
  'gst-plugins-good: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
  'tracker3-miners: Share indexed media files'
  'tumbler: Thumbnailing service'
)
provides=(librygel-{core,db,renderer,renderer-gst,ruih,server}-2.8.so)
backup=(etc/rygel.conf)
groups=(gnome)
_commit=64a5f90b44bc845a4c59f37cb49d9b7693cde6b5  # tags/shotwell-0.42.6^0
source=("git+https://gitlab.gnome.org/GNOME/rygel.git#commit=$_commit")
b2sums=('52543e25d32c455db5e8f5bd54ec1b0874175e5307dfa5ac2717f2dc30445d3fae69bcafb0f6e252ed8489177fdeb576cdfeef3825d4bc6673c00b70c44d7911')

pkgver() {
  cd rygel
  git describe --tags | sed 's/^[a-z]*-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd rygel
}

build() {
  arch-meson rygel build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
