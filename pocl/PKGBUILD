# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Olaf Leidinger <oleid@mescharet.de>
# Contributor: fabien Cellier <fabien.cellier@gmail.com>

pkgname=pocl
pkgver=6.0
pkgrel=2
pkgdesc="Portable OpenCL is an open-source implementation of OpenCL which can be easily adapted for new targets"
arch=('loong64' 'x86_64')
url="http://portablecl.org/"
license=('MIT')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz")
depends=('clang' 'hwloc' 'opencl-icd-loader')
makedepends=('llvm' 'cmake' 'opencl-headers' 'ocl-icd' 'ninja' 'python')
sha512sums=('a24efadbbb81c810cd5c4bbb8abfa75b9bbdfeca786e8471dd75d40a78024d04c1b5c5a7114e75e1eb70a1b6a3756bb47ba741de0f1c4d1416fbce4688d62cc0')

build() {
  cd "$pkgname-$pkgver"
  cmake \
      -GNinja \
      -Bbuild \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_BUILD_TYPE=Release \
      -DKERNELLIB_HOST_CPU_VARIANTS=distro
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build check
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir"/ ninja -C build install
  install -v -Dm 644 -t "$pkgdir"/usr/share/licenses/"$pkgname" LICENSE
}
