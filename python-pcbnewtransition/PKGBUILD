# Maintainer:

_pkgname=pcbnewTransition
pkgname=python-pcbnewtransition
pkgver=0.4.1
pkgrel=2
pkgdesc='Library that allows you to support both, KiCAD 5 and KiCAD 6 in your plugins'
arch=(any)
url='https://github.com/yaqwsx/pcbnewTransition'
license=(MIT)
depends=(python kicad)
makedepends=(python-build python-installer python-setuptools python-wheel python-versioneer)
source=(https://pypi.io/packages/source/p/$_pkgname/$_pkgname-$pkgver.tar.gz)
sha512sums=('1a0e49bede2d809982858191313688b51d0d815e03d3eec32ad48e83ee26269bb44dadd3f8f28ddc73f09fbf69081cc828654199db546d71d0aa8d2fdf654fc6')

prepare() {
  cd $_pkgname-$pkgver
  rm versioneer.py
}

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
