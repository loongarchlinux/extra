# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-psycopg-pool
# source is shared with python-psycopg package, hence the weird naming
_pkgname=python-psycopg
pkgver=3.2.2
pkgrel=1
pkgdesc='Connection pool for psycopg'
arch=('any')
url='https://www.psycopg.org/psycopg3/'
license=('LGPL-3.0-only')
depends=('python' 'python-psycopg')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
source=("$_pkgname::git+https://github.com/psycopg/psycopg.git#tag=pool-$pkgver")
sha512sums=('34ea2ccf607da2101e3828fb4d0c8780c5f02e3789130b7d4b908f69406782e9f5d558ff5be11937ed9da808d97c7c7cade7ddf9b776f2ef4b4c6d32c274d261')
b2sums=('214b7cd949324c50b85a3b3b7e48b51960d8c7f0c5767051ad4125849cc59a96d4787ed4601c87c5a6f674fb1b37ef9537d08e908743ed2c78b7bdf1106fa6d5')

build(){
  cd "$_pkgname/psycopg_pool"

  python -m build --wheel --no-isolation
}

package(){
  cd "$_pkgname/psycopg_pool"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
