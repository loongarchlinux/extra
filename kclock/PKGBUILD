# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kclock
pkgver=24.02.2
pkgrel=1
pkgdesc='Clock app for Plasma Mobile'
url='https://apps.kde.org/kclock/'
arch=(loong64 x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcmutils
         kconfig
         kcoreaddons
         kdbusaddons
         ki18n
         kirigami
         kirigami-addons
         knotifications
         kstatusnotifieritem
         ksvg
         libplasma
         qt6-base
         qt6-declarative
         qt6-multimedia
         qt6-svg)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('9ae9872b35ecb5b08b9e89c67b8db38bea985494f4602e98ee3f740106ed605a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
