# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=ldproxy
pkgver=0.3.3
pkgrel=3
pkgdesc="A linker proxy tool"
url='https://github.com/esp-rs/embuild'
arch=('loong64' 'x86_64')
license=('MIT' 'Apache-2.0')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
)
source=(https://github.com/esp-rs/embuild/archive/${pkgname}-v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('da159083ee9cd0de1fb197b38336d44463d2c93a6bd8ee218cf28fd052a2a4a7'
            'af2964490ef5da85dc84a042023fe61942c5425cb0d3b1e729ab5f2b032c715c')
b2sums=('d8571ddf5ab3684206e4802871aedea216f6edc978adcb80b7879e58aa01d564634ec654f62eaf401ef1992c48d1cbf8b0b7d7b4cfafb5b2d7e36e3b4d7c22c2'
        'aba0e9a86cc83d2964995bb96ba2e004eac091266af2642f389894289d17c24e2873b71d168cd94b66eb6f2c712326d08c78bb9527a4613c4bc09b7a029b384b')

prepare() {
  cd "embuild-${pkgname}-v${pkgver}/${pkgname}"
  cp ../../Cargo.lock ..
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "embuild-${pkgname}-v${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "embuild-${pkgname}-v${pkgver}/${pkgname}"
  cargo build --frozen --release
}

check() {
  cd "embuild-${pkgname}-v${pkgver}/${pkgname}"
  cargo test --frozen
}

package() {
  cd "embuild-${pkgname}-v${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/${pkgname}

  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
