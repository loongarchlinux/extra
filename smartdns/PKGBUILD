# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zlowly <zlowly@gmail.com>

pkgname=smartdns
pkgver=46
pkgrel=1
pkgdesc="A local DNS server to obtain the fastest website IP for the best Internet experience"
arch=('loong64' 'x86_64')
license=('GPL')
url="https://github.com/pymumu/smartdns"
backup=("etc/smartdns/smartdns.conf")
depends=('gcc-libs' 'openssl')
makedepends=('systemd')  # systemd.pc
checkdepends=('gtest' 'bind')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pymumu/smartdns/archive/Release$pkgver.tar.gz")
sha512sums=('4798b5d75412864f707a2de5bc72c38fdfdc278156dc00c7f5d0ab3af95d605544a86368b56cc41bc7465fa6c063bc742a0c0e797c266aa472bf9d21398313f7')

prepare() {
  # Unfortunately upstream does not provide a way to reuse .o files
  cp -a smartdns-Release$pkgver smartdns-Release$pkgver-test
}

build() {
  cd smartdns-Release$pkgver
  make RUNSTATEDIR=/run SBINDIR=/usr/bin
}

check() {
  cd smartdns-Release$pkgver-test
  make -C test test
}

package() {
  cd smartdns-Release$pkgver
  make RUNSTATEDIR=/run SBINDIR=/usr/bin DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/etc/init.d
}
