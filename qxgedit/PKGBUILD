# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qxgedit
pkgver=1.0.0
pkgrel=1
pkgdesc="GUI for editing MIDI System Exclusive files for XG devices (eg. Yamaha DB50XG)"
arch=(loong64 x86_64)
url="https://qxgedit.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  qt6-tools
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('55bfefed2a7f817ab16fc8bd89ff3ca7a970b160ae21a64074142313df79ea0142f2d94b9ec11c18f67b15cfe01e9fe1ed298cf1dec5d8bbc018c22396b9548e')
b2sums=('904c12a7738174e4b5566fecb41e6afa9f3a55bb29fa0af0a245680db854f38b15178e7dbf47efa61261eef0c779b6601b778736c272c656ebd901104f3a1b98')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    alsa-lib libasound.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
