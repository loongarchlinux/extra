# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Cristian Maureira <saint@archlinux.cl>
# Contributor: Dr.Egg <rwhite@archlinux.us>

pkgname=musescore
pkgver=4.3.2
pkgrel=1
pkgdesc='Create, play and print beautiful sheet music'
arch=(loong64 x86_64)
url=https://musescore.org/
license=(GPL)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  libasound.so
  libfreetype.so
  libsndfile.so
  qt5-base
  qt5-declarative
  qt5-graphicaleffects
  qt5-networkauth
  qt5-quickcontrols
  qt5-quickcontrols2
  qt5-svg
  qt5-x11extras
  qt5-xmlpatterns
  zlib
)
makedepends=(
  cmake
  doxygen
  git
  lame
  ninja
  python
  qt5-tools
)
optdepends=('lame: MP3 export')
options=(!lto)
_tag=22b46f27e224cd38a0100be44409e648575d1931
source=(git+https://github.com/musescore/MuseScore.git#tag=${_tag})
sha256sums=('c1c6e956194e0cd33e1c3e7515dafbd0edd9539fa287aeb0e6daecf1884452b6')

prepare() {
  cd MuseScore
  # Fix display of scores with recent qt5-declarative
  git cherry-pick -n c747bdbcba81109e2749015a575827b2494af971
}

pkgver() {
  cd MuseScore
  git describe --tags | sed 's/^v//'
}

build() {
  cmake -S MuseScore -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_C_FLAGS_RELEASE="$CFLAGS" \
    -DCMAKE_CXX_FLAGS_RELEASE="$CXXFLAGS" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DMUSESCORE_BUILD_CONFIGURATION=app \
    -DMUSESCORE_BUILD_MODE=release \
    -DMUSESCORE_REVISION=$(git rev-parse --short=7 HEAD) \
    -DMUE_BUILD_CRASHPAD_CLIENT=OFF \
    -DMUE_BUILD_UNIT_TESTS=OFF \
    -DMUE_COMPILE_USE_SYSTEM_FREETYPE=ON \
    -DMUE_ENABLE_FILE_ASSOCIATION=ON \
    -DMUE_INSTALL_SOUNDFONT=ON \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  rm -rf "${pkgdir}"/usr/{bin/crashpad_handler,include,lib}
}

# vim: ts=2 sw=2 et:
