# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: naelstrof <naelstrof@gmail.com>

pkgname=maim
pkgver=5.8.0
pkgrel=1
pkgdesc='Utility to take a screenshot using imlib2'
arch=('loong64' 'x86_64')
url='https://github.com/naelstrof/maim'
license=('GPL3')
depends=('freetype2' 'libpng' 'libjpeg-turbo' 'libwebp' 'libx11' 'libxcomposite' 'libxext' 'libxfixes' 'libxrandr' 'slop')
makedepends=('cmake' 'glm')
source=("${pkgname}-${pkgver}.tar.gz"::"$url/archive/v${pkgver}.tar.gz")
sha256sums=('ecafe01dcbe4246071c58ff36acdcd93d290ed501f67933334b646436650450e')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
