# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Contributor: Andrea Feletto <andrea@andreafeletto.com>

pkgname=river
pkgver=0.3.5
pkgrel=2
pkgdesc='A dynamic tiling wayland compositor'
arch=('loong64' 'x86_64')
url='https://codeberg.org/river/river'
license=('GPL-3.0-or-later')
depends=('glibc'
         'libevdev'
         'libinput'
         'libxkbcommon'
         'mesa'
         'pixman'
         'sh'
         'wayland'
         'wlroots'
         'xorg-xwayland')
makedepends=('git'
             'scdoc'
             'wayland-protocols'
             'zig')
optdepends=('polkit: access seat through systemd-logind')
source=(  git+https://codeberg.org/river/river.git#tag=v${pkgver}?signed)
validpgpkeys=('5FBDF84DD2278DB2B8AD8A5286DED400DDFD7A11') # Isaac Freund <mail@isaacfreund.com>
sha256sums=('aaddb72441e1d4d79be4820b574447fdc35659706c71d515301eee2067019627')

prepare() {
    # PACKAGING.md -> build.zig.zon
    zig fetch --global-cache-dir ./zig-global-cache https://codeberg.org/ifreund/zig-pixman/archive/v0.2.0.tar.gz
    zig fetch --global-cache-dir ./zig-global-cache https://codeberg.org/ifreund/zig-wayland/archive/v0.2.0.tar.gz
    zig fetch --global-cache-dir ./zig-global-cache https://codeberg.org/ifreund/zig-wlroots/archive/v0.18.0.tar.gz
    zig fetch --global-cache-dir ./zig-global-cache https://codeberg.org/ifreund/zig-xkbcommon/archive/v0.2.0.tar.gz
}

build() {
  cd $pkgname

  DESTDIR="build" zig build \
    --summary all \
    --prefix /usr \
    --search-prefix /usr \
    --global-cache-dir ../zig-global-cache \
    --system ../zig-global-cache/p \
    -Dtarget=native-linux.6.1-gnu.2.38 \
    -Dcpu=baseline \
    -Dpie \
    -Doptimize=ReleaseSafe \
    -Dxwayland
}

check() {
  cd $pkgname
  zig build test \
    --summary all \
    --prefix /usr \
    --search-prefix /usr \
    --global-cache-dir ../zig-global-cache \
    --system ../zig-global-cache/p \
    -Dtarget=native-linux.6.1-gnu.2.38 \
    -Dcpu=baseline \
    -Dpie \
    -Doptimize=ReleaseSafe \
    -Dxwayland
}

package() {
  cd $pkgname

  cp -a build/* "$pkgdir"

  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 contrib/river.desktop -t "$pkgdir/usr/share/wayland-sessions"

  install -d "$pkgdir/usr/share/$pkgname"
  cp -a example "$pkgdir/usr/share/$pkgname"
}
