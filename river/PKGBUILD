# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Contributor: Andrea Feletto <andrea@andreafeletto.com>

pkgname=river
pkgver=0.3.0
pkgrel=2
pkgdesc='A dynamic tiling wayland compositor'
arch=('loong64' 'x86_64')
url='https://codeberg.org/river/river'
license=('GPL-3.0-or-later')
depends=('libevdev'
         'libxkbcommon'
         'mesa'
         'pixman'
         'wayland'
         'wlroots'
         'xorg-xwayland')
makedepends=('git'
             'scdoc'
             'wayland-protocols'
             'zig')
optdepends=('polkit: access seat through systemd-logind')
source=(
  git+https://codeberg.org/river/river.git#tag=v${pkgver}?signed
  git+https://github.com/ifreund/zig-pixman.git
  git+https://github.com/ifreund/zig-wayland.git
  git+https://codeberg.org/ifreund/zig-wlroots.git
  git+https://github.com/ifreund/zig-xkbcommon.git
)
validpgpkeys=('5FBDF84DD2278DB2B8AD8A5286DED400DDFD7A11') # Isaac Freund <mail@isaacfreund.com>
sha256sums=('c6c33e6dce92e729635b244f0e63a24a004e7339f4ae885db2bd937d022ef93a'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule."deps/zig-pixman".url "${srcdir}/zig-pixman"
  git config submodule."deps/zig-wayland".url "${srcdir}/zig-wayland"
  git config submodule."deps/zig-wlroots".url "${srcdir}/zig-wlroots"
  git config submodule."deps/zig-xkbcommon".url "${srcdir}/zig-xkbcommon"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd $pkgname

  export PKG_CONFIG_PATH='/usr/lib/wlroots0.16/pkgconfig'
  DESTDIR="build" zig build \
    --prefix /usr \
    --search-prefix /usr \
    -Dtarget=native-linux.5.15-gnu.2.37 \
    -Dcpu=baseline \
    -Dpie \
    -Doptimize=ReleaseSafe \
    -Dxwayland
}

check() {
  cd $pkgname

  zig build test \
    --prefix /usr \
    --search-prefix /usr \
    -Dtarget=native-linux.5.15-gnu.2.37 \
    -Dcpu=baseline \
    -Dpie \
    -Doptimize=ReleaseSafe \
    -Dxwayland
}

package() {
  depends+=(libwlroots.so=12)
  cd $pkgname

  cp -a build/* "$pkgdir"

  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 contrib/river.desktop -t "$pkgdir/usr/share/wayland-sessions"

  install -d "$pkgdir/usr/share/$pkgname"
  cp -a example "$pkgdir/usr/share/$pkgname"
}
