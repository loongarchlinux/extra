# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: PyroDevil <p dot devil at gmail dot com>

pkgname=python-queuelib
pkgver=1.7.0
pkgrel=1
pkgdesc="Collection of persistent (disk-based) queues."
arch=('any')
license=('BSD-3-Clause')
url="https://scrapy.org"
depends=('python')
makedepends=('git' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("git+https://github.com/scrapy/queuelib.git#tag=v$pkgver")
sha512sums=('fb980956428f07f014d2e19d1937d3139dcea424a802cf14b26b11fd7b0da81d59476e8b92ef9ee90a65d93e76ad8e6546ca004e1beb280bd1a34e5b1707b9b3')

build() {
  cd queuelib
  python -m build --wheel --no-isolation
}

check() {
  cd queuelib
  pytest
}

package() {
  cd queuelib
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -D -m644 README.rst -t "$pkgdir"/usr/share/doc/$pkgname
}
