# Maintainer: David Runge <dvzrv@archlinux.org>

_name=vimrunner
pkgname=ruby-vimrunner
pkgver=0.3.5
pkgrel=2
pkgdesc="Spawn a Vim instance and control it programatically"
arch=(any)
url="https://github.com/AndrewRadev/vimrunner"
license=(MIT)
depends=(
  ruby
  gvim
)
makedepends=(
  ruby-bundler
  ruby-rake
  ruby-rdoc
  ruby-rspec
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('dff20bcf7765d27f65d8f6e3f9ed521d2bb20d488a34b11bb1527a1b104bc4a9446a2f4a8ad1e36bb80bd899256c66dfc84779cd51b0d7821aa482b28f4b4085')
b2sums=('896a69087e453683518e3b43bd24c9b7af4335bee19a4387d248b854fc305b78a4b6ba838c8f36979e172445bbd8c771bb20fd392646099562412ad8aedc5518')

prepare() {
  cd $_name-$pkgver

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed -i -e 's|~>|>=|g' $_name.gemspec
}

build() {
  local gemdir="$(gem env gemdir)"
  local gem_install_options=(
    --local
    --verbose
    --ignore-dependencies
    --no-user-install
    --install-dir tmp_install/$gemdir
    --bindir tmp_install/usr/bin
    $_name-$pkgver.gem
  )
  local unrepro_files=(
    tmp_install/$gemdir/cache/
    tmp_install/$gemdir/gems/$_name-$pkgver/vendor/
    tmp_install/$gemdir/doc/$_name-$pkgver/ri/ext/
  )

  cd $_name-$pkgver

  gem build $_name.gemspec
  gem install "${gem_install_options[@]}"

  # remove unrepreducible files
  rm -frv "${unrepro_files[@]}"

  find tmp_install/$gemdir/gems/ \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find tmp_install/$gemdir/extensions/ \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

# NOTE: there is no rake test target

package() {
  cd $_name-$pkgver

  mv -v tmp_install/* "$pkgdir/"

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
