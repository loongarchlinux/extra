# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=ser2net
pkgver=4.3.13
pkgrel=1
pkgdesc="A proxy that allows telnet/tcp connections to be made to serial ports"
arch=(loong64 x86_64)
url="https://sourceforge.net/projects/ser2net"
license=('GPL')
depends=('gensio' 'libyaml')
backup=(etc/ser2net/ser2net.yaml)
source=("$pkgname-$pkgver.tar.gz::https://github.com/cminyard/ser2net/archive/v$pkgver.tar.gz"
	ser2net.service)
sha256sums=('3013eda7fdfc54cb87123d73635ee357d5384beeee922b4c536772b3382eacdb'
            'b093e1f5bf7dd826816828623a5c6858f83a9f0dcb1a9785f545b29efd1c320c')

build() {
  cd "$srcdir"/${pkgname}-${pkgver/rc/-rc}
  test -x configure || ./reconf
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver/rc/-rc}
  make DESTDIR="$pkgdir" install
  install -Dm0644 ser2net.yaml "$pkgdir"/etc/ser2net/ser2net.yaml
  install -Dm0644 "$srcdir"/ser2net.service "$pkgdir"/usr/lib/systemd/system/ser2net.service
}
