# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: ThatOneCalculator <kainoa@t1c.dev>

pkgname=hyprpaper
pkgver=0.7.0
pkgrel=1
pkgdesc='a blazing fast wayland wallpaper utility with IPC controls'
arch=(loong64 x86_64)
url="https://github.com/hyprwm/$pkgname"
license=(BSD)
depends=(cairo libcairo.so
         file libmagic.so
         gcc-libs
         glibc
         hyprlang libhyprlang.so
         libglvnd
         libjpeg libjpeg.so
         libwebp libwebp.so
         pango
         wayland # libwayland-cursor.so libwaylant-client.so
         wlroots)
makedepends=(cmake
             wayland-protocols
             xorgproto)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('4840425013b95b49120aeb87d4d4e7d83f8956e847a537e1e6dd7f58a4b643b4')

build() {
	cd "$_archive"
	cmake -B build \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D CMAKE_BUILD_TYPE=Release
	make protocols
	cmake --build build
}

package() {
	cd "$_archive"
	# DESTDIR="$pkgdir" cmake --install build
	install -Dm0755 -t "$pkgdir/usr/bin/" "build/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
