# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=mitmproxy2swagger
pkgver=0.13.0
pkgrel=2
pkgdesc='Automagically reverse-engineer REST APIs via capturing traffic'
url='https://github.com/alufers/mitmproxy2swagger'
arch=('any')
license=('MIT')
depends=(
  mitmproxy
  python
  python-json-stream
  python-msgpack
  python-ruamel-yaml
)
makedepends=(
  python-build
  python-installer
  python-poetry-core
  python-wheel
)
source=(https://github.com/alufers/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('95f9d5aa49bac77bfc0fcc336a04b5f9865ce3a50ae9a9e980b6d1b44531c121')
b2sums=('f7ebb4afe745ce1bc8dfae126cff4e5e9f072aa8c21d74c24303bce3943cabd457a1b8b57661c58a13db0206ebe8201ff4913729b1a1afd412b10ef23aae78e5')

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd ${pkgname}-${pkgver}
  python -m installer --destdir=test_dir dist/*.whl

  PATH="test_dir/usr/bin:$PATH" PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH" mitmproxy2swagger --help
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
}

# vim: ts=2 sw=2 et:
