# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=arch-signoff
pkgver=0.5.2
pkgrel=4
pkgdesc='Sign off Arch Linux testing packages'
arch=(any)
url="https://github.com/archlinux/arch-signoff"
license=("ISC")
depends=('python' 'pyalpm' 'python-click' 'python-dateutil'
         'python-requests')
makedepends=('python-setuptools-scm' 'python-setuptools' 'python-build'
             'python-installer' 'python-wheel' 'asciidoc' 'git')
checkdepends=('python-pytest-pacman' 'python-pytest' 'python-pytest-cov')
groups=('archlinux-tools')
source=("$pkgname::git+https://github.com/archlinux/arch-signoff.git?signed#tag=$pkgver")
validpgpkeys=(
    'E499C79F53C96A54E572FEE1C06086337C50773E' # Jelle van der Waa
)
sha256sums=('ad9eeaaa26f64b8a9d7a4bcf06ed38fa80090cc4b95adac5c0f71305f7e57316')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
  make -C man
}

check() {
  cd "$pkgname"
  make test
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "man/signoff.1" "$pkgdir/usr/share/man/man1/signoff.1"
}
