# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-johnnycanencrypt
_name="${pkgname#python-}"
pkgver=0.14.1
pkgrel=2
pkgdesc="Python module for OpenPGP written in Rust"
arch=(loong64 x86_64)
url="https://github.com/kushaldas/johnnycanencrypt"
license=(LGPL-3.0-or-later)
depends=(
  bzip2
  gcc-libs
  glibc
  gmp
  nettle
  pcsclite
  python
  python-httpx
)
makedepends=(
  clang
  git
  python-build
  python-installer
  python-setuptools
  python-setuptools-rust
  python-wheel
  rust
)
checkdepends=(
  python-pytest
  python-vcrpy
)
source=(git+$url.git?signed#tag=v$pkgver)
sha512sums=('635a3dc07f7cf7acea0ccda7a2e3a586adf8b5f0f66997892b6fb1ad0411700f045d5c374de0034c2d6fe31f1b9b06c4db49a6ea38ba98cb8f7a78cd7a016a61')
b2sums=('fce4a07964d961ca74c0bef5e93019c3631f6355f78ec24a84ca5a195194f4a5d225d2c351c52758b53ba0870122bb9bb8d1fd68fcffcdd81dbba1d56ed92488')
validpgpkeys=(A85FF376759C994A8A1168D8D8219C8C43F6C5E1)  # Kushal Das <kushal@freedom.press>

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name
  # install to temporary location and copy tests in place because test imports are relative :(
  python -m installer --destdir=test_dir dist/*.whl
  cp -av -- tests "test_dir/$site_packages/"
  export PYTHONPATH="$PWD/test_dir/$site_packages"
  pytest "${pytest_options[@]}" test_dir/$site_packages/tests/
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl
}
