# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Miles McLean <mills00013@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Brad Pitcher <bradpitcher@gmail.com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=networkmanager-l2tp
pkgver=1.20.14
_pppver=2.5.0
pkgrel=1
pkgdesc='L2TP support for NetworkManager'
arch=(loong64 x86_64)
url='https://github.com/nm-l2tp/NetworkManager-l2tp'
license=(GPL2)
conflicts=(networkmanager-l2tp-gtk3 networkmanager-l2tp-gtk4)
replaces=(networkmanager-l2tp-gtk3 networkmanager-l2tp-gtk4)
depends=(ppp=$_pppver xl2tpd)
makedepends=(intltool libnma libnma-gtk4 python-packaging)
optdepends=(
  'libnma: GUI support (GTK 3)'
  'libnma-gtk4: GUI support (GTK 4)'
  'strongswan: IPSec support')
source=("$pkgname-$pkgver.tar.gz"::"$url/archive/$pkgver.tar.gz")
b2sums=('6e63b5b960469ca148e436da6bb9b157b0baa11998e2451a8d892bd947543e3023b3c41c823bb17fd8207b18de8d9cc7c6a6839f6a84352e58f232226e9d20e8')

prepare() {
  ln -sf NetworkManager-l2tp-$pkgver $pkgname-$pkgver
}

build() {
  export NOCONFIGURE=1
  cd $pkgname-$pkgver
  ./autogen.sh
  ./configure \
    --libexecdir=/usr/lib/NetworkManager \
    --localstatedir=/var \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver \
    --with-gtk4
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
  install -Dm644 $pkgname-$pkgver/NEWS "$pkgdir/usr/share/doc/$pkgname/NEWS"
}
