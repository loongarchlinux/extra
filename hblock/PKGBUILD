# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Héctor Molinero Fernández <hector at molinero dot dev>
# Contributor: Erazem Kokot <contact at erazem dot eu>

pkgname=hblock
pkgver=3.4.5
pkgrel=1
pkgdesc='Adblocker that creates a hosts file from multiple sources'
arch=('any')
url='https://hblock.molinero.dev/'
license=('MIT')
depends=('sh' 'curl' 'systemd')
makedepends=('git')
source=("$pkgname::git+https://github.com/hectorm/hblock#tag=v$pkgver")
sha512sums=('71418118def72a2ab73a8e173c71c1ab7faedf2604f6c3b4fe9b959cb2919803b59678f61002a83b873185f2258476486ddfe9b35a0c92ba573e1206bf91f29c')
b2sums=('717497e78376d64d970b1d29d9d6905e3409d7f871056c678dba59a1505a2facd279755f8ee97de48100ab6df1c7f222986925ecde867f685048a72682dac33b')

check() {
  cd "$pkgname"

  make test
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" prefix='/usr' install

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr resources/alt-formats "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
