# Maintainer: Lukas Jirkovsky <l.jirkovsky AT gmail.com>
# Contributor: Bogdan Szczurek <thebodzio(at)gmail.com>
# Contributor: Vaclav Kramar <vaclav.kramar@tiscali.cz>
# Contributor: Archie <mymaud@gmail.com>

pkgname=rawtherapee
epoch=1
pkgver=5.10
pkgrel=2
pkgdesc='A powerful cross-platform raw image processing program'
arch=('loong64' 'x86_64')
url='https://www.rawtherapee.com/'
license=('GPL3')
depends=('exiv2' 'fftw' 'gtk3' 'glibmm' 'gtkmm3' 'lcms2' 'lensfun' 'libcanberra'
         'libiptcdata' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('cmake')
source=("https://github.com/Beep6581/RawTherapee/releases/download/${pkgver}/rawtherapee-$pkgver.tar.xz"
        '0001-jpeg-turbo.patch')
sha1sums=('d3ea8ba31bbe34f3387df4b3f11c645678ef1b56'
          '38c1409719cb6fac0e1ac3ffda6789ec1d05e06a')
sha256sums=('a799b53cd54dba4a211479e342ffc9c5db1c44d3d6c3a27d5cc13adf0debd2da'
            'cd627950bc622ff53dc0ee4aea234543994c887bbbd9b6c0af8bc59b30959ef3')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 < ../0001-jpeg-turbo.patch
}

build() {
  mkdir -p "$srcdir/$pkgname-build"
  cd "$srcdir/$pkgname-build"

  cmake "$srcdir/$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCACHE_NAME_SUFFIX=""
  make
}

package() {
  cd "$srcdir/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
