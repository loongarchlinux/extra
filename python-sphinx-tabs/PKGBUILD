# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-sphinx-tabs
pkgver=3.4.5
pkgrel=2
pkgdesc='Create tabbed content in Sphinx documentation'
arch=('any')
url='https://sphinx-tabs.readthedocs.io/'
license=('MIT')
depends=(
  'python'
  'python-sphinx'
  'python-docutils'
  'python-pygments'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
_commit='b8666a582f9bb571de4186fea7fa0871b1b198fb'
source=("$pkgname::git+https://github.com/executablebooks/sphinx-tabs#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/sphinx_tabs-$pkgver.dist-info/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
