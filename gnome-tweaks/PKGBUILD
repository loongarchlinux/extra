# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-tweaks
pkgver=46.1
pkgrel=1
pkgdesc="Graphical interface for advanced GNOME 3 settings (Tweak Tool)"
url="https://wiki.gnome.org/Apps/Tweaks"
arch=(any)
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gnome-desktop-4
  gsettings-desktop-schemas
  gtk4
  hicolor-icon-theme
  libadwaita
  libgudev
  libnotify
  pango
  python
  python-gobject
)
makedepends=(
  git
  meson
)
groups=(gnome-extra)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-tweaks.git?signed#tag=$pkgver"
)
b2sums=('09e3b9a9fd95dddd46c3aa8149cd0d36642eaf251aa5c2aa70007bf8096d89a67c0d6be0f2e1a2cff8d96ef74ac068fd24df2bbcb7490c9f3d6ca6b55553ceb9')
validpgpkeys=(
  A3C5EBBF43FBA5F829F1A2548D66762250C07E85 # Evan Welsh <contact@evanwelsh.com>
)

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
