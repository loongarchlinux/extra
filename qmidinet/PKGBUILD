# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidinet
pkgver=1.0.0
pkgrel=1
pkgdesc="A MIDI Network Gateway via UDP/IP Multicast"
arch=(loong64 x86_64)
url="https://qmidinet.sourceforge.io"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  jack
  qt6-tools
)
optdepends=(
  'qt6-wayland: for native wayland support'
)
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('d887836a552d7f28e9667a42a611ae272c8e3c86326971d0ff856e1b56e6b30fe99f80f3801f4971006767a96249201642aac3656efd968c21410f954e02408d')
b2sums=('872ecdc3cea493a78b6fd43d6b1977672a23ee186b30d7eeb6f1f5898ea04be47ea1e0320fed2173fc7df37900fbd5b971b6da4c1537ea43124923834b5e66ac')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    alsa-lib libasound.so
    jack libjack.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"
}
