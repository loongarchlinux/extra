# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=python-formencode
_pkgname=formencode
_pypi=FormEncode
pkgver=2.1.0
pkgrel=3
pkgdesc="A validation library for Python."
arch=("any")
url="http://www.formencode.org/en/latest/"
license=("MIT")
depends=("python")
checkdepends=("python-pytest" "python-dnspython" "python-pycountry")
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools' 'python-setuptools-scm')
source=("$pkgname-$pkgver.tar.gz::https://github.com/formencode/formencode/archive/$pkgver.tar.gz")
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/${_pkgname}/${_pypi}-${pkgver}.tar.gz")
sha256sums=('eb74d223078a28cf015fa88966c6e34f2d18d75127318d65c144bed9afc4263f')

prepare() {
  cd "${_pypi}-${pkgver}"
  sed '/setuptools_scm_git_archive/d' -i setup.py 
}

build() {
  cd "${_pypi}-${pkgver}"
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check(){
  cd "${_pypi}-${pkgver}"
  python -m installer --destdir="$PWD/tmp_install" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$PWD/tmp_install/$site_packages" 
  PYTHONDONTWRITEBYTECODE=1 python -m unittest
}

package() {
  cd "${_pypi}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
