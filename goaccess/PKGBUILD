# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josh VanderLinden <arch@cloudlery.com>
# Contributor: Nicolas Storme <nicolas@atelier-web.com>

pkgname=goaccess
pkgver=1.9.3
pkgrel=1
pkgdesc="An open source real-time web log analyzer and interactive viewer"
arch=('loong64' 'x86_64')
url='https://goaccess.io'
license=('GPL')
depends=('ncurses' 'libmaxminddb' 'openssl')
makedepends=('git')
optdepends=('geoip2-database: for geoip support')
backup=('etc/goaccess/goaccess.conf')
source=("git+https://github.com/allinurl/goaccess.git#tag=v$pkgver")
sha512sums=('1c6b83feaac817f5c297fa5572c42ebb6f9d0e896e3aa159cc6130993ea8899f48f2d55ec06ee542c9013bfc32fb614c2db5b3b6479d3f7c9bc4592f68b98ec4')

prepare() {
  cd $pkgname
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --enable-geoip=mmdb --enable-utf8 --with-openssl
  make prefix=/usr sysconfdir=/etc
}

package() {
  cd $pkgname
  make prefix="$pkgdir/usr" sysconfdir="$pkgdir/etc" install
}
