# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgbase=ghex
pkgname=(
  ghex
  ghex-docs
)
pkgver=46.0
pkgrel=1
pkgdesc="A simple binary editor for the Gnome desktop"
url="https://wiki.gnome.org/Apps/Ghex"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(libadwaita)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
_commit=573bc9b9304f815b0d58427d54852cb2099b421e  # tags/46.0^0
source=("git+https://gitlab.gnome.org/GNOME/ghex.git#commit=$_commit")
b2sums=('00ba3ef6276e59c33736a1657d179557b10eb0ee5c6568b374881747b67cdaccd7b89e874b4910fa5a5c154fe3f305dd0ad55cf4ba40664ea17d93b730decc3a')

pkgver() {
  cd ghex
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd ghex
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D vapi=true
  )

  arch-meson ghex build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_ghex() {
  provides=(libgtkhex-4.so)
  groups=(gnome-extra)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_ghex-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
