# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=zita-jclient
pkgver=0.5.2
pkgrel=1
pkgdesc="Create a Jack client with a few lines of C++"
arch=(loong64 x86_64)
url="https://kokkinizita.linuxaudio.org/linuxaudio/downloads/"
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
)
makedepends=(jack)
provides=(libzita-jclient.so)
source=($url/$pkgname-$pkgver.tar.bz2)
sha512sums=('2a033172b40ac743ae72ee7014a432cae3102b3787c140d0b95efb4da83a29eb18d7a8825863770e211b17e39a143084e163d7ab649bff3cf389f0c286d61bfe')
b2sums=('a42c8990af26a74550b123aea444f980525a768eed76ebe717882578d3f9e05d80ff763e9fc63fe3d36f78bc1667d5a90599af6e1c82989d5a9af3c886fcd59a')

prepare() {
  # remove optimization
  # call ldconfig in local context
  sed -e '/march=native/d; s/ldconfig/& -N $(DESTDIR)\/$(LIBDIR)/' -i $pkgname-$pkgver/source/Makefile
}

build() {
  make -C $pkgname-$pkgver/source
}

package() {
  depends+=(
    jack libjack.so
  )

  make DESTDIR="$pkgdir/" PREFIX=/usr SUFFIX="" install -C $pkgname-$pkgver/source
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
