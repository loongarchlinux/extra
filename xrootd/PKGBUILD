# Maintainer: Konstantin Gizdov < arch at kge dot pw >
# Contributor: Jan Ka≈°par < jan.kaspar at gmail dot com >
# Contributor: Alex Pearce < alex at alexpearce dot me >
pkgname=xrootd
pkgdesc="Software framework for fast, low latency, scalable and fault tolerant data access."
pkgver=5.6.4
pkgrel=2
arch=('loong64' 'x86_64')
url="https://xrootd.slac.stanford.edu/"
license=('LGPL-3.0-or-later')
depends=('curl' 'fuse2' 'json-c' 'libxml2' 'openssl' 'perl' 'python' 'systemd'
         'python-setuptools' 'tinyxml' 'davix')
makedepends=('cmake' 'git' 'python-pip')
checkdepends=('cppunit')
_commit=730df73734e3d3f6fd09714c0b4812f0efb67bd6  # v5.6.4
source=("${pkgname}-${pkgver}::git+https://github.com/${pkgname}/${pkgname}.git#commit=$_commit")
sha256sums=('SKIP')

get_pyver () {
    python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

build() {
    local cmake_args=(
        -Wno-dev
        -S "${pkgbase}-${pkgver}"
        -B build
        -D CMAKE_INSTALL_PREFIX=/usr
        -D CMAKE_BUILD_TYPE=Release
        -D CMAKE_INSTALL_LIBDIR=lib
        -D CMAKE_CXX_STANDARD="17"
        -D WITH_OPENSSL3=TRUE
        -D ENABLE_TESTS=OFF
        -DXRD_PYTHON_REQ_VERSION="$(get_pyver)"
    )
    cmake "${cmake_args[@]}"
    cmake --build build
}

# check() {
    # cd "${srcdir}/build/tests"

    # Check has significantly changed, to-be-updated
    # ./common/text-runner ./XrdCephTests/libXrdCephTests.so "All Tests"

    ##
    # This requires a running XRootD server with multiIP DNS forwarder and local disk servers
    # only run this if you have configured the env correctly,
    # examples in https://github.com/xrootd/xrootd-test-suite
    # sample environment can be configured like so:
    # export XRDTEST_MAINSERVERURL=metaman.xrd.test
    # or export XRDTEST_MAINSERVERURL=http://xrootd.cern.ch/
    # export XRDTEST_DISKSERVERURL=srv1.xrd.test
    # or export XRDTEST_DISKSERVERURL=http://xrootd.cern.ch/
    # export XRDTEST_DATAPATH=/tests/test-files/
    # export XRDTEST_LOCALFILE=/data/a048e67f-4397-4bb8-85eb-8d7e40d90763.dat
    # or export XRDTEST_LOCALFILE=/tmp/accwe.root
    # export XRDTEST_REMOTEFILE=${XRDTEST_MAINSERVERURL}${XRDTEST_DATAPATH}/a048e67f-4397-4bb8-85eb-8d7e40d90763.dat
    # or export XRDTEST_MULTIIPSERVERURL=multiip.xrd.test
    # ./common/text-runner ./XrdClTests/libXrdClTests.so "All Tests"
# }

package() {
    DESTDIR="${pkgdir}" cmake --install build
    install -Dm644 "${pkgbase}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"
}
