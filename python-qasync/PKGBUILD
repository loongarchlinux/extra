# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: gmes78 <gmes.078 at gmail dot com>

pkgname=python-qasync
pkgver=0.27.1
pkgrel=1
pkgdesc="Python library for using asyncio in Qt-based applications"
arch=(any)
url="https://github.com/CabbageDevelopment/qasync"
license=('BSD')
depends=('python')
optdepends=('pyside6' 'python-pyqt5' 'python-pyqt6')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest-raises' 'pyside6' 'python-pyqt5' 'python-pyqt6'
              'xorg-server-xvfb')
source=("https://github.com/CabbageDevelopment/qasync/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0165b915a0f8c026b3269ed127a323de4ca40602776afc5109b9c0f57fbc9a1cb43a78d6a729f7d1ca753aae4d9e79ad0b2e5f55209237dfd620c9a7ac0c22cb')

build() {
  cd qasync-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd qasync-$pkgver
  QT_API=pyside6 xvfb-run python -m pytest -k 'not test_regression_bug13'
  QT_API=pyqt5 xvfb-run python -m pytest -k 'not test_regression_bug13'
  QT_API=pyqt6 xvfb-run python -m pytest -k 'not test_regression_bug13'
}

package() {
  cd qasync-$pkgver
  python -m installer --destdir="$pkgdir/" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
