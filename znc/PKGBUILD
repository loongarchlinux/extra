# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: SÃ©bastien Luttringer
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: mickael9 <mickael9 at gmail dot com>

pkgname=znc
pkgver=1.9.1
pkgrel=1
pkgdesc='An IRC bouncer with modules & scripts support'
url='https://wiki.znc.in/'
license=('Apache-2.0')
arch=('loong64' 'x86_64')
depends=(
    'libsasl'
    'icu'
    'boost-libs'
    'argon2'
)
makedepends=(
    'tcl'
    'python'
    'perl'
    'cmake'
    'boost'
)
optdepends=(
    'tcl: modtcl module'
    'python: modpython module & znc-buildmod script'
    'perl: modperl module'
    'cyrus-sasl: saslauth module'
    'cmake: znc-buildmod script'
)
install=$pkgname.install
source=("https://znc.in/releases/archive/$pkgname-$pkgver.tar.gz"{,.sig}
        "$pkgname.tmpfiles"
        "$pkgname.sysusers")
validpgpkeys=('D5823CACB477191CAC0075555AE420CC0209989E')  # Alexey Sokolov <alexey@asokolov.org>
sha256sums=('e8a7cf80e19aad510b4e282eaf61b56bc30df88ea2e0f64fadcdd303c4894f3c'
            'SKIP'
            '8cd1b5b011c706fea9dc0c70d4daa75a53bf0966d6e96a3fce3a541777aa2771'
            '8802f1b84ab3031db7cc45678f008ceca9b08b2012412a3d4ff1ea596586bb2a')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWANT_PYTHON=YES \
    -DWANT_PERL=YES \
    -DWANT_TCL=YES \
    -DWANT_SYSTEMD=YES \
    -DWANT_ARGON=YES \
    -DSYSTEMD_DIR=/usr/lib/systemd/system
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -Dm644 "$srcdir/$pkgname.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
