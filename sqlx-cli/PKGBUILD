# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname=sqlx-cli
pkgver=0.8.0
pkgrel=1
pkgdesc='Command-line utility for SQLx, the Rust SQL toolkit'
license=('Apache' 'MIT')
arch=('loong64' 'x86_64')
url="https://github.com/launchbadge/sqlx"
depends=('gcc-libs' 'openssl')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::https://github.com/launchbadge/sqlx/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('6345755cf69c87965c11ef7175694eaaa1beca7ac2ce00fda299ce00183f6fec49d8a046c7b1bddfc66235a6585539692fb0925c0f08a85747c3b6a7d1e3cb95')
options=('!lto')

build() {
    cd "sqlx-$pkgver/sqlx-cli"
    cargo build --locked --release
}

check() {
    cd "sqlx-$pkgver/sqlx-cli"
    cargo test --locked
}

package() {
    cd "sqlx-$pkgver"
    install -Dm755 -t "$pkgdir/usr/bin" \
        "target/release/cargo-sqlx" \
        "target/release/sqlx"

    # install completions
    install -d "$pkgdir/usr/share/bash-completion/completions" \
        "$pkgdir/usr/share/zsh/site-functions" \
        "$pkgdir/usr/share/fish/vendor_completions.d"
    "$pkgdir/usr/bin/sqlx" completions bash > "$pkgdir/usr/share/bash-completion/completions/sqlx"
    "$pkgdir/usr/bin/sqlx" completions zsh > "$pkgdir/usr/share/zsh/site-functions/_sqlx"
    "$pkgdir/usr/bin/sqlx" completions fish > "$pkgdir/usr/share/fish/vendor_completions.d/sqlx.fish"

    # install license files
    install -Dm644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}
