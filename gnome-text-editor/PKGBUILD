# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-text-editor
pkgver=46.1
pkgrel=1
pkgdesc="A simple text editor for the GNOME desktop"
url="https://gitlab.gnome.org/GNOME/gnome-text-editor"
arch=(loong64 x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  editorconfig-core-c
  enchant
  gcc-libs
  glib2
  glibc
  gtk4
  gtksourceview5
  hicolor-icon-theme
  icu
  libadwaita
  pango
)
makedepends=(
  appstream-glib
  git
  meson
  yelp-tools
)
groups=(gnome)
source=("git+$url.git#tag=${pkgver/[a-z]/.&}")
b2sums=('bc3546bbf636df14290d272451eea66376e8d028da6ea0c0fb64b2b6fab7cd2ca035ca5642fc44b0a54650c58633eee0c1e78362124be03fb85bb2f9b8c6f0e3')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D bugreport_url="https://gitlab.archlinux.org/archlinux/packaging/packages/gnome-text-editor/-/issues"
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
