# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: VojtÄ›ch Aschenbrenner <v@asch.cz>

_gemname='locale'
pkgname="ruby-${_gemname}"
pkgver=2.1.4
pkgrel=2
pkgdesc='A pure ruby library which provides basic and general purpose APIs for localization.'
arch=('any')
url="https://github.com/ruby-gettext/locale"
license=('Ruby' 'LGPL-3.0-or-later')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-bundler' 'ruby-yard')
checkdepends=('ruby-test-unit' 'ruby-test-unit-rr')
depends=('ruby')
options=(!emptydirs)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('65f5fde22c4ab4dc404204aea8d87b53d5a2bca01fa860ac7953de3806d4ca69b36bc4fce28d57a6a9d04be5a10c2fdeadbbe10fc48fa0a495b0da4ccfc71626')

build() {
  cd "${_gemname}-${pkgver}"
  # remove dependencies that aren't actual dependencies
  sed --in-place '/test\/unit\/notify/d' test/run-test.rb
  sed --in-place '/s.add_development_dependency("redcarpet")/d' "${_gemname}.gemspec"
  sed --in-place '/s.add_development_dependency("test-unit-notify")/d' "${_gemname}.gemspec"
  rake build
}

check() {
  cd "${_gemname}-${pkgver}"
  rake test
}

package() {
  cd "${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "pkg/${_gemname}-${pkgver}.gem"

  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm 644 README.rdoc ChangeLog doc/text/news.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
