# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=git-warp-time
pkgver=0.7.6
pkgrel=4
pkgdesc='reset timestamps of Git repository files to the time of the last modifying commit'
arch=(loong64 x86_64)
url="https://github.com/alerque/$pkgname"
license=(GPL-3.0-only)
depends=(gcc-libs # libgcc_s.so 
         glibc # libc.so libm.so
         libgit2)
makedepends=(cargo
             git
             jq)
_archive="$pkgname-$pkgver"
source=("$url/releases/download/v$pkgver/$_archive.tar.zst"{,.asc})
sha256sums=('5d4d1f6abaad6af76fcad356cc5882e5ca3583e916e50dbbff2d2a5acede4b0f'
            'SKIP')
validpgpkeys=('9F377DDB6D3153A48EB3EB1E63CC496475267693') # Caleb Maclennan <caleb@alerque.com> (@alerque)

prepare() {
	cd "$_archive"
	sed Makefile.am -i \
		-e 's/cargo \(build\|install\|test\)/cargo --offline \1/'
	autoreconf
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_archive"
	CFLAGS+=' -ffat-lto-objects'
	./configure --prefix /usr --enable-shared
	make
}

check() {
	cd "$_archive"
	make check
}

package() {
	depends+=(libgit2.so)
	cd "$_archive"
	make DESTDIR="$pkgdir" install
}
