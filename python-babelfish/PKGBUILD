# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-babelfish
pkgver=0.6.1
pkgrel=1
pkgdesc='A Python module to work with countries & languages'
arch=('any')
url='https://github.com/Diaoul/babelfish'
license=('BSD-3-Clause')
depends=('python' 'python-setuptools')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest')
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('c9c9e1e688355ad25103408f75d3f7b9d8fdcd9878fa068361437e34d0a99d270df34210b65684a53748ce1a41cb52a1f1086fb7b53125a1e8460767a7c7aa92')
b2sums=('12aec1aa803b13916c7de8ac7a60034202b13ea82973202e54ca3f519f454266f98490ca2a62b0082dd7b7a28bff0d74cdcf0d130afe4c2d19db2c7ee2fc3806')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$PWD:$PYTHONPATH" pytest
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
