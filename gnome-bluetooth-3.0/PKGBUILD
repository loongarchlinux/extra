# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth-3.0
pkgver=46.1
pkgrel=1
pkgdesc="GNOME Bluetooth Subsystem"
url="https://wiki.gnome.org/Projects/GnomeBluetooth"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  bluez
  gcc-libs
  glib2
  glibc
  gsound
  gtk4
  libadwaita
  libnotify
  systemd-libs
  upower
)
makedepends=(
  docbook-xsl
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  meson
  python
  systemd
)
checkdepends=(python-dbusmock)
provides=(libgnome-bluetooth{,-ui}-3.0.so)
source=("git+https://gitlab.gnome.org/GNOME/gnome-bluetooth.git#tag=${pkgver/[a-z]/.&}")
b2sums=('121f9b5c469bc94a314b1bbc28de2db9ae6c02464594d3c57c72879f54ef96c03a25a652483cfadafd5470c9e83a482425f52c893858694d47b1a27892252a65')

prepare() {
  cd gnome-bluetooth
}

build() {
  local meson_options=(
    -D gtk_doc=true
  )

  arch-meson gnome-bluetooth build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
