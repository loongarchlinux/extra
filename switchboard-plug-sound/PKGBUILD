# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-sound
pkgver=2.3.3.r91.8cabffc
pkgrel=1
pkgdesc='Switchboard Sound Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-sound
license=(LGPL-2.1-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gtk4
  libcanberra
  libadwaita-1.so
  libgee
  libgranite-7.so
  libpulse
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_commit=8cabffc0e19a19bb72f42c531199076c02f84a46
source=(git+https://github.com/elementary/switchboard-plug-sound.git#commit=${_commit})
b2sums=(SKIP)

pkgver() {
  cd switchboard-plug-sound
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-sound build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
