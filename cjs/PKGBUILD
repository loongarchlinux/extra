# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=cjs
pkgver=6.2.0
pkgrel=1
pkgdesc="Javascript Bindings for Cinnamon"
arch=('loong64' 'x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('gtk3' 'gobject-introspection-runtime' 'js115')
makedepends=('meson' 'samurai' 'gobject-introspection')
checkdepends=('xorg-server-xvfb')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('5f7ce7f1ba648db834d7703e909ef65cf369d47147ca02e90b8844c31b4360dcc0fb94bbd3265632c3de8ef2b026934dd799b94a2ff3895442a2f9fc469f53db')
b2sums=('7b1afda55bc0b5da165a20afa7be58382b8f03f1421bde960f6a3f134d7bdb2bdd44320b0b613f4f710258379da6666383a20d92556a1172971ca8340ae6d348')

build() {
    mkdir -p ${pkgname}-${pkgver}/builddir
    cd ${pkgname}-${pkgver}/builddir

    meson --prefix=/usr \
          --libexecdir=/usr/lib \
          --buildtype=plain \
          -Dinstalled_tests=false \
          ..

    samu
}

check() {
    cd ${pkgname}-${pkgver}/builddir

    # Needs a display
    xvfb-run meson test --print-errorlogs
}

package() {
    cd ${pkgname}-${pkgver}/builddir

    DESTDIR="${pkgdir}" samu install
}
