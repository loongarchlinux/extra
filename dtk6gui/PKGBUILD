# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtk6gui
pkgver=6.0.11
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dtk6gui"
license=('LGPL3')
# 'libqtxdg' disabled until it supports Qt6
depends=('glibc' 'dtk6core' 'freeimage' 'libraw' 'librsvg' 'qt6-base' 'glib2' 'libx11' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtk6gui.git#tag=$pkgver")
sha512sums=('5f6332728cf35379921baa7f0a7849e69a34b9433ed6b808c7d1815dba92093495b0d9530e3a27bc32b4808969f7f5d34b2fea31e528485fe88b4772d1028502')

build() {
  cd dtk6gui
  cmake . -GNinja \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6gui
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6gui
  DESTDIR="$pkgdir" ninja install
}
