# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtk6gui
pkgver=6.0.16
pkgrel=2
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dtk6gui"
license=('LGPL3')
# 'libqtxdg' disabled until it supports Qt6
depends=('glibc' 'dtk6core' 'freeimage' 'libraw' 'librsvg' 'qt6-base' 'glib2' 'libx11' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtk6gui.git#tag=$pkgver")
sha512sums=('7826179293fd64b974e13e81282078756dda1a25818c7576448bcd33607f872c24bed463f33ba608934fa9b1eb6398b5d190b669003178542059a73a8878c08e')

build() {
  cd dtk6gui
  cmake . -GNinja \
    -DBUILD_DOCS=ON \
    -DBUILD_TESTING=ON \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6gui
  # TODO
  ninja test || echo "Tests failed"
}

package() {
  cd dtk6gui
  DESTDIR="$pkgdir" ninja install
}
