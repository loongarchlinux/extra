# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=input-parsers
pkgname=haskell-input-parsers
pkgver=0.3.0.2
pkgrel=19
pkgdesc="Extension of the parsers library with more capability and efficiency"
url="https://github.com/blamario/input-parsers"
license=('BSD')
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-monoid-subclasses' 'haskell-parsers')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('d18c09afa167f11ce9c7a9f6b95a75ec98694e56cc5a5962356b0d998d56e7f0d0f38169f28af09f93bcf9b2e9de95ece4b0d06f8aed286918a85508c16830a5')

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
        -fbinary -fparsec -fattoparsec
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
