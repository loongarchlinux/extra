# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: gem2arch 0.1
# Contributor: theapodan

pkgname=ruby-pkg-config
_pkgname="${pkgname#ruby-}"
pkgver=1.5.6
pkgrel=2
pkgdesc='Implementation of pkg-config in Ruby'
arch=('any')
url='https://github.com/ruby-gnome/pkg-config'
license=('LGPL-2.1-only')
depends=('ruby')
makedepends=('git')
provides=('ruby-pkgconfig')
replaces=('ruby-pkgconfig')
conflicts=('ruby-pkgconfig')
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('72cf212a4b5bda208abdc585c0a11c14b9fdc13476e77b1880d1c8a73f9b2e688391d2dea3eb6e75cc14080099f273a13e4f263373c03414f5a87aab9bc212d4')
b2sums=('8e073de6886166401c9c2f34d0890d93b8f41662342554855f5db55129d1494a102d24c6c9af7d71bd393c5272b7e6b1f3a9ea24668ebfbc53a70d6a6cbe48be')

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

package() {
  cd "$pkgname"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LGPL-2.1" "$pkgdir/usr/share/licenses/$pkgname"
}
