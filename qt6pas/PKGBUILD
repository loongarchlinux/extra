# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>

pkgname=qt6pas
_lazarus_tag=3_0
_lazarus_version=3.0.0
pkgver=6.2.7
pkgrel=2
pkgdesc='Free Pascal Qt6 binding library updated by lazarus IDE'
arch=('loong64' 'x86_64')
url='https://gitlab.com/freepascal.org/lazarus/lazarus/-/tree/main/lcl/interfaces/qt6/cbindings'
license=('LGPL3')
depends=('qt6-base')
options=(libtool staticlibs)
source=("https://gitlab.com/freepascal.org/lazarus/lazarus/-/archive/lazarus_${_lazarus_tag}/lazarus-lazarus_${_lazarus_tag}.tar.bz2"
         qt-6.7.patch)
sha512sums=('131cc6ffc31fdc98a8ae3a9edd75d2e1d4e5cf1b963a5849c3e42aafea9ea1ae78ba0eaffd38c4ea6848cc58a024ebd878ae1771f2af65de7355f43a1b9b766c'
            '65ea22b1a6fd46ee8db5118a225daa967d07858533f3655bda0fbe8f97e506c1fbe26040e0b5bf2f2dc716d707697bb05903624160dfeec5834e172cbbce6483')

prepare() {
    cd lazarus-lazarus_${_lazarus_tag}
    patch -p1 -i ../qt-6.7.patch # Fix build with Qt 6.7
}

build() {
    cd "lazarus-lazarus_${_lazarus_tag}/lcl/interfaces/qt6/cbindings"
    qmake6
    make
}

package() {
    cd "lazarus-lazarus_${_lazarus_tag}/lcl/interfaces/qt6/cbindings"
    make INSTALL_ROOT="$pkgdir" install
}
