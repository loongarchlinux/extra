# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-protego
pkgver=0.3.1
pkgrel=2
pkgdesc="A pure-Python robots.txt parser with support for modern conventions"
url="https://github.com/scrapy/protego"
license=('BSD-3-Clause')
arch=('any')
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/protego/archive/$pkgver.tar.gz")
sha512sums=('0f6d2915c189c8c04da30b31bee40773a9cec77f8d1025b498d8ec29aea8a5fe918341b0bae9515b83cb276ed5c9218dcb4b075406ee8d57a503ab761a275338')

build() {
  cd protego-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd protego-$pkgver
  PYTHONPATH="$PWD"/src pytest
}

package() {
  cd protego-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
