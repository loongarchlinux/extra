# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Loui Chang <louipc dot ist at gmail company>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgname=tig
pkgver=2.5.9
pkgrel=1
pkgdesc='Text-mode interface for Git.'
depends=('git' 'ncurses')
makedepends=('asciidoc' 'xmlto')
url='https://jonas.github.io/tig/'
license=('GPL')
arch=('loong64' 'x86_64')
backup=('etc/tigrc')
source=("https://github.com/jonas/$pkgname/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('54d0d7a17b75832eac632b6232f785fe')
sha256sums=('0cb4d9e3de00dc92aaa7996e1517845bd9b9a0d4368f3206f618d813e8db8b39')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install install-doc-man

  install -Dm0644 contrib/tig-completion.bash "$pkgdir/usr/share/bash-completion/completions/tig"
}
