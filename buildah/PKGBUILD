# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=buildah
pkgver=1.37.0
pkgrel=1
pkgdesc="A tool which facilitates building OCI images"
arch=(loong64 x86_64)
url="https://github.com/containers/buildah"
license=(Apache)
depends=(oci-runtime skopeo passt)
makedepends=(go git bats btrfs-progs device-mapper gpgme libassuan
             bzip2 go-md2man skopeo systemd)
options=(!lto !debug)
source=(git+https://github.com/containers/buildah.git#tag=v$pkgver)
sha512sums=('fbf534a151ef149343f24cbcfc42e17d9b25a6eb4551fb6ac324e07ed7b5c488ba33d024faaa1cfb044db45a3912a2a7d6b4631824238ebfbb2fc3cea1554829')

pkgver() {
  cd buildah
  git describe --tags | sed 's/^[vV]//;s/-/+/g'
}

build() {
  cd $pkgname
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  make PREFIX=/usr BUILDTAGS="containers_image_ostree_stub seccomp"
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=usr install install.completions
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
