# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Hoream <hoream@qq.com>
# Contributor: Asuka Minato

# Switched to crates source since repo still not tagged even after raising
# upstream issue: https://github.com/ekzhang/sshx/issues/50
_sha=7393800564e87ba8cfd80a42d51eba199c0a391d

pkgname=sshx
pkgver=0.2.4
pkgrel=1
pkgdesc='A secure web-based, collaborative terminal'
url="https://github.com/ekzhang/$pkgname"
license=(MIT)
arch=(loong64 x86_64 aarch64)
depends=(gcc-libs
         glibc)
makedepends=(cargo
             protobuf)
options=(!lto) # https://github.com/briansmith/ring/issues/1444
_archive="$pkgname-$_sha"
source=("$url/archive/$_sha/$_archive.tar.gz")
# _archive="$pkgname-$pkgver"
# source=("$_archive.tar.gz::https://static.crates.io/crates/$pkgname/$_archive.crate"
        # "$pkgname-LICENSE::$url/raw/$_sha/LICENSE")
sha256sums=('73555ab4544fec7214da6a2b35f4b2412688e0a0e5c372cd663bee7934404982')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_archive"
	# export CFLAGS+=" -ffat-lto-objects"
	cargo build --frozen --release
}

check() {
	cd "$_archive"
	cargo test --frozen
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"{,-server}
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
