# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=fwupd
pkgver=1.9.1
pkgrel=1
pkgdesc="Simple daemon to allow session software to update firmware"
arch=(loong64 x86_64)
url="https://github.com/fwupd/fwupd"
license=(LGPL)
depends=(libxmlb efivar python libsmbios libgusb
         polkit shared-mime-info tpm2-tss flashrom
         libjcat fwupd-efi gcab hicolor-icon-theme
         bluez gnutls
         libarchive.so libcurl.so libcbor.so
         libjson-glib-1.0.so libgudev-1.0.so libmm-glib.so
         libqmi-glib.so libprotobuf-c.so)
optdepends=(
    'udisks2: UEFI firmware upgrade support'
)
makedepends=(meson valgrind gobject-introspection gi-docgen
             python-cairo noto-fonts noto-fonts-cjk python-gobject vala
             bash-completion python-pillow pandoc gnu-efi-libs)
checkdepends=(umockdev)
provides=(libfwupd.so)
backup=('etc/fwupd/daemon.conf'
        'etc/fwupd/redfish.conf'
        'etc/fwupd/remotes.d/dell-esrt.conf'
        'etc/fwupd/remotes.d/fwupd-tests.conf'
        'etc/fwupd/remotes.d/lvfs-testing.conf'
        'etc/fwupd/remotes.d/lvfs.conf'
        'etc/fwupd/remotes.d/vendor-directory.conf'
        'etc/fwupd/remotes.d/vendor.conf'
        'etc/fwupd/thunderbolt.conf'
        'etc/fwupd/uefi_capsule.conf'
)
source=("https://github.com/fwupd/fwupd/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc}
    fwupd-1.8.10-loong64.patch)
sha512sums=('70a02c52dbaa4e06502fc64c486301bcb5c0f9b0d2cf80aebe5c0ec48f340e04702d43f0a1d5cb44a21d8c4ddb92eb90788fd03daa7494a1f0b3a82b36a2c940'
            'SKIP'
            'b2a6d66223e79c443562d567cdbff1a16c77ab98367dccd14249787e14176a38cb598777138a48677df65065882900504f41d1bf705cfb35d2ab0b68d96cbb07')
b2sums=('2ed50bd698d145fe6819c700688ee865be8d65c531ca20b3b02472191710142485f2b8b5b559798fe93304d2a281b96a6600449112f7e81b67b59d95e38971ab'
        'SKIP'
        '7b650b1217401a4a3a864e88910dfac36b320171413f39832cf20f081cdc2fad0cd1e3fe855efaa5f759f7dd63bbe8a808a017cc9631d200c06946cc052293f6')
validpgpkeys=(163EB50119225DB3DF8F49EA17ACBA8DFA970E17) # Richard Hughes <richard@hughsie.com>

prepare() {
    cd ${pkgname}-${pkgver}
    patch -p1 -i "$srcdir/fwupd-1.8.10-loong64.patch"
}

build() {
    arch-meson ${pkgname}-${pkgver} build \
        -D b_lto=false \
        -D docs=enabled \
        -D plugin_msr=disabled \
        -D plugin_intel_spi=false \
        -D supported_build=enabled \
        -D efi_binary=false

    meson compile -C build
}

check() {
    meson test -C build
}

package() {
    DESTDIR="${pkgdir}" meson install -C build
    # Fixup mode to match polkit
    install -d -o root -g 102 -m 750 "${pkgdir}"/usr/share/polkit-1/rules.d
    # Remove the tests
    rm -r "${pkgdir}"/usr/share/installed-tests/
    mv "${pkgdir}"/usr/bin/{,fwupd-}dbxtool
    mv "${pkgdir}"/usr/share/man/man1/{,fwupd-}dbxtool.1
    # Remove msr module-load config as it is built-in
#    rmdir "${pkgdir}"/usr/lib/modules-load.d
}
