# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Blaž Tomažič <blaz.tomazic@gmail.com>

pkgname=clamtk
pkgver=6.18
pkgrel=1
pkgdesc='Easy to use, light-weight, on-demand virus scanner for Linux systems'
url='https://gitlab.com/dave_m/clamtk/-/wikis/home'
arch=(any)
license=(
  GPL-1.0-or-later
  BSD-3-Clause
)
depends=(
  clamav
  cron
  perl
  perl-gtk3
  perl-http-message
  perl-json
  perl-libwww
  perl-locale-gettext
  perl-lwp-protocol-https
  perl-text-csv
  python
  zenity
)
source=(https://github.com/dave-theunsub/clamtk/releases/download/v${pkgver}/clamtk-${pkgver}.tar.xz{,.asc})
sha512sums=('f126b61e2493473ecb4df1adbc0583a844b9abfd9e0c62e8f82a5b635b72690c91cc1a44f70aca46a7987edd63713ec2a67ac634241a33e5d993abe77026c323'
            'SKIP')
b2sums=('f33a0799550978859706b94f10e9125654cdbfa6384f4929f4a87cecc4a8524b65098d13a50215ca5155105200c09e0e6655b1f75f6ef7f72f1cb40214879472'
        'SKIP')
validpgpkeys=(
  '5DD47B3B121EE5C354A20305F51D19546ADA59DE'  # Dave Mauroni <dave.nerd@gmail.com>
  'DC18B5EB12B82897DF0D0AAEC81DF0FAC4AFEB22'  # Dave M <dave.nerd@gmail.com>
)

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 clamtk -t "${pkgdir}/usr/bin"
  for f in lib/* ; do
    install -Dm 644 "${f}" "${pkgdir}/usr/share/perl5/vendor_perl/ClamTk/$(basename "${f}")"
  done

  for f in po/*.mo ; do
    install -Dm 644 "${f}" "${pkgdir}/usr/share/locale/$(basename "${f}" .mo)/LC_MESSAGES/clamtk.mo"
  done

  install -Dm 644 clamtk.1.gz -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 CHANGES DISCLAIMER.md README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  install -Dm 644 images/clamtk.{xpm,png} -t "${pkgdir}/usr/share/pixmaps"
  install -Dm 644 clamtk.desktop -t "${pkgdir}/usr/share/applications"
}

# vim: ts=2 sw=2 et:
