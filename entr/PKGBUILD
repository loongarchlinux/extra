# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=entr
pkgver=5.6
pkgrel=1
pkgdesc="Run arbitrary commands when files change"
url="http://eradman.com/entrproject"
arch=('loong64' 'x86_64')
license=('MIT')
depends=('glibc')
checkdepends=('procps-ng' 'git' 'vim' 'tmux')
source=("${pkgname}-${pkgver}::https://github.com/eradman/entr/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('9c65449b6fd7e172fde23a6d4d776b6fdbcec210ef54234db0af056e481acd11f53f9b25432a278435943bd998be734fcd997b5debdcace58112ac99d103f653')

build() {
	cd "${pkgname}-${pkgver}"
	export PREFIX='/usr'
	./configure
	make
}

check() {
	cd "${pkgname}-${pkgver}"
	TERM='xterm' make test
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
