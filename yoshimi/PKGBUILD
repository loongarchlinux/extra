# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgbase=yoshimi
pkgname=(yoshimi{,-{data,docs,lv2,standalone}})
pkgver=2.3.2
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=(loong64 x86_64)
url="https://yoshimi.github.io/"
_url="https://github.com/yoshimi/yoshimi"
license=(GPL-2.0-or-later)
makedepends=(
  alsa-lib
  cairo
  cmake
  fftw
  fltk
  jack
  lv2
  mxml
  ncurses
  readline
  zlib
)
checkdepends=(
  kxstudio-lv2-extensions
  lv2lint
)
# plugin exposes symbols globally if built with LTO:
# https://github.com/Yoshimi/yoshimi/issues/164
options=(!lto)
source=($pkgname-$pkgver.tar.gz::$_url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('6ddca4a5cffc609880fcbb625eca471b9926cb3cc0bef9f349ef3fafe6280f5e3957e62db0c103856c680d0a3dde0b15225569d7171553ae74ee4c78c848b598')
b2sums=('37a1c1ee9d07aba2b4fa22bbff200c16abf3437b6c4b48b53842be6a7b8e7999dd4bc294446d9b80b5a5a9e7485a5e3c3c73f2cf4029581e60f9644b705f47ee')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_BUILD_TYPE=None
    -Wno-dev
    -B build
    -S $pkgbase-$pkgver/src
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  cp -v build/LV2_Plugin/${pkgbase}_lv2.so $pkgbase-$pkgver/src/LV2_Plugin/
  lv2lint -Mpack -I $pkgbase-$pkgver/src/LV2_Plugin/ "http://yoshimi.sourceforge.net/lv2_plugin"
  rm -v $pkgbase-$pkgver/src/LV2_Plugin/${pkgbase}_lv2.so
  ctest --test-dir build --output-on-failure
}

package_yoshimi() {
  depends=($pkgbase-{data,docs,lv2,standalone})

  DESTDIR="$pkgdir" cmake --install build

  (
    cd "$pkgdir"
    _pick $pkgbase-data usr/share/$pkgbase/
    _pick $pkgbase-docs usr/share/doc/$pkgbase/
    _pick $pkgbase-lv2 usr/lib/lv2/
    _pick $pkgbase-standalone usr/bin/
    _pick $pkgbase-standalone usr/share/applications/
    _pick $pkgbase-standalone usr/share/icons/
    _pick $pkgbase-standalone usr/share/man/
    _pick $pkgbase-standalone usr/share/metainfo/
    _pick $pkgbase-standalone usr/share/pixmaps/
  )
}

package_yoshimi-data() {
  pkgdesc+=" - data files"

  mv -v $pkgname/* "$pkgdir"
}

package_yoshimi-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
  install -vDm 644 $pkgbase-$pkgver/{Changelog,Dependencies,README.txt,Yoshimi_Helpers} -t "$pkgdir/usr/share/doc/$pkgbase/"
}

package_yoshimi-lv2() {
  pkgdesc+=" - LV2 plugin"
  groups=(
    lv2-plugins
    pro-audio
  )
  depends=(
    cairo
    fftw
    fltk
    gcc-libs
    glibc
    lv2-host
    mxml
    yoshimi-data
    zlib
  )
  optdepends=('yoshimi-docs: for documentation')

  mv -v $pkgname/* "$pkgdir"
}

package_yoshimi-standalone() {
  pkgdesc+=" - standalone"
  groups=(pro-audio)
  depends=(
    alsa-lib
    cairo
    fftw
    fltk
    gcc-libs
    glibc
    hicolor-icon-theme
    jack
    mxml
    ncurses
    readline
    yoshimi-data
    zlib
  )
  optdepends=('yoshimi-docs: for documentation')

  mv -v $pkgname/* "$pkgdir"
}

# vim:set ts=2 sw=2 et:
