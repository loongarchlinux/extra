# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=blosc2
_name=c-blosc2
pkgver=2.15.0
pkgrel=1
pkgdesc="A fast, compressed, persistent binary data store library for C."
arch=(loong64 x86_64)
url="https://www.blosc.org"
license=(BSD-3-Clause)
depends=(glibc lz4 zlib zstd)
makedepends=(cmake)
source=(https://github.com/Blosc/$_name/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('9b28a933f3f639e8b5adcd7fa7917b683636dccfbb7ac94198ce90219b9655ac180fd36962719f2f546cf07d1c4ceb16da7469f7256d72287a045d0feacc8147')
b2sums=('1a6948a4a7815c10e22e4ee44cb05badadee9162e6e4adf60fee75eb1001a5f6d2a6922826ae49999908b80df7575014151151b91617ce38d744023e4e9f3bbe')

build() {
  cmake -B build -S $_name-$pkgver \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D PREFER_EXTERNAL_LZ4=ON \
    -D PREFER_EXTERNAL_ZLIB=ON \
    -D PREFER_EXTERNAL_ZSTD=ON \
    -W no-dev
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 $_name-$pkgver/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
