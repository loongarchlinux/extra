# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=tlsh
pkgname=(tlsh python-tlsh)
pkgver=4.11.2
pkgrel=1
pkgdesc='Fuzzy matching library that generates a hash value which can be used for similarity comparisons'
url='https://github.com/trendmicro/tlsh'
arch=(loong64 x86_64)
license=(Apache-2.0)
makedepends=(
  cmake
  python
  gcc-libs
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=(https://github.com/trendmicro/${pkgbase}/archive/refs/tags/${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('8df833535f981f4d472bc82837a82dfffb82dfd2a6daf6fb10b9bc947cadc62c')
sha512sums=('d38e4fc79dfb64ffc968bc75e31942310b4f3f0ab17cb08f6e867fb29b9c713d91821d6b18a508e1265dc1b366870a9973fc5205a1159598592b5798a0796d7c')

prepare() {
  cd ${pkgbase}-${pkgver}
  # remove garbage
  sed '/set(CMAKE_EXE_LINKER_FLAGS/d' -i CMakeLists.txt
  sed '/set(CMAKE_CXX_FLAGS/d' -i CMakeLists.txt
  sed 's|TLSH_SHARED_LIBRARY 0|TLSH_SHARED_LIBRARY 1|' -i src/CMakeLists.txt
}

build() {
  cd ${pkgbase}-${pkgver}
  mkdir -p build
  (cd build
    cmake .. \
      -DCMAKE_BUILD_TYPE=None \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr \
      -DCMAKE_CXX_FLAGS="${CXXFLAGS} ${CPPFLAGS}"
    make
  )
  (cd bin
    cmake -E create_symlink tlsh_unittest tlsh
  )
  (cd py_ext
    python -m build --wheel --no-isolation
  )
}

check() {
  cd ${pkgbase}-${pkgver}
  (cd build
    make test
  )
  (cd Testing
    ./test.sh
  )
}

package_tlsh() {
  depends=(
    glibc
    gcc-libs
  )
  provides=(libtlsh.so)
  cd ${pkgbase}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 755 bin/tlsh_unittest -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python-tlsh() {
  depends=(
    python
    glibc
    gcc-libs
  )
  cd ${pkgbase}-${pkgver}/py_ext
  python -m installer --destdir="${pkgdir}" dist/*.whl
}

# vim: ts=2 sw=2 et:
