# Contributor: Lars Boettcher <lars@newyew.de>
# Maintainer: Aaron Ali <t0nedef@causal.ca>

pkgname=klayout
pkgver=0.29.0
pkgrel=4
pkgdesc="High Performance Layout Viewer And Editor. Support of GDS and OASIS files."
arch=('loong64' 'x86_64')
url="https://www.klayout.de"
license=('GPL')
depends=('libgit2' 'qt5-base' 'qt5-tools' 'qt5-multimedia' 'qt5-xmlpatterns' 'qt5-svg' 'python'
         'ruby')
source=(http://www.klayout.org/downloads/source/klayout-$pkgver.tar.bz2
        klayoutEditor.desktop klayoutViewer.desktop)
sha512sums=('0f1b317e71edd25559e07de188cd93142348e4652bc85a90a21927befe7d6590c58744bc9f0aa2f30325f3770650354fd0dcabf1bc6955d95f478f488056ad4a'
            'bc6c7621e5cda754509a86664fb3e7ecd15f577dec712f4b0c52c9a7455ba692043a67145f5bf143d8fdc502b84e4212303906d62107537eefb4bebe57b2ffe1'
            '37a3bbb06ab155c1c22e3990cf160383570e8ea945dbf4964bc94b6fd4e6321627d31071a29d3dbeafc9e2fc406b6905761e299bfccdfe0a35663f2021624f4f')

build() {
  cd klayout-$pkgver
  ./build.sh
}

package() {
  install -D -m 644 klayoutEditor.desktop "$pkgdir"/usr/share/applications/klayoutEditor.desktop
  install -D -m 644 klayoutViewer.desktop "$pkgdir"/usr/share/applications/klayoutViewer.desktop
  cd klayout-$pkgver
  install -D -m 644 etc/logo.png "$pkgdir"/usr/share/icons/hicolor/32x32/apps/klayout.png
  install -D -m 755 bin-release/{klayout,strm*} -t "$pkgdir"/usr/bin/
  cp -ar bin-release "$pkgdir"/usr/lib/
  rm "$pkgdir"/usr/lib/{klayout,strm*}
}
