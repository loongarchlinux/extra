# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=2.28
pkgrel=4
pkgdesc='Tool for monitoring webpages for updates'
arch=(any)
url=https://thp.io/2008/urlwatch/
license=(BSD-3-Clause)
depends=(
  python-appdirs
  python-cssselect
  python-keyring
  python-lxml
  python-minidb
  python-requests
  python-yaml
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-docutils
  python-pytesseract
  python-pytest
)
optdepends=(
  'lynx: for one of the html2text filter methods'
  'python-beautifulsoup4: for the beautify filter and one of the html2text filter methods'
  'python-cssbeautifier: for the beautify filter'
  'python-html2text: for one of the html2text filter methods'
  'python-jq: for the jq filter'
  'python-jsbeautifier: for the beautify filter'
  'python-pdftotext: for the pdf2text filter'
  'python-playwright: for the browser job'
  'python-pytesseract: for the ocr filter'
)
source=("git+https://github.com/thp/urlwatch.git#tag=$pkgver")
sha256sums=('f2868ba625c560b801e519d6310c1624f11382d14cd040ef1aa4234e20d5b9b8')
b2sums=('7703127d0c4e96c31aaaf415ca16e12cac797508651fd28fa7c6eb18c2ca7e1b98a04da362c136eb520ba13a12a1bbaaa732c471e57eccdc799ad54edc1a0d87')

prepare() {
  cd $pkgname

  # Fix the CSS selector filter with python-lxml>=5
  # https://gitlab.archlinux.org/archlinux/packaging/packages/urlwatch/-/issues/1
  git cherry-pick -n 123de66d019aef7fc18fab6d56cc2a54d81fea3f || true
}

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname
  pytest -k 'not test_pep8_conformance'
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/COPYING \
    "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
