# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=loupe
pkgver=46.2
pkgrel=1
pkgdesc="A simple image viewer for GNOME"
arch=(loong64 x86_64)
url="https://gitlab.gnome.org/GNOME/loupe"
license=('GPL-3.0-or-later')
groups=('gnome')
depends=(
  cairo
  dconf
  gcc-libs
  glib2
  glibc
  glycin
  graphene
  gtk4
  hicolor-icon-theme
  lcms2
  libadwaita
  libgweather-4
  libseccomp
)
makedepends=(
  git
  itstool
  meson
  rust
)
source=("git+$url.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('3b217f7cfcc3adadf41f0babd90b7f99c6916e384d77580431f3b945d8549bf99cad82c87a14d18f0c7095a928dab13bcb6dea42383e8a7d10ca0aab69161d0a')
validpgpkeys=(
  4587A0EE1EE5478AAB82C0A93BA28A5559F08EBD # Sophie Herold <sophieherold@gnome.org>
)

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
