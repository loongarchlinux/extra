# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>

pkgname=noise-suppression-for-voice
pkgver=1.10
pkgrel=1
pkgdesc='A real-time noise suppression plugin for voice'
arch=(loong64 x86_64)
url='https://github.com/werman/noise-suppression-for-voice'
license=(GPL-3.0-only)
depends=(freetype2
         gcc-libs
         glibc)
makedepends=(cmake
             git
             libx11
             libxcursor
             libxinerama
             libxrandr)
source=(git+https://github.com/werman/noise-suppression-for-voice#tag=v$pkgver)
sha256sums=('bf7eb24d7e049a2df4d01be189d8dc82cf4f8ec8a0499d275167415d4889b4c9')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
