# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: arkhan <arkhan@disroot.org>

pkgname=ueberzug
pkgver=18.2.2
pkgrel=2
pkgdesc='Command line util which allows to display images in combination with X11'
arch=('loong64' 'x86_64')
url=https://github.com/ueber-devel/ueberzug
license=('GPL3')
depends=('libxext' 'libxres' 'python-attrs' 'python-docopt' 'python-pillow')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
provides=("python-$pkgname")
replaces=("python-$pkgname")
source=("https://github.com/ueber-devel/ueberzug/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('97bb0206bff918d4cfd7869807b79474bac9432fc3fcbeb1d5641a45e517aa01')
b2sums=('885991f3400fc039d652443cb866a6743530e6f928e74a4e008980455e6780425574a36865286a11b12d967cc2698c5deb263d54cb5332dc17920855a1f3bf2a')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
