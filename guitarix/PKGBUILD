# Maintainer: David Runge <dvzrv@archlinux.org

pkgname=guitarix
pkgver=0.46.0
pkgrel=1
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=(loong64 x86_64)
url="https://guitarix.org"
license=(
  GPL-2.0-or-later
  GPL-3.0-or-later
  LGPL-3.0-or-later
)
groups=(
  lv2-plugins
  pro-audio
)
depends=(
  atkmm
  bluez-libs
  cairo
  cairomm
  gcc-libs
  glibc
  glibmm
  gtkmm3
  libsigc++
  libx11
  pangomm
  ttf-roboto
)
makedepends=(
  avahi
  boost
  curl
  gdk-pixbuf2
  eigen
  faust
  fftw
  glib2
  gperf
  gtk3
  intltool
  jack
  liblo
  liblrdf
  libsndfile
  libx11
  lilv
  lv2
  pango
  sassc
  waf
  zita-convolver
  zita-resampler
)
optdepends=('new-session-manager: for session management')
provides=(
  guitarix2 libgxw.so libgxwmm.so
  lv2-host
)
conflicts=(guitarix2)
replaces=(guitarix2)
source=(
  $pkgname-$pkgver.tar.xz::https://github.com/brummer10/guitarix/releases/download/V$pkgver/${pkgname}2-$pkgver.tar.xz
  $pkgname-0.46.0-waf_libx11.patch::https://github.com/brummer10/guitarix/commit/af86767f3196eeb153fd62e76c61ddf7881b8959.patch
)
sha512sums=('cdb8eccdd230bade493cf0b7fd2a6e748fcaf3c3fd6b5ed5fbd6134d08be9518c93400625d63e386b436fcef3e1738cae543256b09941445436d7f2bf41649a0'
            '6b0632a7eccce88ee001d31b0019e432366f6aeb90d3683441e65fd1f04f92301bc5117450b35cf7cd3c66b350802f8ff334a42cc7b314481d20410aad5d363e')
b2sums=('b650d53f4e73539419bbe194e07ee770494f80b547d407f5a269c035b91fbf3e0fe078a9e82da21f773d69395523f70fc179be7942f08e2b8689b220414c9925'
        'd96d726be2d986a91285c0087e147f6cba646caf6dd765ca52a769910c132d862c01eed5744250be2bdfa7e0856463108410fdb69556a27760074c18bfae86f9')

prepare() {
  # fix issue with older waf: https://github.com/brummer10/guitarix/issues/151
  patch -Np2 -d $pkgname-$pkgver -i ../$pkgname-0.46.0-waf_libx11.patch
}

build() {
  local waf_options=(
    --prefix=/usr
    --enable-nls
    --shared-lib
    --lib-dev
    --cxxflags='-flto'
    --ldflags="$LDFLAGS"
  )

  export LINKFLAGS="$LDFLAGS"

  cd $pkgname-$pkgver
  waf configure "${waf_options[@]}"
  waf build -vv
}

package() {
  depends+=(
    avahi libavahi-common.so libavahi-gobject.so
    boost-libs libboost_iostreams.so
    curl libcurl.so
    fftw libfftw3f.so
    gdk-pixbuf2 libgdk_pixbuf-2.0.so
    glib2 libgio-2.0.so libglib-2.0.so libgobject-2.0.so
    gtk3 libgdk-3.so
    jack libjack.so
    liblo liblo.so
    liblrdf liblrdf.so
    lilv liblilv-0.so
    pango libpangocairo-1.0.so libpango-1.0.so
    libsndfile libsndfile.so
    zita-convolver libzita-convolver.so
    zita-resampler libzita-resampler.so
  )

  cd $pkgname-$pkgver
  waf install --destdir="$pkgdir"
  # docs
  install -vDm 644 {changelog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
