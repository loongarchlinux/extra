# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libmediaart
pkgver=1.9.6
pkgrel=3
pkgdesc='Library tasked with managing, extracting and handling media art caches'
arch=('loong64' 'x86_64')
url='https://gitlab.gnome.org/GNOME/libmediaart'
license=('LGPL-2.1-or-later')
depends=('gdk-pixbuf2' 'glib2' 'glibc')
makedepends=('git' 'glib2-devel' 'gobject-introspection' 'gtk-doc' 'meson' 'vala')
source=("git+https://gitlab.gnome.org/GNOME/$pkgname.git#tag=$pkgver")
b2sums=('e5186df01967337cacdefe2e4035145bef47202f6eb58cd03b7fe49f6adcbfc0505c4f1f89305600872e5403cae7a22fdc47c6e3ea246daa0cc25593a5d30fbb')

build() {
  arch-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
