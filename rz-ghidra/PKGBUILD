# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=rz-ghidra
pkgver=0.7.0
pkgrel=2
pkgdesc='Deep ghidra decompiler integration for rizin and rz-cutter'
url='https://github.com/rizinorg/rz-ghidra'
arch=('loong64' 'x86_64')
license=('LGPL-3.0-only')
depends=('rizin' 'glibc')
makedepends=('git' 'cmake' 'rz-cutter')
source=("git+${url}#tag=v${pkgver}"
        rizin-ghidra::git+https://github.com/rizinorg/ghidra.git
        git+https://github.com/zeux/pugixml.git)
sha512sums=('55d9f1dd661bd3501e32c2c5349e378a8e4fd2006563188e5d18c8d53378abdcde280a635319681dcea2701a857466b3e1623e64b5cf4b078b319aee90916a60'
            'SKIP'
            'SKIP')

pkgver() {
  cd ${pkgname}
  git describe --tags --match 'rz-*' | sed 's/^rz-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${pkgname}
  git submodule init
  git config submodule."third-party/pugixml".url "${srcdir}/pugixml"
  git config submodule."ghidra/ghidra".url "${srcdir}/rizin-ghidra"
  git -c protocol.file.allow=always submodule update --recursive
}

build() {
  cd ${pkgname}
  cmake \
    -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_CUTTER_PLUGIN=ON \
    -DBUILD_SLASPECS=ON \
    -DCUTTER_INSTALL_PLUGDIR=/usr/share/rizin/cutter/plugins/native
  make -C build VERBOSE=1
}

package() {
  cd ${pkgname}
  make -C build -j1 DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
