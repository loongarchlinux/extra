# Maintainer: Campbell Jones <serebit at archlinux dot org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
pkgver=10.9.1
pkgrel=2
pkgdesc='A familiar, modern desktop environment'
arch=('loong64' 'x86_64')
url='https://github.com/BuddiesOfBudgie/budgie-desktop'
license=('GPL-2.0-only AND LGPL-2.1-only')
groups=('budgie')
depends=(
    'accountsservice'
    'budgie-control-center'
    'budgie-screensaver' 
    'budgie-session'
    'gnome-themes-standard'
    'ibus'
    'libgee'
    'libpeas'
    'libwnck3'
    'libxfce4windowing'
    'magpie-wm'
    'xdg-desktop-portal-gtk'
    'zenity'
)
makedepends=(
    'gobject-introspection'
    'intltool'
    'meson'
    'sassc'
    'vala'
)
optdepends=(
    'budgie-desktop-view: Desktop icons'
    'budgie-backgrounds: Default background set'
    'network-manager-applet: Network management in the panel'
    'power-profiles-daemon: Manage power and performance from the panel'
    'switcheroo-control: Open apps with the secondary GPU'
)
source=("https://github.com/BuddiesOfBudgie/$pkgname/releases/download/v$pkgver/$pkgname-v$pkgver.tar.xz"{,.asc}
        "30_org.archlinux.budgie-desktop.gschema.override"
        "a040ccb96094f1d3a1ee81a6733c9434722bdf6c.patch")
validpgpkeys=('0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B') # Joshua Strobl <me at joshuastrobl dot com>
b2sums=('444ceb9f8b226f6d95ebbd1f625c82f913a0d0ff2d5de1a07ddd23564e6e4a83a2d801506897860f01b209deb1b952a5e363e8cdef31965d06c9e3b2bb5f104e'
        'SKIP'
        '8b2e5dce5b93af93b1843f5f81b7496653aca5d536e3b77ae143749e8395a8158b044406f75c923f7c78416348ed4880521a6a5e67668352b8616124ceb08ce3'
        '0d0cd8323e5250401d113e5bd307deaa8998c844d0a93bad910f5369e5175a62459326c9bc722259d51449e4df03ccb2ec748e7bbdfd5251b6cfbfb443fc04fd')

prepare() {
    cd "$pkgname-$pkgver"
    patch -Np1 < "$srcdir/a040ccb96094f1d3a1ee81a6733c9434722bdf6c.patch"
}

build() {
    arch-meson "$pkgname-$pkgver" build
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
    install -Dm 644 30_org.archlinux.budgie-desktop.gschema.override -t "${pkgdir}/usr/share/glib-2.0/schemas"
}
