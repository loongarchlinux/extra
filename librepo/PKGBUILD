# Contributor: larchunix
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=librepo
pkgver=1.18.0
pkgrel=1
pkgdesc="Repodata downloading library"
arch=('loong64' 'x86_64')
url="https://github.com/rpm-software-management/$pkgname"
license=('GPL')
depends=('curl' 'glib2' 'gpgme' 'libxml2' 'openssl')
makedepends=('cmake' 'python')
checkdepends=('check' 'python-gpgme' 'python-pyxattr' 'python-requests')
optdepends=('python: for python bindings')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz" "gpgme-config.patch")
sha256sums=('c6f531df6d148d29d192f25383c13cbe0cfcbba4c8046513f0d660bbfcb9ab1b'
            'c0d08e34a0a02613036d1113fd0257a97e1896284afde7f8417f49abb34b2278')

prepare() {
  cd "$pkgname-$pkgver"
  # https://github.com/rpm-software-management/librepo/issues/269
  patch -Np1 -i ${srcdir}/gpgme-config.patch
}

build() {
  cd "$pkgname-$pkgver"

  cmake -B build \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_C_FLAGS_RELEASE='-DNDEBUG' \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DENABLE_DOCS=OFF \
        -DENABLE_PYTHON=ON \
        -DWITH_ZCHUNK=OFF \
	-DENABLE_SELINUX=off

  make -C build
}

check() {
  cd "$pkgname-$pkgver"

  make -C build ARGS="-V" test
}

package() {
  cd "$pkgname-$pkgver"

  make -C build DESTDIR="$pkgdir/" install

  install -Dp -m644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
