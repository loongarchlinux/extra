# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: graysky <graysky AT archlinux dot us>

pkgname=lostfiles
pkgver=4.14
pkgrel=1
pkgdesc="Find orphaned files not owned by any Arch packages"
arch=(any)
url="https://github.com/graysky2/lostfiles"
license=(MIT)
backup=(etc/lostfiles.conf)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0d479b7085d7fb5ef482978b10eeeea60ea063d80ebc3f7c3072b8701b5bf67d')

build() {
  cd ${pkgname}-${pkgver}
  make 
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm0644 MIT "${pkgdir}/usr/share/licenses/${pkgname}/MIT"
}
