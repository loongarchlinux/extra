# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Marco A Rojas <marco.rojas@zentek.com.mx>

pkgname=dbmate
pkgver=2.19.0
pkgrel=1
pkgdesc='A lightweight, framework-agnostic database migration tool'
arch=('loong64' 'x86_64')
url='https://github.com/amacneil/dbmate'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
optdepends=(
  'mariadb: for local MariaDB instance'
  'postgresql: for local PostgreSQL instance'
  'sqlite: for local SQLite instance'
)
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('447e21874d6a352bf74e36d1160e8aa4280907752406f4cc690657984d6aebc1883daaf6f159d246c5797ce782463a1c9ad43398821a290a2ad56869b8b61f3a')
b2sums=('4c056dced9c793faf1c88348fcc4c928de57e3d5a827074ff5efbdbc0976ce1d6689f49bb8ffa548904b1fd14fb326b9f27eefd68c43a8d5ae473a8f36759b1a')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "build/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
