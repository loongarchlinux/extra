# Maintainer: David Runge <dvzrv@archlinux.org>

_name=crc32c
pkgname=python-crc32c
pkgver=2.4.1
pkgrel=1
pkgdesc="A python package implementing the crc32c algorithm in hardware and software"
arch=(loong64 x86_64)
url="https://github.com/ICRAR/crc32c"
license=(
  BSD-3-Clause
  LGPL-2.1-or-later
)
depends=(
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('63469d7a0df72171a0956be1fade202bed75003d852b0ccece19949ca67126bb29c180561937982d86aa415fc38181bad94f31b0d68a37a86190a9d8c2c523c5')
b2sums=('45c880debd530664c930b3589a232e4665fe29cbdfbcc008a3d6b5e1853add0952b479f682ef77bd1b0a7a25d7b7fb0a09b1cebcd87ac4c2417d317a7c3f1eb7')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.* -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
