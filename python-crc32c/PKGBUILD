# Maintainer: David Runge <dvzrv@archlinux.org>

_name=crc32c
pkgname=python-crc32c
pkgver=2.4
pkgrel=2
pkgdesc="A python package implementing the crc32c algorithm in hardware and software"
arch=(loong64 x86_64)
url="https://github.com/ICRAR/crc32c"
license=(
  BSD-3-Clause
  LGPL-2.1-or-later
)
depends=(
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('ea18259a72b51af91f16a191bda30c2eaeee50404579001fa78082f7a472b1bdb2200ab4fd05620c9fe37dbe20ef047588801ddf3182c33b55091dff800e7076')
b2sums=('6772fb98c7ad8885c1583d7c935432e828671ab7e4d43774151777494a2c553cb11a414b32ce09243d645a217f115cf4cb9b292cdea9a33ba6ed5cd9199eef15')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.* -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
