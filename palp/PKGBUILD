# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=palp
epoch=1
pkgver=2.21
pkgrel=2
pkgdesc='A Package for analyzing Lattice Polytopes'
arch=(loong64 x86_64)
url='http://hep.itp.tuwien.ac.at/~kreuzer/CY/CYpalp.html'
license=(GPL-3.0-only)
depends=(glibc)
source=(https://gitlab.com/stringstuwien/PALP/-/archive/v$pkgver/PALP-v$pkgver.tar.gz)
sha256sums=('8cb5cb9289b2ac88618c792f7720de1a3c08b4b43da7393553d6b68cc3548711')

build() {
  cd PALP-v$pkgver
  make all-dims
}

package() {
  cd PALP-v$pkgver
  for _exe in *.x; do
    install -Dm755 $_exe -t "$pkgdir"/usr/bin
  done
  for _file in poly class cws nef mori; do
    ln -sf $_file-6d.x "$pkgdir"/usr/bin/$_file.x
  done
}
