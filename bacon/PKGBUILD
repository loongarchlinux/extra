# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jokler <aur@jokler.xyz>

pkgname=bacon
pkgver=2.18.2
pkgrel=1
pkgdesc='A background rust code checker'
arch=('loong64' 'x86_64')
url='https://dystroy.org/bacon'
license=('AGPL-3.0-only')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'rust')
options=('!lto')
source=("$pkgname::git+https://github.com/Canop/bacon#tag=v$pkgver")
sha512sums=('7951c797e7554dd3ca6aa9725728c9254cbf9785d0dcacae8fcdebaf75c817ae3d0427d4be420d074953f7eaace53d99f60550da9e5b056851ac762ef913ca8b')
b2sums=('d4f6926565fa4d38154ddfb0ba9145777d72fbaf7e7a9a4b7dea3ac990dce56511e212f2ed4f413d67874ab8f16b10b62cfd65fb3e8ac13ead98ee6ef9f83d17')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr doc img "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
