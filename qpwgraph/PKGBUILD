# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>

pkgname=qpwgraph
pkgver=0.7.5
pkgrel=1
pkgdesc='PipeWire Graph Qt GUI Interface'
arch=(loong64 x86_64)
url="https://gitlab.freedesktop.org/rncbc/qpwgraph"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  pipewire
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('08d00fe043ede66e37826ada199ea75949304713eb77c067dbc2555c9a355e6fec5ea8ec97c9dd49372687b4504795e37439203031cffa150422999d5284d135')
b2sums=('30371b807a60371349e1a14c3824cb3df0d08940a98ec4522fa0d382e86429d256929c757795d031d5e9e84eb49f903d7ebe8c53711cdd02acedcd6c4891cc90')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_WAYLAND=ON
    -S $pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  depends+=(
    alsa-lib libasound.so
    libpipewire libpipewire-0.3.so
  )
  DESTDIR="$pkgdir" cmake --install build
}
