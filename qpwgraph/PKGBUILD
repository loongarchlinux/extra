# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Witschel <diabonas@archlinux.org>

pkgname=qpwgraph
pkgver=0.7.1
pkgrel=1
pkgdesc='PipeWire Graph Qt GUI Interface'
arch=(loong64 x86_64)
url="https://gitlab.freedesktop.org/rncbc/qpwgraph"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  alsa-lib
  cmake
  pipewire
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('e8ccb19bbc0e4c70d379bc3d1c9bfc7cd167189cbcd86d4dd9d3a09225e684de3a8d6c236050178ba5c5e0143b3bebd3b444e4e1d2ff0dab09e14480f5f329d1')
b2sums=('78b3b0c52b60e69d22cf0e37a99fcbc07b70abd7f7e085bcea1bac58daf09ce5599d67d5db80578222096dd00a48cc80bbcd78a9473696aa73104caba8e34527')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_WAYLAND=ON
    -S $pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

package() {
  depends+=(
    alsa-lib libasound.so
    libpipewire libpipewire-0.3.so
  )
  DESTDIR="$pkgdir" cmake --install build
}
