# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx-sitemap
pkgname=python-sphinx-sitemap
pkgver=2.5.1
pkgrel=2
pkgdesc="Sphinx extension to generate a multi-lingual, multi-version sitemap for HTML builds"
arch=(any)
url="https://github.com/jdillard/sphinx-sitemap"
license=(MIT)
depends=(
  python
  python-sphinx
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('9a470d0717f69f01178c2bc0ef8fc825824a534ceb37f1d0aec8feb36aea8c5d3fe92a3d60e34d2e4f30bc0318f497abfdb6221de42418d935da7563caeaca48')
b2sums=('390050befcd3295d7d9e63495bc5095eb18c9d510db834fb221a796d28d6b8944aa8b7935c3f3018eb9ae59f6b8aba47703ae2af2cc8b9ed1ebb144fe093736a')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGELOG,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
