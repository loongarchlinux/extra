# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=sequoia-sop
pkgver=0.32.0
pkgrel=1
pkgdesc='Implementation of the Stateless OpenPGP Interface using Sequoia'
url='https://gitlab.com/sequoia-pgp/sequoia-sop'
arch=('loong64' 'x86_64')
license=('GPL-2.0-or-later')
groups=(
  'sequoia'
  'stateless-openpgp'
)
depends=(
  'glibc'
  'gcc-libs'
  'gmp'
)
makedepends=(
  'bzip2'
  'git'
  'cargo'
  'clang'
  'nettle'
)
source=(sequoia-sop::"git+$url#tag=v$pkgver?signed")
sha512sums=('25cbc8dc083bccd29c81121e0d8a29f5132936825b7c60c7e059a5928918d572624a7cfa8a2e01b24a6fe6acd3698948fd93d2eb483d8c5982b598dd01df4703')
validpgpkeys=(
  CBCD8F030588653EEDD7E2659B7DD433F254904A  # Justus Winter <justus@sequoia-pgp.org>
)

prepare() {
  cd ${pkgname}
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cd ${pkgname}
  cargo build --release --frozen --all-features
}

check() {
  export RUSTUP_TOOLCHAIN=stable
  cd ${pkgname}
  cargo test --release --frozen --all-features
}

package() {
  depends+=(
    'bzip2' 'libbz2.so'
    'nettle' 'libnettle.so' 'libhogweed.so'
  )

  cd ${pkgname}
  install -Dm 755 target/release/sqop -t "${pkgdir}/usr/bin"
  install -Dm 644 man-sqop/*.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
