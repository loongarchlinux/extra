# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=sequoia-sop
pkgver=0.34.0
pkgrel=1
pkgdesc='Implementation of the Stateless OpenPGP Interface using Sequoia'
url='https://gitlab.com/sequoia-pgp/sequoia-sop'
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
groups=(
  sequoia
  stateless-openpgp
)
depends=(
  glibc
  gcc-libs
  gmp
)
makedepends=(
  bzip2
  git
  cargo
  clang
  nettle
)
source=(sequoia-sop::"git+$url#tag=v$pkgver?signed")
sha512sums=('99689bbeb241f0148df44ede5bacc6d018a25300bacb6b38bed6d268d014a2cc8d74de2c83aa1775c383e1df0f68367ba146f764c8ad2169e00dfdef149b3d84')
b2sums=('d1cb8fed12b92101ed8a560730934e985bc95f3c7f410baa83c0d44842044f0d1a2edf5c734892ca4167ed9b95ba33e02cc4141eab8a6edc206bc96fa2691394')
validpgpkeys=(
  CBCD8F030588653EEDD7E2659B7DD433F254904A  # Justus Winter <justus@sequoia-pgp.org>
)

prepare() {
  cd ${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cd ${pkgname}
  cargo build --release --frozen --all-features
}

check() {
  export RUSTUP_TOOLCHAIN=stable
  cd ${pkgname}
  cargo test --frozen --all-features
}

package() {
  depends+=(
    bzip2 libbz2.so
    nettle libnettle.so libhogweed.so
  )

  cd ${pkgname}
  install -Dm 755 target/release/sqop -t "${pkgdir}/usr/bin"
  install -Dm 644 man-sqop/*.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
