# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-videos
pkgver=8.0.0
pkgrel=1
pkgdesc='The Pantheon Video Player'
arch=(loong64 x86_64)
url='https://github.com/elementary/videos'
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gst-plugins-base-libs
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita-1.so
  libgee
  libgranite-7.so
)
makedepends=(
  cmake
  git
  intltool
  meson
  vala
)
optdepends=(
  'gst-plugins-base: "Base" plugin libraries'
  'gst-plugins-good: "Good" plugin libraries'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
)
_tag=627bf9be24f2bef9cf7648065746b27dc9fa9769
source=(pantheon-videos::git+https://github.com/elementary/videos.git#tag=${_tag})
sha256sums=('a2a68138084346aa1c1fd0ee6aa66acc046007d80114410a284d645de52217be')

pkgver() {
  cd pantheon-videos
  git describe --tags
}

build() {
  arch-meson pantheon-videos build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
