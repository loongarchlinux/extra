# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: robertfoster
pkgname=rnnoise
epoch=1
pkgver=0.2
pkgrel=1
pkgdesc="A noise suppression library based on a recurrent neural network"
arch=(loong64 x86_64)
url="https://gitlab.xiph.org/xiph/rnnoise"
license=('BSD-3-Clause')
depends=('glibc')
makedepends=('wget')
source=("https://gitlab.xiph.org/xiph/rnnoise/-/archive/v${pkgver}/rnnoise-v${pkgver}.tar.gz")
sha512sums=('930aa892299edbc1d512803df6b845ea6164eb498cacdab9970e5ae799bc6cf3c8c94d2b9576955fb9a2d8aa13a6d255e58fb99d0367a0d0ef842a1cb938e674')

build() {
  cd "rnnoise-v${pkgver}"
  ./autogen.sh
  ./configure --prefix=/usr --enable-x86-rtcd
  make
}

package() {
  cd "rnnoise-v${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
