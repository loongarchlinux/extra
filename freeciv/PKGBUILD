# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=freeciv
pkgver=3.1.2
pkgrel=1
pkgdesc="A multiuser clone of the famous Microprose game of Civilization"
arch=('loong64' 'x86_64')
url="http://www.freeciv.org/"
license=('GPL-2.0-or-later')
depends=(
  'bzip2'
  'cairo'
  'curl'
  'gdk-pixbuf2'
  'glib2'
  'glibc'
  'gtk3'
  'hicolor-icon-theme'
  'icu'
  'libtool'
  'lua'
  'pango'
  'readline'
  'sdl_mixer'
  'sqlite'
  'xz'
  'zlib'
  'zstd'
)
makedepends=('python')
backup=('etc/freeciv/database.lua')
source=("https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('461ee77a4aaa1b18bdaf3bf27e7e2a2cb0ab98703b0ddd353c894c74b7f1cc591ad3386c2d2c3c9fc6d96c640ce51640e97f372cdea422bed2cc083a0801199b')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-shared\
    --enable-sys-lua \
    --enable-client=gtk3 \
    --enable-fcmp=gtk3 \
    --enable-fcdb=sqlite3 \
    --enable-aimodules
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
