# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Peter Simons <simons@cryp.to>

pkgname=shhmsg
pkgver=1.4.2
pkgrel=6
pkgdesc="Library for displaying messages"
arch=('loong64' 'x86_64')
url="https://shh.thathost.com/pub-unix"
license=('PerlArtistic')
options=('staticlibs')
source=($url/files/$pkgname-$pkgver.tar.gz)
sha256sums=('88c69e3f0b920b1ef93f6c10f354786f171d7cb3ab170a463bb9ab8bbf13a02b')

build() {
  cd $pkgname-$pkgver

  CFLAGS+=' -ffat-lto-objects'
  make OPTIM="$CFLAGS" dep
  make OPTIM="$CFLAGS"
}

package() {
  cd $pkgname-$pkgver

  make OPTIM="$CFLAGS" INSTBASEDIR="$pkgdir"/usr/ install

  for i in ChangeLog CREDITS INSTALL README; do
    install -Dm644 $i "$pkgdir"/usr/share/doc/$pkgname/$i
  done
}
