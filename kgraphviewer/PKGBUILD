# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kgraphviewer
pkgver=2.4.3
pkgrel=5
pkgdesc='A Graphviz dot graph file viewer'
arch=(loong64 x86_64)
url='https://www.kde.org/applications/graphics/kgraphviewer'
license=(GPL FDL)
depends=(gcc-libs
         glibc
         graphviz
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         ki18n5
         kiconthemes5
         kparts5
         kwidgetsaddons5
         kxmlgui5
         qt5-base
         qt5-svg)
makedepends=(boost
             extra-cmake-modules
             kdoctools5)
source=(https://download.kde.org/stable/kgraphviewer/$pkgver/kgraphviewer-$pkgver.tar.xz{,.sig})
sha256sums=('54e0c218453912ea82ad9a96edfb2d4dfd8cb15f6a567ef843b4ef97d07bd7c0'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E') # Jonathan Riddell <jr@jriddell.org>

build(){
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
