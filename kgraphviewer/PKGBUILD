# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kgraphviewer
pkgver=2.5.0
pkgrel=1
pkgdesc='A Graphviz dot graph file viewer'
arch=(loong64 x86_64)
url='https://apps.kde.org/kgraphviewer/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         graphviz
         kconfig
         kconfigwidgets
         kcoreaddons
         ki18n
         kparts
         kwidgetsaddons
         kxmlgui
         qt6-5compat
         qt6-base
         qt6-svg)
makedepends=(boost
             extra-cmake-modules
             kdoctools)
source=(https://download.kde.org/stable/kgraphviewer/$pkgver/kgraphviewer-$pkgver.tar.xz{,.sig})
sha256sums=('872bee63fb4df6f7fb2b4eaf02ff825cba3ca953ac02509a287fe5cd0f1e2b69'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              'D81C0CB38EB725EF6691C385BB463350D6EF31EF') # Heiko Becker <heiko.becker@kde.org>

build(){
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
