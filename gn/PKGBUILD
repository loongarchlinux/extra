# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=gn
pkgver=0.2174.b3a0bff4
_commit=b3a0bff47dd81073bfe67a402971bad92e4f2423
pkgrel=1
pkgdesc="Meta-build system that generates build files for Ninja"
arch=('loong64' 'x86_64')
url="https://gn.googlesource.com/gn/"
license=('BSD')
depends=('gcc-libs')
makedepends=('clang' 'ninja' 'python' 'git')
source=(git+https://gn.googlesource.com/gn#commit=$_commit
gn-fix-build.patch)
sha256sums=('63edc92709366f6df536fe73b4c3c10390c2507258ab863215fd1f205f560abc'
            '072db93d2c4a1486a5a70aa7a17e562e3478bbff5c2d5b219729ddda630bb31e')

pkgver() {
  cd $pkgname
  echo 0.$(git rev-list --count initial-commit..).$(git rev-parse --short=8 HEAD)
}

prepare() {
  cd $pkgname
  patch -p1 -i $srcdir/gn-fix-build.patch
}

build() {
  cd $pkgname
# clang didn't support -mlsx
  CFLAGS=${CFLAGS/-mlsx /}
  CXXFLAGS=${CXXFLAGS/-mlsx /}
  CFLAGS=${CFLAGS/-fstack-clash-protection/}
  CXXFLAGS=${CXXFLAGS/-fstack-clash-protection/}
  ./build/gen.py
  ninja -C out
}

check() {
  cd $pkgname
  ./out/gn_unittests
}

package() {
  cd $pkgname
  install -D out/gn "$pkgdir/usr/bin/gn"
  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" docs/*
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  mkdir -p "$pkgdir/usr/share/vim/vimfiles"
  cp -r misc/vim/{autoload,ftdetect,ftplugin,syntax} \
    "$pkgdir/usr/share/vim/vimfiles/"
  install -Dm644 -t "$pkgdir/usr/share/emacs/site-lisp" misc/emacs/gn-mode.el
}

# vim:set ts=2 sw=2 et:
