# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-datetime
pkgver=2.4.1
pkgrel=4
pkgdesc='Date & Time indicator for Wingpanel'
arch=(loong64 x86_64)
url=https://github.com/elementary/wingpanel-indicator-datetime
license=(GPL-3.0-or-later)
groups=(pantheon)
depends=(
  cairo
  evolution-data-server
  glib2
  gtk3
  libgee
  libgranite.so
  libhandy-1.so
  libical
  wingpanel
)
makedepends=(
  git
  meson
  vala
)
_tag=80b746494c9a36ea4b6fc3de3a140716e17abbd9
source=(git+https://github.com/elementary/wingpanel-indicator-datetime.git#tag=${_tag})
b2sums=('50820ddaf583d7d54f27cfd76343ebfab9eeb970834c4b8ac8f3e9348487a784d2d28d29b97881d526e2e9f2eb8756c6b4522c940258f4b09c0372762d500877')

pkgver() {
  cd wingpanel-indicator-datetime
  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-datetime build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
