# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>
# Contributor: kpcyrd <git@rxv.cc>

pkgname=websocat
pkgver=1.13.0
pkgrel=1
pkgdesc='Command-line client for web sockets, like netcat/curl/socat for ws://'
url='https://github.com/vi/websocat/'
arch=(loong64 x86_64)
license=(MIT)
depends=(
  glibc
  openssl
  gcc-libs
)
makedepends=(
  cargo
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('119cb6d0c226ed4f1df86a42a26903ba8465db30e5d0908d4a28601a58636e9a906fbb44d2d811ec9b25103a8c48b4ccea8b31f2f61854d062bc436ac6afac78')
b2sums=('287e869538246d9d5c62080aeb1627b0e8155585553800c0d639e2a8bebf9b4d65e3df35ff424c48076cdccbe79b54a15119793ae22cf68d1ce1774c0e795236')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --frozen --release --features=ssl
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen --features=ssl
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release/websocat -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md doc.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
