# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-uvloop
pkgver=0.19.0
pkgrel=5
pkgdesc='Ultra fast asyncio event loop'
arch=(loong64 x86_64)
url=https://github.com/MagicStack/uvloop
license=(
  APACHE
  MIT
)
depends=(
  glibc
  python
  libuv
)
makedepends=(
  cython
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=6c770dc3fbdd281d15c2ad46588c139696f9269c
source=(git+https://github.com/MagicStack/uvloop.git#tag=${_tag}
        cython3.patch)
b2sums=('56c45089a5cdae9bd25adb75357ae748ebb60e19cd26ce11b24bcdcb8c700c52a881959cb0aeaeb2c2d65c2667764a2a8f4ef521ab24c8b75d523d4c43ae1593'
        'dac5056392d3851d8a2fd7b093f1a08b577f1579ae032f33c38be1eb23415d99e9b98c6b61bfc419d183282bf0b6f2a564cb55968a16bf685346ce8f1c5b2884')

prepare() {
  sed 's/self.use_system_libuv = False/self.use_system_libuv = True/' -i uvloop/setup.py
  sed -e 's|>=0.29.36,<0.30.0|>=0.29.36|g' -i uvloop/pyproject.toml
  patch -d uvloop -p1 < cython3.patch
}

pkgver() {
  cd uvloop
  git describe --tags | sed 's/^v//'
}

build() {
  cd uvloop
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" uvloop/dist/*.whl
  install -Dm 644 uvloop/LICENSE-APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-APACHE
  install -Dm 644 uvloop/LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
