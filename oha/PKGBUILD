# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=oha
pkgver=1.3.0
pkgrel=1
pkgdesc="Ohayou(おはよう), HTTP load generator, inspired by rakyll/hey with tui animation"
arch=('loong64' 'x86_64')
url="https://github.com/hatoo/oha"
license=('MIT')
depends=('openssl' 'gcc-libs' 'glibc')
makedepends=('rust')
options=('!lto')
source=($pkgname-$pkgver.tar.gz::https://github.com/hatoo/oha/archive/refs/tags/v${pkgver}.tar.gz)
sha512sums=('88f886992e78a8c22051a374825c29653c67422ac8975dc5e8abfaa7f655698db712763ef3b1124544f75bedd3485e97517577847139e7c093f9485c63800002')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --release --locked
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo test --release --locked
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 target/release/oha "$pkgdir"/usr/bin/oha
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
