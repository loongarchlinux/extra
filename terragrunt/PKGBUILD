# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Benjamin Denhartog <ben@sudoforge.com>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=terragrunt
pkgver=0.58.10
pkgrel=1
pkgdesc='Thin wrapper for Terraform that provides extra tools for working with multiple Terraform modules'
url='https://github.com/gruntwork-io/terragrunt'
arch=('loong64' 'x86_64')
makedepends=('git' 'go')
depends=('glibc' 'terraform')
license=('MIT')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('53e3f7d407d12773e8990b18041dcd40bdd760db37acf06223f62b808b5d3fe9')
b2sums=('7d32259eee14ca814f50e48007501d0fd2181bcca1bd3141b676701369207ee89b5d553d73dff2144550d88b14de1522cc28cf2ae114efbb072b030593eb4405')
options=('!lto')

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  go build -v \
    -ldflags "-linkmode=external -extldflags '${LDFLAGS}' -X github.com/gruntwork-io/go-commons/version.Version=${pkgver}" \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 ${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
