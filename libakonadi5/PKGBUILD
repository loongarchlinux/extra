# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

_name=akonadi
pkgname=libakonadi5
pkgver=23.08.5
pkgrel=2
pkgdesc='Libraries used by applications based on Akonadi'
arch=(loong64 x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kitemmodels5 kio5)
makedepends=(extra-cmake-modules postgresql qt5-tools)
conflicts=('libakonadi<24')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$_name-$pkgver.tar.xz{,.sig})
sha256sums=('abdbdb28c1084a6ad119d7292175ad31efb4a7898a32cc32a35aa1485d9c4f38'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $_name-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/{etc,usr/{bin,share}}
}
