# Maintainer: Jürgen Hötzel <juergen@archlinux.org>

_pkgname=astring
pkgname=ocaml-${_pkgname}
pkgver=0.8.5
pkgrel=4
pkgdesc="Alternative String module for OCaml"
arch=('loong64' 'x86_64')
url="https://erratique.ch/software/astring"
license=('ISC')
depends=('ocaml')
makedepends=('ocaml-findlib' 'ocamlbuild' 'ocaml-topkg' 'opam')
options=('!strip')
source=("https://erratique.ch/software/astring/releases/${_pkgname}-${pkgver}.tbz")
sha256sums=('865692630c07c3ab87c66cdfc2734c0fdfc9c34a57f8e89ffec7c7d15e7a70fa')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ocaml pkg/pkg.ml build --dev-pkg false
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  opam-installer --prefix="$pkgdir/usr" --libdir='lib/ocaml' --docdir='share/doc'

  mv "${pkgdir}/usr/share/doc/${_pkgname}" "${pkgdir}/usr/share/doc/${pkgname}"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "/usr/share/doc/$pkgname/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
