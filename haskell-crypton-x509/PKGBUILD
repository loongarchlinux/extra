# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=crypton-x509
pkgname=haskell-crypton-x509
pkgver=1.7.6
pkgrel=6
pkgdesc="X509 reader and writer"
url="https://github.com/kazu-yamamoto/crypton-certificate"
license=("BSD")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-asn1-encoding' 'haskell-asn1-parse' 'haskell-asn1-types'
         'haskell-crypton' 'haskell-hourglass' 'haskell-memory' 'haskell-pem')
makedepends=('ghc' 'uusi' 'haskell-tasty' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('ebb74aca2d00261e2fb4927d211ba1a174e190e5257f309e190f019727f8caff')

prepare() {
  cd $_hkgname-$pkgver
  uusi -r x509:crypton-x509 $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
