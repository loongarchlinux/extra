# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joost Bremmer <contact at madeofmagicandwires dot online>
# Contributor: Eliezio Oliveira <eliezio@pm.me>
# Contributor: Matthew Gamble <internet@matthewgamble.net>

pkgname=sad
pkgver=0.4.31
pkgrel=1
pkgdesc='Space Age seD'
arch=('loong64' 'x86_64')
url='https://github.com/ms-jpq/sad'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=(
  'cargo'
  'git'
  'python'
)
optdepends=(
  'fzf: for tui usage'
  'diff-so-fancy: for diff colourizer'
  'git-delta: for diff colourizer'
)
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha256sums=('02336b3f1de062dd97224a52f25bcfd41fe650d68304f3c87b2de919b445205f')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"
  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}

# vim: set ts=2 sts=2 sw=2 et :
