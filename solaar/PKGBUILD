# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Arnaud Taffanel <dev@taffanel.org>
# Contributor: Victor HÃ¤ggqvist <aur a snilius d com>

_name=Solaar
pkgname=solaar
pkgver=1.1.12
pkgrel=1
pkgdesc="Linux device manager for a wide range of Logitech devices"
url="https://pwr-solaar.github.io/Solaar/"
_url="https://github.com/pwr-Solaar/Solaar"
license=(GPL-2.0-or-later)
arch=(any)
depends=(
  glib2
  gobject-introspection-runtime
  gtk3
  hicolor-icon-theme
  libnotify
  python
  python-dbus
  python-evdev
  python-gobject
  python-psutil
  python-pyudev
  python-xlib
  python-yaml
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
optdepends=('libayatana-appindicator: Display tray icon')
source=(
  "git+$_url.git?signed#tag=$pkgver"
)
sha512sums=('a2ffdf3ef2a6bdab39601ec7b1db70f4a83c8b1cb60f04ea87ecc4572991354aec5faed37d7f4ca9ca71827dd7c3c4db30ab3927749648a352a7bd214c437253')
validpgpkeys=('2FF2ACA9D97FF7B42CF8494A1E32E75672D6A196') # Peter F. Patel-Schneider (key generated for github) <pfpschneider@gmail.com>

prepare() {
  cd $_name
  tools/po-compile.sh
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl

  # udev
  rm -rf "$pkgdir/etc"
  install -vDm 644 rules.d/42-logitech-unify-permissions.rules -t "$pkgdir/usr/lib/udev/rules.d/"
  # docs
  install -vDm 644 {CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
