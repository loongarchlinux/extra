# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hoauth2
pkgname=haskell-hoauth2
pkgver=2.7.0
pkgrel=15
pkgdesc="Haskell OAuth2 authentication client"
url="https://github.com/freizl/hoauth2"
license=("BSD")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-base64' 'haskell-cryptonite' 'haskell-data-default'
         'haskell-http-conduit' 'haskell-http-types' 'haskell-memory' 'haskell-microlens'
         'haskell-uri-bytestring' 'haskell-uri-bytestring-aeson')
makedepends=('ghc' 'uusi' 'haskell-hspec' 'haskell-hspec-discover')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('858178d9b8b96b87d8e02f5f94b447d98c67b0dcb89c703dc9b2d3ce7ac1f11b')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
  uusi -u memory $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
