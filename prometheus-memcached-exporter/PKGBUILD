# Maintainer: Jelle van der Waa <jelle@dwaa.nl>

pkgname=prometheus-memcached-exporter
pkgver=0.14.2
pkgrel=1
pkgdesc="Exports metrics from memcached servers for consumption by Prometheus"
arch=('loong64' 'x86_64')
url="https://github.com/prometheus/memcached_exporter"
license=('Apache')
depends=(glibc memcached)
makedepends=(go)
backup=('etc/conf.d/prometheus-memcached-exporter')
source=($pkgname-$pkgver.tar.gz::https://github.com/prometheus/memcached_exporter/archive/v${pkgver}.tar.gz prometheus-memcached-exporter.service
        prometheus-memcached-exporter.conf)
sha512sums=('c6ae6240fed275d010c7d6b4fc600a1042eb1727f9fbce45a3009ba0eba0b8c1f94a922526419a0e5d8c0a2a08c111a29707be4137b9f4acf7b922269a4089fa'
            '13ae8e9cc8445cffab1e51f000fbe5ca0dd2a30532f8303957dc376c8891552e719681e49ffd8a83c4cd01aca897031770c9d96ed92276ff85c2e32c4fa999ee'
            'bc92af08207bee78a48cc764f038d6f5175b0051873f1238d90531f81d9b403085ec7ac62dfe934ee804794426c6bed4c309b374a418c9f7a4dbdbb157d58c23')

check() {
  cd memcached_exporter-$pkgver
  memcached_pids=()
  memcached -p 11211 -U 11211 >/dev/null        & memcached_pids+=($!)

  local ret=0
  go test ./... || ret=1

  kill ${memcached_pids[@]}

  return $ret
}

build() {
  cd memcached_exporter-$pkgver
  go mod edit -replace=golang.org/x/sys=github.com/golang/sys@v0.0.0-20220622161953-175b2fd9d664
  go mod edit -replace=golang.org/x/net=github.com/golang/net@v0.0.0-20220622184535-263ec571b305
  go mod tidy
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" \
      -X github.com/prometheus/common/version.Version=$pkgver \
      -X github.com/prometheus/common/version.Revision=$pkgver \
      -X github.com/prometheus/common/version.Branch=tarball \
      -X github.com/prometheus/common/version.BuildUser=someone@builder \
      -X github.com/prometheus/common/version.BuildDate=$(date -d=@"$SOURCE_DATE_EPOCH" +%Y%m%d-%H:%M:%S)" \
    -o memcached_exporter cmd/memcached_exporter/main.go
}

package() {
  install -Dm644 prometheus-memcached-exporter.service "$pkgdir"/usr/lib/systemd/system/prometheus-memcached-exporter.service
  install -Dm644 prometheus-memcached-exporter.conf "${pkgdir}"/etc/conf.d/prometheus-memcached-exporter

  cd memcached_exporter-$pkgver
  install -Dm755 memcached_exporter "$pkgdir"/usr/bin/prometheus-memcached-exporter
}
