# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=shfmt
pkgver=3.8.0
pkgrel=1
pkgdesc="Format shell programs"
url="https://github.com/mvdan/sh"
license=(BSD-3-Clause)
arch=(loong64 x86_64)
depends=(glibc)
makedepends=(
  go
  scdoc
)
options=(!lto)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('b2235c88d2601c0c223b8bc55682ee7490da7bf97696fdf3fa32787e5ef123bf4072e281daf29851695b590fe93f7bf180b03558974ad726fab10f799881ec92')

prepare() {
  cd "sh-$pkgver"

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd "sh-$pkgver"

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"

  local ld_flags="-compressdwarf=false -linkmode=external -X main.version=$pkgver"
  go build -v \
    -ldflags "$ld_flags" \
    ./cmd/shfmt/...

  scdoc < cmd/shfmt/shfmt.1.scd > shfmt.1
}

check() {
  cd "sh-$pkgver"

  # Ensure the executable reports the correct version.
  test "$(./shfmt --version)" = "$pkgver"

  go test ./cmd/shfmt/...
}

package() {
  cd "sh-$pkgver"

  install -Dm755 -t "$pkgdir/usr/bin" shfmt
  install -Dm644 -t "$pkgdir/usr/share/man/man1" shfmt.1
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
