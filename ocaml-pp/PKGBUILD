# Maintainer: Konstantin Gizdov <arch at kge dot pw>
_pkgname='pp'
pkgname="ocaml-${_pkgname}"
pkgver=1.2.0
pkgrel=2
pkgdesc="OCaml Pretty printing: a lean alternative to the Format module of the OCaml standard library."
arch=('loong64' 'x86_64')
url="https://github.com/ocaml-dune/pp"
license=('MIT')
makedepends=('dune' 'ocaml')
depends=('glibc')
options=('!strip' '!debug')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ab1e833736b994200009805e78f88a309cef365fc3e57b98b3cfc86b9216a8b44ae3a63cf2a6755ff14250a1a27bcfde8083409e77c0cd8e3e4a57fda3771d2c')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    dune build -p "${_pkgname}"
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    DESTDIR="${pkgdir}" dune install --prefix "/usr" --libdir="$(ocamlc -where)"

    install -dm755 "${pkgdir}/usr/share/"
    mv "${pkgdir}/usr/doc" "${pkgdir}/usr/share/"

    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -s /usr/share/doc/pp/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
