# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-default-settings
pkgver=8.0.0
pkgrel=1
pkgdesc='Default settings for Pantheon'
arch=(any)
url=https://github.com/elementary/default-settings.git
license=(GPL3)
groups=(pantheon)
depends=(
  accountsservice
  dbus
  dconf
  polkit
)
makedepends=(
  git
  meson
)
_tag=70f24f5fd100b33b8ada0b10e57ea6e6ba0de189
source=(pantheon-default-settings::git+https://github.com/elementary/default-settings.git#tag=${_tag})
b2sums=('4bf2850f16c19bdf91fa7614c0cde95f5dadfa0f93a18cccc650bc43cfb0f64ca74130f163479e3ba63776bb1c134dc1c1404b53e04f13ecd24aedddff7c8346')

pkgver() {
  cd pantheon-default-settings
  git describe --tags
}

build() {
  arch-meson pantheon-default-settings build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm "${pkgdir}"/usr/share/cups/data/default-testpage.pdf
}

# vim: ts=2 sw=2 et:
