# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=clarabel
pkgname=python-$_pyname
pkgver=0.9.0
pkgrel=1
pkgdesc='Interior-point solver for convex conic optimisation problems'
arch=(loong64 x86_64)
url='https://github.com/oxfordcontrol/Clarabel.rs'
license=(Apache-2.0)
depends=(gcc-libs
         glibc)
makedepends=(git
             python-build
             python-installer
             python-maturin
             rust)
source=(git+https://github.com/oxfordcontrol/Clarabel.rs#tag=v$pkgver)
sha256sums=('467eff358d1a165d1c03da00a192df8e2b0cafd93679946990691411034ac9e6')

prepare() {
  cd Clarabel.rs
  cargo generate-lockfile # https://github.com/oxfordcontrol/Clarabel.rs/pull/64
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd Clarabel.rs
  python -m build --wheel --no-isolation
}

package() {
  cd Clarabel.rs
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
