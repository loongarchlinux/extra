# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: XavierCLL <xavier.corredor.llano (a) gmail.com>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# PKGBUILD base; Simper < simonpersson1 AT gmail DOT com >

pkgname=kup
pkgver=0.9.1.20240317
pkgrel=2
_commit=f705664ce437ca2a40b4a91dadc4c3fe1e700dfd
pkgdesc='A backup software for helping people to keep up-to-date backups'
arch=(loong64 x86_64)
url='https://apps.kde.org/kup/'
license=(GPL-2.0-only GPL-2.0-or-later GPL-3.0-only)
depends=(gcc-libs
         glibc
         kcmutils
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kdbusaddons
         ki18n
         kidletime
         kio
         kirigami
         kjobwidgets
         knotifications
         kwidgetsaddons
         kxmlgui
         libgit2
         libplasma
         plasma5support
         qt6-base
         qt6-declarative
         solid)
makedepends=(extra-cmake-modules
             git)
optdepends=('bup: bup backend'
            'rsync: support for synced backups')
# source=(https://download.kde.org/stable/kup/kup-$pkgver.tar.xz{,.sig})
source=(git+https://invent.kde.org/system/kup#commit=$_commit)
sha256sums=('SKIP')
validpgpkeys=(CBA1DCD0B06A2B2E9B133E5C5C7D83CE9812E291)

build() {
  cmake -B build -S $pkgname \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
