# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Xeonacid <h.dwwwwww at gmail dot com>

_pyname=cle
pkgname=python-${_pyname}
pkgver=9.2.101
_testbinaries_ver=9.2.101
pkgrel=1
pkgdesc='Loads binaries and their libraries, resolves imports and provides an abstraction of process memory'
url='https://github.com/angr/cle'
arch=('any')
license=('custom:BSD')
depends=('python' 'python-pyelftools' 'python-cffi' 'python-pyvex' 'python-pefile' 'python-sortedcontainers')
optdepends=(
  'python-claripy: symbolic constraints'
  'python-minidump: minidump'
  'python-pyxbe: xbe'
  'python-arpy: ar'
  'python-pyaxmlparser: apk'
)
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest' 'python-minidump' 'python-pyxbe' 'python-arpy' 'python-claripy' 'python-pyaxmlparser')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "https://github.com/angr/binaries/archive/v${_testbinaries_ver}/angr-binaries-${_testbinaries_ver}.tar.gz")
sha512sums=('745b800c72994416001cb1aaf7f9b15b71fa77c016d1098a19e7367f0206bd0a385fcff8e848eb6ed2db2a2dd3b7337e429f570fe0d1f0dece2537192d8515d4'
            'd08ab57282381e57380f1bcdfeb1ebdb23d0ac4a0d97aa0d2694366ed54fe35bf9704ba7fef1c33be9b502e1e0480012d12e63fa385789d6c14e58b85ba1456c')
b2sums=('2985724634a66c598145b37d4ded4309d3f6ab6665e410430827a8e105ae2090010db861c5d310155d6d3484fc27c2a404887201f510e4da501f7fcc51417990'
        'ce9b370e757b0efff4f9ed5689ae60de27d85b3c8d4899c89b9cdb090a562f2f269ccb3b862cba895d8b4b64e63e0b5dc26eee89d15fbce8a1212e6c10a21564')

prepare() {
  ln -s binaries-${_testbinaries_ver} binaries
}

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=build/lib pytest
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
