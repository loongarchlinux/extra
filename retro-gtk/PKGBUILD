# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Tom Richards <tom@tomrichards.net>
# Contributor: TingPing <tingping@tingping.se>
# Contributor: Omar Pakker <omar.pakker@oracle.com>

pkgname=retro-gtk
pkgver=1.0.2
pkgrel=4
pkgdesc='Toolkit to write GTK based Libretro frontends'
arch=(loong64 x86_64)
url='https://gnome.pages.gitlab.gnome.org/retro-gtk/'
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  libepoxy
  libpulse
  libsamplerate
)
makedepends=(
  git
  glib2-devel
  gobject-introspection
  meson
  vala
)
source=("git+https://gitlab.gnome.org/GNOME/$pkgname.git#tag=$pkgver")
b2sums=(e183a3da1d42704f8c76c3c33c0acd3308e8f129bef6ed9f0e831d862a14cb7fb9fa29340f3ad2f113132bbbd61fedff8523f2558b0aa99483d955bb62374f7a)

prepare() {
  cd $pkgname

  # https://gitlab.gnome.org/GNOME/retro-gtk/-/merge_requests/167
  git cherry-pick -n 8016c10e7216394bc66281f2d9be740140b6fad6
}

build() {
  arch-meson $pkgname build \
    -D build-doc=true \
    -D install-doc=true
  meson compile -C build
}

check() {
  # Tests try to run the installed /usr/lib/retro-runner

  #dbus-run-session xvfb-run \
  #  -s '-screen 0 1920x1080x24 -nolisten local' \
  #  meson test -C build --print-errorlogs
  :
}

package() {
  meson install -C build --destdir "$pkgdir"
}
