# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-polkit-agent
pkgver=8.0.0
pkgrel=1
pkgdesc='Pantheon Polkit Agent'
arch=(loong64 x86_64)
url=https://github.com/elementary/pantheon-agent-polkit
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgranite-7.so
  polkit
)
makedepends=(
  git
  meson
  intltool
  vala
)
_tag=0809b756a64e89c07f368a3996800d099cb41ee3
source=(pantheon-polkit-agent::git+https://github.com/elementary/pantheon-agent-polkit.git#tag=${_tag})
b2sums=('ab295e225df69f2e3dc4acd92b9f7d8c96cf8ec45a4e095f529a2bc8246d6617fe5ba8d3c6db930f32b1c257e6e7d6bde7be1e374a752c41f19b838a8b12bad5')

pkgver() {
  cd pantheon-polkit-agent
  git describe --tags
}

build() {
  arch-meson pantheon-polkit-agent build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
