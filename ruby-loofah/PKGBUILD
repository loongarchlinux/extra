# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='loofah'
pkgname="ruby-${_gemname}"
pkgver=2.22.0
pkgrel=1
pkgdesc='HTML/XML manipulation and sanitization based on Nokogiri'
arch=('any')
url='https://github.com/flavorjones/loofah'
license=('MIT')
options=(!emptydirs)
depends=(
  ruby
  ruby-crass
  ruby-nokogiri
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-minitest
  ruby-rake
  ruby-rr
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b1b9ba490334563b935d8a176ba6eee8d98c5c3ebbecc878c654e64a48311c38d0684a9b8e2704797c36492a33bc1248137a7611eb9f796a5ea9aa4df1b5f90e')
b2sums=('d465dcc83cac2a994145f7840b8607f92a080fef7ef6860ae81a3eecaf41be6be7e3b59263c05d09c5a729844d4f2490ce3e39046fdd863647ac157367c16427')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  # no need for markdown beautifying
  sed --in-place --regexp-extended '/hoe/Id' Rakefile "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  gem build "${_gemname}.gemspec"
}

check() {
  cd "${_gemname}-${pkgver}"

  rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "${pkgdir}/${_gemdir}/cache/" \
    "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "${pkgdir}/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "${pkgdir}/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete

  install --verbose -D --mode=0644 MIT-LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
