# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: Stick <stick@stma.is>

_name=i3ipc-python
pkgname=python-i3ipc
pkgver=2.2.1
pkgrel=6
pkgdesc="An improved Python library to control i3wm"
url="https://github.com/altdesktop/i3ipc-python"
arch=('any')
license=('BSD-3-Clause')
conflicts=('i3ipc-python')
replaces=('i3ipc-python')
depends=('python' 'python-xlib')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-poetry')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
b2sums=('19d62ad3cf5a3b2081cff9e193aef64d5108b9e2b304862d3bff60e11140873d8190d287e6ca89566602fc5300d80101576dc2d837b6f1e75d30adeb3772667e')

build() {
	cd "${_name}-${pkgver}"
	python -m build --wheel --no-isolation
}

package() {
	cd "${_name}-${pkgver}"
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm 755 examples/*.py -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
