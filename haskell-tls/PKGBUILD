# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=tls
pkgname=haskell-tls
pkgver=1.8.0
pkgrel=3
pkgdesc="TLS/SSL protocol native implementation (Server and Client)"
url="https://github.com/haskell-tls/hs-tls"
license=("BSD")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-asn1-encoding' 'haskell-asn1-types' 'haskell-async' 'haskell-cereal'
         'haskell-crypton' 'haskell-crypton-x509' 'haskell-crypton-x509-store'
         'haskell-crypton-x509-validation' 'haskell-data-default-class' 'haskell-memory'
         'haskell-network' 'haskell-unix-time')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-hourglass' 'haskell-tasty'
             'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('23463ebbe331e02ed3b8d9c5a4e7dccc0b921898709c600e4a0a7b50575029c00ddbdbe6e3a44ffb094059cae377cb388b55be53ab847a1843989359200894b3')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-hans -fnetwork -fcompat
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
