# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: peeweep <peeweep at 0x0 dot ee>

pkgname=neovide
pkgver=0.13.3
pkgrel=2
pkgdesc='No Nonsense Neovim Client in Rust'
arch=(loong64 x86_64)
url="https://github.com/$pkgname/$pkgname"
license=(MIT)
depends=(gcc-libs
         fontconfig libfontconfig.so
         freetype2 libfreetype.so
         glibc
         hicolor-icon-theme
         libglvnd
         libjpeg libjpeg.so
         libxkbcommon
         neovim
         sndio
         libpng libpng16.so
         harfbuzz libharfbuzz.so
         icu libicuuc.so
         expat libexpat.so
         zlib libz.so)
makedepends=(cargo
             cmake
             python
             gn
             git
             ninja
             clang)
optdepends=('vulkan-intel: vulkan support for intel'
            'libxkbcommon-x11: run on X11 (not needed for wayland)')
options=(!lto)
_archive=("$pkgname-$pkgver")
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('21c8eaa53cf3290d2b1405c8cb2cde5f39bc14ef597b328e76f1789b0ef3539a')

prepare() {
	cd "$_archive"
	sed -r -i -e '/^incremental/a opt-level = 3' Cargo.toml
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_archive"
	# this uses malloc_usable_size, which is incompatible with fortification level 3
	# upstream tracking issue https://github.com/neovide/neovide/issues/2040
	export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
	export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
	CFLAGS+=' -ffat-lto-objects'
	export SKIA_USE_SYSTEM_LIBRARIES=true
	export FORCE_SKIA_BUILD=true
	export SKIA_GN_COMMAND=gn
	export SKIA_NINJA_COMMAND=ninja
	cargo build --frozen --release --features embed-fonts
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/applications/" "assets/$pkgname.desktop"
	for px in 16 32 48 256; do
		install -Dm0644 "assets/$pkgname-${px}x${px}.png" \
			"$pkgdir/usr/share/icons/hicolor/${px}x${px}/apps/$pkgname.png"
	done
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
