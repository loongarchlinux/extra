# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=speedcrunch
pkgver=0.12.0
pkgrel=4
pkgdesc="Simple but powerful calculator using Qt"
arch=(loong64 x86_64)
url='https://speedcrunch.org/'
license=(GPL-2.0-only)
depends=(gcc-libs
         glibc
         qt5-base
         qt5-tools)
makedepends=(cmake
             git)
source=(git+https://bitbucket.org/heldercorreia/speedcrunch#tag=release-$pkgver)
sha256sums=('368356c8614182c3eff16aa396c730e5ebbb0d11d30bb111ac86418e98824ed8')

prepare() {
  cd $pkgname
  sed -e 's|/appdata/|/metainfo/|g' -i src/CMakeLists.txt # Update metainfo install dir
}

build() {
  cmake -B build -S $pkgname/src \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
