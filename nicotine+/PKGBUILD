# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>

pkgname=nicotine+
pkgver=3.3.4
pkgrel=1
pkgdesc='Soulseek music-sharing client, written in python'
arch=(any)
url='https://github.com/Nicotine-Plus/nicotine-plus'
license=(GPL-3.0-only)
depends=(gdk-pixbuf2
         gtk4
         gobject-introspection-runtime
         pango
         python
         python-gobject)
makedepends=(git
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/Nicotine-Plus/nicotine-plus#tag=$pkgver)
sha256sums=('ca0910a66327afb29692d9140bb61178c6ea88942d994e213a869a3c7ec0f6d7')

build() {
  cd nicotine-plus
  
  python -m build --wheel --no-isolation
}

package() {
  cd nicotine-plus

  python -m installer --destdir="$pkgdir" dist/*.whl
}
