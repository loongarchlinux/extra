# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=bandit
pkgver=1.7.9
pkgrel=1
pkgdesc='Python security linter from OpenStack Security'
arch=('any')
url='https://github.com/PyCQA/bandit'
license=('Apache')
depends=('python-yaml' 'python-stevedore' 'python-pbr' 'python-gitpython' 'python-rich')
makedepends=('python-setuptools' 'git')
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('a71e3eb359fad952afc9e210795fb8cc6fe178305bb837e002af83eccc4a958664b69422ba93d7d72b24089ba3142667ca17909d712db1b447e78d6f7f4c9634')

build() {
    cd bandit-$pkgver
    python setup.py build
}

package() {
    cd bandit-$pkgver
    python setup.py install --root="$pkgdir" --skip-build --optimize=1
}
