# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=dns
pkgname=haskell-dns
pkgver=4.1.1
pkgrel=12
pkgdesc="DNS library in Haskell"
url="https://hackage.haskell.org/package/${_hkgname}"
license=("BSD")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-auto-update' 'haskell-attoparsec'
         'haskell-base16-bytestring' 'haskell-base64-bytestring' 'haskell-case-insensitive'
         'haskell-cryptonite' 'haskell-hourglass' 'haskell-iproute' 'haskell-network'
         'haskell-psqueues')
makedepends=('ghc' 'haskell-doctest' 'haskell-hspec' 'haskell-hspec-discover' 'haskell-word8'
             'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('8335c4ffae9fbd1f05a90ccd2eeb119e97f7c997f8ea5e580662bdec9f9d26bf9c341983b66973657b04a99199df9e1dafcfe5fcc19a10fa2efaaa2b47d7e578')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || echo "Tests failed"
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
