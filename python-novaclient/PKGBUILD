# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-novaclient
pkgver=18.6.0
_commit=533d5df5168123578da5eb5380efb64096aa3c43
pkgrel=2
pkgdesc="Client library for OpenStack Compute API"
arch=('any')
url="https://docs.openstack.org/python-novaclient/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneauth1'
         'python-requests' 'python-iso8601' 'python-oslo-utils' 'python-oslo-i18n'
         'python-oslo-serialization')
makedepends=('git' 'python-setuptools')
checkdepends=('python-oslotest' 'python-osprofiler' 'python-requests-mock' 'python-ddt' 'python-stestr' 'python-testscenarios')
source=("git+https://github.com/openstack/python-novaclient.git#commit=$_commit")
sha512sums=('e78543c62509099a97c1fae69ff4902796a988796c528b293b59ee9212ed1f3fcefb7f93e4784bb20cc16cfefed52a1d899f3b0995e870f4ded102275ac4dd1b')

build() {
  cd python-novaclient
  python setup.py build
}

check() {
  cd python-novaclient
  stestr run
}

package() {
  cd python-novaclient
  python setup.py install --root="$pkgdir" --optimize=1
}
