# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=stellarsolver
pkgver=2.6
pkgrel=1
pkgdesc='The cross platform Sextractor and Astrometry.net-Based internal astrometric solver'
arch=(loong64 x86_64)
url='https://github.com/rlancaste/stellarsolver'
license=(GPL-3.0-or-later)
depends=(cfitsio
         gcc-libs
         glibc
         gsl
         qt5-base
         wcslib)
makedepends=(cmake
             git)
source=(git+https://github.com/rlancaste/stellarsolver#tag=$pkgver)
sha256sums=('52c300aa7b145ce033b77afc4877866b86809c269b58c566af57a925ffff5d5f')

build() {
  CFLAGS+=" -Wno-implicit-function-declaration" \
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTER=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
