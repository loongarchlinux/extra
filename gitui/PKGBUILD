# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Simon Hauser <Simon-Hauser@outlook.de>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=gitui
pkgver=0.26.1
pkgrel=5
pkgdesc='Blazing fast terminal-ui for git written in Rust'
url='https://github.com/extrawurst/gitui'
arch=('loong64' 'x86_64')
license=('MIT')
# work around two libssl.so providers being in the repos by only depending on package for now
depends=('gcc-libs' 'glibc' 'libgit2' 'libxcb' 'openssl' 'zlib')
makedepends=('cargo' 'git' 'python') # xcb crate needs python
source=("git+$url.git?tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"
  CFLAGS+=' -ffat-lto-objects'
  cargo build --frozen --release
}

check() {
  cd "$pkgname"
  cargo test --frozen
}

package() {
  depends+=(libgit2.so libz.so)
  cd "$pkgname"
  install -Dm 755 target/release/gitui -t "${pkgdir}/usr/bin/"
  install -Dm 644 {KEY_CONFIG,README,THEMES}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
