# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>

pkgname=gitlab-exporter
pkgver=0.5.2
pkgrel=1
pkgdesc='A Gitlab prometheus metric exporter'
arch=('loong64' 'x86_64')
url='https://gitlab.archlinux.org/archlinux/gitlab-exporter'
license=('MIT')
depends=('gcc-libs' 'openssl')
makedepends=('cargo')
source=("https://gitlab.archlinux.org/archlinux/${pkgname}/-/releases/v${pkgver}/downloads/${pkgname}-${pkgver}.tar".gz{,.sig})
sha256sums=('aa6f3ef81afcca34f53bd73ce6869371b9f84b09db70a0c11ceaf871cf3b5d25'
            'SKIP')
b2sums=('f1ec1318b7f9f5d3e0c619ec10fbf9f8058a938c67b4676052baed7a612a90a287a2d85925310ceaec2b3cc36c5b7f37e7b8167a25695b00a31df465d28d8a59'
        'SKIP')
validpgpkeys=(
  'B4B759625D4633430B74877059E43E106B247368' # Leonidas Spyropoulos <artafinde@archlinux.org>
) 

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgname}-${pkgver}"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  CFLAGS+=' -ffat-lto-objects'
  cargo build --release --frozen
}

check() {
  cd "${pkgname}-${pkgver}"
  export RUSTUP_TOOLCHAIN=stable
  CFLAGS+=' -ffat-lto-objects'
  cargo test --frozen -- --test-threads 1
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 target/release/${pkgname} -t "${pkgdir}"/usr/bin
  install -Dm644 README.md -t "${pkgdir}"/usr/share/doc/${pkgname}
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
