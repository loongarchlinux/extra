# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pint
pkgver=0.59.0
pkgrel=1
pkgdesc='Rule linter/validator for Prometheus'
arch=('loong64' 'x86_64')
url='https://github.com/cloudflare/pint'
license=('Apache-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('1a41bcdabd236dc5b4bba4a1462f362f4554e775ada44bf85c51aaeb9868dabd1c6213a49b70ae2018b509ed6cf9b171556e3ca9e8e0825ad8d62ef0689221c6')
b2sums=('4585e499d20746273ed05a5991acb317f08db66a0a933314765e135904ab759755fe914c457e6ee98dabcfdd13395a63db5a9850cea82f4926a1df233e62e334')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X main.version=${pkgver} \
    -X main.commit=${_commit}" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/pint
}
