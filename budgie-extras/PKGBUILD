# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Campbell Jones <arch at serebit dot com>

pkgname=budgie-extras
pkgver=1.8.0
pkgrel=1
pkgdesc='Additional Budgie Desktop components from the Ubuntu Budgie team'
arch=('loong64' 'x86_64')
license=('GPL3')
url='https://github.com/UbuntuBudgie/budgie-extras'
depends=('budgie-desktop' 'wmctrl' 'xdotool' 'xprintidle' 'python' 'python-gobject' 'python-cairo' 'libgee'
         'zenity' 'vorbis-tools' 'python-psutil' 'sound-theme-freedesktop' 'imagemagick' 'python-pillow'
         'python-svgwrite' 'python-cairosvg' 'python-pyudev' 'python-requests' 'libpeas' 'json-glib'
         'libkeybinder3' 'python-pyperclip' 'python-xlib' 'xorg-xinput' 'granite' 'appstream'
         'libnma' 'xorg-xprop' 'libsoup')
makedepends=('gobject-introspection' 'meson' 'ninja' 'vala' 'intltool' 'git')
source=("git+$url.git#tag=v$pkgver"
        "git+https://github.com/UbuntuBudgie/QuickChar.git"
        "git+https://github.com/UbuntuBudgie/applications-menu.git"
        "git+https://github.com/UbuntuBudgie/budgie-network-applet.git")
sha512sums=('db8c20f0efcc1c5b32be61711c679cedaab681a80c925d2d8d3054ce1bc15b32c90c69bf07a91005f2234d022453351325b2e45a2a820640048362c441e4a02d'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd "$pkgname"

  git submodule init
  git config submodule.budgie-quickchar/quickchar.url ../QuickChar
  git config submodule.budgie-applications-menu/applications-menu.url ../applications-menu
  git config submodule.budgie-network-manager/budgie-network-applet.url ../budgie-network-applet
  git -c protocol.file.allow=always submodule update
}

build() {
  arch-meson "$pkgname" build -D b_pie=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
