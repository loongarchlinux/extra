# Maintainer:  George Rawlinson <george@rawlinson.net.nz>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Oleg Shparber <trollixx+aur@gmail.com>
# Contributor: Thomas Weißschuh <thomas t-8ch de>

pkgname=zeal
pkgver=0.7.1
pkgrel=1
pkgdesc='Offline API documentation browser'
arch=('loong64' 'x86_64')
url='https://zealdocs.org'
license=('GPL-3.0-or-later')
depends=(
  'glibc'
  'gcc-libs'
  'hicolor-icon-theme'
  'qt6-webengine'
  'qt6-base'
  'qt6-webchannel'
  'sqlite'
  'libarchive'
  'libxcb'
  'libx11'
  'xcb-util-keysyms'
)
makedepends=(
  'cmake'
  'extra-cmake-modules'
  'git'
)
source=("$pkgname::git+https://github.com/zealdocs/zeal#tag=v$pkgver")
sha512sums=('a97653fa7609b055c5221cd0138307f7f2b94367b795ebca155cae6cbe664318628c0b8e078cf84a5b738927c44c7f072d2f3b3ae18423da5d1675c6b1bf882b')
b2sums=('b512ec98ba2fdd0ce2e42d2f0e572ef6b42ff0abebb3eb3556eddf5cdc712da975d91ae9cde29c2466134a07e50171b384139102ef35f4f44ef9cee6234d0284')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_BUILD_TYPE=Release \
    -D ZEAL_RELEASE_BUILD=ON

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
