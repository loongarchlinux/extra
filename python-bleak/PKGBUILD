# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Francesco Minnocci <ascoli.minnocci@gmail.com>

pkgname=python-bleak
pkgver=0.21.1
pkgrel=3
pkgdesc='Bluetooth Low Energy platform Agnostic Klient for Python'
arch=(any)
url=https://github.com/hbldh/bleak
license=(MIT)
depends=(
  python
  python-async-timeout
  python-dbus-fast
  python-service-identity
)
makedepends=(
  bluez-utils
  git
  python-build
  python-installer
  python-poetry-core
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
)
source=(git+$url.git#tag=v$pkgver)
b2sums=('7b87ba78915f6cb81442752181f9c17955497ab7b8406b5ccd26fc051a97dba9698e93184c2a1edf4aa7221c404a1892eb32242ab64b50d865621665a69f67d2')

build() {
  cd bleak
  python -m build --wheel --no-isolation
}

check() {
  cd bleak
  PYTHONPATH="$PWD" pytest -vv
}

package() {
  python -m installer --destdir="${pkgdir}" bleak/dist/*.whl
  install -Dm 644 bleak/LICENSE -t "${pkgdir}"/usr/share/licenses/python-bleak/
}

# vim: ts=2 sw=2 et:
