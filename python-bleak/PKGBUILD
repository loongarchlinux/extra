# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Francesco Minnocci <ascoli.minnocci@gmail.com>

pkgname=python-bleak
pkgver=0.22.2
pkgrel=1
pkgdesc='Bluetooth Low Energy platform Agnostic Klient for Python'
arch=(any)
url=https://github.com/hbldh/bleak
license=(MIT)
depends=(
  python
  python-async-timeout
  python-dbus-fast
  python-service-identity
)
makedepends=(
  bluez-utils
  git
  python-build
  python-installer
  python-poetry-core
  python-wheel
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
)
_tag=c746071a3fcc3b5e69db6d6b23445ec3505d7730
source=(git+https://github.com/hbldh/bleak.git#tag=${_tag})
b2sums=('162b45b2b00e38300f9dfe9234db3ddadff2f5faef63a683c84fb427f1a0086a37d74ed960e3cf0469e6f4a5036606399ce193f8f9b512a6caf06d78b1c8742e')

pkgver() {
  cd bleak
  git describe --tags | sed 's/^v//'
}

build() {
  cd bleak
  python -m build --wheel --no-isolation
}

check() {
  cd bleak
  PYTHONPATH="$PWD" pytest -vv
}

package() {
  python -m installer --destdir="${pkgdir}" bleak/dist/*.whl
  install -Dm 644 bleak/LICENSE -t "${pkgdir}"/usr/share/licenses/python-bleak/
}

# vim: ts=2 sw=2 et:
