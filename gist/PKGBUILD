# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Chris van Dijk (quigybo) <quigybo at hotmail dot com>
# Contributor: Scott Olson <scott at scott-olson dot org>

pkgname=gist
pkgver=6.0.0
pkgrel=2
pkgdesc='Potentially the best command line gister'
url='https://github.com/defunkt/gist'
arch=('any')
license=('custom:MIT')
depends=('ruby')
makedepends=('ruby-rake' 'ruby-ronn')
optdepends=("xclip: put pasted gist's url into X selection automagically"
            "xsel: put pasted gist's url into X selection automagically")
source=(https://github.com/defunkt/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        ronn-ng-fix.patch)
sha512sums=('9544d21c68ec46265c310f74af10c63cf7129868f83e1cdf5c76d73641ba14a97e6e689fb17696e7799e57b141a7bdad164ea7c9ce3a7293a81d2d1d866168c7'
            'e2140264c6ca789f1362a675ecfeaf26ac3bd7093e053ec7121c2cb7011c981c2715e4f2df177cbae7079176847ab318f2ee221cd1bf1832153e003fe56e2384')
b2sums=('ba5d0fef518bb0f1baca760d71eb259942057720932f6452adc3b420b70408b34ae2eb221f41320cf8aaadf6eedb03470a39d17f5d721484d6cfff1d8c1d598e'
        '585f6d52bae202176a2a1545850e9e4805ba82af416ad4d27b3ff487e78eb94e253b200f0599878dc772d900f0b02acd70e775209e4342f732649573c7b31461')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np0 -i ${srcdir}/ronn-ng-fix.patch
}

build() {
  cd ${pkgname}-${pkgver}
  rake build
}

package() {
  cd ${pkgname}-${pkgver}
  PREFIX="${pkgdir}/usr" rake install
  install -Dm 644 LICENSE.MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
