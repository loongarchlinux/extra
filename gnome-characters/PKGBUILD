# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-characters
pkgver=46.0
pkgrel=2
pkgdesc="A character map application"
url="https://gitlab.gnome.org/GNOME/gnome-characters"
arch=(loong64 x86_64)
license=("BSD-3-Clause AND GPL-2.0-or-later")
depends=(
  dconf
  emoji-font
  gjs
  glib2
  glibc
  gnome-desktop-4
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  appstream
  git
  glib2-devel
  gobject-introspection
  gperf
  meson
)
checkdepends=(weston)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-characters.git#tag=${pkgver/[a-z]/.&}")
b2sums=('60ddc29cf067797ac8d6db93aa33229a21ea12513c63c07a2039183bedc4ce16b1789d32a5952dc6ed7465efab13420324a5ff102fedce9f276e2160e999f6cb')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() (
  export XDG_RUNTIME_DIR="$PWD/runtime-dir"
  mkdir -p -m 700 "$XDG_RUNTIME_DIR"

  export WAYLAND_DISPLAY=wayland-5
  weston --backend=headless-backend.so --socket=$WAYLAND_DISPLAY --idle-time=0 &
  _w=$!

  trap "kill $_w; wait" EXIT

  meson test -C build --print-errorlogs
)

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set sw=2 sts=-1 et:
