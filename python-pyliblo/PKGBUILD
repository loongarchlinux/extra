# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

_name=pyliblo
pkgname=python-pyliblo
pkgver=0.10.0
pkgrel=13
pkgdesc='Python wrapper for the liblo OSC library'
arch=(loong64 x86_64)
url="http://das.nasophon.de/pyliblo/"
license=(LGPL-2.1-or-later)
depends=(
  glibc
  liblo
  python
)
makedepends=(
  cython
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=(
  $_name-$pkgver.tar.gz::https://github.com/dsacre/pyliblo/archive/refs/tags/$pkgver.tar.gz
  $pkgname-0.10.0-python3.11.patch  # https://github.com/dsacre/pyliblo/issues/29
  cython3.patch
)
sha512sums=('c475ddfea9abf9e155deed12ab01ff2e585a6ed2946fac8d5017f36e3aee84b294f21cf5d0dd79098a4bd2f7911beed791bed67124cec3fc4bbd5087d42b4eed'
            '66e1543937a125e72184d49fd7c70dc74f61063a05c7d2026618a0d2778480256c58cb80e23645e96d967b5dfbfc1437862523a898bcacbb24ebba9a09cb7fc6'
            '900d0b3cbb04cfc7f56b124650b0c29e3a442ccf50796feb87dcf4d17c4e365de2c26899d78713804410590d62be9412b69d79b0aadababb49da3bff54d6709e')
b2sums=('dc14ab1dd64cdff5946d0e05a871c7cc75be89e8a57b12302cbee3bbeab1e9e1cfedce493fd4dbb6a4ba5a095a8539a4d4b7c9fe04431cf8376812b01bf78001'
        '4b2fb3672cb1b58697cd73484d622869e34dcbbac95d0cb94c98e0954092b788b43a30af2c180ee69a84621915799209ab75213325a8d61cfb69d4cced368d68'
        '2345c593195d66a99b06e25ae796c118c55b1597bee0c2ae0badcaacdbcd02cfd5832c92b5686bf3d171fcdb5d12c1652e3e0e6a9b18f9de6e9351c1dae8f474')

prepare() {
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-0.10.0-python3.11.patch
  patch -d $_name-$pkgver -p1 < cython3.patch # Fedora patch
}

build() {
  cd $_name-$pkgver
  CFLAGS+=" -Wno-incompatible-pointer-types" \
  python -m build --wheel --no-isolation
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
  pytest -vv
}

package_python-pyliblo() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
