# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=cbindgen
pkgver=0.25.0
pkgrel=1
pkgdesc="A tool for generating C bindings to Rust code"
url="https://github.com/eqrion/cbindgen"
arch=(loong64 x86_64)
license=(MPL2)
depends=(gcc-libs)
makedepends=(
  cargo
  git
)
checkdepends=(
  cmake
  cython0
  python
)
_commit=dd9a550152cd162a3aa01757a55dd22fc56d0d8a  # tags/v0.25.0^0
source=("git+$url#commit=$_commit")
b2sums=('SKIP')

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

pkgver() {
  cd cbindgen
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd cbindgen
  cargo fetch --locked --target x86_64-unknown-linux-gnu
}

build() {
  cd cbindgen
  cargo build --release --frozen --all-targets
}

check() {
  cd cbindgen
  # Tests need nightly features
  RUSTC_BOOTSTRAP=1 cargo test --release --frozen
}

package() {
  cd cbindgen
  install -Dt "$pkgdir/usr/bin" target/release/cbindgen
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 CHANGES \
    {README,docs,internals}.md
}

# vim:set sw=2 sts=-1 et
