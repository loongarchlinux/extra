# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Cedric Girard <cgirard.archlinux@valinor.fr>
# Contributor: kevku <kevku@gmx.com>

pkgname=kodi-addon-inputstream-adaptive
pkgver=21.4.6
_codename=Omega
pkgrel=2
pkgdesc="InputStream client for adaptive streams for Kodi 18+"
arch=('loong64' 'x86_64')
url="https://github.com/peak3d/inputstream.adaptive"
license=('GPL2')
groups=('kodi-addons' 'kodi-addons-inputstream')
makedepends=('cmake' 'kodi-dev' 'gtest' 'rapidjson' 'pugixml')
options=(!lto)

_bento4_version="1.6.0-641"

source=("$pkgname-$pkgver.tar.gz::https://github.com/peak3d/inputstream.adaptive/archive/$pkgver-$_codename.tar.gz"
        "$pkgname-bento4-$_bento4_version.tar.gz::https://github.com/xbmc/Bento4/archive/refs/tags/$_bento4_version-$_codename.tar.gz")
noextract=("$pkgname-bento4-$_bento4_version.tar.gz")
sha512sums=('4d8cc670f387d825caa140e6fc12617e37c245ea919cc3894fe792844968b1b160439843d9168b349c91c90537384aa554c6748b139e4efa2b0a277bc42217db'
            '6d08b8e028cc5a73fa634e42cb4c0e89cc4a7a700177df7ef8e9be18c02077baae25e5a0f1b8d9cb4ceb2a6ff3108e41a4a07aae5f64d9372e915efce0fbd861')

#prepare() {
    #cd "inputstream.adaptive-$pkgver-$_codename"
#}

build() {
    cd "inputstream.adaptive-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        -DENABLE_INTERNAL_BENTO4=1 \
        -DBENTO4_URL="$srcdir/$pkgname-bento4-$_bento4_version.tar.gz" \
        .
    make
}

package() {
    depends=('kodi' 'kodi-platform' 'pugixml')
    cd "inputstream.adaptive-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}
