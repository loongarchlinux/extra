# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Magnus Bergmark <magnus.bergmark@gmail.com>

pkgname=rofi-emoji
pkgver=3.4.1
pkgrel=1
pkgdesc='A Rofi plugin for selecting emojis'
url='https://github.com/Mange/rofi-emoji'
arch=('loong64' 'x86_64')
license=('MIT')
depends=(
  'cairo'
  'gcc-libs'
  'glib2'
  'glibc'
  'rofi'
)
checkdepends=('check')
optdepends=(
  'xsel: X11 support'
  'xclip: X11 support'
  'wl-clipboard: Wayland support'
)
source=(https://github.com/Mange/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('fcb9907c9d82b6759c844a1d06cd954bb61de624c960d17f6d663d7e86966ef9')
b2sums=('b07139bb5bac666295da282382c9a41eb91b933a93fcfca8916c65e30739a32864917d3c8ab853330fe1afb318554de9cbce9ea0aef07ce508dea5dfe565bc74')

build() {
  cd ${pkgname}-${pkgver}
  autoreconf -i
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
