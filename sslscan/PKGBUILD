# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Remi Gacogne <rgacogne-arch at coredump dot fr>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=sslscan
pkgver=2.1.3
pkgrel=1
pkgdesc="Fast tool to scan SSL services such as HTTPS to determine supported ciphers"
url="https://github.com/rbsec/sslscan"
arch=(loong64 x86_64)
license=(GPL-3.0-or-later)
depends=(
  glibc
  openssl
)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('62dbb8c97598cdc5bf22cb8311eaba6cff186f6b874d8eb4165a80d0577cd0a13837222fe6ee4e015c353acef2691ff044f4538c04044869805b5455abf36155')
b2sums=('2c3dd21838bca82035dc4d1c48869d4d2c2a18ea603b947d39040744d2f51c4946e6ca6681d734aafc62cf71b6e380b31f6f29c7164c14c2d75e0d1ab5925245')

prepare() {
  cd "$pkgname-$pkgver"
  sed -e 's/CFLAGS ?=/CFLAGS +=/' \
    -e 's/LDFLAGS ?=/LDFLAGS +=/' \
    -e "s/^GIT_VERSION =.*/GIT_VERSION = ${pkgver}/" \
    -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
