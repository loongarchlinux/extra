# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=libcdio-paranoia
pkgver=10.2+2.0.2
pkgrel=1
pkgdesc='Compact Disc Digital Audio (CDDA) extraction tool using libcdio for CDROM access'
arch=('loong64' 'x86_64')
url='https://www.gnu.org/software/libcdio/'
license=('GPL-3.0-or-later')
depends=('glibc' 'libcdio')
source=("https://github.com/rocky/$pkgname/releases/download/release-$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('b875d45c3f788fce82a5091c7e87869dd4298cd945e7fb873b61b3e613a32e43')
validpgpkeys=('DAA63BC2582034A02B923D521A8DE5008275EC21') # R. Bernstein <rocky@panix.com>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
