# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Patrick Palka <patrick@parcs.ath.cx>
# Contributor: Loui Chang <louipc.ist at gmail com>
# Contributor: Andreas Baumann <abaumann at yahoo dot com>

pkgname=cgit
_commit='09d24d7cd0b7e85633f2f43808b12871bb209d69'
pkgver=1.2.3.r70.g09d24d7
pkgrel=1
pkgdesc='A web interface for git written in plain C'
arch=('loong64' 'x86_64')
url='https://git.zx2c4.com/cgit/'
license=('GPL-2.0-only')
depends=('openssl' 'luajit')
makedepends=('git' 'zlib' 'curl' 'asciidoc')
optdepends=('groff: about page using man page syntax'
            'python-pygments: syntax highlighting support'
            'python-docutils: about page formatted with reStructuredText'
            'python-markdown: about page formatted with markdown'
            'lua51-luaossl: for lua filters'
            'gzip: gzip compressed snapshots'
            'bzip2: bzip2 compressed snapshots'
            'lzip: lzip compressed snapshots'
            'xz: xz compressed snapshots'
            'zstd: zstd compressed snapshots'
            'mime-types: serve file with correct content-type header')
install=cgit.install
validpgpkeys=('AB9942E6D4A4CFC3412620A749FC7012A5DE03AE')
source=("git+https://git.zx2c4.com/cgit.git#commit=${_commit}"
        'git+https://github.com/git/git.git'
        'tmpfiles.conf'
        'apache.example.conf')
sha256sums=('423a95f4fee9bdc5a50e332703d5e5f5289f523f620e275d26b950d92ae5a836'
            'SKIP'
            '4004b72d433e5810b046fc6019171a11a0dae3e9c6b29a44f16ed41705c46c3d'
            '89927d462c0504863c163eb8a210e5d65db30ee6e4300ff6a2f92460e4f20a62')

pkgver() {
  cd cgit/

  GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"
  printf '%s.r%s.g%s' \
    "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG})" \
    "$(git rev-list --count ${GITTAG}..)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  cd cgit/

  # branch 'for-jason' for recent git...
  # Please do *NOT* merge/cherry-pick, as commit hashes may change before landing in master!
  #git apply --index < ../0001-for-jason.patch

  git config --file=.gitmodules submodule.git.url ../git/
  git submodule init
  git -c protocol.file.allow=always submodule update
}

build() {
  cd cgit/

  make all doc-man
}

check() {
  cd cgit/

  make test
}

package() {
  cd cgit/

  make \
    CGIT_SCRIPT_PATH="/usr/share/webapps/${pkgname}" \
    DESTDIR="${pkgdir}" \
    prefix='/usr' \
    filterdir="/usr/lib/${pkgname}/filters" \
    install install-man

  install -vDm0644 "${srcdir}/tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  install -vDm0644 "${srcdir}/apache.example.conf" "${pkgdir}/etc/webapps/${pkgname}/apache.example.conf"
  install -d "${pkgdir}/usr/lib/${pkgname}"
  mv "${pkgdir}/usr/share/webapps/${pkgname}/cgit.cgi" "${pkgdir}/usr/lib/${pkgname}"
  ln -sf ../../../lib/${pkgname}/cgit.cgi "${pkgdir}/usr/share/webapps/${pkgname}/cgit.cgi"
}
