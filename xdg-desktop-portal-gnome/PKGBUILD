# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=xdg-desktop-portal-gnome
pkgver=46.1
pkgrel=1
pkgdesc="A backend implementation for xdg-desktop-portal for the GNOME desktop environment"
url="https://gitlab.gnome.org/GNOME/xdg-desktop-portal-gnome"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  cairo
  dconf
  fontconfig
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop-4
  gnome-keyring
  graphene
  gtk4
  libadwaita
  libx11
  wayland
  xdg-desktop-portal
  'xdg-desktop-portal-gtk>=1.10.0-2'
)
makedepends=(
  git
  meson
  python-packaging
)
optdepends=('evince: Print previews')
provides=(xdg-desktop-portal-impl)
conflicts=('xdg-desktop-portal-gtk<1.10.0-2')
replaces=('xdg-desktop-portal-gtk<1.10.0-2')
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/xdg-desktop-portal-gnome.git#tag=${pkgver/[a-z]/.&}")
b2sums=('40b16d6a21565c899ac977db01d1611ec7e82aadfecd906764a08b30c34c5e7bb426741b8eb90c24014ab06ef693926a4396c287dc48cda2722f4ee3969161b7')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
