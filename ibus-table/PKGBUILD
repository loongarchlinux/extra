# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: coderoar <coderoar@gmail.com>
# Contributor: leemars <leemars@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-table
pkgver=1.17.4
pkgrel=2
pkgdesc='IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on'
arch=('any')
url="https://github.com/kaio/ibus-table"
license=('LGPL')
makedepends=('docbook2x' 'docbook-sgml' 'docbook-utils')
depends=('python-pyenchant' 'ibus' 'aspell-en')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kaio/ibus-table/archive/$pkgver.tar.gz")
sha512sums=('67145d9bc0fcc02226aad6cfb49551348744f7cb13f752e94b0211f38205cc7c1565a5a3a61b4c25189523421112b9df9a5b5b372a2996421edaf4c172a7aaeb')

build() {
  cd $pkgname-$pkgver

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
