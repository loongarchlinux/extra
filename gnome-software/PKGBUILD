# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Yosef Or Boczko <yoseforb@gnome.org>

pkgname=gnome-software
pkgver=46.1
pkgrel=1
pkgdesc="GNOME Software Tools"
url="https://wiki.gnome.org/Apps/Software/"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  appstream
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gsettings-desktop-schemas
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libgudev
  libsoup3
  libsysprof-capture
  libxmlb
  pango
  polkit
)
makedepends=(
  docbook-xsl
  flatpak
  fwupd
  git
  gobject-introspection
  gtk-doc
  libglib-testing
  malcontent
  meson
  python-packaging
)
optdepends=(
  'flatpak: Flatpak support plugin'
  'fwupd: fwupd support plugin'
  'malcontent: Parental control plugin'
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-software.git?signed#tag=${pkgver/[a-z]/.&}"
  "git+https://gitlab.gnome.org/mwleeds/gnome-pwa-list.git"
)
b2sums=('0f7b401da058f279a7b004c5bcb76d3f3118b2f50629a46f008dc7193436645cf9045d91d58aa147e59d4f7ce164f18cfed53cb91b2efb0a8245c735805d9c32'
        'SKIP')
validpgpkeys=(
  2AA441054BD2F1715EC11610FB183E7EF3C36A0D # Milan Crha <mcrha@redhat.com>
)

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url subprojects/gnome-pwa-list "$srcdir/gnome-pwa-list"
  git -c protocol.file.allow=always -c protocol.allow=never submodule update
}

build() {
  local meson_options=(
    -D packagekit=false
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

# Not running tests - need root and a D-Bus system bus

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
