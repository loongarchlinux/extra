# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-minidb
_name=${pkgname#python-}
pkgver=2.0.8
pkgrel=1
pkgdesc='Simple SQLite3 store for Python objects'
arch=('any')
url=https://thp.io/2010/minidb/
license=('ISC')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("git+https://github.com/thp/minidb.git#tag=$pkgver")
b2sums=('23f267bc4cd775206f1df577f1b996380d174d14e68766ae87ca681781b85af0cbc167f06a92f48db0bc5ddd6d6eedb2ef921a1dea2ab9b2694181133c40aeae')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  PYTHONPATH="$PWD" pytest
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  sed -n '/# Copyright/,/OF THIS SOFTWARE./p' minidb.py > "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
