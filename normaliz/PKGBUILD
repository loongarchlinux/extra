# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tarn Burton <twburton at gmail dot com>

pkgname=normaliz
pkgver=3.10.2
pkgrel=1
pkgdesc='A tool for computations in affine monoids, vector configurations, lattice polytopes, and rational cones'
arch=(loong64 x86_64)
url='https://www.normaliz.uni-osnabrueck.de/'
license=(GPL-3.0-or-later)
depends=(flint
         gcc-libs
         glibc
         gmp
         e-antic)
makedepends=(boost
             nauty)
source=(https://github.com/Normaliz/Normaliz/releases/download/v$pkgver/Normaliz-$pkgver.tar.gz)
sha256sums=('0f649a8eae5535c18df15e8d35fc055fd0d7dbcbdd451e8876f4a47061481f07')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
