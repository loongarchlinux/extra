# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-doit
_name="${pkgname#python-}"
pkgver=0.36.0
pkgrel=6
pkgdesc="A task management and automation tool"
arch=(any)
url="https://pydoit.org/"
license=(MIT)
depends=(
  ipython
  python
  python-cloudpickle
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-doit-py
  python-pyflakes
  python-pytest
  python-toml
)
optdepends=(
  'python-toml: for TOML support'
  'python-setuptools: for plugin support'
)
source=($pkgname-$pkgver.tar.gz::https://github.com/pydoit/doit/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('e8cc79e1c1696e0efe9c466625855e75d800d183babcb80c7b82b6e86ca4b2360318cf2d01c89a5af70c4ef3c77d27e3fcfd3ef446e8b10630c63a4f6c4513e5')
b2sums=('f88fbc78059ffce6951cdada6d720e46103c5e06e17015792eb660d099cce09854380e311c71c6e8042c200d77043a2cc49ffef233878ab14ae296003550c117')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build/lib:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 {AUTHORS,CHANGES,CONTRIBUTING.md,README.rst} -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 bash_completion_$_name "$pkgdir/usr/share/bash-completion/completions/$_name"
  install -vDm 644 zsh_completion_$_name "$pkgdir/usr/share/zsh/site-functions/_$_name"
}
