# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Justin Dray
# Contributor: Andy Kluger
# Contributor: Alexander 'hatred' Drozdov
# Contributor: G_Syme
# Contributor: SpepS
# Contributor: Bastien Dejean

pkgname=libicns
pkgver=0.8.1
pkgrel=11
pkgdesc='Library for manipulating icns/IconFamily files'
arch=('loong64' 'x86_64')
url='https://icns.sourceforge.io/'
license=('LGPL-2.1-or-later')
depends=('libpng' 'jasper')
source=("https://downloads.sf.net/icns/$pkgname-$pkgver.tar.gz"
        "Add-missing-headers.patch")
sha256sums=('335f10782fc79855cf02beac4926c4bf9f800a742445afbbf7729dab384555c2'
            'e4d0c96a47b270316bfb598aa61367ca0a5c7cd07651189d0b0c56dc60e69fd0')

prepare() {
    cd "$pkgname-$pkgver"
    patch -p1 < ../Add-missing-headers.patch
}

build() {
    cd "$pkgname-$pkgver"
    ./configure --prefix=/usr
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
