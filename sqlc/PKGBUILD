# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alkindi42

pkgname=sqlc
pkgver=1.26.0
pkgrel=1
pkgdesc='Compile SQL to type-safe Go'
arch=('loong64' 'x86_64')
url='https://sqlc.dev'
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/sqlc-dev/sqlc.git#tag=v$pkgver")
sha512sums=('b8b3977082f7b1fe6b6577606f386731b9e78572d2b2809836778bb09ae8f2e55c585db335acbede42ae29d8836915aeac739497f641344929929f3484e5a99b')
b2sums=('9cde7a2009851e1009f7f77f4fad320c90b5364740c072585adc472081f6c60cfc80ab4e7ccafc7c17d0898983411f2957fb76594b4598ae76665b47a2b4ad85')

prepare() {
  cd "$pkgname"

  # create folder for build output
  mkdir -p build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # build binary
  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build \
    ./cmd/...

  # generate shell completions
  for sh in bash fish zsh; do
    ./build/sqlc completion "$sh" > "build/$sh"
  done
}

check() {
  cd "$pkgname"

  # skip internal/endtoend
  rm -f internal/endtoend/endtoend_test.go

  go test -v ./...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/sqlc

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # shell completions
  install -vDm644 build/bash "$pkgdir/usr/share/bash-completion/completions/sqlc"
  install -vDm644 build/fish "$pkgdir/usr/share/fish/vendor_completions.d/sqlc.fish"
  install -vDm644 build/zsh "$pkgdir/usr/share/zsh/site-functions/_sqlc"
}
