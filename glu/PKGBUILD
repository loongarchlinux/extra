# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=glu
pkgver=9.0.3
pkgrel=2
pkgdesc="Mesa OpenGL utility library"
arch=('loong64' 'x86_64')
url="https://gitlab.freedesktop.org/mesa/glu"
# no license file included, can be found inside the source files
license=('SGI-B-2.0' 'MIT')
depends=('libgl' 'gcc-libs' 'glibc')
makedepends=('meson')
source=(https://mesa.freedesktop.org/archive/glu/glu-$pkgver.tar.xz{,.sig})
sha512sums=('b2781059c0e176192c3fc0d7244645020937a463311171efddb9f35fb94ee43faabcf627fa7f429d48fceaf6dd9c5adb69c86c7a21ec4ea490f4ab143d52e3ba'
            'SKIP')
#validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"
validpgpkeys=('57551DE15B968F6341C248F68D8E31AFC32428A6') # Eric Engestrom <eric@engestrom.ch>

build() {
  arch-meson glu-$pkgver build \
    -D default_library=shared

  # Print config
  meson configure build

  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
