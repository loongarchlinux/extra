# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=kvazaar
pkgver=2.3.1
pkgrel=1
pkgdesc='An open-source HEVC encoder'
arch=('loong64' 'x86_64')
url='https://github.com/ultravideo/kvazaar/'
license=('BSD-3-Clause')
depends=('crypto++')
makedepends=('git')
checkdepends=('ffmpeg' 'hm')
source=("git+https://github.com/ultravideo/kvazaar.git#tag=v${pkgver}"
        'git+https://github.com/ultravideo/greatest.git')
sha256sums=('0de1aff6b72e58dffc70515ba91db46667454e651c231d5d53cba4b31b35c206'
            'SKIP')

prepare() {
    git -C kvazaar submodule init
    git -C kvazaar config --local submodule.greatest.url "${srcdir}/greatest"
    git -C kvazaar -c protocol.file.allow='always' submodule update
    autoreconf -fi kvazaar
}

build() {
    cd kvazaar
    ./configure --prefix='/usr' --with-cryptopp
    make
}

check() {
    make -C kvazaar check
}

package() {
    make -C kvazaar DESTDIR="$pkgdir" install
    install -d -m755 "${pkgdir}/usr/share/licenses/kvazaar"
    mv "${pkgdir}/usr/share/doc/kvazaar"/LICENSE* "${pkgdir}/usr/share/licenses/${pkgname}"
}
