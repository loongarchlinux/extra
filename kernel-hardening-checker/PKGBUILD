# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=kernel-hardening-checker
pkgver=0.6.6
pkgrel=3
pkgdesc='Tool for checking the security hardening options of the Linux kernel'
url="https://github.com/a13xp0p0v/kernel-hardening-checker"
arch=(any)
license=(GPL3)
depends=(python)
makedepends=(git python-setuptools python-build python-installer python-wheel)
replaces=(kconfig-hardened-check)
conflicts=(kconfig-hardened-check)
source=("git+https://github.com/a13xp0p0v/kernel-hardening-checker.git#tag=v${pkgver}")
sha256sums=('b406b0b1174d50b18f50281131c89296c6e26880e3d329a76e967e63690a0e8f')
b2sums=('389f4b4d85180242d2a440019a14462933cf5025743fdf610f418b0e744516da77d527cff7d6c25366f988f7b1da7369cc759ac3460996a00270d6503e37ec34')

build() {
  cd "$pkgname"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
