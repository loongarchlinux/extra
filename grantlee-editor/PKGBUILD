# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=grantlee-editor
pkgver=24.05.2
pkgrel=1
pkgdesc='Editor for Grantlee themes'
arch=(loong64 x86_64)
url='https://kontact.kde.org/'
license=(GPL-2.0-or-later)
depends=(akonadi-contacts
         gcc-libs
         glibc
         grantleetheme
         karchive
         kcompletion
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kio
         kmime
         ktextaddons
         kwidgetsaddons
         kxmlgui
         messagelib
         pimcommon
         qt6-base
         qt6-webengine
         syntax-highlighting)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('8f0e134c6b783d5ee6dc8f2455b9556decf5ce9d9ca5ac7cbe51751262e48345'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
