# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=msolve
pkgver=0.7.1
pkgrel=1
pkgdesc='Library for polynomial system solving through algebraic methods'
url='https://msolve.lip6.fr/'
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(flint
         gcc-libs
         glibc
         gmp)
makedepends=(git)
source=(git+https://github.com/algebraic-solving/msolve#tag=v$pkgver)
sha256sums=('9e988165b31a375e8ece3f77f0e1a5da4e9a9e0d0ac1b6f1b660a66d0465c2a5')

prepare() {
  cd $pkgname
  sed -e '/AX_EXT/d' -i configure.ac # Disable CPU autodetection
  ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
