# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=dolphin
pkgver=24.02.2
pkgrel=2
pkgdesc='KDE File Manager'
arch=(loong64 x86_64)
url='https://apps.kde.org/dolphin/'
license=(LGPL-2.0-or-later)
depends=(baloo
         baloo-widgets
         gcc-libs
         glibc
         kbookmarks
         kcmutils
         kcodecs
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         kfilemetadata
         ki18n
         kiconthemes
         kio
         kio-extras
         kjobwidgets
         knewstuff
         knotifications
         kparts
         kservice
         ktextwidgets
         kuserfeedback
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         phonon-qt6
         plasma-activities
         qt6-base
         solid)
makedepends=(extra-cmake-modules
             kdoctools)
optdepends=('ffmpegthumbs: video thumbnails'
            'kde-cli-tools: for editing file type options'
            'kdegraphics-thumbnailers: PDF and PS thumbnails'
            'kio-admin: for managing files as administrator' 
            'konsole: terminal panel'
            'kio-admin: for managing files as administrator'
            'purpose: share context menu')
groups=(kde-applications
        kde-system)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/system/dolphin/-/commit/a07f2422.patch)
sha256sums=('50844490d45159ca16c1d8d9f5f0701f118772e4b09adad270835b67bb26a701'
            'SKIP'
            '9a0d799832323ca78be78189fcd569b6911f0d024dda6b9429312ebcea4bc22e')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < a07f2422.patch # Fix crash when entering selection mode
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
