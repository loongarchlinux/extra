# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=chronic
pkgname=ruby-${_gemname}
pkgver=0.10.2
pkgrel=4
pkgdesc='Natural language date/time parser written in pure Ruby'
url='https://github.com/mojombo/chronic'
arch=('any')
license=('MIT')
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
  ruby-rake
)
checkdepends=(
  ruby-minitest
)
options=('!emptydirs')
source=(
  "${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
  # https://github.com/mojombo/chronic/pull/414
  "${pkgname}_fix_minitest.patch::${url}/commit/b8a02c1a628e18d5d5cd606468ad00139a3dcbf3.patch"
)
sha512sums=('a8f9b12caf529374a163b13307208272e210d8d885a1bed47b64613dd319cc2cbbf2728c9a171f603bc2fe7a1383dd17b35f690b8597462fb329dbe4afe46c6e'
            '0d24db08c973d46c0171bc75db9dcadd61518f37a2932407eb50351f3a8c11a251a337c19ce3e2105450cd923a8a19d6e99daddfe2738375070530c62cc23790')
b2sums=('4dcd9a8dbbe0a4ea0177df85e5716dce5e696fddefdd21a8bf6ccf13a2b8fdc68ce52f3604b87216186862e613a394d8766e673bd4544305c94a2b790c314a38'
        '5283f48d739000d20df9f4e85734f2a332ad0a39af3e5234a6a9a1f911706535f1bcb26abda6b8910435c039c58d94f474d9694b802470d5f03e3f5a8d6d96a2')

prepare() {
  cd ${_gemname}-${pkgver}

  patch --verbose --strip=1 --input="../${pkgname}_fix_minitest.patch"

  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

check() {
  cd ${_gemname}-${pkgver}
  MC_COMPAT="true" rake test
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md HISTORY.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  rm -r "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/test"
}

# vim: ts=2 sw=2 et:
