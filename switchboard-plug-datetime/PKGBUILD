# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-datetime
pkgver=2.2.0.r194.3d22837
pkgrel=1
pkgdesc='Switchboard Date & Time Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-datetime
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_commit=3d22837762f05d25412498650364cfab3425af59
source=(git+https://github.com/elementary/switchboard-plug-datetime.git#commit=${_commit})
b2sums=('8cb3e8a607ef1653e75a939b425cb44bab831fbd163992180176881322a7122a0c6a04d35fc294d069ce4c122321ea3add0521e2df745cf022a7748312960aca')

pkgver() {
  cd switchboard-plug-datetime
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-datetime build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
