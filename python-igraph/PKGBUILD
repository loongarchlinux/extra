# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukasz Pozarlik <lpozarlik@gmail.com>

_pyname=igraph
pkgname=python-$_pyname
pkgver=0.11.5
pkgrel=1
url='https://igraph.org/python/'
license=(GPL-2.0-or-later)
arch=(loong64 x86_64)
pkgdesc='Python bindings for the igraph library'
depends=(glibc
         igraph
         python
         python-numpy
         python-texttable)
makedepends=(git
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/igraph/python-igraph#tag=$pkgver)
sha256sums=('7a123df3ba069265f4f97fffcf8bc3961518979e92753cea2d3159d0f3a791f2')

build() {
  cd $pkgname
  IGRAPH_USE_PKG_CONFIG=1 \
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  PYTHONPATH="$PWD"/`ls -d build/lib.*` python -m unittest
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl 
}
