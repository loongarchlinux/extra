# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=xdg-desktop-entry
pkgname=haskell-xdg-desktop-entry
pkgver=0.1.1.2
pkgrel=6
pkgdesc="Parse files conforming to the xdg desktop entry spec"
url="https://github.com/taffybar/xdg-desktop-entry"
license=("BSD")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-either' 'haskell-ini' 'haskell-multimap' 'haskell-safe'
         'haskell-unordered-containers')
makedepends=('ghc' 'haskell-hspec' 'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('cfe040c6aa615c917a25ff1c27bfb556ba08aa4a079506e34ecd76c5d57fb6a0')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
