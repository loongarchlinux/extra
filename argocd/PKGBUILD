# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=argocd
pkgver=2.12.0
# find matching argo-ui commit via ui/yarn.lock
# commit id also needs to be updated in argo-ui.patch
_argoui_commit=4836620cfc729d7c732b9b562fc4c03051e9598a
pkgrel=1
pkgdesc="Declarative continuous deployment for Kubernetes"
arch=('loong64' 'x86_64')
url='https://argo-cd.readthedocs.io/en/stable/'
license=('Apache-2.0')
makedepends=('go' 'python' 'yarn')
depends=('glibc')
provides=('argocd-cli')
replaces=('argocd-cli')
source=("argo-cd-${pkgver}.tar.gz::https://github.com/argoproj/argo-cd/archive/v${pkgver}.tar.gz"
        "argo-ui-${_argoui_commit}.tar.gz::https://github.com/argoproj/argo-ui/archive/${_argoui_commit}.tar.gz"
        "argo-ui.patch")
sha512sums=('8bea76cbe372928b40c379a70c3f7b0bc797145d5b703131f512268ea2c71b21d35db552f079f3c8fdf5145a9aa036233dfb34b5d5830d1c0060ea70f316b437'
            '5ad27a2f1f460d6cbdf2b6d5fc1c35497951ead5b1251ad1cbad771ab267e09b8815a838fae538f35c5417b3f6b3ac6429cfe2a1fc59d4dfd91b53d6380ce134'
            '630518871a74b070eacad2401e50f4741b0296237e4b6882d0193b6f44b1a4a0c936934738f66f5a88a488366df5e2ed7283e19f3fcdafc1736b48f8233e9583')

prepare() {
  cd "argo-cd-$pkgver"

  patch -p0 -i ../argo-ui.patch

  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "argo-cd-$pkgver"

  (
    cd ui
    yarn install --cache-folder "${srcdir}/yarn-cache"
    ARGO_VERSION="$pkgver" NODE_ENV='production' NODE_ONLINE_ENV='online' yarn build
  )

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOPATH="${srcdir}"
  export GOPROXY=off
  go build -o argocd \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-X github.com/argoproj/argo-cd/v2/common.version=${pkgver} \
              -X github.com/argoproj/argo-cd/v2/common.buildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH}) \
              -X github.com/argoproj/argo-cd/v2/common.gitTag=${pkgver} \
              -compressdwarf=false \
              -linkmode=external \
              -extldflags \"${LDFLAGS}\"" \
    ./cmd

  ./argocd completion bash > argocd.bash
  ./argocd completion zsh > argocd.zsh
}

package() {
  cd "argo-cd-$pkgver"

  install -D argocd "$pkgdir/usr/bin/argocd"
  ln -s argocd "$pkgdir/usr/bin/argocd-server"
  ln -s argocd "$pkgdir/usr/bin/argocd-application-controller"
  ln -s argocd "$pkgdir/usr/bin/argocd-repo-server"
  ln -s argocd "$pkgdir/usr/bin/argocd-cmp-server"
  ln -s argocd "$pkgdir/usr/bin/argocd-dex"
  ln -s argocd "$pkgdir/usr/bin/argocd-notifications"
  ln -s argocd "$pkgdir/usr/bin/argocd-git-ask-pass"

  install -Dm644 argocd.bash "$pkgdir/usr/share/bash-completion/completions/argocd"
  install -Dm644 argocd.zsh "$pkgdir/usr/share/zsh/site-functions/_argocd"
}
