# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-ml-commons-plugin
pkgver=2.13.0.0
pkgrel=1
_opensearchver=2.13.0
_jdkver=11
pkgdesc="OpenSearch Machine Learning Commons Plugin"
arch=('loong64' 'x86_64')
url="https://opensearch.org/docs/latest/ml-commons-plugin/index/"
license=('Apache-2.0')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/ml-commons/archive/${pkgver}.tar.gz"
  "set_only_run_on_ml_node_default_value_to_false.patch::https://github.com/opensearch-project/ml-commons/commit/410753ab1e9ab420b7152dd15af782711743eac2.patch"
)
sha256sums=('ab15b4366e231a38b4ea18e195e91a8ed6d06992d3589fc5d4eeac3ef40cc4f4'
            '2b96f121409e295af42e220c3a364a76e4585668e424cf265592cedf2ea806fe')

prepare() {
  cd "ml-commons-${pkgver}"
  patch -Np1 < "$srcdir/set_only_run_on_ml_node_default_value_to_false.patch"
}

build() {
  cd "ml-commons-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  ./gradlew build \
    --exclude-task ":opensearch-ml-algorithms:jacocoTestReport" \
    --exclude-task ":opensearch-ml-algorithms:jacocoTestCoverageVerification" \
    --exclude-task ":opensearch-ml-common:jacocoTestReport" \
    --exclude-task ":opensearch-ml-common:jacocoTestCoverageVerification" \
    --exclude-task ":opensearch-ml-client:jacocoTestReport" \
    --exclude-task ":opensearch-ml-client:jacocoTestCoverageVerification"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-ml"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-ml"
  unzip "${srcdir}/ml-commons-${pkgver}/plugin/build/distributions/opensearch-ml-${pkgver}.zip"

  install -Dm644 "${srcdir}/ml-commons-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
