# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=contourpy
pkgname=python-$_pyname
pkgver=1.1.1
pkgrel=1
pkgdesc='Python library for calculating contours in 2D quadrilateral grids'
arch=(loong64 x86_64)
url='https://github.com/contourpy/contourpy'
license=(BSD)
makedepends=(python-build python-installer meson-python pybind11)
depends=(python-numpy)
checkdepends=(python-pytest python-matplotlib python-wurlitzer)
optdepends=('python-matplotlib: matplotlib renderer')
source=(https://pypi.python.org/packages/source/${_pyname:0:1}/$_pyname/$_pyname-$pkgver.tar.gz)
sha256sums=('96ba37c2e24b7212a77da85004c38e7c4d155d3e72a45eeaf22c1f03f607e8ab')

prepare() {
  sed -e '/ninja/d' -i $_pyname-$pkgver/pyproject.toml
}

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
