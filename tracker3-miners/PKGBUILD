# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=tracker3-miners
pkgver=3.7.3
pkgrel=2
pkgdesc="Filesystem indexer and metadata extractor"
url="https://tracker.gnome.org/"
arch=(loong64 x86_64)
license=(
  LGPL-2.1-or-later
  GPL-2.0-or-later
)
depends=(
  dconf
  exempi
  gcc-libs
  giflib
  glib2
  glibc
  gstreamer
  gst-plugins-base-libs
  icu
  libcue
  libexif
  libgexiv2
  libgrss
  libgsf
  libgxps
  libiptcdata
  libjpeg-turbo
  libnm
  libosinfo
  libpng
  libseccomp
  libtiff
  poppler-glib
  totem-plparser
  tracker3
  upower
  util-linux-libs
  libxml2
)
makedepends=(
  asciidoc
  git
  meson
  python-packaging
)
groups=(gnome)
source=(
  "git+https://gitlab.gnome.org/GNOME/tracker-miners.git#tag=$pkgver"
)
b2sums=('bd6bee52a297b443890835abfb73744848e39d41511cd21d21491d26c5fd624c33ad96965eccaddb71572a12b7f61e44c4dd8147e2c31eee527b119e3d077b10')

prepare() {
  cd tracker-miners
}

build() {
  local meson_options=(
    -D tests_tap_protocol=true
    -D landlock=enabled
  )

  arch-meson tracker-miners build "${meson_options[@]}"
  meson compile -C build
}

package() {
  depends+=(libtracker-sparql-3.0.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
