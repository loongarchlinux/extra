# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

_name=progressbar2
pkgname=python-progressbar
pkgdesc="A progress bar for Python"
pkgver=4.4.2
pkgrel=2
arch=(any)
url="https://github.com/WoLpH/python-progressbar"
license=(BSD-3-Clause)
depends=(
  python
  python-utils
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-sphinx
  python-wheel
)
checkdepends=(
  python-dill
  python-pytest
  python-freezegun
)
source=($url/releases/download/v$pkgver/$_name-$pkgver.tar.gz{,.asc})
sha512sums=('705383badb3943d3d339854d02ee4114f0e76261373deb8f9aff00fe6aae7f37bacccbbc673c2cb36114c7f8ebbafa96a06408ce6e90c3d26a2e2c65ece86958'
            'SKIP')
b2sums=('67625a794bd19a4ed78be25789980038c69a6590277f085befeef805397972ff1160543814523c425218e019647f1c1641451198e2dd08a5f8eacd9e507a04af'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

build() {
  cd $_name-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
