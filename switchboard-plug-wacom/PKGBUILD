# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-wacom
pkgver=8.0.0
pkgrel=1
pkgdesc='Switchboard wacom Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-wacom
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  cairo
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libgudev-1.0.so
  libswitchboard-3.so
  libwacom
  libx11
  libxi
)
makedepends=(
  git
  meson
  vala
)
_tag=d61b1bfd6e12f3071ee5691ea8891ec9be19af4e
source=(git+https://github.com/elementary/switchboard-plug-wacom.git#tag=${_tag})
sha256sums=('fb1091de46411bc96c979cca3d26da1fd0635fb5d9d418190803e575c5271842')

pkgver() {
  cd switchboard-plug-wacom
  git describe --tags
}

build() {
  arch-meson switchboard-plug-wacom build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
