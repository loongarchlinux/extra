# Maintainer: Robin Broda <robin@broda.me>
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Justin Dray <justin@dray.be>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas S Hatch <thatch45 ar gmail dot com>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Matt Heagney <matt@heagney.com>

pkgname=multipath-tools
pkgver=0.9.9
pkgrel=1
pkgdesc='Multipath tools for Linux (including kpartx)'
arch=('loong64' 'x86_64')
url='https://github.com/opensvc/multipath-tools'
license=('GPL2')
depends=('libaio' 'device-mapper' 'json-c' 'liburcu' 'systemd-libs')
makedepends=('git' 'systemd')
source=("multipath-tools::git+https://github.com/opensvc/multipath-tools#tag=${pkgver}")
sha256sums=('92e96963707c389f545089d31006918dbd3ab021f0f5368a14e4b10aba175b8e')

_make_opts=(
  prefix='/usr/'
  bindir='/usr/bin'
  etc_prefix=""
  configfile='/etc/multipath.conf'
  statedir='/etc/multipath'
  LIB='lib'
)


build() {
  cd "${pkgname}"

  make \
    "${_make_opts[@]}" \
    FORTIFY_OPT=''
}

package() {
  cd "${pkgname}"

  make -j1 \
    "${_make_opts[@]}" \
    DESTDIR="${pkgdir}" \
    install

  install -D -m0644 -t "${pkgdir}/usr/share/doc/${pkgname}" README*
}
