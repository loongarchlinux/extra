# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nethsm-pkcs11
pkgver=1.3.1
pkgrel=1
pkgdesc="PKCS#11 driver for NetHSM"
arch=(loong64 x86_64)
url="https://github.com/Nitrokey/nethsm-pkcs11"
license=(Apache-2.0)
depends=(glibc)
makedepends=(rust)
options=(!lto)  # NOTE: as ring is used we can not use LTO
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
sha512sums=('69b20cbfa57e87cbf1c84c2093bbd98095dcd8478a95762528e440f38b9280f8c33d1207f958f36f21578e40b3eed3210ad3032e5ac6448a91328ef99de47c39')
b2sums=('42d8c48eeddc068f343569a99aa96795b3f7b42bc2e712934c0b781e067385675e7ed843e9c8a103ee6f5293a576303963e7efff57776660bad499f915c99426')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/lib${pkgname/-/_}.so -t "$pkgdir/usr/lib/"
  install -vDm 644 {{README,features}.md,p11nethsm*.conf} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vdm 700 "$pkgdir/etc/nitrokey/"
}
