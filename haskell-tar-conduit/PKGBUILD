# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tar-conduit
pkgname=haskell-tar-conduit
pkgver=0.4.1
pkgrel=27
pkgdesc="Extract and create tar files using conduit for streaming"
url="https://github.com/snoyberg/tar-conduit"
license=('MIT')
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-conduit' 'haskell-safe-exceptions')
makedepends=('ghc' 'haskell-conduit-extra' 'haskell-hspec' 'haskell-quickcheck' 'haskell-weigh')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('cf11bdd4dee3666673a18a0e045524a9c697d9be6c631d0745ee4d791fbad334fc7291d735f972bce5c38996fc59ff365d59ebfdf3a71764d15837fa684524e0')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '/conduit-combinators/d' $_hkgname.cabal

    # S8.toStrict is not available in our bytestring version
    sed -i 's/S8.toStrict/LS8.toStrict/g' tests/Spec.hs
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
