# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgbase=sysdig
pkgname=(sysdig sysdig-dkms)
pkgver=0.36.1
pkgrel=1

# from src/sysdig-*/cmake/modules/falcosecurity-libs.cmake
_falcover=0.15.1

pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('loong64' 'x86_64')
url="https://www.sysdig.com/"
license=('Apache-2.0')
makedepends=('cmake' 'pandoc' 'gtest' 'jsoncpp' 'libjsoncpp.so' 'luajit' 'curl' 'libcurl.so'
             'jq' 'libb64' 'intel-tbb' 'grpc' 'yaml-cpp' 're2' 'uthash')
options=('!lto')
source=("https://github.com/draios/sysdig/archive/$pkgver/$pkgbase-$pkgver.tar.gz"
        "falcosecurity-libs-$_falcover.tar.gz::https://github.com/falcosecurity/libs/archive/$_falcover.tar.gz"
        "bashcomp-location.patch"
        "falcosecurity-libs-nodownload.patch")
sha256sums=('e0737ce660637d72859822b5bec835d8e0e3337620a366d0e9c5dc5a1300feb4'
            '8c2596fec3c25e9718a8b357b0504450cd26efbe653b79afcd66a8405f0dc08c'
            '3b659326176c314eee9115adac39a249dc4b9530511b344ea6a2b23236bb8386'
            '3392204c265ef46c2a1378fc2acbb74b2b440585de4c9127a007f97ce10f0cfa')

prepare() {
  cd "$srcdir/libs-$_falcover"
  echo 'target_link_libraries(sinsp INTERFACE zstd)' >> userspace/libsinsp/CMakeLists.txt

  cd "$srcdir/$pkgbase-$pkgver"
  patch -p1 -i "$srcdir"/bashcomp-location.patch
  patch -p1 -i "$srcdir"/falcosecurity-libs-nodownload.patch

  sed s/USE_BUNDLED_DEPS/USE_BUNDLED_NJSON/ -i cmake/modules/nlohmann-json.cmake
  # Fix build with abseil-cpp 2023
  sed -e 's|-std=c++0x||' -i CMakeLists.txt -i ../libs-${_falcover}/cmake/modules/CompilerFlags.cmake
}

build() {
  cd "$srcdir"/$pkgbase-$pkgver
  rm -rf build
  mkdir build
  cd build

  cmake .. \
        -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_CXX_FLAGS="${CXXFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_CXX_STANDARD=17 \
        -DCMAKE_CXX_EXTENSIONS=OFF \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSDIG_VERSION=$pkgver \
        -DDRIVER_VERSION=$_falcover \
        -DUSE_BUNDLED_DEPS=OFF \
        -DUSE_BUNDLED_TBB=OFF \
        -DUSE_BUNDLED_B64=OFF \
        -DUSE_BUNDLED_JSONCPP=OFF \
        -DUSE_BUNDLED_RE2=OFF \
        -DUSE_BUNDLED_VALIJSON=ON \
        -DUSE_BUNDLED_NJSON=ON \
        -DUSE_BUNDLED_TINYDIR=ON \
        -DBUILD_DRIVER=OFF \
        -DBUILD_LIBSINSP_EXAMPLES=OFF \
        -DBUILD_LIBSCAP_EXAMPLES=OFF
  make
}

package_sysdig() {
  optdepends=('sysdig-dkms: kernel module for live inspection')
  depends=('jsoncpp' 'libjsoncpp.so' 'luajit' 'curl' 'libcurl.so' 'jq' 'libb64' 'intel-tbb' 'grpc' 'libgrpc++.so'
           'uthash' 'yaml-cpp' 'zstd')

  cd "$srcdir"/$pkgbase-$pkgver/build
  make install DESTDIR="$pkgdir"
  rm -rf "$pkgdir"/usr/{include,lib,src} "$pkgdir"/sysdig

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 "$srcdir"/$pkgbase-$pkgver/{NOTICES,COPYING} "$pkgdir"/usr/share/licenses/$pkgname
}

package_sysdig-dkms() {
  pkgdesc="DKMS kernel module for sysdig"
  depends=('sysdig' 'dkms')

  cd "$srcdir"/$pkgbase-$pkgver/build
  make install DESTDIR="$pkgdir"
  rm -rf "$pkgdir"/usr/{bin,include,lib,share} "$pkgdir"/sysdig

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 "$srcdir"/$pkgbase-$pkgver/{NOTICES,COPYING} "$pkgdir"/usr/share/licenses/$pkgname
}
