# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-screensaver
pkgver=1.28.0
pkgrel=1
pkgdesc='Screensaver for MATE'
url="https://mate-desktop.org"
arch=('loong64' 'x86_64')
license=('GPL')
depends=('mate-panel' 'libmatekbd' 'gettext' 'libnotify' 'libxss' 'mate-desktop' 'mate-menus' 'mate-session-manager')
makedepends=('systemd')
groups=('mate-extra')
conflicts=('mate-screensaver-gtk3')
replaces=('mate-screensaver-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('6a0f24a8f84a2f95e10114ab53e63fd4aca688a55fdc503ed650e0a410e3ea70')

build() {
	cd "${pkgname}-${pkgver}"
	./configure \
	           --prefix=/usr \
	           --libexecdir=/usr/lib/${pkgname} \
	           --sysconfdir=/etc \
	           --with-xscreensaverdir=/usr/share/xscreensaver/config \
	           --with-xscreensaverhackdir=/usr/lib/xscreensaver \
	           --with-mit-ext \
	           --with-libnotify \
	           --enable-locking \
	           --without-console-kit
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
