# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-inter
pkgver=1.11
pkgrel=1
pkgdesc='Framework for more readable interactive test scripts'
arch=('any')
url="https://metacpan.org/release/Test-Inter"
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/S/SB/SBECK/Test-Inter-$pkgver.tar.gz")
sha512sums=('a7e3f6f49e4a6ba60679a5c203c33585d42a2b930354bb2b948554f476409971ecf789f607fb15c5b7af785f45ff55543ef02974b91006a726fdbf27d788a5a9')

build() {
  cd Test-Inter-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test-Inter-$pkgver
  make test
}

package() {
  cd Test-Inter-$pkgver
  make DESTDIR="$pkgdir" install
}
