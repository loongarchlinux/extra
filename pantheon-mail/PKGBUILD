# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>
# Contributor: Riley Trautman <asonix@tamu.edu>

pkgname=pantheon-mail
pkgver=8.0.0
pkgrel=1
pkgdesc='Mail app designed for elementary OS'
arch=(loong64 x86_64)
url=https://github.com/elementary/mail
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libcamel-1.2.so
  libedataserver-1.2.so
  libedataserverui-1.2.so
  libfolks.so
  libgranite.so
  libhandy-1.so
  libportal-gtk3
  webkit2gtk
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=21df72f7d4c15588116f7e7e76820f9c0a542913
source=(pantheon-mail::git+https://github.com/elementary/mail.git#tag=${_tag})
sha256sums=('b6735f0c6947e208400a0a72c66dad5e2d5996ab1b45275c3413c746bf3672f9')

pkgver() {
  cd pantheon-mail
  git describe --tags
}

build() {
  arch-meson pantheon-mail build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
