# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=linuxsampler
pkgver=2.3.1
pkgrel=1
pkgdesc="Professional-grade audio sampler alternative to Gigasampler"
arch=(loong64 x86_64)
url="https://www.linuxsampler.org/"
license=(LicenseRef-LinuxSampler-Non-Commercial)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  sqlite
)
makedepends=(
  alsa-lib
  dssi
  jack
  ladspa
  libgig
  libsndfile
  lv2
)
optdepends=(
  'dssi-host: for DSSI plugin'
  'lv2-host: for LV2 plugin'
)
provides=(liblinuxsampler.so)
source=(
  https://download.linuxsampler.org/packages/$pkgname-$pkgver.tar.bz2
  $pkgname-2.2.0-libdir.patch
  $pkgname-2.2.0-libgig_package.patch
)
sha512sums=('160820d1a1c44169bb84fd5b6380256af31e42701ff2d2e4394df8c680fa0b6f9b8a21047ff2ec757009f53d1694bafdf254067d8aae9994c244aaf247a5890d'
            'fd552a6efff47ddd0114f7d394d699ba2df624c5a9dfd3b88f754f38b4aa7c9e04523b79b179e1f4d8f73cb142158134d74a11eb8718f8a22ace2a0d5b2f0c86'
            '84c5e2a322783e33e83aa319466c5e49f32d4b5175f63515df7857ddcdb9d2a3a11afde376b77d1f18c474ab341d42d2d1e018ab2525971b680fe36abbd221d1')
b2sums=('e07866d1ccaadf4bb95e4bb17f4b643b8dec5ff8c3b363b0e55b7fcb1383be59d2a22c84536db3e31f7c67467ef293a01147120294884005bd1d4c1f5b6c326b'
        '98df780dfe7bf1a0379e7b4a15390aab411b1a5b8ef5c9c9fe04cdcd963ed572eeeaa00eb184017b61248bc396f58cfca571a748d6d17caf5030a447a05ebbf0'
        '599ff6db7c87c4c4ee2306f62c6bec2a612d3c556c371a660430d09c93cda7b0b951f78e742d679407215fabb60939619fc9edcbf3813bf1665a9ee5fe0ddf95')

prepare(){
  # install shared object in global namespace
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-2.2.0-libdir.patch
  # fix generation of instruments in package()
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-2.2.0-libgig_package.patch

  # extract custom license exception
  sed -n '11,15p' $pkgname-$pkgver/README > LinuxSampler-Non-Commercial.txt
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  depends+=(
    alsa-lib libasound.so
    jack libjack.so
    libgig libgig.so
    libsndfile libsndfile.so
  )

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 ../LinuxSampler-Non-Commercial.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  # lscp files conflict with nilfs-utils:
  # https://bugs.archlinux.org/task/45827
  mv -v "$pkgdir/usr/bin/lscp" "${pkgdir}/usr/bin/lscp-$pkgname"
  mv -v "$pkgdir/usr/share/man/man1/lscp.1" "$pkgdir/usr/share/man/man1/lscp-$pkgname.1"
}
# vim:set ts=2 sw=2 et:
