# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-njs
pkgver=0.8.5
pkgrel=1

_modname="${pkgname#nginx-mod-}"

pkgdesc='nginScript module for nginx'
arch=('loong64' 'x86_64')
depends=('glibc' 'libxslt' 'openssl' 'zlib' 'pcre2' 'libxml2')
makedepends=('nginx' 'nginx-src')
url="https://nginx.org/en/docs/njs_about.html"
license=('BSD-2-Clause')

source=(https://github.com/nginx/njs/archive/refs/tags/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('b5e38e1424d84e48c6ca1592a336bf1d110b8f27ff1d5ef492c6d312ea003491')

prepare() {
  mkdir -p build
  cd build
  ln -sf /usr/src/nginx/auto
  ln -sf /usr/src/nginx/src
}

build() {
  cd "$srcdir/njs-$pkgver"
  CFLAGS="$CFLAGS -Wno-dangling-pointer -Wno-maybe-uninitialized" ./configure
  make njs

  # next configure cleans the build directory
  mv build/njs ../build/

  cd "$srcdir/build"
  /usr/src/nginx/configure \
    --with-ld-opt="$LDFLAGS" \
    --with-compat \
    --with-stream \
    --with-http_ssl_module \
    --with-http_v2_module \
    --add-dynamic-module=../njs-$pkgver/nginx
  make modules
}

package() {
  local _nginx_version=$(nginx -v 2>&1)
  _nginx_version=${_nginx_version/* nginx\/}
  depends+=("nginx=${_nginx_version}")

  install -Dm644 "$srcdir"/njs-$pkgver/LICENSE \
              "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  install -Dm755 build/njs "$pkgdir"/usr/bin/njs

  cd build/objs
  for mod in *.so; do
      install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
  done
}
