# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: damir <damir@archlinux.org>

pkgname=gnugo
pkgver=3.8
pkgrel=8
pkgdesc='Program that plays the game of Go'
url='https://www.gnu.org/software/gnugo/'
license=(GPL-3.0-or-later)
arch=(loong64 x86_64)
depends=(glibc
         ncurses)
source=(https://ftp.gnu.org/gnu/gnugo/${pkgname}-${pkgver}.tar.gz{,.sig}
        Wformat-security.patch)
sha256sums=('da68d7a65f44dcf6ce6e4e630b6f6dd9897249d34425920bfdd4e07ff1866a72'
            'SKIP'
            'c5c1c45ca30793a4c872aa6b2c5d2dc3ca201831c20a4bb595d82c8cf27eb20e')
validpgpkeys=('67CCE2F5220D3E03024A175B68E7E09135C3EC3E')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../Wformat-security.patch # Fix build with -Wformat-security
}

build() {
  cd $pkgname-$pkgver
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./configure \
    --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
