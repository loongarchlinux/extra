# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=cargo-watch
pkgver=8.5.2
pkgrel=1
pkgdesc="Utility for Cargo to compile projects when sources change"
url='https://github.com/watchexec/cargo-watch'
arch=('loong64' 'x86_64')
license=('CC0-1.0')
depends=('cargo')
makedepends=('ruby-ronn')
source=("https://github.com/watchexec/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('850b8de75c618fa1fcf5e2d56a6b2477e7224fbdfa793f5b5f30b31bf63fb358')
b2sums=('f6b1a250b0ba4a79d525f7d2038f1a4ddae1495261c38cff1c32f6f73dae500a689dfcd9059c86c637287b6ec1d4841b494984746a270de03a3ee7b7c27dc697')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
  ronn --roff cargo-watch.1.ronn
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm 644 completions/zsh "${pkgdir}/usr/share/zsh/site-functions/_cargo-watch"
  install -Dm 644 cargo-watch.1 -t "${pkgdir}/usr/share/man/man1"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
