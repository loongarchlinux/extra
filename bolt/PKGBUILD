# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=bolt
pkgver=0.9.8
pkgrel=1
pkgdesc="Thunderbolt 3 device manager"
arch=('loong64' 'x86_64')
url="https://gitlab.freedesktop.org/bolt/bolt"
license=('LGPL-2.1-or-later')
depends=('polkit' 'systemd')
makedepends=('asciidoc' 'meson' 'python-setuptools' 'patch')
checkdepends=('umockdev')
source=(https://gitlab.freedesktop.org/$pkgname/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('5a4306aa21ee398e1e9f2a5072748c9469c9360bf5edc7dcec2f12fc17be122e')

build() {
  cd $pkgname-$pkgver

  arch-meson ../build
  meson compile -v -C ../build
}

check() {
  cd $pkgname-$pkgver

  meson test -C ../build --print-errorlogs
}

package() {
  cd $pkgname-$pkgver

  meson install -C ../build --destdir "${pkgdir}"
}
