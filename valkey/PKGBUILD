# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>
# Contributor: nofxx <x@<nick>.com>

pkgname=valkey
pkgver=7.2.6
pkgrel=1
pkgdesc='An in-memory database that persists on disk'
arch=('loong64' 'x86_64')
url='https://valkey.io/'
license=('BSD')
depends=(
  'grep'
  'jemalloc'
  'shadow'
  'systemd-libs'
)
# pkg-config fails to detect systemd libraries if systemd is not installed
makedepends=(
  'systemd'
  'openssl'
)
conflicts=(
  'redis'
)
backup=(
  'etc/valkey/valkey.conf'
  'etc/valkey/sentinel.conf'
)
install=valkey.install
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/valkey-io/valkey/archive/${pkgver}.tar.gz"
  valkey.service
  valkey-sentinel.service
  valkey.sysusers
  valkey.tmpfiles
  valkey.conf-sane-defaults.patch
  valkey-5.0-use-system-jemalloc.patch
)
sha512sums=('99005b82ecbcdfde38ff823aff149b9f8c97052641b098a3197bab9fb6804b068a3ecedce11b99ef790af4ceb04c2eea25ba3ec37e39be6bb9382319933226af'
            '286ce5be525166814f8ffce64fdb8ddc3abbaefe75bfc3044e8a4ee2111359fda3b35b3e548538bc05e4393bb4f5c716f549a23d069e5c1f4327b9a60594fbb9'
            'f1f8265106604c068e1d1ef4bb5755ff85af7965b40b2515adcbe65780a4b45ca9fcdda3c4e6cab52bf9483e0a1d95d11c18a15eb6e504c22591c88c71fd3039'
            'd1f494a066bcfa24140292522560de97431c2cc458ea3fcdd24b65702d1abd04fa117faffde48e01983082bf84990b02d1d0be0073aa51170d906bc65baa229c'
            '11cf6d6999329af7a9fa4bcbbcf22242b461cec0c16ad949cc6b0383703f19417092782569bf6224f94167a560de0b4ba53ec0d8522683736a14f01bc5986b28'
            '5ec15f8380c9e0515f120288ea8b53e62c754054e5c51c30ca7e60f0293b36c82ab3e3c5f8373b3eff9d98378849a6fba0d12b7088c7c63ca46464410a645dc8'
            '0acb08a6e0eaba239db7461bcfeddfbe0c1aaa517dc33c3918c9e991a1d5067cfe135b7f75085caade8c3ababd51ec9cefcc4120f57818bea1f7029a548a7732')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 < ../valkey.conf-sane-defaults.patch
  patch -Np1 < ../valkey-5.0-use-system-jemalloc.patch
}

build() {
  make BUILD_TLS=yes \
       USE_SYSTEMD=yes \
       -C $pkgname-$pkgver
}

package() {
  cd $pkgname-$pkgver
  make PREFIX="$pkgdir"/usr install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/valkey/LICENSE
  install -Dm644 -t "$pkgdir"/etc/valkey valkey.conf sentinel.conf
  install -Dm644 -t "$pkgdir"/usr/lib/systemd/system/ ../valkey.service ../valkey-sentinel.service
  install -Dm644 "$srcdir"/valkey.sysusers "$pkgdir"/usr/lib/sysusers.d/valkey.conf
  install -Dm644 "$srcdir"/valkey.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/valkey.conf
}
