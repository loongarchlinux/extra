# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>

pkgname=python-cogapp
pkgver=3.3.0
pkgrel=5
pkgdesc="A code generator for executing Python snippets in source files"
url="https://nedbatchelder.com/code/cog"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest')
conflicts=('cocogitto')
source=("https://pypi.io/packages/source/c/cogapp/cogapp-$pkgver.tar.gz")
sha512sums=('937b9372095ed0805d57083ff1642c66fc231ab65b09156fc0966df4439ced98f220d85e4ebca206b56969e1d721ff7dbb2c2d2550852b41a0dc17594317c322')

build() {
  cd cogapp-$pkgver
  python -m build -wn
}

check() {
  cd cogapp-$pkgver
  pytest
}

package() {
  cd cogapp-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt
}
