# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=konsolepart5
pkgver=23.08.5
pkgrel=1
arch=(loong64 x86_64)
url='https://apps.kde.org/konsole/'
pkgdesc='KDE terminal emulator'
license=(GPL LGPL FDL)
depends=(gcc-libs
         glibc
         icu
         kbookmarks5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kcrash5
         kglobalaccel5
         kguiaddons5
         ki18n5
         kiconthemes5
         kio5
         kjobwidgets5
         knewstuff5
         knotifications5
         knotifyconfig5
         kparts5
         kpty5
         kservice5
         ktextwidgets5
         kwidgetsaddons5
         kwindowsystem5
         kxmlgui5
         qt5-base
         qt5-multimedia)
makedepends=(extra-cmake-modules)
conflicts=('konsole<24')
source=(https://download.kde.org/stable/release-service/$pkgver/src/konsole-$pkgver.tar.xz{,.sig})
sha256sums=('c6fb20753ac27a46bce62e31f7b105b6b99497035830b34a7bba1bc60260c1ca'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S konsole-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/{etc,usr/{bin,lib/kconf_update_bin,share}}
  install -Dm644 konsole-$pkgver/desktop/konsolepart.desktop -t "$pkgdir"/usr/share/kservices5
}
