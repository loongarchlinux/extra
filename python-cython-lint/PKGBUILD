# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-cython-lint
_name="${pkgname#python-}"
pkgver=0.16.0
pkgrel=2
pkgdesc='A tool to lint Cython files'
arch=('any')
url='https://github.com/MarcoGorelli/cython-lint'
license=('MIT')
depends=(
  'python'
  'cython'
  'python-tokenize-rt'
  'python-pycodestyle'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
checkdepends=('python-pytest')
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('784172d204b575b9e7c4009dd8e6e65ca7d25b4a5fd13edfe5c464d9b6d385fce2232ac92c606663623fd1aad2cf2f4a99742d8cd458a0393f4d7ef91188ec76')

build() {
  cd "$_name-$pkgver"

  python -m build --wheel --no-isolation
}

check() {
  cd "$_name-$pkgver"

  PYTHONPATH="$(pwd)" pytest -v
}

package() {
  cd "$_name-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
