# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=python-evdev
pkgver=1.7.1
pkgrel=1
pkgdesc="Python bindings for the Linux input subsystem"
url="https://python-evdev.readthedocs.io/en/latest/"
arch=(loong64 x86_64)
license=(BSD-3-Clause)
depends=(
  glibc
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("git+https://github.com/gvalkov/python-evdev#tag=v$pkgver")
b2sums=('27b45bd3195b635dbf003dbe2628d40aa2aa285035025fca79000f993f64dce7fbbd23f086cf847a6de3b23f91c466877c94b536954d7e8173f939433c846303')

prepare() {
  cd $pkgname
}

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE
}

# vim:set sw=2 sts=-1 et:
