# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=doomretro
pkgver=5.5
pkgrel=1
pkgdesc='The classic, refined DOOM source port'
arch=('loong64' 'x86_64')
url='http://doomretro.com'
license=('GPL-3.0-only')
depends=(
  'glibc'
  'sdl2'
  'sdl2_image'
  'sdl2_mixer'
)
makedepends=('git' 'cmake')
optdepends=('timidity++: for music playback')
source=("$pkgname::git+https://github.com/bradharding/doomretro.git#tag=v$pkgver")
sha512sums=('b6e9aa7ee18a9ffee928e42c530b251ea6b09049107c4685af6a1ab40b89a937e22da526991d6f941bd9f9b6f2ddbb2b89a721cfbbcddcbf71cbe36662145830')
b2sums=('d30f364dae69c33a59b1c7d53ce356acd557713971e091b4a6f90d7ac7a7ab1f6984a159d19c5ecada6bfab22ba3c7a4be819f7d6dfe49ebee5b90a9736e16a8')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
