# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=reapack
pkgver=1.2.4.5
_commit='9282e03643afead4171e4cac70a51ba32fb7dece'  # refs/tags/v1.2.4.5^{}
pkgrel=1
pkgdesc='Package manager for the Reaper DAW'
arch=('loong64' 'x86_64')
url='https://reapack.com'
license=(
  'GPL-3.0-or-later'
  'LGPL-3.0-or-later'
)
depends=(
  'glibc'
  'gcc-libs'
  'openssl'
  'zlib'
  'reaper'
  'libxml2'
  'curl'
  'sqlite'
)
makedepends=(
  'git'
  'cmake'
  'boost'
  'ruby'
  'php'
  'catch2'
)
source=(
  "$pkgname::git+https://github.com/cfillion/reapack#commit=$_commit"
  'github.com-justinfrankel-wdl::git+https://github.com/justinfrankel/WDL'
  'github.com-justinfrankel-reaper-sdk::git+https://github.com/justinfrankel/reaper-sdk'
  $pkgname-1.2.4.5-system_wide.patch
)
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        '0f24c87d822322db6807a262fdb808b72937838fcb998b8d69cd938a9b33a22dfcdd95cf620ef862134f30e87681c40d99323a7762c92aef4e29f83f665e036d')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # prepare git submodule(s)
  git submodule init
  git config submodule.vendor/WDL.url "$srcdir/github.com-justinfrankel-wdl"
  git config submodule.vendor/reaper-sdk.url "$srcdir/github.com-justinfrankel-reaper-sdk"
  git -c protocol.file.allow=always submodule update

  # allow installing the plugin to reaper's system-wide plugin path: https://github.com/cfillion/reapack/issues/80
  patch -Np1 -i ../$pkgname-1.2.4.5-system_wide.patch
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    # out-of-tree build is broken: https://github.com/cfillion/reapack/issues/79
    -S .
    -W no-dev
  )

  cd "$pkgname"
  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  make -C "$pkgname/build" test
}

package() {
  cd "$pkgname"

  # NOTE: install target is not configurable: https://github.com/cfillion/reapack/issues/81
  # plugin
  install -vDm755 -t "$pkgdir/usr/lib/REAPER/Plugins/" "build/reaper_reapack-$CARCH.so"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
