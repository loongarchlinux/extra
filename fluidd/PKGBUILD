# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=fluidd

pkgver=1.30.1
pkgrel=1
pkgdesc='A free and open-source Klipper web interface for managing your 3d printer'
arch=(any)
url=https://docs.fluidd.xyz/
license=(GPL-3.0-only)
makedepends=(
  git
  nodejs-lts-iron
  npm
)
optdepends=('ustreamer: webcam streaming support')
_tag=62fe4041059ce46b4b44f8ec13f6e77ad9b7c90a
source=(git+https://github.com/fluidd-core/fluidd.git#tag=${_tag})
validpgpkeys=(4C717BDC0E452F5B13490409AB4C64C58BA9C3ED) # Pedro Lamas <pedrolamas@gmail.com>
b2sums=('4ab2a2af3c152dbe7c7082a2eb379c3be12ef7ce42827238b572dc2dd047bfdba6fc24830322ecef45cfa9a4fded4781392f70a333cbfff55b4303d4b9eb6ef6')

pkgver() {
  cd fluidd
  git describe --tags | sed 's/^v//'
}

build() {
  cd fluidd
  npm install --frozen-lockfile
  npm run build
}

package() {
  install -dm 755 "${pkgdir}"/usr/share/webapps
  cp -dr --no-preserve=ownership fluidd/dist "${pkgdir}"/usr/share/webapps/fluidd
}

# vim: ts=2 sw=2 et:
