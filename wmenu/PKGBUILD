# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: nullcoder

pkgname=wmenu
pkgver=0.1.7
pkgrel=1
pkgdesc="Efficient dynamic menu for Wayland and wlroots based Wayland compositors"
arch=('loong64' 'x86_64')
url="https://git.sr.ht/~adnano/wmenu"
license=('MIT')
makedepends=(
	'meson'
	'ninja'
	'scdoc'
	'wayland-protocols'
)
depends=(
	'libcairo.so'
	'libxkbcommon.so'
	'libpango-1.0.so'
	'libwayland-client.so'
)
source=(
	"$pkgname-$pkgver.tar.gz::https://git.sr.ht/~adnano/wmenu/archive/$pkgver.tar.gz"
)
b2sums=('7aa1ccc5a8f4ea4fd0252ce6fe31168fb8346558093df0a72d29023fa2e8c38cec9562f1d4bb733abc3b00ba4bf952dc29ef22dba373db01a05229b420791a7f')

build() {
	cd "$pkgname-$pkgver"
	arch-meson build
	ninja -C build
}

package() {
	cd "$pkgname-$pkgver"
	DESTDIR="$pkgdir" ninja -C build install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

