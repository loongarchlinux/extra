# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: nullcoder

pkgname=wmenu
pkgver=0.1.9
pkgrel=1
pkgdesc="Efficient dynamic menu for Wayland and wlroots based Wayland compositors"
arch=('loong64' 'x86_64')
url="https://codeberg.org/adnano/wmenu"
license=('MIT')
makedepends=(
	'meson'
	'ninja'
	'scdoc'
	'wayland-protocols'
)
depends=(
	'libcairo.so'
	'libxkbcommon.so'
	'libpango-1.0.so'
	'libwayland-client.so'
)
source=(
	"$pkgname-$pkgver.tar.gz::https://codeberg.org/adnano/wmenu/archive/$pkgver.tar.gz"
)
b2sums=('28eeb3d1dd9b5f8bdb2fbbcd8e2b303fa4d9f926871dd11011e63da930787f05788bbe01afc5a3c3dfb210d3dd1117e2375e5ba0b5cfff7ab9e866bfd564f12c')

build() {
	cd wmenu
	arch-meson build
	ninja -C build
}

package() {
	cd wmenu
	DESTDIR="$pkgdir" ninja -C build install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

