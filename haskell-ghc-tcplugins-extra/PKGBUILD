# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ghc-tcplugins-extra
pkgname=haskell-ghc-tcplugins-extra
pkgver=0.4.5
pkgrel=1
pkgdesc="Utilities for writing GHC type-checker plugins"
url="https://github.com/clash-lang/ghc-tcplugins-extra"
license=('custom:BSD2')
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-ghc')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('9e5963aafc192e5fa6019b51120363963e1a625e127128eb2af55d1e148093f8c707993e81649d0b75cca09c477bbe3797da74d279924b2f2d01733edfeeb6a7')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
