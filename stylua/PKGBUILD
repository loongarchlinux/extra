# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_name=StyLua
pkgname=stylua
pkgver=0.20.0
pkgrel=3
pkgdesc='Code formatter for Lua'
arch=('loong64' 'x86_64')
url=https://github.com/JohnnyMorganz/StyLua
license=('MPL-2.0')
depends=('gcc-libs')
makedepends=('git' 'rust')
replaces=(stylua-bin)
source=("git+$url.git#tag=v$pkgver")
b2sums=('e7b121c168ec5a8609acda7308776cf2851f1e88179e1c95a5d73048f0c768f6a5c6b92039beffaa3eaf4872ecbac5c33548160e0cd0aadbee5b66194473fd27')

prepare() {
  cd $_name
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  cargo build --release --frozen --all-features
}

check() {
  cd $_name
  cargo test --frozen --all-features
}

package() {
  cd $_name
  install -Dm0755 -t "$pkgdir"/usr/bin target/release/$pkgname
}
