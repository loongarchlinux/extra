# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=popeye
pkgver=0.21.0
pkgrel=2
pkgdesc="Kubernetes cluster resource sanitizer"
arch=('loong64' 'x86_64')
url="https://github.com/derailed/popeye"
license=('Apache')
depends=('glibc')
makedepends=('go')
optdepends=('bash-completion')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/derailed/popeye/archive/v${pkgver}.tar.gz")
sha512sums=('72299c306ad19fb6510973c450b469421d92790b7759acc8d3dcedad97bce32cb6530065990f35eefe3775b23fab8f2bac857fd71c62592803bfe65a2d395d88')

build(){
  cd "${pkgname}-${pkgver}"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  go build -v -x \
    -ldflags "-linkmode external -w -X github.com/derailed/popeye/cmd.version=${pkgver} -X github.com/derailed/popeye/cmd.date=$(date -ud "@${SOURCE_DATE_EPOCH}" +%FT%T%Z)"\
    .
}

check(){
  cd "${pkgname}-${pkgver}"

  go test -v ./...
}

package(){
  cd "${pkgname}-${pkgver}"

  install -Dsm755 ./popeye "${pkgdir}/usr/bin/popeye"

  "${pkgdir}/usr/bin/popeye" completion bash | install --verbose -D --mode=0644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/popeye"
  "${pkgdir}/usr/bin/popeye" completion fish | install --verbose -D --mode=0644 /dev/stdin  "${pkgdir}/usr/share/fish/vendor_completions.d/popeye.fish"
  "${pkgdir}/usr/bin/popeye" completion zsh | install --verbose -D --mode=0644 /dev/stdin  "${pkgdir}/usr/share/zsh/site-functions/_popeye"
}
