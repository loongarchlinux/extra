# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=store
pkgname=haskell-store
pkgver=0.7.18
pkgrel=18
pkgdesc="Fast binary serialization"
url="https://github.com/mgsloan/store"
license=('BSD')
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-base-orphans' 'haskell-base64-bytestring'
         'haskell-bifunctors' 'haskell-contravariant' 'haskell-cryptohash-sha1' 'haskell-free'
         'haskell-hashable' 'haskell-hspec' 'haskell-hspec-smallcheck' 'haskell-lifted-base'
         'haskell-monad-control' 'haskell-mono-traversable' 'haskell-network' 'haskell-primitive'
         'haskell-resourcet' 'haskell-safe' 'haskell-smallcheck' 'haskell-store-core'
         'haskell-syb' 'haskell-th-lift' 'haskell-th-lift-instances' 'haskell-th-orphans'
         'haskell-th-reify-many' 'haskell-th-utilities' 'haskell-unordered-containers'
         'haskell-vector' 'haskell-void')
makedepends=('ghc' 'uusi' 'haskell-clock' 'haskell-hspec-discover')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('84a7b4590c6934d988afa8d67b6362015b14145c6365861cb40a96c57e65382289190a73072f199ce58f961b0df6f5e186ca3cae35a4b3dfb2bdc3940117c8cc')

prepare() {
    cd $_hkgname-$pkgver
    uusi -d nats $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-comparison-bench -f-small-bench
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
