# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Contributor: acxz <akashpatel2008 at yahoo dot com>
pkgname=rocprofiler
pkgver=6.0.2
pkgrel=2
pkgdesc="ROC profiler library. Profiling with perf-counters and derived metrics."
arch=('loong64' 'x86_64')
url='https://rocm.docs.amd.com/projects/rocprofiler/en/latest/rocprof.html'
license=('MIT')
depends=('rocm-core' 'hip' 'python' 'python-lxml' 'hsa-amd-aqlprofile')
makedepends=('cmake' 'rocm-llvm' 'hsa-rocr' 'rocm-dbgapi' 'gtest' 'rocm-smi-lib'
             'python-cppheaderparser' 'python-yaml' 'python-barectf')
_git='https://github.com/ROCm/rocprofiler'
_roctracer='https://github.com/ROCm/roctracer'
source=("$pkgname-$pkgver.tar.gz::$_git/archive/refs/tags/rocm-$pkgver.tar.gz"
        "$pkgname-roctracer-$pkgver.tar.gz::$_roctracer/archive/refs/tags/rocm-$pkgver.tar.gz")
sha256sums=('d3f24e639a5e151fa418a92ae6fe150bdf14120b8982a5baa52844ce2fba0b82'
            '1e0105b32fdd9c010aab304bb2ca1a5a38ba323cea610afe1135657edda8f26e')
options=(!lto)
_dirname="$(basename "$_git")-$(basename "${source[0]}" ".tar.gz")"
_dirtracer="$(basename "$_roctracer")-$(basename "${source[1]}" ".tar.gz")"

build() {
  local cmake_args=(
    -Wno-dev
    -B build
    -S "$_dirname"
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_PREFIX=/opt/rocm
    -D ROCPROFILER_BUILD_TESTS=OFF
    -D GPU_TARGETS="gfx803 gfx900 gfx906 gfx908 gfx90a gfx940 gfx941 gfx942 gfx1030 gfx1100 gfx1101 gfx1102"
    -D PROF_API_HEADER_PATH="$srcdir/$_dirtracer/inc/ext"
  )
  HIP_CLANG_PATH=/opt/rocm/llvm/bin \
  cmake "${cmake_args[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$_dirname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
