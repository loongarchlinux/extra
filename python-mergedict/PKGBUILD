# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-mergedict
_name="${pkgname#python-}"
pkgver=1.0.0
pkgrel=8
pkgdesc="A Python dict with a merge() method"
arch=(any)
url="https://github.com/schettino72/mergedict/"
license=(MIT)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('b48d13cb6995423a25ee5589491a80416f49d4cd3934581c5e91e1a509da67c47c5202722efd6bf142912d1072a136dd499b8f13ca6ec11dd484f708061bf823')
b2sums=('f911441a4b87a99edc02af43cb8ac7e323b6e92851193a9de4241a0ba3c3a42ab4c79d02f3d24bbdcb45adbf7f7864b20f5cbe212dbe067cf9af87156d0d9a98')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build:$PYTHONPATH"
  pytest -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGES,README.rst} -t "$pkgdir/usr/share/doc/$pkgnam/"
}
