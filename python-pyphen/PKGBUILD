# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-pyphen
_name="${pkgname#python-}"
pkgver=0.15.0
pkgrel=2
pkgdesc="Pure Python module to hyphenate text"
arch=(any)
url="https://pyphen.org/"
_url=https://github.com/Kozea/Pyphen
license=(
  GPL-2.0-or-later
  LGPL-2.1-or-later
  MPL-1.1
)
depends=(python)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::$_url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('a3c42e5831ef5b73a4583089807b5fd8de8cc2ef17e7ee4822780eb8df59eb38c1b21cab847ae85edc6687c1738f488850e08614cfca6aa6766bdb6f2934b693')
b2sums=('1b26da3aac163a19cf2ec7f9770d4f9bb2c0fd30b67cbe0daa32e2753b52a25d34c314db4aea2884ab20de7704b3af267234b4328860494cef607bea5933bd75')

build() {
  cd ${_name^}-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd ${_name^}-$pkgver
  pytest -vv
}

package() {
  cd ${_name^}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
