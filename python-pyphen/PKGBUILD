# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-pyphen
_name="${pkgname#python-}"
pkgver=0.16.0
pkgrel=1
pkgdesc="Pure Python module to hyphenate text"
arch=(any)
url="https://pyphen.org/"
_url=https://github.com/Kozea/Pyphen
license=(
  GPL-2.0-or-later
  LGPL-2.1-or-later
  MPL-1.1
)
depends=(python)
makedepends=(
  python-build
  python-flit-core
  python-installer
  python-wheel
)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::$_url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('836df939adc9269075ebec34120564343cd27e5d42e68867f276d60c764a6e5eb368dae1a9d6603cdc7fa9c5fc8d08206123840f2a5e660a27957eeeaa647231')
b2sums=('785323ef98d18490fc5c14307b925e1365ac84d95be9a9d7b2ae6b31b7a8f01635b7881cf6a7560f1460e023da14643a0e8df09467d0d56ca47ab4f52800ecf1')

build() {
  cd ${_name^}-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd ${_name^}-$pkgver
  pytest -vv
}

package() {
  cd ${_name^}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
