# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: erm67 <erm67@yahoo.it>

pkgname=cairo-dock
pkgver=3.5.0
pkgrel=1
pkgdesc='Light eye-candy fully themable animated dock'
arch=('loong64' 'x86_64')
url='https://glx-dock.org/'
license=('GPL')
depends=('curl' 'dbus-glib' 'glu' 'gtk3')
makedepends=('cmake')
optdepends=('cairo-dock-plug-ins: Plugins for Cairo-Dock')
source=("cairo-dock-${pkgver}.tar.gz::https://github.com/Cairo-Dock/cairo-dock-core/archive/${pkgver}.tar.gz")
sha256sums=('67af81683d8178e12f8c2f190936f68658eb1e091d974416a7d1472cc8c7ab63')

build() {
  cd cairo-dock-core-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd cairo-dock-core-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
