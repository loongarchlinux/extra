# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-font-viewer
pkgver=46.0
pkgrel=2
pkgdesc="A font viewer utility for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-font-viewer"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  gnome-desktop-4
  gtk4
  libadwaita
)
makedepends=(
  git
  meson
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-font-viewer.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('a730ebfd2a09abade74be298b3fff89be3159174e2e4751acb90e664376678e556065b62a490ee7ede23ee1310c8745d0ff5689c987e5c9b52fbb4a98e3c3800')
validpgpkeys=('3475CBA8D3483594C889B470D64A8D747F6FE706') # Maximiliano Sandoval <msandova@gnome.org>

prepare() {
  cd $pkgname

  # Fix function callback definition
  git cherry-pick -n 565d795731471c27542bb9ee60820a2d0d15534e
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
