# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-font-viewer
pkgver=46.0
pkgrel=1
pkgdesc="A font viewer utility for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-font-viewer"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  gnome-desktop-4
  gtk4
  libadwaita
)
makedepends=(
  git
  meson
)
groups=(gnome)
_commit=5b64402841b2d8b5f4c5a6c5b63c79ce0e3541be  # tags/46.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-font-viewer.git#commit=$_commit")
b2sums=('a730ebfd2a09abade74be298b3fff89be3159174e2e4751acb90e664376678e556065b62a490ee7ede23ee1310c8745d0ff5689c987e5c9b52fbb4a98e3c3800')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
