# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-pass
pkgver=1.2.2
pkgrel=1
arch=(loong64 x86_64)
pkgdesc='Plasma applet for the Pass password manager'
url='https://www.dvratil.cz/2018/05/plasma-pass/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         gpgme
         kcoreaddons
         ki18n
         kitemmodels
         libplasma
         oath-toolkit
         plasma-workspace
         plasma5support
         qgpgme-qt6
         qt6-base
         qt6-declarative)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('2a726455084d7806fe78bc8aa6222a44f328b6063479f8b7afc3692e18c397ce'
            'SKIP')
validpgpkeys=('0ABDFA55A4E6BEA99A83EA974D69557AECB13683'  # Daniel Vr√°til <dvratil@kde.org>
              'E0A3EB202F8E57528E13E72FD7574483BB57B18D') # Jonathan Esk-Riddell <jr@jriddell.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
