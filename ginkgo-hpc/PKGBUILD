# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_pkgname=ginkgo
pkgbase=ginkgo-hpc
pkgname=(ginkgo-hpc ginkgo-hpc-docs ginkgo-hpc-cuda ginkgo-hpc-hip)
pkgver=1.7.0
pkgrel=5
pkgdesc="Numerical linear algebra software package"
arch=(loong64 x86_64)
url=https://ginkgo-project.github.io/
license=(BSD-3-Clause)
depends=(
  gcc-libs
  glibc
  hwloc
  openmpi
)
# FIXME: system 'rapidjson' does not work due to some C++ issues
# (see the 'declared protected here' error in https://github.com/Tencent/rapidjson/issues/1338#issuecomment-449849215 )
makedepends=(
  cmake
  ninja
  nlohmann-json
  gflags  # for benchmarks (not installed yet)
  gtest
  numactl
  doxygen
  graphviz
  texlive-bin
  texlive-latexextra
  # -cuda
  cuda
  # -hip
  hip-runtime-amd
  hipblas
  hipfft
  hiprand
  hipsparse
  rocthrust
  roctracer
)
source=("$_pkgname-$pkgver.tar.gz::https://github.com/ginkgo-project/ginkgo/archive/refs/tags/v$pkgver.tar.gz"
        "fix-cuda-12.4.patch")
sha256sums=('f4b362bcb046bc53fbe2e578662b939222d0c44b96449101829e73ecce02bcb3'
            '20cfc54e8128f0d6eb2917828eb95d7c115d06d19c3c87a41d4eef08e6126530')

prepare() {
  # fix build with CUDA 12.4 - based on https://github.com/ginkgo-project/ginkgo/pull/1569
  patch --directory=$_pkgname-$pkgver -Np1 < fix-cuda-12.4.patch

  # replace deprecated flag, will be fixed by https://github.com/ginkgo-project/ginkgo/pull/1334
  sed -i 's|--amdgpu-target|--offload-arch|g' $_pkgname-$pkgver/cmake/hip.cmake
  # fix finding roctracer
  sed -i 's|${ROCTRACER_PATH}/include|${ROCM_PATH}/include/roctracer|' $_pkgname-$pkgver/cmake/Modules/FindROCTX.cmake
}

build() {
  local common_cmake_flags=(
    -S $_pkgname-$pkgver -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DGINKGO_BUILD_REFERENCE=ON
    -DGINKGO_BUILD_OMP=ON
    -DGINKGO_BUILD_MPI=ON
    -DGINKGO_HAVE_GPU_AWARE_MPI=ON
    -DGINKGO_BUILD_BENCHMARKS=ON
    -DGINKGO_BUILD_EXAMPLES=ON
    -DGINKGO_BUILD_DOC=ON
    -DGINKGO_BUILD_TESTS=ON
  )
  local _cuda_archs="All"
  # archs gfx1010;gfx1012;gfx1030;gfx1100;gfx1101;gfx1102 are not supported: https://github.com/ginkgo-project/ginkgo/issues/1429
  local _amdgpu_archs="gfx906;gfx908;gfx90a;gfx940;gfx941;gfx942"

  # base package
  cmake -B build "${common_cmake_flags[@]}" \
    -DGINKGO_BUILD_CUDA=OFF \
    -DGINKGO_BUILD_HIP=OFF \
    -DGINKGO_BUILD_SYCL=OFF
  cmake --build build

  # -cuda package
  cmake -B build-cuda "${common_cmake_flags[@]}" \
    -DGINKGO_CUDA_ARCHITECTURES="$_cuda_archs" \
    -DGINKGO_BUILD_CUDA=ON \
    -DGINKGO_BUILD_HIP=OFF \
    -DGINKGO_BUILD_SYCL=OFF
  cmake --build build-cuda

  # -hip package
  # ginkgo has insufficient auto-detection for HIP_PATH https://github.com/ginkgo-project/ginkgo/issues/1529
  export ROCM_PATH=/opt/rocm
  export HIP_PATH="$ROCM_PATH"
  export HIP_THRUST_PATH="$HIP_PATH"/include
  export ROCTRACER_PATH="$ROCM_PATH"
  # FIXME: GINKGO_HIPCONFIG_PATH must be set due to https://github.com/ginkgo-project/ginkgo/issues/1565
  # FIXME: CMAKE_MODULE_PATH=/opt/rocm/lib/cmake/hip is needed due to https://github.com/NixOS/nixpkgs/issues/216655
  # FIXME: benchmarks fail to build with ROCm/HIP 6.0 https://github.com/ginkgo-project/ginkgo/issues/1566
  cmake -B build-hip "${common_cmake_flags[@]}" \
    -DGINKGO_HIPCONFIG_PATH=/opt/rocm/bin/hipconfig \
    -DCMAKE_MODULE_PATH=/opt/rocm/lib/cmake/hip \
    -DGINKGO_HIP_AMDGPU="$_amdgpu_archs" \
    -DGINKGO_BUILD_CUDA=OFF \
    -DGINKGO_BUILD_HIP=ON \
    -DGINKGO_BUILD_SYCL=OFF \
    -DGINKGO_BUILD_BENCHMARKS=OFF
  cmake --build build-hip
  unset ROCM_PATH
  unset HIP_PATH
  unset HIP_THRUST_PATH
}

check() {
  # FIXME: some tests fail:
  # - https://github.com/ginkgo-project/ginkgo/issues/1143
  # - https://github.com/ginkgo-project/ginkgo/issues/1567
  local excluded_tests="test/factorization/par_ilu_kernels_omp"  # 1143
  excluded_tests+="|reference/test/reorder/mc64_kernels"  # 1143
  excluded_tests+="|omp/test/reorder/rcm_kernels"  # 1143
  excluded_tests+="|test/matrix/matrix_omp"  # 1143
  excluded_tests+="|benchmark_multi_vector_distributed"  #1567
  excluded_tests+="|benchmark_spmv_distributed"  #1567
  excluded_tests+="|benchmark_solver_distributed"  #1567

  # limit parallel execution of tests to 4 threads and 8 processes
  # note that without a GPU we cannot run tests for -cuda and -hip
  (
    export OMP_NUM_THREADS=4
    export CTEST_PARALLEL_LEVEL=8
    export CTEST_OUTPUT_ON_FAILURE=1
    ctest --test-dir build --exclude-regex "$excluded_tests"
  )
}

_package() {
  DESTDIR="$pkgdir" cmake --install build"$1"

  # install the license
  install -vDm 644 $_pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"

  # TODO: install the benchmarks and examples: https://github.com/ginkgo-project/ginkgo/issues/1144
}

package_ginkgo-hpc() {
  optdepends=(
    'ginkgo-hpc-docs: for documentation'
  )

  _package ""
}

package_ginkgo-hpc-docs() {
  pkgdesc+=" - documentation"
  depends=()

  # install the documentation
  install -vdm755 "$pkgdir/usr/share/doc/$pkgbase"
  cp -r build/doc/usr "$pkgdir/usr/share/doc/$pkgbase/html"

  # install the license
  install -vDm 644 $_pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

package_ginkgo-hpc-cuda() {
  pkgdesc+=" (with CUDA)"
  depends+=(cuda)
  optdepends=(
    'ginkgo-hpc-docs: for documentation'
  )
  conflicts=(ginkgo-hpc)
  provides=(ginkgo-hpc)

  _package -cuda
}

package_ginkgo-hpc-hip() {
  pkgdesc+=" (with ROCm/HIP)"
  depends+=(
    hip-runtime-amd
    hipblas
    hipfft
    hiprand
    hipsparse
    roctracer
  )
  optdepends=(
    'ginkgo-hpc-docs: for documentation'
  )
  conflicts=(ginkgo-hpc)
  provides=(ginkgo-hpc)

  _package -hip
}

# vim:set ts=2 sw=2 et:
