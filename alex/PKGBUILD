# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Philip Nilsson <pnilsson@nullref.se>

pkgname=alex
pkgver=3.2.7.4
pkgrel=2
pkgdesc='Lexical analyser generator for Haskell'
arch=(loong64 x86_64)
url='https://hackage.haskell.org/package/alex'
license=(BSD-3-Clause)
depends=(ghc-libs)
makedepends=(ghc happy)
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8d78d932058a3f8a0f429f31073ec820f8c75607555c5700467f581d9145296544bc846d018e682fdd57c064cb209f36a9e46dd9057aa93081994b5c4a94aaaa')

build() {
  cd $pkgname-$pkgver
  runghc Setup.hs configure \
    --prefix=/usr \
    --datasubdir=$pkgname \
    --enable-executable-dynamic \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'
  runghc Setup.hs build
}

package() {
  cd $pkgname-$pkgver
  runghc Setup.hs copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/alex/BSD3"

  # Doc only contains the license
  rm -rf "$pkgdir/usr/share/doc"
}

# getver: hackage.haskell.org/package/alex/changelog
# vim: ts=2 sw=2 et:
