# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-importlib-metadata
pkgver=5.1.0
_commit=b74765da2d794941012b145bf228eeabc42ba73b
pkgrel=2
pkgdesc="Read metadata from Python packages"
url="https://importlib-metadata.readthedocs.io"
license=('Apache')
arch=('any')
depends=('python-zipp')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest' 'python-pyfakefs' 'python-pip' 'python-tests')
source=("git+https://github.com/python/importlib_metadata.git#commit=$_commit")
sha512sums=('e2e90a68bfcf6878f0e5a9a390c79b632c09bf41a182b6d81cacb7b4da0cdf3b2584ad39f207b878b5bc349b41db29e2c33ebd1e51e6427e9fd1913b23017016')

build() {
  cd importlib_metadata
  python -m build -nw
}

check() {
  cd importlib_metadata
  python -m pytest --ignore exercises.py
}

package() {
  cd importlib_metadata
  python -m installer -d "$pkgdir" dist/*.whl
}
