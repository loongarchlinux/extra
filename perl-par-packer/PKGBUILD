# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Amish <contact at via dot aur>
# Contributor: Justin Davis <jrcd83@gmail.com>
# Contributor: Tony Sokhon <tonyskn@gmail.com>

pkgname=perl-par-packer
pkgver=1.061
pkgrel=1
pkgdesc='PAR Packager'
arch=('loong64' 'x86_64')
url='https://metacpan.org/release/PAR-Packer'
license=('GPL')
depends=('perl-archive-zip' 'perl-getopt-argvfile' 'perl-ipc-run3'
         'perl-module-scandeps' 'perl-par' 'perl-par-dist')
makedepends=('perl' 'perl-extutils-makemaker')
options=('!emptydirs' purge)
source=("https://cpan.metacpan.org/authors/id/R/RS/RSCHUPP/PAR-Packer-${pkgver}.tar.gz")
b2sums=('98ca95d7f48d0ed471045487133a3f06d578423776fe7a5651d312dcd305bfa90be1cb1580cfc1afeda2a7e11fbe4ddd86bff669345b42b9e860e180c3cdfe44')

build() {
  cd PAR-Packer-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd PAR-Packer-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  local _perlver=$(perl -e 'print substr($^V, 1)')
  depends+=("perl=${_perlver}") # versioned dependency down to patch version

  cd PAR-Packer-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  #remove conflicting and empty man page
  rm "${pkgdir}"/usr/share/man/man1/pp.1
}
