# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: NOGISAKA Sadata <ngsksdt@gmail.com>
# Contributor: Bennett Piater <bennett at piater dot name>

pkgname=doctl
pkgver=1.111.0
pkgrel=1
pkgdesc='The official command line interface for the DigitalOcean API'
url='https://github.com/digitalocean/doctl'
arch=('loong64' 'x86_64')
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=(https://github.com/digitalocean/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('8bbb6c632f15338f2bd38124e6ee479bd39cee0779570762f776779db0132130')
b2sums=('7c8be7057d8a239a09cff489ccfb5e8ebd7ce37640cbf38bc9abfd51ef1cfaaffc2f773d69fa45ba328d21170f771f944252ee2e25fa7361a857233f525427d0')

build() {
  cd ${pkgname}-${pkgver}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false -linkmode=external
      -X github.com/digitalocean/doctl.Major=$(echo $pkgver | cut -d. -f1)
      -X github.com/digitalocean/doctl.Minor=$(echo $pkgver | cut -d. -f2)
      -X github.com/digitalocean/doctl.Patch=$(echo $pkgver | cut -d. -f3)
      -X github.com/digitalocean/doctl.Label=release
    " \
    ./cmd/...
}

check() {
  cd ${pkgname}-${pkgver}
  ./doctl --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 "${pkgname}" -t "${pkgdir}/usr/bin"

  # setup completions
  install -dm 755 "${pkgdir}/usr/share/bash-completion/completions" \
                  "${pkgdir}/usr/share/zsh/site-functions" \
                 "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completion bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
