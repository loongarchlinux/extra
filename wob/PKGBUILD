# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Martin Franc <me@martinfranc.eu>

pkgname=wob
pkgver=0.15.1
pkgrel=2
pkgdesc="A lightweight overlay volume/backlight/progress/anything bar for Wayland"
arch=(loong64 x86_64)
url="https://github.com/francma/wob"
license=(ISC)
depends=(
  glibc
  libinih
  libseccomp
  wayland
)
makedepends=(
  cmocka
  meson
  scdoc
  wayland-protocols
)
source=(
	"$pkgname-$pkgver.tar.gz::$url/releases/download/$pkgver/wob-$pkgver.tar.gz"
	"$pkgname-$pkgver.tar.gz.sig::$url/releases/download/$pkgver/wob-$pkgver.tar.gz.sig"
	"wob.service"
	"wob.socket"
)
sha256sums=(
  'b7c8495771139f549752db59f8f52f019d10a2db1e4fa361b4e5c5903df06298'
  'SKIP'
  'a51139e1d27833aa7bbf86afda67822cb93a9948e5f0cd4fcc670e49191019e5'
  '521888b6667bd77eec569d5dce5264d4bb3bc8f9eabdc818b26c2908452b522f'
)
validpgpkeys=('5C6DA024DDE27178073EA103F4B432D5D67990E3')

build() {
  cd $pkgname-$pkgver

	arch-meson . build
  meson compile -C build
}

check() {
  cd $pkgname-$pkgver

  meson test -C build --print-errorlogs
}

package() {
  cd $pkgname-$pkgver

  meson install -C build --destdir "$pkgdir"
	install -vDm644 -t "$pkgdir/usr/lib/systemd/user" "$srcdir/wob.service"
	install -vDm644 -t "$pkgdir/usr/lib/systemd/user" "$srcdir/wob.socket"
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
