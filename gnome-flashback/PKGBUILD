# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-flashback
pkgver=3.52.1
pkgrel=1
pkgdesc='GNOME Flashback session'
arch=('loong64' 'x86_64')
url='https://wiki.gnome.org/Projects/GnomeFlashback'
license=('GPL-3.0-or-later')
depends=('alsa-lib' 'at-spi2-core' 'bash' 'cairo' 'dconf' 'gcc-libs' 'gdk-pixbuf2' 'glib2' 'glibc'
         'gnome-bluetooth-3.0' 'gnome-desktop' 'gnome-panel' 'gnome-session'
         'gnome-settings-daemon' 'gsettings-desktop-schemas' 'gtk3' 'libcanberra' 'libgdm'
         'libibus' 'libpulse' 'libx11' 'libxcb' 'libxext' 'libxfixes' 'libxi' 'libxkbfile'
         'libxrandr' 'libxxf86vm' 'metacity' 'nautilus' 'pam' 'pango' 'polkit' 'systemd-libs')
makedepends=('python')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'network-manager-applet: Network management')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
        '0001-background-Support-dark-wallpapers.patch'
        '0001-polkit-Reset-the-idle_id-before-emitting-the-signal.patch'
        '0001-application-honor-color-scheme-setting.patch'
        '0001-theme-improve-screensaver-style.patch'
        '0001-notifications-Fix-use-after-free-in-timeout_bubble.patch'
        'gnome-flashback.pam')
sha256sums=('ba04613cdadb62bda206437c047299e16025cde1bd8095e094aa77769c78603a'
            'f02afd29538555ab408085c60d8a7139f34fa639add7cd4c3c08a236dc027dc8'
            'd5637b43428b84271d0a5658997b6f3aa7b4ccb808651454b685b90e227944fa'
            '04fffb068a2b6653f041cfa7d22b6a38e40bf255b92300c6eaa5323f62ccc4a0'
            '095a05c12ca0689149f9c9543ebea2229c62755a85be35b34087437dbbfe31a3'
            '79a2e9112590968b5cd27de33ea9c5eb661ffda0907089a749d475f9c3605e31'
            '7148b3b1773fa312d6cd14798338cc70cffb6df7504ff1787687edd0363138d2')

prepare() {
  cd $pkgname-$pkgver

  # https://gitlab.gnome.org/GNOME/gnome-flashback/-/merge_requests/51
  patch -Np1 -i ../0001-background-Support-dark-wallpapers.patch

  # https://gitlab.gnome.org/GNOME/gnome-flashback/-/merge_requests/52
  patch -Np1 -i ../0001-polkit-Reset-the-idle_id-before-emitting-the-signal.patch

  # https://gitlab.gnome.org/GNOME/gnome-flashback/-/merge_requests/53
  patch -Np1 -i ../0001-application-honor-color-scheme-setting.patch

  # https://gitlab.gnome.org/GNOME/gnome-flashback/-/merge_requests/54
  patch -Np1 -i ../0001-theme-improve-screensaver-style.patch

  # https://gitlab.gnome.org/GNOME/gnome-flashback/-/merge_requests/63
  patch -Np1 -i ../0001-notifications-Fix-use-after-free-in-timeout_bubble.patch

  # Regenerate resources file
  rm gnome-flashback/gf-resources.{c,h}

  # Recreate autotools
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Install pam file for the screensaver
  install -Dm644 ../gnome-flashback.pam "$pkgdir/etc/pam.d/gnome-flashback"
}
