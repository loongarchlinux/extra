# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=updlockfiles
pkgver=0.1.1
pkgrel=1
pkgdesc="Manage lockfiles in PKGBUILDs for upstreams that don't ship them"
url="https://github.com/kpcyrd/updlockfiles"
arch=('loong64' 'x86_64')
license=('GPL-3.0-or-later')
depends=(
  'gcc-libs'
  'glibc'
  'pacman-contrib'
)
makedepends=('cargo')
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('91bd19071cc5f8f2da4c3f6f7cfb15c0f3b809874980974dc4aa2dac7ceb4f949c8c05326887d12b24025fadd221337be1b611e3ac3215c7a2969d74ab96cdcd')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
