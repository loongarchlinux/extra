# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Phillip Schichtel <phillip@schich.tel>
# Contributor:	Jesse Jaara	<gmail.com: jesse.jaara>
# Contributor:	p2k		<uni-ulm.de: Patrick.Schneider>

pkgname=ktlint
pkgver=1.3.1
pkgrel=1
pkgdesc="An anti-bikeshedding Kotlin linter with built-in formatter"
arch=('any')
url="https://ktlint.github.io/"
license=('MIT')
depends=('java-runtime>=11')
makedepends=('jdk-openjdk>=21' 'git' 'gradle')
source=("git+https://github.com/pinterest/ktlint.git#tag=${pkgver}")
validpgpkeys=(ADBC987D1A7B91DB6B0AAA81995EFBF4A3D20BEB)
sha256sums=('ed88f8e0ff9c2216afe7dd8d9889e510b9f06700652f53246e523c0116ab7b31')

build() {
  cd "ktlint"
  gradle --no-configuration-cache --no-scan --no-daemon --console plain -Pktlint.publication.signing.enable=false shadowJarExecutable
}

package() {
  install -D -m755 "${srcdir}/ktlint/ktlint-cli/build/run/ktlint" "${pkgdir}/usr/bin/ktlint"
  install -D -m644 "${srcdir}/ktlint/LICENSE" "${pkgdir}/usr/share/licenses/ktlint/LICENSE"
}

