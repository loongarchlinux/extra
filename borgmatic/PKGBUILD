# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Kr1ss $(echo \<kr1ss+x-yandex+com\>|sed s/\+/./g\;s/\-/@/)
# Contributor: Alexander GÃ¶rtz <aur@nyloc.de>
# Contributor: Dan Beste <dan.ray.beste@gmail.com>
# Contributor: Julien Nicoulaud <julien dot nicoulaud at gmail dot com>
# Contributor: stef204 <https://aur.archlinux.org/account/stef204>

pkgname=borgmatic
pkgver=1.8.13
pkgrel=1
pkgdesc='Simple, configuration-driven backup software for servers and workstations'
arch=(any)
url=https://torsion.org/borgmatic
license=(GPL-3.0-or-later)
depends=(
  borg
  python
  python-colorama
  python-jsonschema
  python-packaging
  python-requests
  python-ruamel-yaml
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  apprise
  python-flexmock
  python-pytest
)
optdepends=(
  'apprise: for Apprise notifications'
  'python-llfuse: for mount action'
)
source=("git+https://projects.torsion.org/borgmatic-collective/borgmatic.git#tag=$pkgver")
b2sums=('c528f3d10dec476e00d95483e72ad00c8522bed5a9c51ed0eba46cf518217cd6c8d5f44c7211da1292a0b42804ec672366a0642423e38730663e5bdc3f832a14')

prepare() {
  cd $pkgname
  sed -i 's,root/.local,usr,' sample/systemd/borgmatic{,-user}.service
}

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation

  # Build shell completions
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  mkdir completions
  ./test-env/bin/borgmatic --bash-completion > completions/bash
  ./test-env/bin/borgmatic --fish-completion > completions/fish
}

check() {
  cd $pkgname
  # Override addopts as they invoke coverage testing
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest \
    --override-ini="addopts=" --ignore=tests/end-to-end
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/lib/systemd/system \
    sample/systemd/borgmatic.{service,timer}
  install -Dm644 -t "$pkgdir"/usr/lib/systemd/user \
    sample/systemd/borgmatic-user.{service,timer}

  install -Dm644 completions/bash \
    "$pkgdir"/usr/share/bash-completion/completions/$pkgname
  install -Dm644 completions/fish \
    "$pkgdir"/usr/share/fish/vendor_completions.d/$pkgname.fish
}
