# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=zls
pkgver=0.12.0
pkgrel=1
pkgdesc="A language server for Zig"
arch=('loong64' 'x86_64')
url="https://github.com/zigtools/zls"
license=('MIT')
depends=('zig')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/zigtools/zls/archive/refs/tags/${pkgver}.tar.gz"
  # From build.zig.zon
  "known-folders-bf79988adcfce166f848e4b11e718c1966365329.tar.gz::https://github.com/ziglibs/known-folders/archive/bf79988adcfce166f848e4b11e718c1966365329.tar.gz"
  "diffz-e10bf15962e45affb3fcd7d9a950977a69c901b3.tar.gz::https://github.com/ziglibs/diffz/archive/e10bf15962e45affb3fcd7d9a950977a69c901b3.tar.gz"
)
sha256sums=('c8c59dc6a708f3857ffbc1f593db4f6409e50e5ff1319b84dc65b84271e5a3d8'
            'f93ef6c14192bf79fba608f3c37bc31e85c4c7370fb855e9afa930a17cd85ff2'
            'b7978837f68c73d46f3f3ec6d12420d9ee52999d9da09cfa833ed974a7ad20e0')

prepare() {
  zig fetch --global-cache-dir ./zig-global-cache known-folders-bf79988adcfce166f848e4b11e718c1966365329
  zig fetch --global-cache-dir ./zig-global-cache diffz-e10bf15962e45affb3fcd7d9a950977a69c901b3
}

build() {
  cd "${pkgname}-${pkgver}"
  DESTDIR="build" zig build \
    --summary all \
    --global-cache-dir ../zig-global-cache \
    --system ../zig-global-cache/p \
    --prefix /usr \
    --release=safe \
    -Dtarget=native-linux.6.1-gnu.2.38 \
    -Dcpu=baseline \
    -Dpie=true
}

package() {
  cd "${pkgname}-${pkgver}"
  cp -a build/* "$pkgdir"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
