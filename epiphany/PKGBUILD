# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=46.3
pkgrel=1
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
url="https://apps.gnome.org/Epiphany"
arch=(loong64 x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairo
  dconf
  gcc-libs
  gcr-4
  gdk-pixbuf2
  glib2
  glibc
  gmp
  graphene
  gstreamer
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libarchive
  libportal
  libportal-gtk4
  libsecret
  libsoup3
  libxml2
  nettle
  pango
  sqlite
  webkitgtk-6.0
)
makedepends=(
  docbook-xml
  git
  glib2-devel
  gobject-introspection
  lsb-release
  meson
  startup-notification
  yelp-tools
)
checkdepends=(
  aspell
  glibc-locales
  hspell
  hunspell
  libvoikko
  nuspell
  xorg-server-xvfb
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/epiphany.git#tag=${pkgver/[a-z]/.&}")
b2sums=('b40b637619a658826187d1a8a228560262c0d32c268b6f5690f86d2bb703cd681f597c77c54f33c40e36c481abf760ec01ae6a39d54914f8d984d8f705409d39')

prepare() {
  cd epiphany
}

build() {
  arch-meson epiphany build
  meson compile -C build
}

check() {
  WEBKIT_DISABLE_SANDBOX_THIS_IS_DANGEROUS=1 LC_COLLATE=en_US.UTF-8 \
    dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
