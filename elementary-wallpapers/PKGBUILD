# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=elementary-wallpapers
pkgver=8.0.0
pkgrel=1
pkgdesc='Collection of wallpapers for elementary OS'
arch=(any)
url=https://github.com/elementary/wallpapers
license=(GPL3)
groups=(pantheon)
makedepends=(
  git
  meson
)
_tag=2ae7f028a7d807864bdc6b28947d5eb3f36add61
source=(elementary-wallpapers::git+https://github.com/elementary/wallpapers.git#tag=${_tag})
b2sums=('45e096a50e27a2b1dc16813dcbbf97694dcc9ffc9e5d31cc91fac27dbff28ccf8a589dc5ea5487ce391e9d10c52a0ba7f3b6a5887e2afeb491f4b042b2ddfec4')

pkgver() {
  cd elementary-wallpapers
  git describe --tags
}

build() {
  arch-meson elementary-wallpapers build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
