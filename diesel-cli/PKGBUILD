# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=diesel-cli
pkgver=2.1.6
pkgrel=1
pkgdesc="CLI for the Diesel crate"
arch=('loong64' 'x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache-2.0')
replaces=('diesel_cli')
depends=(
  'gcc-libs'
  'glibc'
  'libmariadb.so'
  'libpq.so'
  'libsqlite3.so'
)
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('fc1a8478e42b2f1c578da7de6a28832be842bbc3c1069b3219daf29a648eeef2e560a23b79729e7b523baa1afa52ea67765abbc198df9f0b85cbb4b3836276e4'
            '37e0cb187480f9eac590b52111945c757537d11e7a02d3b5829cd2d493554610292ce6a1bc1cfec6d3f4e163dd4bc64cf2124f807554acd5574f940ea108e998')
b2sums=('5fe7d68ad47289b4cafada37e177e5048be044c651c038332d70629a9e0514773914bc5650853f0902b4ace4073ebb913ff469d4b9a1ba339a63a578a08cbcc1'
        '17e03f8c10b43b8538a9e046f343a52dd9ba47e8b97c4ff31fa050ddcce22661d50e0b7ec56dd820cafb3eb660ebf1cb032d8f2550891ae7fe6aaa51d5ede33d')

prepare() {
  cp Cargo.lock "diesel-${pkgver}"
  cd "diesel-${pkgver}/diesel_cli"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "diesel-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "diesel-${pkgver}/diesel_cli"
  RUSTFLAGS="--cap-lints allow" cargo build --frozen --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
