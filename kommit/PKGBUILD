# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kommit
pkgver=1.6.0
pkgrel=1
pkgdesc='Git gui client'
arch=(loong64 x86_64)
url='https://apps.kde.org/kommit'
license=(GPL-3.0-only)
depends=(gcc-libs
         glibc
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kio
         kparts
         kwidgetsaddons
         kxmlgui
         libgit2
         qt6-base
         sh
         syntax-highlighting)
makedepends=(extra-cmake-modules
             dolphin
             kdoctools
             ktexteditor)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ac30eb5f1a8c4035c6499d27325d499121ac2de7b3d381cacb0dd7427fb45025'
            'SKIP')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D') # Jonathan Esk-Riddell <jr@jriddell.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
