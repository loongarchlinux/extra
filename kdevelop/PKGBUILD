# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevelop
pkgver=24.02.2
_clangver=17.0.6
pkgrel=1
pkgdesc='C++ IDE using KDE technologies'
arch=(loong64 x86_64)
url='http://www.kdevelop.org/'
license=(GPL-2.0-or-later)
depends=(clang=$_clangver
         gcc-libs
         glibc
         grantlee
         karchive5
         kbookmarks5
         kcmutils5
         kcodecs5
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kcrash5
         kdeclarative5
         kguiaddons5
         ki18n5
         kiconthemes5
         kio5
         kitemmodels5
         kitemviews5
         kjobwidgets5
         knewstuff5
         knotifications5
         knotifyconfig5
         kparts5
         kservice5
         ktexteditor5
         ktextwidgets5
         kwidgetsaddons5
         kwindowsystem5
         kxmlgui5
         libkomparediff2
#        libksysguard
         qt5-base
         qt5-declarative
         qt5-webengine
         sh
         sonnet5
         syntax-highlighting5
         threadweaver5)
makedepends=(astyle
             boost
             extra-cmake-modules
             kdevelop-pg-qt
             kdoctools5
#            krunner5
             llvm
             okteta
             plasma-framework5
             purpose5
             qt5-tools
             subversion)
optdepends=('apr: SVN support'
            'apr-util: SVN support'
            'astyle: astyle plugin'
            'clazy: clazy analyzer plugin'
            'cmake: cmake integration'
            'cppcheck: code analyzer'
            'gdb: GNU debugger support'
            'git: Git support'
            'heaptrack: memory profiler'
            'kate: for additional plugins'
            'kio5-extras: man pages plugin'
            'konsolepart5: embedded terminal'
            'lldb-mi: LLDB debugger support'
            'meson: meson integration'
            'okteta: GNU debugger support'
            'plasma-framework5: for the plasma addons'
            'purpose5: patch review plugin'
            'python: debugger support'
            'subversion: SVN support'
            'qt5-doc: Qt documentation integration'
            'qt5-tools: qthelp plugin'
            'subversion: SVN support')
groups=(kde-applications
        kdevelop)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('339e23870ee328736f6ad3f1713c72a84af4d156529bf2947f1133fad7a48ab7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
