# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@tutanota.com>

pkgname=python-moddb
pkgver=0.11.0
pkgrel=1
pkgdesc='A Python scraper to access ModDB mods, games and more as objects'
arch=(any)
url=https://github.com/ClementJ18/moddb
license=('MIT')
depends=(
  python-beautifulsoup4
  python-requests
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=d616d5a0f3a830de10ecda69b0f92bb86e8592e9
source=(git+https://github.com/ClementJ18/moddb.git#tag=${_tag})
b2sums=('0cc3a43a5282bfc56d4cdb011d3f62b574bee4e2845f79e86f14100c225e5f40f7fcb6c52f71577c49fb36bf419b8d5076b74aa8b09bae628455f5603d7d56ec')

pkgver() {
  cd moddb
  git describe --tags | sed 's/^v//'
}

build() {
  cd moddb
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" moddb/dist/*.whl
  install -Dm 644 moddb/LICENSE -t "${pkgdir}"/usr/share/licenses/python-moddb/
}

# vim: ts=2 sw=2 et:
