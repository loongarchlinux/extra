# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=kompose
pkgver=1.32.0
pkgrel=1
pkgdesc="Docker compose to Kubernetes transformation tool"
arch=('loong64' 'x86_64')
url="https://github.com/kubernetes/kompose"
license=('Apache')
makedepends=('go' 'git' 'jq') # git binary is needed for test scripts
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kubernetes/kompose/archive/v${pkgver}.tar.gz")
sha512sums=('cb5deb689e6cc97ebc6beba9e35b1a546bd497e19668131958971b7479773ded82ea01b656c182cd0ab6b843b9ced6b1ee50bba7eedae66a468fd8b57764a756')
options=('!lto')

prepare() {
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/kubernetes "${GOPATH}/bin"
  mv "${pkgname}-${pkgver}" "src/github.com/kubernetes/${pkgname}"
  export PACKAGE_ROOT="${GOPATH}/src/github.com/kubernetes/${pkgname}"
}

build() {
  cd "${PACKAGE_ROOT}"
  export CGO_ENABLED=1
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -v .
  chmod +x ./kompose
}

# tests are failing right now due to a bug in script/cmd/cmd_test.go:28
#check() {
#  cd "${PACKAGE_ROOT}"
#  go test -v ./...
#}

package() {
  cd "${PACKAGE_ROOT}"
  install -Dsm755 ./kompose "${pkgdir}/usr/bin/kompose"
}
