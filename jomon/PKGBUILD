# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=jomon
pkgver=0.3.4
pkgrel=1
pkgdesc='Network forensics and sniffer tool'
arch=(loong64 x86_64)
url='https://github.com/jo-lund/jomon'
license=(MIT)
depends=(geoip ncurses)
makedepends=(git python re2c)
optdepends=("geoip-database-extra: for extra geolocation data")
source=("git+$url#commit=cae1b74e2f638871051c974ddac1642d125a8cad") # tag: v0.3.4
b2sums=('eb5148ba05a74237fb340284c905dc20793a6482581beb31ccfa16aab4705203bdcf7ad0fc63c29e377b8daa7bf4401e8ebc81169c64f028111113f80981f4b8')

build() {
  cd $pkgname
  ./configure --prefix /usr
  make CFLAGS+='-Wl,-z,now -w'
}

package() {
  cd $pkgname
  sed -i "s,X=/,X=\"$pkgdir/\",g" config.mk
  make install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
