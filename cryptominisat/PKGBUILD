# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=cryptominisat
pkgver=5.11.22
pkgrel=1
_cadiback=ea65a9442fc2604ee5f4ffd0f0fdd0bf481d5b42
_cadical=c90592eab35a4a26ad901367db3cd727c5ab79c5
pkgdesc='A modern, multi-threaded, feature-rich, simplifying SAT solver'
arch=(loong64 x86_64)
url='https://github.com/msoos/cryptominisat/'
license=(MIT)
depends=(gcc-libs
         glibc
         gmp
         zlib)
makedepends=(cmake
             git
             help2man
             python-build
             python-installer
             python-setuptools
             python-toml
             python-wheel
             vim)
optdepends=('python: python module')
source=(git+https://github.com/msoos/cryptominisat#tag=$pkgver
        git+https://github.com/meelgroup/cadiback#commit=$_cadiback
        git+https://github.com/meelgroup/cadical.git#commit=$_cadical
        python-system-libs.patch)
sha256sums=('0e6fc417cfbe03f1b993cdaf5ba8c22e98585f58321df24ad87a28fc3854bb2f'
            'e7e96d6e91bba3c9d28c798545f25236f107aec97780ee0c459352606c7c3a0c'
            '1769c624bb55a26de5a027f17bfaa323fac8d338db8069eeb76934aa3c0625d4'
            'd5f0d3c314a36283dbea3022c45d1c21b0857b6e5ccddec61985b15391f611bb')

prepare() {
  cd $pkgname
  sed -e 's|, \"pathlib\"||' -i pyproject.toml
  git cherry-pick -n 2905d6d9a755e9f20ec4a4c22f7f27070c4455e7 \
                     1d735b6ce0e3d17cdec182db491646fc89aa8cf4 # Fix python build
  patch -p1 -i ../python-system-libs.patch # Build python module against shared library
  sed -e '/makeSuite(TestDump)/d' -i python/tests/test_pycryptosat.py # Remove broken tests
  sed -e 's|/$COMPILE/|/${COMPILE//\\//\\\\/}/|' -i ../cadiback/configure # Honor system build flags
}

build() {
  (
   cd cadical
   ./configure -static
   make -C build libcadical.a
  )
  (
   cd cadiback
   CXX="g++ $CXXFLAGS" ./configure
   make libcadiback.a
  )
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build

  cd $pkgname
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname
  PYTHONPATH="$PWD/"`echo build/lib.*` \
  LD_LIBRARY_PATH="$srcdir/build/lib" \
  python python/tests/test_pycryptosat.py
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
# Remove stray source files
  rm -r "$pkgdir"/usr/lib/python*/site-packages/{msvc,oracle,mpicosat}
}
