# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=cryptominisat
pkgver=5.11.21
pkgrel=2
pkgdesc='A modern, multi-threaded, feature-rich, simplifying SAT solver'
arch=(loong64 x86_64)
url='https://github.com/msoos/cryptominisat/'
license=(MIT)
depends=(gcc-libs
         glibc
         zlib)
makedepends=(cmake
             help2man
             python-build
             python-installer
             python-setuptools
             python-toml
             python-wheel
             vim)
optdepends=('python: python module')
source=(https://github.com/msoos/cryptominisat/archive/$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/msoos/cryptominisat/commit/9e13b568.patch)
sha256sums=('288fd53d801909af797c72023361a75af3229d1806dbc87a0fcda18f5e03763b'
            'cdf47cc22de3d94d4eb24cffc2c06859ab2a42a341c3379bb6407815f89836c5')

prepare() {
  sed -e 's|, \"toml\", \"pathlib\"||' -i $pkgname-$pkgver/pyproject.toml
  patch -d $pkgname-$pkgver -p1 < 9e13b568.patch # Fix tests with Python 3.12
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DNOBOSPHORUS=OFF
  cmake --build build

  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  _pythonpath=`echo build/lib.*`
  PYTHONPATH="$PWD/$_pythonpath" python python/tests/test_pycryptosat.py
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd cryptominisat-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
# Remove stray source files
  rm -r "$pkgdir"/usr/lib/python*/site-packages/{msvc,oracle,picosat}
}
