# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=optimist
pkgname=ruby-${_gemname}
pkgver=3.1.0
pkgrel=1
pkgdesc='Commandline option parser for Ruby that just gets out of your way'
url='https://github.com/ManageIQ/optimist'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-bundler')
checkdepends=('ruby-minitest' 'ruby-chronic')
options=('!emptydirs')
source=(https://github.com/ManageIQ/optimist/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('8b0403e7b166644264b297641b8a666ed3a50bbc0a1722b9a46ca256eb45147c51ec02f1152af2030fd360828a4358c14af9afe35200ec2120764691412dc1af')
b2sums=('861cf94a147246a9d5491497219efbdb0016cc6a0db57661d686e4d124c479286a95e1314fe31f640339321b3b64faadc1c40d9c385666dbaa25cea9d35e24a1')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
  sed 's|git ls-files -z|find -print0|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

check() {
  cd ${_gemname}-${pkgver}
  rake test
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md History.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  rm -r "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/test"
}

# vim: ts=2 sw=2 et:
