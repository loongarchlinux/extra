# Maintainer: Leonidas Spyropoulos <artafinde at archlinux dot org>

pkgname=vulture
pkgver=2.11
pkgrel=3
pkgdesc="Finds dead code in Python projects"
arch=('any')
url="https://github.com/jendrikseipp/vulture"
license=('MIT')
depends=(python-setuptools python-toml)
makedepends=(python-build python-installer python-wheel)
checkdepends=(python-pytest python-pint)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('5f510b05e12a8e0d75cdfde40aa2bb905d7fe24991928ac75ad0f833128a42d7')
b2sums=('c5df4d5135a8a33fcee80f991f505fa845904c9b7677738a857835994763587a114db9299386a0a3dec99fc578c3f866280983b079c866faa482d09f34708cd3')

build() {
  cd "${pkgname}"-"${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${pkgname}"-"${pkgver}"
  pytest
}

package() {
  cd "${pkgname}"-"${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
