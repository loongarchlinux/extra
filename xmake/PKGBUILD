# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: <waruqi@gmail.com>
# Contributor: lumpyzhu <lumpy.zhu@gmail.com>

pkgname=xmake
pkgver=2.9.4
pkgrel=1
pkgdesc='A cross-platform build utility based on Lua'
arch=(loong64 x86_64 i686 aarch64)
url="https://github.com/xmake-io/$pkgname"
license=(Apache)
makedepends=(ncurses
             readline)
_archive="$pkgname-$pkgver"
source=("$url/releases/download/v$pkgver/${_archive/-/-v}.tar.gz")
sha256sums=('75e2dde2bd2a48a332989b801ae65077c452d491fec517a9db27a81c8713cdc5')

# Upstream botched their tarball and double wrapped it. Also they keep
# switching up where they use what version tag format. Bah.
prepare() {
	tar xaf "${_archive/-/-v}.tar"
}

build() {
	cd "$_archive"
	./configure
	make
}

package() {
	cd "$_archive"
	make install DESTDIR="$pkgdir" PREFIX="/usr"
}
