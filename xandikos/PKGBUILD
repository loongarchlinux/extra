# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=xandikos
pkgver=0.2.11
pkgrel=2
pkgdesc='Lightweight yet complete CardDAV/CalDAV server that backs onto a Git repository'
arch=('any')
url=https://www.xandikos.org
license=('GPL-3.0-or-later')
depends=(
  'python-aiohttp'
  'python-defusedxml'
  'python-dulwich'
  'python-icalendar'
  'python-jinja'
  'python-multidict'
  'python-vobject'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
optdepends=(
  'python-aiohttp-openmetrics: for metrics with the standalone server'
  'python-systemd: for systemd socket activation'
  'uwsgi-plugin-python: for the WSGI interface'
)
source=(
  "git+https://github.com/jelmer/$pkgname.git#tag=v$pkgver"
  "$pkgname.sysusers.conf"
  "$pkgname.tmpfiles.conf"
)
b2sums=('d68a9c59e3c567ec2614f3f66077329cdd74156ee7dc242161f03ec46066cb2f6e7ac83a529e5c67ec68493b0c9bc2e294ca6a66a4890929bec2d0a50bdc3dfe'
        '086423882a085cb7fbecad3f53ad60aa6c322a545c180fe8b4c9f2bba5331fa52d6fc3e96ec068c589d3ab3e0b8f215e1a94473c511560188bd873b7ff5780d6'
        'be8df371a3cdd55e2efd4549251b44cbb5699762f14aa3dcb0e85cdbb3710d75fae8e38b615057f6da37fe744019aabc2c366136ca577a152cd03d380db189c2')
#validpgpkeys=('DC837EE14A7E37347E87061700806F2BD729A457') # Jelmer VernooÄ³ <jelmer@jelmer.uk>

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname
  make check
}

package() {
  install -Dm644 $pkgname.sysusers.conf \
    "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
  install -Dm644 $pkgname.tmpfiles.conf \
    "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf

  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.rst
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname/examples examples/*
  install -Dm644 -t "$pkgdir"/usr/share/man/man8 man/$pkgname.8
}
