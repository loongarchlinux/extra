# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=csfml
pkgver=2.6.0
pkgrel=1
pkgdesc='C bindings for sfml'
arch=('loong64' 'x86_64')
url='http://www.sfml-dev.org/'
license=('custom:zlib')
depends=("sfml")
makedepends=('cmake' 'doxygen' 'ninja')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/SFML/CSFML/archive/${pkgver}.tar.gz")
sha512sums=('d4d00349cbfa47a39be13ff02ae473e3dcd3100c4d9456c491a539e1dca6aabcaa3d80056d282b4a24c8344ee1e0b1802e916d7af21384916ee2b71f7756286f')

build() {
  cd CSFML-*

  cmake \
      -Bbuild \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCSFML_BUILD_DOC=true
  ninja -C build
  ninja -C build doc
}

package() {
  cd CSFML-*

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 license.md ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

