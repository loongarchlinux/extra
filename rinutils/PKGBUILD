# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=rinutils
pkgver=0.10.3
pkgrel=1
pkgdesc='C11 / gnu11 utilities C library'
arch=(any)
url='https://www.shlomifish.org/open-source/projects/'
license=(MIT)
depends=()
makedepends=(cmake
             git)
source=(git+https://github.com/shlomif/rinutils#tag=$pkgver)
sha256sums=('330800f03349988c358e9f444ac4995915f0413e6d9b31dbe7d27750d5023f35')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_TEST_SUITE=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
