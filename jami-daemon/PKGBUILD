# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=jami-daemon
pkgver=20240322
pkgrel=3
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (daemon component)"
arch=(loong64 x86_64)
url="https://jami.net"
license=(GPL-3.0-or-later)
groups=(jami)
depends=(glibc opendht gnutls nettle libgit2 libsecp256k1 libsecp256k1.so ffmpeg speexdsp
         fmt yaml-cpp jsoncpp zlib alsa-lib libpulse jack libupnp libnatpmp
         openssl webrtc-audio-processing systemd-libs libarchive)
# portaudio needs a not-yet-upstream patch https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/650
makedepends=(git cmake perl asio msgpack-c msgpack-cxx restinio systemd meson)
checkdepends=(cppunit)
_commit=32f39e65483cb22729eb922d72434013b337f2c9
_pjprojectver=e12ea3bfa81cc4f46031599f80707e81133f1353
_dhtnetver=024c46fb1f14276d4adf15764ed97b733890826e
source=(git+https://git.jami.net/savoirfairelinux/${pkgname}.git#commit=${_commit}
        https://github.com/savoirfairelinux/pjproject/archive/${_pjprojectver}/pjproject-${_pjprojectver}.tar.gz
        dhtnet-$_dhtnetver.tar.gz::https://review.jami.net/plugins/gitiles/dhtnet/+archive/$_dhtnetver.tar.gz
        libgit2-1.8.patch
        ffmpeg-7.patch)
noextract=(pjproject-${_pjprojectver}.tar.gz
           dhtnet-${_dhtnetver}.tar.gz)
sha512sums=('fe6bb7d9b78d2da6ec5be971a5e0e663379f541adba5a9103bba2c7252b6f0b347983f3a0c29300acc9bf0758779a7c7f55ab6b67eb0b1620c46600bf0535068'
            '7d481f4dcbe7232ff5a63cae46206943b64ac2d30f3b4598a9caf863322c1cfcd93c6c1232e2fa8f4e747f54f70b71a9b8f42f7e90d98fdd02124088afbdd37d'
            'SKIP'
            'eef5c72149baaf9765917046a6e0dd8daffa422b69d1df15037cb0a0a756f632ddfb5f0b2299116554615972787cee2b9e2cd171063324d5d9a60c2e54038006'
            '2a895a82e8fad356445ed1a8adf860154e268f89c0812b3bfac8bb37fd45198ed8b076e81b2cc2cdd635d27cb0496a8d88ae982dcdc46fa42d7799b913695731')

pkgver() {
  cd ${pkgname}
  TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD
}

prepare() {
  cd ${pkgname}
  cp ../pjproject-${_pjprojectver}.tar.gz contrib/tarballs
  cp ../dhtnet-$_dhtnetver.tar.gz contrib/tarballs/
  mkdir contrib/native

  patch -p1 -i ../libgit2-1.8.patch # Fix build with libgit2 1.8
  patch -p1 -i ../ffmpeg-7.patch # Fix build with ffmpeg 7
}

build() {
  cd ${pkgname}/contrib/native
  ../bootstrap \
      --disable-downloads \
      --disable-all \
      --enable-pjproject \
      --enable-dhtnet
  make DEPS_pjproject= DEPS_dhtnet=pjproject

  cd "${srcdir}"
  arch-meson ${pkgname} build -D interfaces=library -D opensl=disabled -D portaudio=disabled \
                              $( ((CHECKFUNC)) && echo -D tests=true) \
                              -D pkg_config_path="${PWD}/${pkgname}/contrib/$(cc -dumpmachine)/lib/pkgconfig"
  meson compile -C build
}

#check() {
# https://git.jami.net/savoirfairelinux/jami-daemon/-/issues/649
#  meson test -C build --print-errorlogs
#}

package() {
  meson install -C build --destdir "${pkgdir}"
}
