# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=desync
pkgver=0.9.6
pkgrel=1
pkgdesc="Alternative casync implementation"
arch=(loong64 x86_64)
url="https://github.com/folbricht/desync"
license=(BSD-3-Clause)
depends=(glibc)
makedepends=(go)
options=(!lto)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('b1f840fda8aba170c93f4b17f53595290fed1eb0a3a408e47c60214e6aabe20583c8032aec838b883d1251b31f7a819c413dca0137ab8c7d83e8de04f466d4dd')
b2sums=('eb56e5706421d2b63d9f05fe0b4d24d9c5c6eedb676c52b13563e35792198f382d48536bd0393661c20cd8fa4588949f954a7a404735ab4a078baa06e5adb2bd')

build() {
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOPATH="$srcdir"
  export GOFLAGS="-buildmode=pie -mod=readonly -modcacherw"

  cd $pkgname-$pkgver
  go build -ldflags "-compressdwarf=false -linkmode external" -o $pkgname ./cmd/$pkgname
}

check() {
  cd $pkgname-$pkgver
  go test ./cmd/$pkgname
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 $pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
