# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Luca Weiss <luca (at) z3ntu (dot) xyz>

pkgname=coeurl
pkgver=0.3.1
pkgrel=1
pkgdesc="A simple async wrapper around CURL for C++"
arch=("loong64" "x86_64")
url="https://nheko.im/nheko-reborn/coeurl"
license=('MIT')
depends=(gcc-libs glibc)
makedepends=(curl fmt libevent meson spdlog)
provides=(libcoeurl.so)
source=("https://nheko.im/nheko-reborn/coeurl/-/archive/v$pkgver/coeurl-v$pkgver.tar.gz")
sha256sums=('6203654c2eec7a286388a6cfa8d8ea5698c57b7b8be235b2cc165f0b4c7bee0b')
b2sums=('5e4bc57e0c3f89c81bcdf2520d519fa9c2c97e876b9bd9d6fccbda31bddd7ce4dc0943549a55d0f5593c48f6219fa1d01d1c8d6c872583aea91a135dcd8d8a5c')

build() {
  arch-meson $pkgname-v$pkgver build
  meson compile -C build
}

# Tests fail 
# - Needs cmake to properly detect doctest dependency https://github.com/doctest/doctest/issues/742
# - Needs to enable them with `-D tests=true`
# - Needs to execute ./scripts/run_testserver.sh and ./scripts/run_tls_testserver.sh prior to tests
# check() {
#   ninja test -C build
# }

package() {
  depends+=(
    curl libcurl.so
    fmt libfmt.so
    libevent libevent_{core,pthreads}-2.1.so
    spdlog libspdlog.so
  )

  meson install -C build --destdir "$pkgdir"
  install -vDm 644 $pkgname-v$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set ft=sh sw=2 sts=2 et:
