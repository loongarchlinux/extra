# Maintainer: Filipe La√≠ns <lains@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=fwupd-efi
pkgver=1.6
pkgrel=1
pkgdesc='EFI Application used by uefi-capsule plugin in fwupd'
arch=('loong64' 'x86_64')
url='https://github.com/fwupd/fwupd-efi'
license=('LGPL')
makedepends=(
  'git'
  'gnu-efi'
  'meson'
  'python-pefile'
)
source=(git+${url}.git#tag=${pkgver}?signed)
sha256sums=('00a08795795c8f05c685649ac41ff3e4c73203b5314f6fcf8651d6206219dd96')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17') # Richard Hughes <richard@hughsie.com>

build() {
  arch-meson ${pkgname} build  \
    -D efi_sbat_distro_id='arch' \
    -D efi_sbat_distro_summary='Arch Linux' \
    -D efi_sbat_distro_pkgname=${pkgname} \
    -D efi_sbat_distro_version=${pkgver} \
    -D efi_sbat_distro_url="https://archlinux.org/packages/community/x86_64/${pkgname}/"
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
