# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mpfi
pkgver=1.5.4
pkgrel=5
pkgdesc='C library for interval arithmetic'
arch=(loong64 x86_64)
url='http://perso.ens-lyon.fr/nathalie.revol/software.html'
license=(GPL-3.0-or-later
         LGPL-3.0-or-later)
depends=(glibc
         gmp
         mpfr)
makedepends=(git)
source=(git+https://github.com/arpra-project/mpfi#tag=$pkgver)
sha256sums=('b13c9bad628b2c339fd89452f2597d4ff808574cc520779e259493a880a88af8')

prepare() {
  cd $pkgname
  autoreconf -vi
}

build() {
  cd $pkgname
  CFLAGS+=" -Wno-incompatible-pointer-types" \
  ./configure \
    --prefix=/usr \
    --enable-shared
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
