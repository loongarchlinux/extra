# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-backgrounds
pkgver=46.0
pkgrel=1
pkgdesc="Background images and data for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-backgrounds"
arch=(any)
license=(CC-BY-SA-3.0)
depends=(libjxl)
makedepends=(
  glib2
  git
  meson
)
groups=(gnome)
_commit=8f907c75fffa246695334e72e794e16efbf7dd87  # tags/46.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-backgrounds.git#commit=$_commit")
b2sums=('08d9105558fe73cbc6aef3c06b1a51dcd1350a7b8f238f3d7494c8f12b0bd5c7d0237c982b51cd6d18583074685c368c59a35ecf8de621f517ee53bd56e2cefa')

pkgver() {
  cd $pkgbase
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
