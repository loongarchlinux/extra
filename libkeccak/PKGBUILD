# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Mattias Andrée <`base64 -d`(bWFhbmRyZWUK)@kth.se>

pkgname=libkeccak
pkgver=1.4
pkgrel=2
pkgdesc='Keccak-family hashing library'
arch=('loong64' 'x86_64')
url='https://codeberg.org/maandree/libkeccak'
license=('custom:ISC')
depends=('glibc')
makedepends=('git')
source=("git+https://codeberg.org/maandree/libkeccak#tag=${pkgver}")
b2sums=('0062309df2d8e75d549c28fac63756ded032e554c2e3a1888ea57dcb2430264ec91d74bcbf654cd03411e67afd96b168bb77dc1dee8136ef796f73f176ad8a1b')
# validgpgkeys=(
  # 'BC61893EC01FDBDEE061BEF67BE0C42240089FE0' # Mattias Andrée <maandree@kth.se>
# )

prepare() {
  cd ${pkgname}
  sed -i 's#FLAGS\s\+=#FLAGS +=#g' optimised.mk
}

build() {
  cd ${pkgname}
  make CONFIGFILE=optimised.mk PREFIX=/usr
}

check() {
  cd ${pkgname}
  make CONFIGFILE=optimised.mk check
}

package() {
  cd ${pkgname}
  make CONFIGFILE=optimised.mk PREFIX=/usr DESTDIR="${pkgdir}" install
}
