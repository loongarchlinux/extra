# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Nicolas Hureau <archlinux@kalenz.net>

pkgname=jadx
pkgver=1.5.0
pkgrel=1
pkgdesc='Command line and GUI tools to produce Java source code from Android Dex and APK files'
url='https://github.com/skylot/jadx'
arch=(any)
license=(Apache-2.0)
depends=(
  bash
)
makedepends=(
  'java-environment=21'
  gradle
)
source=(https://github.com/skylot/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('fcb62aa255ab8643ed47367e8b1b4224a0a91159bd7b75de54acf37495a0248668db93fa76fe345846aadf91a9ad1735072130d9351434f71d79165e23875ba4')
b2sums=('6aa1a6cbf324d56fff3eab2dc185babf8c5a0d8aacfa5271704c5a6213589c381c9ac1b3a7f0f17316976745f090d69df89ada64d644afd424158b1bdd7f827f')

build() {
  cd ${pkgname}-${pkgver}
  gradle dist
}

check() {
  cd ${pkgname}-${pkgver}
  gradle test
}

package() {
  depends+=('java-runtime>=11')
  cd ${pkgname}-${pkgver}

  pushd build/jadx
  install -Dm 755 bin/{jadx,jadx-gui} -t "${pkgdir}/usr/share/java/${pkgname}/bin"
  install -Dm 644 lib/* -t "${pkgdir}/usr/share/java/${pkgname}/lib"
  popd

  install -d "${pkgdir}/usr/bin"
  ln -s /usr/share/java/${pkgname}/bin/jadx "${pkgdir}/usr/bin/jadx"
  ln -s /usr/share/java/${pkgname}/bin/jadx-gui "${pkgdir}/usr/bin/jadx-gui"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 NOTICE README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
