# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: a821
# Contributor: asm0dey <pavel.finkelshtein+AUR@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=pgcli
pkgver=4.1.0
pkgrel=1
pkgdesc='A command line interface for PostgreSQL with auto-completion and syntax highlighting'
url='https://www.pgcli.com/'
arch=('any')
license=('BSD-3-Clause')
depends=(
  'python'
  'python-cli_helpers'
  'python-click'
  'python-configobj'
  'python-pendulum'
  'python-pgspecial'
  'python-prompt_toolkit'
  'python-psycopg'
  'python-pygments'
  'python-setproctitle'
  'python-sqlparse'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-sshtunnel'
)
optdepends=(
  'python-keyring: for keyring integration'
  'python-sshtunnel: for accessing remote instances over SSH'
)
source=("$pkgname::git+https://github.com/dbcli/pgcli.git#tag=v$pkgver")
sha512sums=('c1cf4390210b033360a09387a045a4a910b76d66908fca08d90e4422204a61d8490a490282ff519ea501a3da9499e85460ec705c8eba215c3b71df35821d92d8')
b2sums=('0b2c95a2fc8f2c83e3f898641ac059ed387e1b99029cdd91c4193bee7e05656c068e7417ba2ab4bd27230d5b04ef5588b2b458c9439b0c8a80642e3b3bf11ae3')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
