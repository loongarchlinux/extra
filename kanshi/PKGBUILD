# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Antoine Damhet

pkgname=kanshi
pkgver=1.7.0
pkgrel=1
pkgdesc='Dynamic output configuration for Wayland WMs'
arch=(loong64 x86_64)
url="https://wayland.emersion.fr/kanshi/"
license=('MIT')
makedepends=(
    'meson'
    'scdoc'
)
depends=(
    'libscfg.so'
    'libvarlink'
    'wayland'
)
source=(
    "https://git.sr.ht/~emersion/kanshi/refs/download/v$pkgver/kanshi-$pkgver.tar.gz"
    "https://git.sr.ht/~emersion/kanshi/refs/download/v$pkgver/kanshi-$pkgver.tar.gz.sig"
)
sha256sums=('76ef49d35c1c8764c2956e05e9d37c4bf5236eb204f5d3a780dca41e24c3f382'
            'SKIP')
validpgpkeys=('34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48')  # Simon Ser (emersion)

build() {
    meson --prefix=/usr \
        --buildtype=plain \
        -Dman-pages=enabled \
        -Dipc=enabled \
        "${pkgname}-${pkgver}" build
    ninja -C build
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install
    cd "$pkgname-$pkgver"
    install -Dm0644 "LICENSE" -t "${pkgdir}/usr/share/licenses/$pkgname/"
    install -Dm0644 "README.md" -t "$pkgdir/usr/share/doc/$pkgname/"
}
