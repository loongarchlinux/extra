# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=geonkick
pkgname=(geonkick geonkick-{common,lv2,standalone,vst3})
pkgver=3.4.0
pkgrel=2
pkgdesc="A free software percussion synthesizer"
arch=(loong64 x86_64)
url="https://geonkick.org/"
_url="https://gitlab.com/Geonkick-Synthesizer/geonkick"
license=(GPL-3.0-or-later)
makedepends=(
  cairo
  cmake
  gcc-libs
  glibc
  gtkmm3
  hicolor-icon-theme
  jack
  libsndfile
  libx11
  libxkbcommon
  libxkbcommon-x11
  lv2
  pango
  rapidjson
  vst3sdk
  xcb-util
  xcb-util-cursor
  xcb-util-keysyms
)
checkdepends=(lv2lint)
source=($_url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('6779b313ae0a859454d7d4af7e9ea5c7c666ba9bfaa6653871b9f55e7f20010a7c475d704e68c844354188a6395b190b48dd9b9bd378fabd96415c577b597295')
b2sums=('560f4f5a5043954c8963ba6ca66f005e625434a474c6f0349fb0b692456eb15fd2a02f6a1b4a951512505db2a5fce27e8719f1d80c8f68f51f6a6e6499b81b0d')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D VST3_SDK_PATH=/usr/src/vst3sdk
    -S $pkgname-v$pkgver
    -W no-dev
  )

  export CFLAGS+=" -DNDEBUG"
  export CXXFLAGS+=" -DNDEBUG"
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  local lv2lint_options=(
    -Mpack
    -t "Plugin Symbols"  # https://gitlab.com/Geonkick-Synthesizer/geonkick/-/issues/5
    -t "UI Symbols"  # https://gitlab.com/Geonkick-Synthesizer/geonkick/-/issues/5
    -I build/src/plugin/lv2/
    "http://geonkick.org/geonkick"
  )

  cp -av $pkgname-v$pkgver/src/plugin/lv2/$pkgname.lv2/*.ttl build/src/plugin/lv2/
  lv2lint "${lv2lint_options[@]}"
  rm -v build/src/plugin/lv2/*.ttl
}

package_geonkick() {
  depends=(
    $pkgbase-{lv2,standalone,vst3}=$pkgver
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-v$pkgver/data/$pkgname.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 $pkgname-v$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"

  (
    cd "$pkgdir"
    _pick $pkgbase-common usr/share/$pkgbase
    _pick $pkgbase-common usr/share/doc

    _pick $pkgbase-lv2 usr/lib/lv2

    _pick $pkgbase-standalone usr/bin
    _pick $pkgbase-standalone usr/share/{applications,icons,man,mime}

    _pick $pkgbase-vst3 usr/lib/vst3
  )
}

package_geonkick-common() {
  pkgdesc+=" - common files"

  mv -v $pkgname/* "$pkgdir"
}

package_geonkick-lv2() {
  pkgdesc+=" - LV2 plugin"
  groups=(
    lv2-plugins
    pro-audio
  )
  depends=(
    cairo
    gcc-libs
    geonkick-common=$pkgver
    glibc
    libsndfile
    libx11
    lv2-host
  )
  
  mv -v $pkgname/* "$pkgdir"
}

package_geonkick-standalone() {
  pkgdesc+=" - JACK standalone"
  groups=(pro-audio)
  depends=(
    cairo
    gcc-libs
    geonkick-common=$pkgver
    glibc
    hicolor-icon-theme
    jack
    libsndfile
    libx11
  )

  mv -v $pkgname/* "$pkgdir"
}

package_geonkick-vst3() {
  pkgdesc+=" - VST3 plugin"
  groups=(
    pro-audio
    vst3-plugins
  )
  depends=(
    cairo
    gcc-libs
    geonkick-common=$pkgver
    glibc
    libsndfile
    vst3-host
  )
  
  mv -v $pkgname/* "$pkgdir"
}
