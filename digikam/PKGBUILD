# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=digikam
_pkgver=8.3.0
pkgver=${_pkgver//-/} # for beta versions
pkgrel=5
pkgdesc='An advanced digital photo management application'
arch=(loong64 x86_64)
license=(GPL)
url='https://www.digikam.org/'
depends=(akonadi-contacts
         exiv2
         expat
         ffmpeg
         gcc-libs
         glib2
         glibc
         imagemagick
         jasper
         kcalendarcore
         kcompletion
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kfilemetadata
         ki18n
         kiconthemes
         kio
         knotifications
         knotifyconfig
         kservice
         kwidgetsaddons
         kxmlgui
         lcms2
         lensfun
         libglvnd
         libgphoto2
         libheif
         libjpeg-turbo
         libksane
         libpng
         libtiff
         libx11
         libxml2
         libxslt
         opencv
         perl
         perl-image-exiftool
         qt6-base
         qt6-multimedia
         qt6-networkauth
         qt6-scxml
         qt6-svg
         qt6-webengine
         sh
         solid
         sonnet
         threadweaver
         x265
         zlib)
makedepends=(boost
             doxygen
             eigen
             extra-cmake-modules
             kdoctools)
optdepends=('darktable: RAW import'
            'hugin: panorama tool'
            'qt6-imageformats: support for additional image formats (WEBP, TIFF)'
            'rawtherapee: RAW import')
source=(https://download.kde.org/stable/$pkgname/${_pkgver%-*}/digiKam-$_pkgver-1.tar.xz{,.sig}
        kf6-dependencies.patch)
sha256sums=('05b145ff7f2f2005fa21bc579c152ab23c8191b678ff2944c8f0406d6b9de6d8'
            'SKIP'
            '3283e70a715436b3431d9931f9842770eb341543cebe5e88c3f14e63ab88b74a')
validpgpkeys=(D1CF2444A7858C5F2FB095B74A77747BC2386E50) # digiKam.org (digiKam project) <digikamdeveloper@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < kf6-dependencies.patch
}

build() {
  cmake -B build -S $pkgname-$_pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_WITH_QT6=ON \
    -DENABLE_KFILEMETADATASUPPORT=ON \
    -DENABLE_MEDIAPLAYER=ON \
    -DENABLE_AKONADICONTACTSUPPORT=ON \
    -DENABLE_MYSQLSUPPORT=ON \
    -DENABLE_APPSTYLES=ON \
    -DENABLE_QWEBENGINE=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
