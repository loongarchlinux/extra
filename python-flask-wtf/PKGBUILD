# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Serge Victor <arch@random.re>

_pyname=flask-wtf
pkgname=python-flask-wtf
pkgver=1.2.1
pkgrel=2
pkgdesc='Simple integration of Flask and WTForms'
url='https://flask-wtf.readthedocs.io/'
arch=('any')
license=('BSD')
depends=('python-flask' 'python-wtforms' 'python-werkzeug' 'python-flask-babel')
makedepends=('python-build' 'python-installer' 'python-sphinx' 'python-pallets-sphinx-themes'
             'python-sphinxcontrib-log-cabinet' 'python-sphinx-issues' 'python-hatchling')
checkdepends=('python-pytest')
options=('!makeflags')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/wtforms/flask-wtf/archive/v${pkgver}.tar.gz)
sha512sums=('07590fa757d92a9f0bf0afae9b726344ab79227a187ddbe62805e3b5b50f7bcad521772c884d342bf687d530a100fa4cb41c4989ebbd670955dbb950bf675501')

prepare() {
  cd ${_pyname}-${pkgver}
  sed -i "s/^release, version =/& '${pkgver}', '${pkgver%.*}.x' #/" docs/conf.py
}

build() {
  cd ${_pyname}-${pkgver}
  make -C docs man text SPHINXBUILD=sphinx-build
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/text "${pkgdir}/usr/share/doc/${pkgname}/text"
  install -Dm 644 docs/_build/man/flask-wtf.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
