# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parsel
pkgver=1.8.1
pkgrel=3
pkgdesc="Library to extract data from HTML and XML using XPath and CSS selectors"
arch=('any')
license=('BSD-3-Clause')
url="https://github.com/scrapy/parsel"
depends=('python-cssselect' 'python-jmespath' 'python-lxml' 'python-packaging' 'python-w3lib')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-sybil' 'python-psutil')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/parsel/archive/v$pkgver.tar.gz")
sha512sums=('3b930e6fa4989bc9301b14cffa946bdfaf23c922ac165d0748b4cb20c3c855c17b14b59eb92cb9d0e03e0a657e15d8aeb01434f024ba8c0936edaa05a289d770')

prepare() {
  cd parsel-$pkgver
  sed -i '/pytest-runner/d' setup.py
}

build() {
  cd parsel-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd parsel-$pkgver
  python -m pytest
}

package() {
  cd parsel-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
