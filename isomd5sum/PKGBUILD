# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Fernando Munoz <f[at]beford.net>
# Contributor: Alexandre Bique <bique.alexandre[at]gmail.com>

pkgbase=isomd5sum
pkgname=('isomd5sum' 'python-isomd5sum')
pkgver=1.2.4
pkgrel=2
arch=('loong64' 'x86_64')
license=('GPL-2.0-or-later')
makedepends=('popt' 'python')
url='https://github.com/rhinstaller/isomd5sum'
source=("$pkgbase-$pkgver.tar.gz::https://github.com/rhinstaller/isomd5sum/archive/$pkgver.tar.gz")
options=('!emptydirs')
sha512sums=('9afd4cb0935e2c81f82a7076af370e7588feeb9dc876ddcbe0433519ef348bb9ed3cb0c4080918f82695d06bf7fb5b9a53ecdaa94540cadd03082e87cfb9dc2c')

prepare() {
  sed -i -e 's/^\(LIBDIR = \)lib64/\1lib/' $pkgbase-$pkgver/Makefile
}

build() {
  cd $pkgbase-$pkgver
  CFLAGS+=' -ffat-lto-objects' \
  make all
}

package_isomd5sum() {
  depends=('popt')
  pkgdesc="Utilities for working with md5sum implanted in ISO images"

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install-bin install-devel
}

package_python-isomd5sum() {
  depends=('python')
  pkgdesc="Python3 bindings for isomd5sum"

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install-python
}
