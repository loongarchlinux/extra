# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Partha Chowdhury <kira.laucas@gmail.com>

pkgname=conky
pkgver=1.20.2
pkgrel=1
pkgdesc='Light-weight system monitor for X, Wayland, and other things, too'
url='https://github.com/brndnmtthws/conky'
license=(
  'BSD-3-Clause'
  'GPL-3.0-or-later'
)
arch=('loong64' 'x86_64')
makedepends=(
  'catch2-v2'
  'cmake'
  'docbook2x'
  'docbook-xsl'
  'git'
  'man-db'
  'pandoc'
  'python-yaml'
  'python-jinja'
  'wayland-protocols'
)
depends=(
  'cairo'
  'curl' libcurl.so
  'fontconfig'
  'gcc-libs'
  'glib2'
  'glibc'
  'hicolor-icon-theme'
  'imlib2'
  'libpulse' libpulse.so
  'libx11'
  'libxdamage'
  'libxext'
  'libxfixes'
  'libxft'
  'libxi'
  'libxinerama'
  'libxml2'
  'libxnvctrl' libXNVCtrl.so
  'lua'
  'ncurses' libncursesw.so
  'pango'
  'systemd-libs' libsystemd.so
  'wayland'
  'wireless_tools'
)
source=("git+$url.git#tag=v${pkgver}?signed")
sha512sums=('3819df8b1f2da0d7b89cd0097b3f72c5248df089fc990c695ee79c56679cf7e45d6b24f7295a5eb99ed6bd16896c56b32ae306ab5e2d79204a794f541a824565')
b2sums=('25f60e912e1c3bb641e05999a982854701d8480284f7d84cdfb89bd13f055463fe96b0789886505a5d238f9e4842362fbfeebf0a1c2f4454c557939025c89cef')
validpgpkeys=(
  'AF310802C8EFA20499CF3556137B7AC2BDFD8DF0' # Brenden Matthews <github@brenden.brndn.io>
)

pkgver() {
  cd ${pkgname}
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname
# Unbundle catch2 to fix build with glibc 2.35
  rm -r tests/catch2
  ln -s /usr/include/catch2 tests
}

build() {
  cd ${pkgname}
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=None \
    -D CMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects" \
    -D MAINTAINER_MODE=OFF \
    -D BUILD_TESTS=ON \
    -D BUILD_DOCS=ON \
    -D BUILD_EXTRAS=ON \
    -D BUILD_WLAN=ON \
    -D BUILD_XDBE=ON \
    -D BUILD_XSHAPE=ON \
    -D BUILD_IMLIB2=ON \
    -D BUILD_CURL=ON \
    -D BUILD_RSS=ON \
    -D BUILD_NVIDIA=ON \
    -D BUILD_PULSEAUDIO=ON \
    -D BUILD_JOURNAL=ON \
    -D BUILD_WAYLAND=ON \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev \
    -S .

  make -C build
}

package() {
  cd ${pkgname}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 build/extras/vim/syntax/conkyrc.vim -t "${pkgdir}"/usr/share/vim/vimfiles/syntax
  install -Dm 644 extras/vim/ftdetect/conkyrc.vim -t "${pkgdir}"/usr/share/vim/vimfiles/ftdetect
}

# vim: ts=2 sw=2 et:
