# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>

pkgname=cm256cc
pkgver=1.1.1
pkgrel=1
pkgdesc='Fast GF(256) Cauchy MDS Block Erasure Codec in C++'
arch=(loong64 x86_64)
url="https://github.com/f4exb/cm256cc"
license=(GPL3)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cmake
)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('fcda338b766bcae1af1e25ab6237c30ed60722b2e6e679e13623005d441d3e8daa462c7a7735ca239c0a1fa5f2d8b28455fe40f40762ebefa997c2ebd3731082')
b2sums=('b15cd0022343c8e0b02b645194610d3065521466c9e58e65418475f64966077fc6646a6fa2e8659ced83d0e0b4509aa62000bee08046f36570097082d4a9a70b')

build() {

  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_LIBDIR=lib
    -D CMAKE_INSTALL_PREFIX=/usr
    -D ENABLE_DISTRIBUTION=TRUE
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

# Project does not provide any tests
# check() {
#   ctest --test-dir build --output-on-failure
# }

package() {
  DESTDIR="$pkgdir" cmake --install build
}
