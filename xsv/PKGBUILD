# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>

pkgname=xsv
pkgver=0.13.0
pkgrel=3
pkgdesc="A CLI for indexing, slicing, analyzing, splitting and joining CSV files"
arch=('loong64' 'x86_64')
url="https://github.com/BurntSushi/xsv"
license=('Unlicense' 'MIT')
depends=('gcc-libs' 'glibc')
makedepends=('cargo' 'git')
source=("git+$url.git#tag=$pkgver")
sha256sums=('4e2cde2ae03acabd1b7f94be2864f28c99dc6e04d82483567ce11ff29c5f5913')

prepare() {
  cd xsv

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd xsv

  cargo build --frozen --release
}

check() {
  cd xsv

  cargo test --frozen --release
}

package() {
  cd xsv

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/xsv

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" ./*LICENSE*
}
