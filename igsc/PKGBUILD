# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=igsc
pkgver=0.8.16
pkgrel=1
pkgdesc="Intel graphics system controller firmware update library"
arch=(loong64 x86_64)
url="https://github.com/intel/igsc/"
license=(Apache-2.0)
depends=(intel-metee systemd-libs)
makedepends=(cmake systemd)
source=(${url}/archive/V${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('03e797f23f48221ce797227a1059edd7bceade189966cc1550f4c1d64dbc1550')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -G 'Unix Makefiles' \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_WERROR=OFF \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
