# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Gobidev <adrian[dot]groh[at]t-online[dot]de>
# Contributor: Hao Long <aur@esd.cc>

pkgname=rustscan
pkgver=2.3.0
pkgrel=2
pkgdesc='A modern port scanner'
arch=('loong64' 'x86_64')
url='https://github.com/rustscan/RustScan'
license=('GPL-3.0-or-later') # https://github.com/RustScan/RustScan/issues/615
depends=('gcc-libs')
makedepends=('git' 'rust')
optdepends=('nmap: Script engine support')
checkdepends=('python')
options=('!lto')
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('a21f951914c71d42ffd0a58a4c394d32d07f33fb82cea39e48657ba60da5a446fe94de50f772ae657e3682a8fd70bb7e4d1560eb07cb98599e68eae9514e2e87')
b2sums=('13234f92c7eb67ce64a2be97e542da540a79e6eae7eb37ca74bdc7e758dacca1f2ac1e2586021813606d51098500ad0e500f57dd4dc17f6e37edcea42d304553')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/rustscan
}
