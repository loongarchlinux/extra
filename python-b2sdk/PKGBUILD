# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-b2sdk
pkgver=2.4.1
pkgrel=1
pkgdesc='Python module to access B2 cloud storage'
arch=('any')
url='https://github.com/Backblaze/b2-sdk-python'
license=('MIT')
depends=(
  'python'
  'python-logfury'
  'python-requests'
  'python-urllib3'
  'python-importlib-metadata'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-pdm-backend'
  'python-wheel'
)
checkdepends=(
  'python-pytest'
  'python-pytest-mock'
  'python-pytest-lazy-fixture'
  'python-pyfakefs'
  'python-tqdm'
  'python-pydantic'
)
optdepends=(
  'python-tqdm: for TqdmProgressListener class'
  'python-pydantic: for improved type hints'
)
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('d91f873561f694bf961ce0bd4cbeb58fa778a448dc0823ad217828b5b7e41d233657960ccc91a779e4994cedee93b36c0efdab26900405b75166c46e1dd1af61')
b2sums=('9723ee5b8330e7aa9baa1d40f52a7ae9872a6288e4abd9792809eba83e4c17946d303df6fd627924b9624869bc352f59b573f6c7d2718869f418f2a0b7a55c12')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  # temporary install so pytest can pick up the compiled extension
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$(pwd)/tmp/$site_packages"

  pytest -v \
    --ignore=test/integration \
    --deselect test/static/test_licenses.py::test_files_headers
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
