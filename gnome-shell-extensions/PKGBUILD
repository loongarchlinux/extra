# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=gnome-shell-extensions
pkgver=46.1
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://wiki.gnome.org/Projects/GnomeShell/Extensions"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gnome-shell
)
makedepends=(
  git
  meson
)
optdepends=(
  'gnome-menus: Apps Menu extension'
  'libgtop: System Monitor extension'
)
groups=(gnome)
source=(
  # GSE tags use SSH signatures which makepkg doesn't understand
  "git+https://gitlab.gnome.org/GNOME/gnome-shell-extensions.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('77530896b7a640498ef99e5067bc8b8bcbd4f9788d0c43055c9d01bf30e224be1595fc8e9ebb7addfa1de76b83a7dc1368508c6b4c9e8cd9aa0f46e8aba0c9d4')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D extension_set=all
    -D classic_mode=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
