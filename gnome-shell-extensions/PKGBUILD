# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=gnome-shell-extensions
pkgver=46.2
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://apps.gnome.org/Extensions"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gnome-shell
)
makedepends=(
  git
  meson
)
optdepends=(
  'gnome-menus: Apps Menu extension'
  'libgtop: System Monitor extension'
)
groups=(gnome)
source=(
  # GSE tags use SSH signatures which makepkg doesn't understand
  "git+https://gitlab.gnome.org/GNOME/gnome-shell-extensions.git#tag=${pkgver/[a-z]/.&}"
)
b2sums=('dfe7af2627a1437067b4cdede82038b908eec0ea9ff669f0c613f4d90ced696c25892d57a7b3ce9c07495ec9d8f4175c2591c45e25a507861bf477cf418b15a5')

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D extension_set=all
    -D classic_mode=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
