# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Peukert <daniel@peukert.cc>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

pkgname=ocaml-base
pkgver=0.17.0
pkgrel=1
pkgdesc='Full standard library replacement for OCaml'
arch=('loong64' 'x86_64')
url="https://github.com/janestreet/base"
license=('MIT')
depends=('ocaml>=4.14.0' 'ocaml-sexplib0>=0.16.0' 'ocaml-intrinsics-kernel')
makedepends=('git' 'dune' 'ocaml-compiler-libs')
options=('!strip' '!debug')
source=("git+https://github.com/janestreet/base.git#tag=v$pkgver")
sha256sums=('87982479451825cced40cd02d11a5490fea769772c68d8080afdd9647564b1f4')

build() {
	cd base
	OCAMLPATH=/usr/lib/ocaml dune build --release
}

package() {
	cd base
	DESTDIR="$pkgdir" dune install --prefix '/usr' --libdir '/usr/lib/ocaml' --release

	install -dm755 "$pkgdir/usr/share/doc/$pkgname"
	mv "$pkgdir/usr/doc/base/"* "$pkgdir/usr/share/doc/$pkgname/"
	rm -r "$pkgdir/usr/doc/"

	install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
	ln -sf "/usr/share/doc/$pkgname/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
