# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Andre Klitzing <andre AT incubo DOT de>
# Contributor: max-k <max-k AT post DOT com>

pkgname=reprepro
pkgver=5.4.3
pkgrel=1
pkgdesc='Debian package repository producer'
url="https://salsa.debian.org/debian/reprepro"
arch=('loong64' 'x86_64')
license=('GPL-2.0-only')
depends=(
  'db5.3'
  'glibc'
  'libarchive.so'
  'libbz2.so'
  'libgpg-error'
  'libgpgme.so'
  'liblzma.so'
  'libz.so'
)
source=("https://salsa.debian.org/debian/reprepro/-/archive/reprepro-${pkgver}/reprepro-reprepro-${pkgver}.tar.bz2"
        bdb_5.3.patch)
sha256sums=('5a93b8802147c1b4ccba678761e1b561976fb4f675b0ed6cbd36261af5e77bf7'
            'c5e982dc7fa1790aabd7976216c56019fdf1844972eca7c65a0cdc3a388564a7')
b2sums=('37667262bdf4b6b641bb88a9d7ece292957aae21d0a11f8b93a8ae74835313bab65d8973e1c0b2e082396fc82856e20d4a67268ef58fabd18a2819323f31b88e'
        'be2bf6f0a198dd90d34a4976154970857463bca22d9df49e9f697586e60d4d577652f13cab4abf105681ad7a100522b29ce3f743ab98a696fe95348c308d782c')

prepare() {
  cd "${pkgname}-${pkgname}-${pkgver}"
  patch -Np1 -i ../bdb_5.3.patch
}

build() {
  cd "${pkgname}-${pkgname}-${pkgver}"
  set -x

  export CPPFLAGS="$CPPFLAGS -I/usr/include/db5.3"

  libtoolize --force
  aclocal
  autoheader
  automake --add-missing
  autoconf

  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
