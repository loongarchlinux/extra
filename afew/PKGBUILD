# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Lucas Hoffmann
# Contributor: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Kazuo Teramoto <kaz.rag at gmail.com>

pkgname=afew
pkgver=3.0.1
pkgrel=7
pkgdesc='Initial tagging script for notmuch mail'
arch=('any')
url=https://github.com/afewmail/afew
license=('ISC')
depends=('notmuch' 'python-chardet' 'python-dkim' 'python-setuptools')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools-scm'
  'python-sphinx'
  'python-wheel'
)
checkdepends=('python-freezegun')
source=("git+$url.git#tag=$pkgver")
b2sums=('9b1e75379270dd2eb680f6f23bc89fa511658137742467d06ff01bd665de25ecd5d4bd5b95c43a6f994d6635cc8914b422788da49d3c959cb0008b262d18ecc9')

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
  PYTHONPATH="build:$PYTHONPATH" sphinx-build -b man docs build
}

check() {
  cd $pkgname
  python -m unittest discover $pkgname/tests
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 build/$pkgname.1

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
