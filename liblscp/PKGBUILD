# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=liblscp
pkgver=1.0.0
pkgrel=1
pkgdesc="LinuxSampler Control Protocol C++ library"
arch=(loong64 x86_64)
url="https://github.com/rncbc/liblscp"
license=(LGPL-2.1-or-later)
depends=(glibc)
makedepends=(cmake)
provides=(liblscp.so)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('22b9c815ced5f5ef9c68d526b9ccd04b408d67ae8a5797ec975f1d53d0a60388c2e7913c0a052182de9c2dc0760a1de27ae6aecd3b5e0b794e2c846bd58cd5a9')
b2sums=('86e930554d1aa7dd358ac6c81f84daac21770fae47c84b4c96c0abbe1d2791d620c2cdc8c39a3321c1a17f1388604e5fea8257d8386e2153335eb8aa89d12b26')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}

