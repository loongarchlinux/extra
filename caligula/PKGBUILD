# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=caligula
pkgver=0.4.7
pkgrel=1
pkgdesc="A user-friendly, lightweight TUI for disk imaging"
arch=('loong64' 'x86_64')
url="https://github.com/ifd3f/caligula"
license=('GPL-3.0')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('48c741eff3f6378bf4cf162a1bec2066bb47d3359f3c1e482d6f312daafdabe49b154dda9b49c2f88c15c4b0d0d21f11a7d5a26414d9d5c5df43a20529b812e5')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  RUSTFLAGS="--cfg tracing_unstable" cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  RUSTFLAGS="--cfg tracing_unstable" cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
