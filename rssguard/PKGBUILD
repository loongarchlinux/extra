# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Alois Nespor <info@aloisnespor.info>
# Contributor: speps <speps dot aur dot archlinux dot org>

pkgbase=rssguard
pkgname=(rssguard{,-lite})
pkgver=4.6.6
pkgrel=1
pkgdesc='Simple (yet powerful) Qt feed reader'
arch=(loong64 x86_64)
url='https://github.com/martinrotter/rssguard'
license=(GPL3)
depends=(gcc-libs
         glibc
         mpv
         qt6-5compat
         qt6-base
         qt6-declarative
         qt6-multimedia
         sqlite)
makedepends=(cmake
             mpv
             qt6-5compat
             qt6-multimedia
             qt6-tools
             qt6-webengine)
optdepends=('clang: Support for beautification of message filter scripts'
            'mariadb-libs: Support for MariaDB-based data storage')
source=(https://github.com/martinrotter/$pkgbase/archive/$pkgver/$pkgbase-$pkgver.tar.gz)
sha256sums=('c3d9fddafe76ab26c8a2a371b6cca73150b38520d1c8fc191ea53c71c20a99b0')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_QT6=ON
  cmake --build build

  cmake -B build-lite -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_QT6=ON \
    -DNO_LITE=OFF
  cmake --build build-lite
}

package_rssguard() {
  depends+=(qt6-webengine)

  DESTDIR="$pkgdir" cmake --install build
}

package_rssguard-lite() {
  pkgdesc+=' without WebEngine support'
  conflicts=(rssguard)
  provides=(rssguard=$pkgver)
  replaces=(rssguard-nowebengine)

  DESTDIR="$pkgdir" cmake --install build-lite
}
