# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=baobab
pkgver=46.0
pkgrel=1
pkgdesc="A graphical directory tree analyzer"
url="https://wiki.gnome.org/Apps/DiskUsageAnalyzer"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gsettings-desktop-schemas
  gtk4
  libadwaita
  librsvg
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
groups=(gnome)
_commit=ff8490573fcedf139f6ab45ee6d434736a0a38ec  # tags/46.0^0
source=(
  "git+https://gitlab.gnome.org/GNOME/baobab.git#commit=$_commit"
  no-mimetype.diff
)
b2sums=('f231d875c081c4f9c290c88b5b5661bf1c6b4097587132235620e56d06657b42ccae07ea5f8f5b66b4548d1a1b4aaea1aa81261ec5ccd3f38b6fb65767f83500'
        '71e04f307e842fa0676f5b64f55e8be908e3075a9b948a6ab3979b51a8db08fdd07c0f214adb74d5461b4f175a14a7bf9867cf2c9b044660a5b46bc8ea296a22')

pkgver() {
  cd baobab
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd baobab

  # https://bugs.archlinux.org/task/31861
  git apply -3 ../no-mimetype.diff
}

build() {
  arch-meson baobab build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
