# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-compressor
pkgver=5.12.23
pkgrel=3
pkgdesc='A fast and lightweight application for creating and extracting archives'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-compressor"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'glibc' 'gcc-libs' 'qt5-base' 'glib2' 'karchive5' 'kcodecs5'
         'libarchive' 'libzip' 'minizip' 'util-linux-libs' 'hicolor-icon-theme')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-compressor/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-fix-build-on-dtk-5.6.20.patch::https://github.com/linuxdeepin/deepin-compressor/pull/151.patch
        $pkgname-fix-build-on-new-dtk.patch::https://github.com/linuxdeepin/deepin-compressor/pull/166.patch)
sha512sums=('11a28afa828dbeb2e158026778a777d5677f6336fb8d54ec1d0a85e5aeb0b1ca4351e8203939b90d7ec0eb499981d7924465b49856591bc01ceade523121059e'
            '1248ce22c188540db43cba475cde7c8d0e95d45ae98ddebb76f791f8d3f723cdda2651235e806fbe52ea5a96380d77a64907832cece41e3b4d981e80e1a92d92'
            '04d0288198118b957362888440fa050630457358ce0a855833f5ed2a990c869a1c68874ccf19d279f8332fe19ed7fd4def3ede3c7e7e02b9155e1406bb98620b')

prepare() {
  cd deepin-compressor-$pkgver
  patch -p1 -i ../$pkgname-fix-build-on-dtk-5.6.20.patch
  patch -p1 -i ../$pkgname-fix-build-on-new-dtk.patch
}

build() {
  cd deepin-compressor-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-compressor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
