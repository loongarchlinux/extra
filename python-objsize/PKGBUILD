# Maintainer: Antonio Rojas <arojas@archlinux.com>

_pyname=objsize
pkgname=python-$_pyname
pkgver=0.7.0
pkgrel=2
pkgdesc="Traversal over Python's objects subtree and calculate the total size of the subtree in bytes (deep size)"
arch=(any)
url='https://liran-funaro.github.io/objsize/'
license=(BSD-3-Clause)
depends=(python)
makedepends=(python-build
             python-installer
             python-setuptools
             python-wheel)
checkdepends=(python-pytest
              python-yaml)
source=(https://github.com/liran-funaro/objsize/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('667956e07034d4a36b15e6d76910964aadbec249ca3f54988a071b43152d9faa')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
