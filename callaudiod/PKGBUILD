# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Thomas Booker (CoderThomasB) <tw.booker@outlook.com>
# Contributor: Philip Goto <philip.goto@gmail.com>

pkgname=callaudiod
pkgver=0.1.10
pkgrel=2
pkgdesc='Call audio routing daemon'
arch=(loong64 x86_64 aarch64)
url='https://gitlab.com/mobian1/callaudiod'
license=(GPL3)
depends=(alsa-lib glib2 libpulse)
makedepends=(glib2-devel meson)
source=("${url}/-/archive/${pkgver}/callaudiod-${pkgver}.tar.gz")
b2sums=('d8b08ea92a27eafd7bc483c4ad8104dfd7d84becca265a60b7eeabd8fc94a53afa0f996870737d39d524e742628ba9c373e5a843a389b4739c9a1d0313c19806')

build() {
  arch-meson callaudiod-${pkgver} build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
