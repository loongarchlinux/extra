# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=c-ares
pkgver=1.33.0
pkgrel=1
pkgdesc="A C library for asynchronous DNS requests"
arch=(loong64 x86_64)
url="https://c-ares.org/"
_url=https://github.com/c-ares/c-ares
license=(MIT)
depends=(glibc)
makedepends=(
  cmake
  git
)
provides=(libcares.so)
source=(
  git+$_url.git?signed#tag=v$pkgver
)
sha512sums=('494c7cc2ef3efd60acb7bb81989afd8db513e8b3ae8aae379025676e14b335461e51a3db1df8427e5f1c28a37431865460cb97f533f513daba656641574fb4cd')
b2sums=('ce89f111183f045e81b1fd11985d3174558802c43e980ee90b466fa104790ef4433f5846c809a5cbd0cc9e2698d53f5ccd9ba6e2ebdc9274420c7310d30500b9')
validpgpkeys=(
  27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2  # Daniel Stenberg <daniel@haxx.se>
  DA7D64E4C82C6294CB73A20E22E3D13B5411B7CA  # Brad House <brad@brad-house.com>
)

build() {
  local cmake_options=(
    -B build
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -S $pkgname
    -W no-dev
  )
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname/{AUTHORS,{CONTRIBUTING,README,RELEASE-NOTES}.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
