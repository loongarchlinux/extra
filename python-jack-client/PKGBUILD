# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-jack-client
_name=jackclient-python
pkgver=0.5.4
pkgrel=4
pkgdesc="JACK Audio Connection Kit (JACK) Client for Python"
arch=(any)
url="https://github.com/spatialaudio/jackclient-python/"
license=(MIT)
depends=(
  jack
  python
  python-cffi
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  jack2
  procps-ng
  python-pytest
  util-linux
)
optdepends=('python-numpy: Access input/output buffers as numpy arrays')
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('7f17fbc63863c0f7fbab62baaab221301892373a45742941e4019086afa8644eaa9dbc3faea2c92dc0943ce70986c98c5e694ddcc564986da5748138db5139aa')
b2sums=('1eee485e5a2672d5dc91dbe0ab776301362f51c7a5237ca185418b8c795556cc1c725d87ca2efe2b9dd01e0e03073fc596eba83c89d819edef570974257cce73')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  # start dummy jack server
  jackd -d dummy &
  pytest -vv
  kill -9 $(pgrep jackd)
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CONTRIBUTING,NEWS,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 755 examples/*.py -t "$pkgdir/usr/share/doc/$pkgname/examples/"
}
