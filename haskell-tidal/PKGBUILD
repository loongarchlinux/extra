# Maintainer: David Runge <dvzrv@archlinux.org>

_name=tidal
pkgname=haskell-tidal
pkgver=1.9.5
pkgrel=2
pkgdesc="A domain specific language for live coding pattern."
arch=(loong64 x86_64)
url="https://tidalcycles.org/"
license=(GPL-3.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  ghc
  ghc-libs
  glibc
  gmp
  haskell-blaze-builder
  haskell-clock
  haskell-colour
  haskell-data-binary-ieee754
  haskell-hosc
  haskell-mwc-random
  haskell-network
  haskell-primitive
  haskell-random
  haskell-splitmix
  haskell-tidal-link
  haskell-vector
  supercollider
)
checkdepends=(haskell-microspec)
optdepends=('sc3-plugins: Examples using special UGens')
conflicts=(tidalcycles)
replaces=(tidalcycles)
source=(https://hackage.haskell.org/packages/archive/$_name/$pkgver/$_name-$pkgver.tar.gz)
sha512sums=('15a963eb17c38678e2d59fa08845cac270bf2a28a9dfe4cf4bfd2fe137c5bb9fbc18439e889743792b3448f0358dee3a787608e7385768616dce8171718fac32')
b2sums=('6ba4f598be6b3e6800a6af15a72fbb044dcfb6de52e46dfc0db4cc9bd959bfdbf16d7ec74f884b4546ef3fca6a46a6af52a2cd294460436fdfeb97f51c1db3db')

build() {
  local configure_options=(
    -O
    --datasubdir=$pkgname
    --disable-library-vanilla
    --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname
    --dynlibdir=/usr/lib
    --enable-executable-dynamic
    --enable-shared
    --enable-tests
    --libsubdir=\$compiler/site-local/\$pkgid
    --prefix=/usr
  )

  cd $_name-$pkgver
  runhaskell Setup configure "${configure_options[@]}"
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_name-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_name-$pkgver
  runhaskell Setup copy --destdir="$pkgdir"
  install -vDm 744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -vDm 744 unregister.sh "$pkgdir/usr/share/haskell/unregister/$pkgname.sh"
}
