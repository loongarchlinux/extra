# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: George Rawlinson <george@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Georg Grabler (STiAT) <ggrabler@gmail.com>

pkgname=proxytunnel
pkgver=1.12.2
pkgrel=1
pkgdesc="Creates tunnels through HTTP(S) proxies for any TCP based protocol"
arch=('loong64' 'x86_64')
url="https://github.com/proxytunnel/proxytunnel"
license=('GPL')
depends=('glibc' 'openssl')
makedepends=('asciidoc' 'xmlto')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('ffa518278660c816b27ae6c497d744d6588ba9be29fab2ce254bcbc4695fc8e5339e836da541c6aed7a73c2d1e5207f529ca9b818ddbf02b699856d43bb85a57')

prepare() {
  cd "$pkgname-$pkgver"
  # append -DOPENSSL11 to CFLAGS via SSL-specific OPTFLAGS: See ptstream.c
  sed -i '/^OPTFLAGS += -DUSE_SSL$/ s/$/ -DOPENSSL11/' Makefile
  # fixup modes otherwise strip fails
  sed -i 's|555|755|g' Makefile
}

build() {
  cd "$pkgname-$pkgver"
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  make prefix=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make prefix=/usr DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
