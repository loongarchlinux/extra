# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: lp76 <l.peduto@gmail.com>

pkgname=vinagre
pkgver=3.22.0+r160+gc86e114
pkgrel=2
epoch=1
pkgdesc="A VNC Client for the GNOME desktop"
url="https://wiki.gnome.org/Apps/Vinagre"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(
  avahi
  cairo
  dbus-glib
  dconf
  freerdp2
  gdk-pixbuf2
  glib2
  glibc
  gtk-vnc
  gtk3
  hicolor-icon-theme
  libsecret
  libxml2
  spice-gtk3
  telepathy-glib
  vte3
)
makedepends=(
  appstream-glib
  docbook-xsl
  git
  gnome-common
  gobject-introspection
  intltool
  itstool
  openssh
  spice-protocol
  vala
)
optdepends=('openssh: SSH plugin')
_commit=c86e114efe31381bff3cad008e9f2cd84a83a803  # master
source=("git+https://gitlab.gnome.org/GNOME/vinagre.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/$pkgname \
    --enable-rdp --enable-spice --enable-compile-warnings=minimum
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
