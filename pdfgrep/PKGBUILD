# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: schuay <jakob.gruber@gmail.com>
# Contributor: Gabriel B. Casella <gbc921@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=pdfgrep
pkgver=2.2.0
pkgrel=4
pkgdesc='A tool to search text in PDF files'
arch=(loong64 x86_64)
url='https://pdfgrep.org'
# url='https://gitlab.com/pdfgrep/pdfgrep'
license=(GPL-2.0-or-later)
depends=(pcre2
         poppler)
_archive="$pkgname-$pkgver"
source=("$url/download/$_archive.tar.gz")
sha256sums=('0661e531e4c0ef097959aa1c9773796585db39c72c84a02ff87d2c3637c620cb')

build() {
	cd "$_archive"
	./configure --prefix=/usr
	make
}

check() {
	cd "$_archive"
	make -k check
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir/" install
}
