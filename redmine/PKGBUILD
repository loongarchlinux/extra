# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Federico Cuello <fedux@fedux.com.ar>

pkgname=redmine
pkgver=5.1.1
pkgrel=1
pkgdesc="A flexible project management web application written using Ruby on Rails framework."
arch=('loong64' 'x86_64')
url="https://www.redmine.org"
license=('GPL2')
depends=('ruby' 'imagemagick' 'libxml2' 'libxslt')
makedepends=('mariadb-libs' 'postgresql-libs' 'sqlite' 'systemd')
optdepends=('apache: A supported application server'
            'nginx: A supported application server'
            'mysql: MySQL database support'
            'postgresql: PostgreSQL database support'
            'sqlite: SQLite database support')
source=("$url/releases/$pkgname-$pkgver.tar.gz"
	"Gemfile.local"
	"redmine.service")
sha512sums=('d5cc4aed3932bb9556a9ae8076371cb1d8421eeac80be798839156dfa84f79fbccfca18555a4d8858962a685c01747027e24e74acff52ddbc0fab857088fd551'
            '1943883fdc5479f8acb4c256108b6ae054360eb7298b696bc3e16995dac0e51529b4a9f4c8e9158645a15d7b90fdcdaedb4502986a8a6dc4015671fae6861b6e'
            'e8cf8b47fdc51f01cbbcfde2b822e9c57eb3989ada116cd2ffe6982d9947feca30f256221e018babc3afedb9c62c65ff158bb024259ebfabc30d719b90692166')

build() {
    cd "$srcdir"/redmine-$pkgver

    cp "${srcdir}"/Gemfile.local Gemfile.local
    bundle config set force_ruby_platform true
    bundle config build.nokogiri --use-system-libraries
#    bundle config set without 'development test'
#    bundle config set no-cache 'true'
#    bundle config set deployment 'true'
    bundle lock
    bundle install --no-cache --deployment --without development test
}

package() {
    install -dm 755 "$pkgdir"/usr/share/webapps/

    rm "$srcdir"/redmine-$pkgver/vendor/bundle/ruby/3*/extensions/*/*/*/{mkmf.log,gem_make.out}
    rm -rf "$srcdir"/redmine-$pkgver/vendor/bundle/ruby/3*/gems/*/ext/

    cp -a --no-preserve=ownership "$srcdir"/redmine-$pkgver "$pkgdir"/usr/share/webapps/redmine

    install -Dm0644 "${srcdir}"/redmine.service "${pkgdir}"/usr/lib/systemd/system/redmine.service

    cd "$pkgdir"/usr/share/webapps/redmine
    rm -rf files log
    install -dm0755 -o http -g http "$pkgdir"/var/lib/redmine/files
    install -dm0755 -o http -g http "$pkgdir"/var/log/redmine
    mv tmp "$pkgdir"/var/lib/redmine/tmp
    chown -R http:http "$pkgdir"/var/lib/redmine/tmp
    ln -s /var/log/redmine log
    ln -s /var/lib/redmine/files files
    ln -s /var/lib/redmine/tmp tmp
}
