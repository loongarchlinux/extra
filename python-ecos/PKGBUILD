# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

_pyname=ecos-python
pkgname=python-ecos
pkgver=2.0.13
pkgrel=2
pkgdesc='Python interface for ECOS'
url='https://github.com/embotech/ecos-python'
depends=(ecos
         glibc
         python
         python-numpy
         python-scipy)
makedepends=(python-build
             python-installer
             python-setuptools
             python-wheel)
checkdepends=(python-pytest)
license=(GPL-3.0-only)
arch=(loong64 x86_64)
source=(https://github.com/embotech/ecos-python/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        unbundle-ecos.patch)
sha256sums=('c52d64f0131d43cdc27ca9160dc38fbfb3faa8d4be48f891d88c343057a1b383'
            'c929cbe71cf8a109a66cbc681c6c501c42517b44e63f88440c36e5c7ca9e3be9')

prepare() {
  cd $_pyname-$pkgver
  patch -p1 -i ../unbundle-ecos.patch
}

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pyname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
