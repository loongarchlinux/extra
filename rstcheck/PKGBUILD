# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rstcheck
pkgver=6.2.1
pkgrel=4
pkgdesc='Checks syntax of reStructuredText and code blocks nested within it'
arch=('any')
url=https://github.com/rstcheck/rstcheck
license=('MIT')
depends=('python-rstcheck-core' 'python-typer')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=('python-sphinx' 'python-pytest')
optdepends=('python-sphinx: for Sphinx support')
source=("git+$url.git#tag=v$pkgver")
b2sums=('e6bac154fa9366ae4820186ee9be51b1632624eaef23ae03781983551fc3f24928b9549ba30e79554715f439d357a1baddd8dad62968c18b3258a7775fad9491')

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
