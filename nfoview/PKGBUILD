# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Wes Brewer <brewerw@gmail.com>
# Contributor: VuDu <vudu.curse@gmail.com>

pkgname=nfoview
pkgver=2.0.1
pkgrel=3
pkgdesc="Simple NFO file viewer in GTK+ 3"
arch=('any')
url="https://otsaloma.io/nfoview/"
license=(GPL-3.0-or-later)
depends=(
  gobject-introspection-runtime
  gtk4
  hicolor-icon-theme
  pango
  python
  python-gobject
)
checkdepends=(
  python-pytest
  xorg-server-xvfb
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
optdepends=('ttf-cascadia-code: recommended font')
source=("git+https://github.com/otsaloma/${pkgname}.git?signed#tag=${pkgver}")
sha256sums=('9d75dcbc2dade5c8225cc47dc6ee9c97773da8c5e54920867a7990f4d9b2c53b')
validpgpkeys=('8DD6B3FCDAB33C91EA915B913A584A9504CACA67') # Osmo Salomaa <otsaloma@iki.fi>

build() {
	cd "${pkgname}"
	make PREFIX=/usr build
}

check() {
	cd "${pkgname}"
	xvfb-run pytest -v "${pkgname}"
}

package() {
	cd "${pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm 644 {AUTHORS,NEWS,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
