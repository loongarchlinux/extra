# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Specter119 <spcter119 AT gmail.com>
# Contributor: Erich Eckner <arch at eckner dot net>
# Contributor: Philip Goto <philip.goto@gmail.com>

_pyname=ipympl
pkgname=python-$_pyname
pkgver=0.9.4
pkgrel=3
pkgdesc='Matplotlib Jupyter Extension'
url='https://pypi.org/project/ipympl/'
depends=(ipython
         python
         python-ipython-genutils
         python-ipywidgets
         python-matplotlib
         python-numpy
         python-pillow
         python-traitlets)
makedepends=(git
             jupyterlab
             nodejs
             python-build
             python-hatch-jupyter-builder
             python-hatchling
             python-installer)
checkdepends=(python-numpy
              python-nbval
              python-pytest)
license=(BSD-3-Clause)
arch=(any)
source=(git+https://github.com/matplotlib/ipympl#tag=$pkgver)
sha256sums=('61661074ffe5e1c4c62e87f98605db58af69d4fe8d6af7839e54b23283164056')

prepare() {
  sed -e 's|>=.*\"|\"|g' -e 's|~=.*\"|\"|g' -i $_pyname/pyproject.toml
#  sed -e '/index.js/d' -e '/plugin.js/d' -i $_pyname/setup.py
}

build() {
  cd $_pyname
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pyname
  PYTHONPATH="$PWD" \
  pytest -v
}

package() {
  cd $_pyname
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir"/{usr/,}etc
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
