# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Greg Erwin <first name last name 256 at gmail dot com>

pkgname=libgpiod
pkgver=2.1.1
pkgrel=1
pkgdesc="C library and tools for interacting with the linux GPIO character device"
url="https://git.kernel.org/pub/scm/libs/libgpiod/libgpiod.git"
arch=('loong64' 'x86_64')
license=('LGPL2.1')
depends=('python')
makedepends=('git' 'autoconf-archive' 'doxygen' 'help2man' 'python-setuptools')
source=("git+$url#tag=v$pkgver")
sha256sums=('cde63fcf982b9868d2e2867c9463f39a9c7eca4b4b0805219281b1a7e0e56bba')

build() {
  cd $pkgname
  ./autogen.sh \
    --prefix=/usr \
    --enable-tools=yes \
    --enable-bindings-cxx \
    --enable-bindings-python
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
