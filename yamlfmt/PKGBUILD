# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=yamlfmt
pkgver=0.11.0
pkgrel=1
pkgdesc='An extensible command line tool or library to format yaml files.'
arch=('loong64' 'x86_64')
url='https://github.com/google/yamlfmt'
license=('Apache')
depends=('glibc')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::https://github.com/google/yamlfmt/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('e4a79cf210c3d3e87de9f625f6f5882450628b57b7a1f6310ecc2e38996c49541d35167713356191552ecf615292b9ae83f39dadeff417d0341333328e0fb9cb')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  cd ${pkgname}-${pkgver}
  go build -o yamlfmt ./cmd/yamlfmt
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 yamlfmt "${pkgdir}/usr/bin/yamlfmt"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
