# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=yamlfmt
pkgver=0.12.1
pkgrel=1
pkgdesc='An extensible command line tool or library to format yaml files.'
arch=('loong64' 'x86_64')
url='https://github.com/google/yamlfmt'
license=('Apache')
depends=('glibc')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::https://github.com/google/yamlfmt/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('ac39234977d11751802a3a8c2e2140c16e408f987d29aa4ac8e7dd329067c1cef686b0950fa4e4478aeeb214506e7d3fe498734e584abcbc4f3aa48f9f1c7504')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  cd ${pkgname}-${pkgver}
  go build -o yamlfmt ./cmd/yamlfmt
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 yamlfmt "${pkgdir}/usr/bin/yamlfmt"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
