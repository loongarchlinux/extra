# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sh4nks <sh4nks7@gmail.com

pkgname=lightdm-pantheon-greeter
pkgver=7.0.0.r111.1472b265
pkgrel=1
pkgdesc='Pantheon greeter for LightDM'
arch=(loong64 x86_64)
url=https://github.com/elementary/greeter
license=(GPL-2.0-or-later)
groups=(pantheon)
depends=(
  accountsservice
  cairo
  gdk-pixbuf2
  glibc
  libgdk-3.so
  libgee
  libgio-2.0.so
  libglib-2.0.so
  libgl
  libgobject-2.0.so
  libgranite.so
  libgtk-3.so
  libhandy-1.so
  lightdm
  mutter
)
makedepends=(
  cmake
  meson
  git
  libglvnd
  vala
)
install=lightdm-pantheon-greeter.install
#_tag=bed49e46baaebf9bcc405e3e5577b99b925f56a1
_commit=1472b265520394d174ad64bf4c66303f83f8eca6
#source=(lightdm-pantheon-greeter::git+https://github.com/elementary/greeter.git#tag=${_tag})
source=(lightdm-pantheon-greeter::git+https://github.com/elementary/greeter.git#commit=${_commit})
b2sums=('aaeb36d65b9d45f2bbb95807072328060d555ce24daff32c76cf3205220b5b7df53967f8549b6dae6e8a31f0c1732b95f3b31b3a90d28e449ca35993b488082c')

pkgver() {
  cd lightdm-pantheon-greeter
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson lightdm-pantheon-greeter build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
