# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Katie Wolfe <wolfe@katie.host>

pkgname=fennel
pkgver=1.5.0
pkgrel=1
pkgdesc='A Lua Lisp language'
arch=('any')
url='https://fennel-lang.org'
license=('MIT')
depends=('lua')
makedepends=('git')
source=("$pkgname::git+https://git.sr.ht/~technomancy/fennel#tag=$pkgver")
sha256sums=('d402ba8a199f84a2b5a2f01f8cf446f2616a8171367a3d1536bb57e07fabdf86')

build() {
  cd "$pkgname"

  make build
}

check() {
  cd "$pkgname"

  make test
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" PREFIX=/usr install

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
