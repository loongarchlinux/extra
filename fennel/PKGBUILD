# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Katie Wolfe <wolfe@katie.host>

pkgname=fennel
pkgver=1.4.2
pkgrel=1
pkgdesc='A Lua Lisp language'
arch=('any')
url='https://fennel-lang.org'
license=('MIT')
depends=('lua')
makedepends=('git')
source=("$pkgname::git+https://git.sr.ht/~technomancy/fennel#tag=$pkgver")
sha256sums=('8a2fa23b522b817502bc1edce0fe4c85ebdd9a71dba43f15639fdcab719802aa')

build() {
  cd "$pkgname"

  make build
}

check() {
  cd "$pkgname"

  make test
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" PREFIX=/usr install

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
