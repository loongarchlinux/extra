# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=eslint
pkgver=9.2.0
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=('any')
url='https://eslint.org'
license=('MIT')
depends=('acorn' 'nodejs')
makedepends=('git' 'jq' 'npm')
options=('!emptydirs')
source=("git+https://github.com/$pkgname/$pkgname.git#tag=v$pkgver")
b2sums=('eb6b240e4394f2ecc2ca6dba9eebe7ca4942c32c3c52c47d1a1c3526cc01ce50631534ec5c2e2e0c7af0d946ad6d890c93ac267beaf3891c8a450bbf7f262378')

prepare() {
  cd $pkgname
  npm install
}

check() {
  cd $pkgname
  node Makefile.js mocha
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/{usr/{bin,share/{doc,licenses}/$pkgname},$mod_dir}
  ln -s $mod_dir/bin/$pkgname.js "$pkgdir"/usr/bin/$pkgname
  ln -s $mod_dir/README.md "$pkgdir"/usr/share/doc/$pkgname
  ln -s $mod_dir/LICENSE "$pkgdir"/usr/share/licenses/$pkgname

  cd $pkgname
  npm prune --production

  readarray -t mod_files < <(jq -r '.files[]' package.json)
  cp -r "${mod_files[@]}" node_modules package.json "$pkgdir"/$mod_dir
  chmod +x "$pkgdir"/$mod_dir/bin/$pkgname.js

  # Experimental dedup
  rm -r "$pkgdir"/$mod_dir/node_modules/acorn
}
