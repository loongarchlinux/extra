# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Marat Bakeev <hawara@gmail.com>

pkgname=pg_auto_failover
pkgver=2.1
pkgrel=1
pkgdesc="extension and service for PostgreSQL that monitors and manages automated failover for a Postgres cluster."
arch=('loong64' 'x86_64')
url="https://github.com/citusdata/pg_auto_failover"
license=('PostgreSQL')
depends=('postgresql' 'ncurses')
optdepends=(
  'python-pygments: /usr/share/doc/pg_auto_failover/conf.py'
  'python-sphinx: /usr/share/doc/pg_auto_failover/conf.py'
)
makedepends=('clang' 'llvm')
options=('!buildflags')
source=("$pkgname-$pkgver.tar.gz::https://github.com/citusdata/pg_auto_failover/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('13bcfb08fe3381e9b9e68f04affc6b0a13ae53463dfaea113824e0e7fab9354b')

build() {
	cd "$pkgname-$pkgver"
	make
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install

	install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
