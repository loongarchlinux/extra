# Maintainer: Leonidas Spyropoulos (artafinde@archlinux.org)
# Contributor: Michael William Le Nguyen <michael at mail dot ttp dot codes>
# Contributor: Buildpacks Maintainers <cncf-buildpacks-maintainers at lists dot cncf dot io>

_base=pack
pkgname=pack-cli
pkgver=0.35.1
pkgrel=1
pkgdesc="CLI for building apps using Cloud Native Buildpacks"
depends=('glibc')
makedepends=('go' 'git')
arch=('loong64' 'x86_64')
url="https://buildpacks.io/"
license=('Apache-2.0')
source=("git+https://github.com/buildpacks/${_base}.git#tag=v${pkgver}")
sha512sums=('41e70b0f4cabe245f4fb9be561a2e24957b3ad0290479dcfb9a54ba5388e86f05c961dc1936c1b1f5f2e7608f50af6ed3ea2c263f672ac560125d9dc46017446')

prepare() {
  cd ${_base}
  mkdir -p dist/
}

build() {
  cd ${_base}
  local _BUILDINFO="-X 'github.com/buildpacks/pack.Version=v${pkgver}'"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" ${_BUILDINFO}" \
    -o "dist/${_base}" \
    .
}

# Seems like it needs docker
# check() {
#   cd ${_base}
#   local GOTESTFLAGS="-v -count=1 -parallel=1"
#   go test ./...
# }

package() {
  cd ${_base}
  install -vDm 755 "dist/${_base}" "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 LICENSE  "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
