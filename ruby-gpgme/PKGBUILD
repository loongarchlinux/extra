# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='gpgme'
pkgname="ruby-${_gemname}"
pkgver=2.0.23
pkgrel=1
pkgdesc='Ruby interface to GnuPG Made Easy (GPGME)'
url='https://github.com/ueno/ruby-gpgme'
arch=('loong64' 'x86_64')
license=('GPL2')
depends=('ruby' 'ruby-mini_portile2' 'gpgme' 'libgpg-error' 'libassuan')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-bundler' 'ruby-yard')
#checkdepends=('ruby-mocha')
options=('!emptydirs')
source=("https://github.com/ueno/ruby-gpgme/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('13d9b383ba638ef1cc21870159b4e7bf3e4aa70097649da185ccd3c159247c657eae651c0fc29c2fcd1b1d2965b432b82461a2f2d85f687787f78d34ddce6da9')
b2sums=('2057a6917aac14bfc82c5e718498d5dce507a4c9de4da2e0973eefee76339066c54ebf57201f739faaab118e624416d7b30d24fb4409b37d53f80dd92c2aa825')

prepare() {
  cd "${pkgname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${pkgname}-${pkgver}"

  export RUBY_GPGME_USE_SYSTEM_LIBRARIES=1

  rake compile
  rake build
}

# https://github.com/ueno/ruby-gpgme/issues/162
#check() {
#  cd "${pkgname}-${pkgver}"
#
#  sed --in-place '/byebug/Id' "${_gemname}.gemspec" test/test_helper.rb
#  sed --in-place '/coveralls/Id' "${_gemname}.gemspec" test/test_helper.rb
#
#  rake test
#}

package() {
  cd "${pkgname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "pkg/${_gemname}-${pkgver}.gem" \
    -- \
    --use-system-libraries

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "${pkgdir}/${_gemdir}/cache/" \
    "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "${pkgdir}/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "${pkgdir}/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete

  install --verbose -D --mode=0644 README.rdoc NEWS --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
  install --verbose -D --mode=0644 examples/* --target-directory "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: ts=2 sw=2 et:
