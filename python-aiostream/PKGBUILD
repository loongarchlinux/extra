# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=python-aiostream
_name=${pkgname#python-}
pkgver=0.6.1
pkgrel=1
pkgdesc='Generator-based operators for asynchronous iteration'
arch=('any')
url=https://github.com/vxgmichel/aiostream
license=('GPL-3.0-only')
depends=('python-typing_extensions')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest' 'python-pytest-asyncio')
source=("git+$url.git#tag=v$pkgver")
b2sums=('24a794b57458d8e45304cacb98a6fccc548bc356e04b51510b0046e2c30bce948efe89a8bb07901cf544685ded7cbb169d40ec8a80122576c009f155507cadb5')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  # Override addopts as they invoke coverage testing
  PYTHONPATH="$PWD/$_name:$PYTHONPATH" pytest --override-ini="addopts=" tests --strict-markers
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
