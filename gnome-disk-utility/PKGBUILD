# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility
pkgver=46.0
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-disk-utility"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  gtk3
  libcanberra
  libdvdread
  libhandy
  libnotify
  libpwquality
  libsecret
  parted
  systemd
  udisks2
)
makedepends=(
  appstream
  docbook-xsl
  git
  meson
)
groups=(gnome)
_commit=d5618e1a91fd3795e3470b95326a16d9f81a246c  # tags/46.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-disk-utility.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
