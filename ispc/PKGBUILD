# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Torsten Ke√üler <tpkessler@archlinux.org>

pkgname=ispc
pkgver=1.24.0
pkgrel=1
pkgdesc="Compiler for high-performance SIMD programming on the CPU"
arch=(loong64 x86_64)
url="https://ispc.github.io/"
license=(BSD-3-Clause)
depends=(clang gcc-libs glibc llvm-libs spirv-llvm-translator onetbb)
makedepends=(cmake git level-zero-headers level-zero-loader lib32-glibc llvm openmp python vc-intrinsics)
checkdepends=(intel-compute-runtime)
optdepends=(
    'intel-compute-runtime: GPU support'
    'level-zero-loader: GPU support'
    'openmp: GPU support'
)
# NOTE: libispcrt_static.a is required by ospray during build time
options=(staticlibs)
source=(
  git+https://github.com/$pkgname/$pkgname#tag=v$pkgver
  $pkgname-benchmark::git+https://github.com/google/benchmark.git
  $pkgname-googletest::git+https://github.com/google/googletest.git
)
sha256sums=('f9a5b259f3664eecbbcbaab8df1964d70ccbb7bcbbc1574de3aae9dfdb9ee823'
            'SKIP'
            'SKIP')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule.benchmarks/vendor/google/benchmark.url "$srcdir/$pkgname-benchmark"
  git config submodule.ispcrt/tests/vendor/google/googletest.url "$srcdir/$pkgname-googletest"
  git -c protocol.file.allow=always submodule update
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=Release  # None not allowed :(
    -D CMAKE_INSTALL_PREFIX=/usr
    -D ISPC_INCLUDE_EXAMPLES=OFF
    -D ISPC_OPAQUE_PTR_MODE=OFF  # suggested in https://github.com/ispc/ispc/issues/2427
    -D ISPC_STATIC_LINK=OFF
    -D XE_ENABLED=ON
    -D XE_DEPS_DIR=/usr
    -S $pkgname
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  # https://github.com/ispc/ispc/issues/2427
  # https://github.com/ispc/ispc/issues/2428
  # https://github.com/ispc/ispc/issues/2429
  make -C build check-all || echo "Tests failed"
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
  # Remove uneeded files: https://github.com/ispc/ispc/issues/2482
  rm -rv "${pkgdir}"/build
}
