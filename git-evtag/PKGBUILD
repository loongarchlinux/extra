# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=git-evtag
pkgver=2022.1
pkgrel=3
pkgdesc="Extended verification for git tags"
url='https://github.com/cgwalters/git-evtag'
arch=('loong64' 'x86_64')
license=('Apache-2.0' 'MIT')
depends=(
  'gcc-libs'
  'git'
  'glibc'
  'openssl' 'libssl.so'
  'zlib' 'libz.so'
)
makedepends=(
  'cargo'
)
options=(!lto)
source=(https://github.com/cgwalters/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('ce8b37638f1fb92f347200b419da074be8515be9b55791116e9cab22d9cd5a67'
            'b1eb9f10c961399e9574576a61f23de9bdb8aa18af9b0a5f94fe5f396b5367ac')

prepare() {
  cd "${pkgname}-${pkgver}/rust"
  cp ../../Cargo.lock .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

updlockfiles() {
  cd "${pkgname}-${pkgver}/rust"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "${pkgname}-${pkgver}/rust"
  cargo build --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}/rust
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/git-evtag

  install -Dm644 ../COPYING.* -t "$pkgdir/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
