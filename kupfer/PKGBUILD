# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: D. Can Celasun <dcelasun[at]gmail[dot]com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Asa Marco <marcoasa90[at]gmail[.]com>

pkgname=kupfer
pkgver=327
pkgrel=1
pkgdesc="Launcher application written in python. Similar to Gnome-Do / Launchy"
arch=('any')
url="https://kupferlauncher.github.io/"
license=('GPL3')
depends=('libkeybinder3' 'libwnck3' 'python-cairo' 'python-dbus' 'python-gobject'
         'python-setproctitle' 'python-xdg')
makedepends=('intltool' 'itstool' 'python-docutils' 'waf')
optdepends=('gvfs: Trash plugin'
            'xautomation: Send Keys plugin')
source=(https://github.com/kupferlauncher/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
#validpgpkeys=('2578ABA1F9023159DFF211B7368712C4D7A44445') # Ulrik Sverdrup
sha256sums=('a2b1d263df902a3f9ec931a1a0fba1919cfa8946cd24c98459f54843ff5295ad')

build() {
  cd $pkgname-$pkgver
  rm -r waf waflib
  waf configure --prefix=/usr \
                  --no-update-mime \
                  --no-update-icon-cache
  waf
}

package() {
  cd $pkgname-$pkgver
  waf install --destdir="$pkgdir"
}
