# Maintainer:
# Contributor: Blair Bonnett <blair.bonnett@gmail.com>

pkgname=python-scikit-build-core
_name=${pkgname#python-}
pkgdesc='Next generation Python CMake adaptor and Python API for plugins'
url='https://github.com/scikit-build/scikit-build-core'
pkgver=0.9.2
pkgrel=1
arch=(any)
license=(Apache-2.0)
depends=(
  cmake
  ninja
  python
  python-packaging
  python-pathspec
)
makedepends=(
  python-build
  python-hatchling
  python-hatch-vcs
  python-installer
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
sha512sums=('362d6599db0595b1d60ff3b6d94c45ec6218bfd8d1ab9f16181fbb0e68bffed8cbec5a06a8a560e2229045d4669f52251330945f0578886a5c393494f2273abe')
b2sums=('5ac4f1d99de25f4550fadefb256af8f0931411d15f4432c1f6792bf8d08136f2223a226ae2e4078345086314f67f137a52228ddb6886993f88f5d38c30da7a30')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

# NOTE: upstream hardcodes downloading the internet with pip: https://github.com/scikit-build/scikit-build-core/issues/720

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
