# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-podcasts
pkgver=0.7.1
pkgrel=1
pkgdesc='Podcast application for GNOME'
arch=('loong64' 'x86_64')
url='https://wiki.gnome.org/Apps/Podcasts'
license=('GPL3')
depends=('gst-plugins-bad' 'gst-plugins-base' 'gst-plugins-good' 'gtk4' 'libadwaita')
makedepends=('meson' 'rust')
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
source=("https://gitlab.gnome.org/World/podcasts/-/archive/$pkgver/podcasts-$pkgver.tar.gz")
sha256sums=('933d33dfd0f36343f9c80f055a48e14307a0665b35097da176767ddbfe583399')

build() {
  arch-meson podcasts-$pkgver build
  meson compile -C build

# run install here to workaround gitconfig access issue
  meson install -C build --destdir tmp-install
}

package() {
  mv build/tmp-install/* "$pkgdir"
}
