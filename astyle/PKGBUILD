# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Thomas Mader <thezema@gmail.com>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=astyle
pkgver=3.5.2
pkgrel=1
pkgdesc='A free, fast and small automatic formatter for C, C++, C#, and Java source code.'
arch=('loong64' 'x86_64')
url='http://astyle.sourceforge.net/'
license=('LGPL-3.0-only')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'jdk8-openjdk')
optdepends=('java-environment-openjdk')
source=(
  "$pkgname::git+https://gitlab.com/saalen/astyle.git#tag=$pkgver"
  'simplify-makefile.patch'
  'ftbfs-update-cxx-standard.patch'
)
sha512sums=('6b00bdec3473d1b1c62a3a235cd52f24e8c88f4592d092a3cf4cc861e915cfbf586e2e4ee6d9fa6e75e62cec5aef7f7d8b2c5663faba9a462565894269003714'
            '4a533c6b073a55206ea2c8351d6631ba6e056d59823c8988eae63a47a762e920ad852442b57ed1860a648199c34fbc08c31bb553f0dc3f1ed145c2c702ad0290'
            '403390b9e2e49ccec5b9d56f7e4a6b51488cdd39cefb15b27fd6eac4ccf2c71e0e7b9a63b41a9079cbcbed4444625f325eaff51158fc49ccb492bf6e26e7300d')
b2sums=('83c30947f2ba756076527e5f4dc1587d423e59347924d33454e9c0aca7a0132e5360662093bd08536a38a4ec443ec59ce77b5b716edca7f3f8e7d720a19114c7'
        '155dca3520e1669713efbf42431e4ba2c9c05006dcd2a14f58252d9e6913aab799af7b64c9040201e99ff8d12f953918124c83845fcf64ec025fc131e7acdf1f'
        '6ddbba4662da28cef2defcb9e5e6c6cacbf71322201b2ca0d2e7a61bf7d23c7389b692d14140acdc68b00fe3c8b0ca167d2adf8c2d4f7c6128e86268aa6c378b')

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/simplify-makefile.patch"

  patch -p1 -i "$srcdir/ftbfs-update-cxx-standard.patch"
}

build() {
  cd "$pkgname/AStyle/build/gcc"

  JAVA_HOME=/usr/lib/jvm/java-8-openjdk make shared release java
}

package() {
  cd "$pkgname/AStyle/build/gcc"

  DESTDIR="$pkgdir" make install
}
