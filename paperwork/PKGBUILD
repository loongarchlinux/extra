# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=paperwork
pkgver=2.2.3
pkgrel=2
pkgdesc='Personal document manager to manage scanned documents and PDFs'
arch=(any)
url='https://openpaper.work/'
license=(GPL-3.0-or-later)
depends=(
  gdk-pixbuf2
  glib2
  gtk3
  hicolor-icon-theme
  libhandy
  libinsane
  libnotify
  pango
  poppler-glib
  python
  python-cairo
  python-distro
  python-fabulous
  python-gobject
  python-numpy
  python-pillow
  python-pillowfight
  python-psutil
  python-pycountry
  python-pydbus
  python-pyocr
  python-pyxdg
  python-rich
  python-scikit-learn
  python-scipy
  python-setuptools
  python-whoosh
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools-scm
  python-wheel
)
source=("git+https://gitlab.gnome.org/World/OpenPaperwork/paperwork.git#tag=$pkgver")
b2sums=(47ffaee956dc7987809a4be056b22a935a308d61dd52652a44a4e9c68945c974c7e1be045c3b4b91980be45672fb08bf6269366fe93165216124d9019ddd8014)

prepare() {
  cd $pkgname
  curl -s "https://download.openpaper.work/data/paperwork/master_$(git rev-parse --short HEAD)/data.tar.gz" | tar -xvz
}

build() {
  cd $pkgname
  make l10n_compile

  for dir in openpaperwork-{core,gtk} paperwork-{backend,shell,gtk}; do
    pushd "$dir"
    python -m build --wheel --no-isolation
    popd
  done
}

package() {
  cd $pkgname
  for dir in openpaperwork-{core,gtk} paperwork-{backend,shell,gtk}; do
    pushd "$dir"
    python -m installer --destdir="$pkgdir" dist/*.whl
    popd
  done

  PYTHONPATH="$pkgdir"$(python -c 'import site; print(site.getsitepackages()[0])') \
    "$pkgdir"/usr/bin/paperwork-gtk install \
      --icon_base_dir="$pkgdir/usr/share/icons" \
      --data_base_dir="$pkgdir/usr/share"
}
