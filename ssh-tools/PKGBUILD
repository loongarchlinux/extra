# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Sven Wick <sven.wick@gmx.de>

pkgname=ssh-tools
pkgver=1.8
pkgrel=1
pkgdesc='A collection of tools making SSH more convenient'
arch=('any')
url='https://codeberg.org/vaporup/ssh-tools'
license=('GPL-3.0-only')
depends=('openssh' 'bash' 'perl')
optdepends=('colordiff: colors for ssh-diff')
makedepends=('git' 'help2man')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('b99281074d4c5eb48eef2ac11e7e19f9f03b71c637ca941f1ef0d79e04ff591ed4a9468fcc27d89112008dbce16c4aeeb9776915d832431956538f9311860441')
b2sums=('fa1c586cc6ff20dabec99c369df6dbe1c24b792b162a6b377de29876d3187628079be40d48f655e08b6bda2b6d0faa1d4842cf6d80a7ae1965135118a253da48')

prepare() {
  cd "$pkgname"

  mkdir man
}

build() {
  cd "$pkgname"

  # dict with command:description pairings
  declare -A pages
  pages["ssh-ping"]="check if host is reachable using ssh_config"
  pages["ssh-version"]="shows version of the ssh server you are connecting to"
  pages["ssh-diff"]="diff a file over ssh"
  pages["ssh-facts"]="get some facts about the remote system"
  pages["ssh-hostkeys"]="prints server host keys in several formats"
  pages["ssh-keyinfo"]="prints keys in several formats"
  pages["ssh-certinfo"]="shows validity and information of ssh certificates"
  pages["ssh-force-password"]="enforces password authentication"

  # generate man pages
  for page in "${!pages[@]}"; do
    help2man \
      --name "${pages[${page}]}" \
      --source "${pkgname^^}" \
      --no-info \
      --version-string "$pkgver" \
      --output "man/${page}.1" \
     "./${page}"
  done
}

package() {
  cd "$pkgname"

  # scripts
  install -vDm755 -t "$pkgdir/usr/bin" ssh-*

  # man pages
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/*

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
