# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jesse Luehrs <archlinux@tozt.net>

pkgname=rbw
pkgver=1.12.1
pkgrel=1
pkgdesc='Unofficial Bitwarden CLI'
arch=('loong64' 'x86_64')
url='https://git.tozt.net/rbw'
license=('MIT')
depends=(
  'gcc-libs'
  'glibc'
  'pinentry'
)
makedepends=(
  'git'
  'rust'
)
options=('!lto')
source=("git+https://github.com/doy/rbw.git#tag=$pkgver")
sha512sums=('2412e6902b47b2fec815a4820402583d3cba1c1ba0aa35fb12031a37417f3f79dd66594a73322e92a15c95568862a7330eaf36f60ff200387624abc0f29b6e03')
b2sums=('83b1f29be986a7e242c4c53c0efdffa17cc2616d7c21603eb8bad066149c3e8bb2d83518c865024cd46ab8df2182489ce46a9d5f1085a4cdbcd77131c043512c')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"
  cargo build --frozen --release --all-features

  # generate shell auto-completions
  for completion in bash fish zsh; do
    cargo run --frozen --release --bin rbw -- \
      gen-completions "$completion" > "$completion-completions"
  done
}

check() {
  cd "$pkgname"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" target/release/rbw{,-agent}

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md CHANGELOG.md

  # shell auto-completions
  install -vDm644 bash-completions "$pkgdir/usr/share/bash-completion/completions/rbw"
  install -vDm644 fish-completions "$pkgdir/usr/share/fish/vendor_completions.d/rbw.fish"
  install -vDm644 zsh-completions "$pkgdir/usr/share/zsh/site-functions/_rbw"
}
