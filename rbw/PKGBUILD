# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jesse Luehrs <archlinux@tozt.net>

pkgname=rbw
pkgver=1.10.0
pkgrel=1
pkgdesc='Unofficial Bitwarden CLI'
arch=('loong64' 'x86_64')
url='https://git.tozt.net/rbw'
license=('MIT')
depends=('glibc' 'gcc-libs' 'pinentry')
makedepends=('rust')
options=('!lto')
source=("$url/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('83e45f32323445fbcb4196120179771d31cf71065f3a1ab921d0dcf194d33a3bd57a149869a467c7120024c4745f837488662f52e0311c2fb43782b2d6c7d92b')
b2sums=('dcf0dd82e3fa71a8655aeeeda8827eb78f2a9f6dfd590f5ab0a7154c219caf35af494ecbcd8c525c8b8dcec94c37287f4f87a070684b6e9b94e9ee8fe47210df')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release --all-features

  # generate shell auto-completions
  for completion in bash fish zsh; do
    cargo run --frozen --release --bin rbw -- \
      gen-completions "$completion" > "$completion-completions"
  done
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" target/release/rbw{,-agent}

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md CHANGELOG.md

  # shell auto-completions
  install -vDm644 bash-completions "$pkgdir/usr/share/bash-completion/completions/rbw"
  install -vDm644 fish-completions "$pkgdir/usr/share/fish/vendor_completions.d/rbw.fish"
  install -vDm644 zsh-completions "$pkgdir/usr/share/zsh/site-functions/_rbw"
}
