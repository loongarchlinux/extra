# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=openimageio
pkgver=2.5.10.1
pkgrel=1
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=('loong64' 'x86_64')
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'openjpeg2' 'glew' 'libtiff' 'opencolorio' 'libpng' 'libraw' 'libwebp'
         'pugixml' 'pybind11' 'libheif' 'hdf5' 'freetype2')
# TODO: Consider adding these deps: 'openvdb' 'ffmpeg' 'ptex' 'opencv'
makedepends=('cmake' 'qt5-base' 'python' 'boost' 'mesa' 'fontconfig' 'libxrender' 'ninja' 'robin-map' 'fmt')
optdepends=('qt5-base: iv image viewer'
            'python: bindings support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenImageIO/oiio/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('82b05040dada8c83d4c1c1d95b01decbbe1f33e006f7b356da1a1c1c4a99b81ba8fdc506ca2a5052d9f06f92dafca24242c49588e59b23b980fa525b53e380a3')

build() {
  cd OpenImageIO-$pkgver

  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cmake \
    -Bbuild \
    -GNinja \
    -DUSE_PYTHON=ON \
    -DPYTHON_VERSION=$python_version \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DOIIO_BUILD_TESTS=ON \
    -DOIIO_BUILD_TOOLS=ON \
    -DBUILD_MISSING_FMT=OFF \
    -DUSE_EXTERNAL_PUGIXML=ON \
    -DSTOP_ON_WARNING=OFF
  ninja -C build
}

package() {
  cd OpenImageIO-$pkgver

  DESTDIR="$pkgdir" ninja -C build install

  # Remove vendored fonts
  rm -r "$pkgdir"/usr/share/fonts

  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE".md
}
