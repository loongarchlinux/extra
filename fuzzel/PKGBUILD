# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel Ekl√∂f <daniel at ekloef dot se>

pkgname=fuzzel
pkgver=1.10.2
pkgrel=1
pkgdesc='Application launcher for wlroots based Wayland compositors'
url="https://codeberg.org/dnkl/$pkgname"
arch=(loong64 x86_64)
license=(MIT)
depends=(cairo
         fcft
         fontconfig
         libfcft.so
         libpng
         libxkbcommon
         pixman
         wayland) # nanosvg is bundled
makedepends=(meson
             scdoc
             tllist
             wayland-protocols)
backup=('etc/xdg/fuzzel/fuzzel.ini')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('5362175f301af3f27c23138ac339294ce33dff61720ffc7eb13e78ec56f60a8b')

build() {
	cd "$pkgname"
	arch-meson \
		-Denable-cairo=enabled \
		-Dpng-backend=libpng \
		-Dsvg-backend=nanosvg \
		. build
	meson compile -C build
}

package() {
	cd "$pkgname"
	meson install -C build --destdir "$pkgdir"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
