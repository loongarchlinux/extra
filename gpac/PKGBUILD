# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=gpac
pkgver=2.4.0
pkgrel=1
epoch=1
pkgdesc='A multimedia framework based on the MPEG-4 Systems standard'
arch=(loong64 x86_64)
url=https://gpac.wp.imt.fr/
license=(LGPL)
depends=(
  glibc
  libnghttp2
  openssl
  xz
  zlib
)
makedepends=(git)
optdepends=('python: python bindings')
provides=(libgpac.so)
_tag=5d70253ac94e5840be7b86054131dd753af63cc7
source=(git+https://github.com/gpac/gpac.git#tag=${_tag})
b2sums=('5f39b777284d3d51947f466d35f884b68869637f4fae9b4f5aaddbc8d7ef6b12afddf1c00aaa5b3b6c59e5f5221f207c1e40c950a323a1d3d2e48b67637c4fc6')

pkgver() {
  cd gpac
  git describe --tags | sed 's/^v//'
}

build() {
  export CFLAGS+=" -ffat-lto-objects"
  export CXXFLAGS+=" -ffat-lto-objects"
  cd gpac
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-oss-audio \
    --disable-dvb4linux \
    --disable-qjs \
    --enable-pic
  make
}

package() {
  make DESTDIR="${pkgdir}" STRIP=true -C gpac install
  make DESTDIR="${pkgdir}" STRIP=true -C gpac install-lib
}

# vim: ts=2 sw=2 et:
