# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Dimitris Kiziridis <ragouel at outlook dot com>

pkgname=minder
pkgver=1.16.4
pkgrel=1
pkgdesc='Mind-mapping application designed for Pantheon'
arch=('loong64' 'x86_64')
url='https://github.com/phase1geo/Minder'
license=('GPL3')
depends=('discount' 'granite' 'gtksourceview4' 'libarchive' 'libhandy')
makedepends=('meson' 'vala')
source=("https://github.com/phase1geo/Minder/archive/$pkgver/Minder-$pkgver.tar.gz")
sha256sums=('3a9c2831d36a7744f7df27412b2f75b2bec38f5974984a027d6fa7f634a37c7e')

build() {
  arch-meson Minder-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  ln -s com.github.phase1geo.minder "$pkgdir/usr/bin/$pkgname"
}
