# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Carlos Aznar√°n <caznaranl@uni.pe>

pkgname=mdformat
pkgver=0.7.17
pkgrel=3
pkgdesc='CommonMark compliant Markdown formatter'
arch=('any')
url=https://github.com/executablebooks/mdformat
license=('MIT')
depends=('python-markdown-it-py')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("git+$url.git#tag=$pkgver")
b2sums=('93b7a634a1ef23042a338f3f7f9806c3882f7eed79715e7c22e2ee1a5068f3917cf216013b649a2d55ea83061dd1439385de45da2eeb42152777b0f3c94170e5')

build() {
  cd $pkgname
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname
  PYTHONPATH="build/lib" pytest
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
