# Maintainer: Morgan <morganamilo@archlinux.org>

pkgname=paccat
pkgver=1.2.0
pkgrel=1
pkgdesc='Print pacman package files'
url='https://github.com/morganamilo/paccat'
source=("$pkgname-$pkgver.tar.gz::https://github.com/Morganamilo/paccat/archive/v$pkgver.tar.gz")
arch=('loong64' 'x86_64')
license=('GPL-3.0-or-later')
makedepends=('cargo')
depends=('libalpm.so')
optdepends=('bat: syntax highlighting')
sha256sums=('ff803a1db3d343877dc7dfb822101b78aa5f127c7c44c979935cc93f77c237a0')

prepare() {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build () {
  cd "$pkgname-$pkgver"
  export RUSTUP_TOOLCHAIN=stable
  COMPLETIONS_DIR=target cargo build --frozen --release --target-dir target
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 target/release/paccat -t "$pkgdir/usr/bin/"

  install -Dm644 man/$pkgname.1 -t "$pkgdir/usr/share/man/man1/"
  install -Dm644 target/$pkgname.bash -t "$pkgdir/usr/share/bash-completion/completions"
  install -Dm644 target/$pkgname.fish -t "$pkgdir/usr/share/fish/completions"
  install -Dm644 target/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions"
}
