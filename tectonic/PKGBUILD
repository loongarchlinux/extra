# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>
# Contributor: tectonic-deploy <sasha+tectonic@hackafe.net>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jan Tojnar <jtojnar@gmail.com>

pkgname=tectonic
pkgver=0.15.0
pkgrel=2
pkgdesc='Modernized, complete, self-contained TeX/LaTeX engine, powered by XeTeX and TeXLive'
arch=('loong64' 'x86_64')
url='https://tectonic-typesetting.github.io/'
license=('MIT')
depends=('fontconfig' 'harfbuzz-icu' 'openssl')
makedepends=('cargo' 'pkg-config')
options=(!lto)
source=("https://github.com/tectonic-typesetting/tectonic/archive/refs/tags/tectonic@${pkgver}.tar.gz")
b2sums=('4049b7a18b79c89c222039ded8d25fbf6249408362d2adc09f0d51c6ba2f1c634581c6451efc27d9138115ab71c14511d56179868d00f13edae206c5b1ba78d4')

prepare() {
  cd ${pkgname}-${pkgname}-${pkgver}
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
  sed -e 's|c++14|c++17|g' -i crates/*/build.rs # Fix build with ICU 75
}

build() {
  cd ${pkgname}-${pkgname}-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked --features external-harfbuzz
}

check() {
  cd ${pkgname}-${pkgname}-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  cargo test --release --locked --features external-harfbuzz
}

package() {
  depends+=(
    icu libicuuc.so
    freetype2 libfreetype.so
    zlib libz.so
    graphite libgraphite2.so
    harfbuzz libharfbuzz.so
    libpng libpng16.so
  )
  cd ${pkgname}-${pkgname}-${pkgver}
  install -Dm755 target/release/tectonic "${pkgdir}"/usr/bin/tectonic
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
