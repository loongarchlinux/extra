# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Christer Solskogen (solskogen@carebears.mine.nu)

pkgname=gtranslator
pkgver=46.1
pkgrel=1
pkgdesc='An enhanced gettext po file editor for the GNOME desktop environment'
arch=('loong64' 'x86_64')
url='https://wiki.gnome.org/Apps/Gtranslator'
license=('GPL-3.0-or-later')
depends=(
  'cairo'
  'dconf'
  'gcc-libs'
  'gettext'
  'glib2'
  'glibc'
  'gtk4'
  'gtksourceview5'
  'hicolor-icon-theme'
  'iso-codes'
  'json-glib'
  'libadwaita'
  'libgda6'
  'libsoup3'
  'libspelling'
  'libxml2'
  'pango'
)
makedepends=(
  'git'
  'gobject-introspection'
  'meson'
  'yelp-tools'
)
source=("git+https://gitlab.gnome.org/GNOME/$pkgname.git#tag=${pkgver/[a-z]/.&}")
b2sums=('a3201356fd007e9d48e7a26dc7ee159efe9d9c8294ccfbac8596a3fb996989595374fa5fd92beed6307f272f5b8e560b1a3daaa5fb2ef702807293218d7a9945')

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
