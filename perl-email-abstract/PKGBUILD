# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+aur@gmail.com>

pkgname=perl-email-abstract
pkgver=3.010
pkgrel=2
pkgdesc="unified interface to mail representations"
arch=('any')
url="https://search.cpan.org/dist/Email-Abstract"
license=('GPL' 'PerlArtistic')
depends=('perl-email-simple>=1.91' 'perl-module-pluggable>=1.5')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Email-Abstract-$pkgver.tar.gz)
sha256sums=('8c12f68b5974cafc99d74942abefc8597193035aafd2763128e6aaafca4b7ed6')

build() {
  cd  "$srcdir"/Email-Abstract-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/Email-Abstract-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
