# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-xkb-plugin
pkgver=0.8.3
pkgrel=2
pkgdesc="Plugin to switch keyboard layouts for the Xfce4 panel"
arch=('loong64' 'x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-xkb-plugin"
license=('custom')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxklavier' 'librsvg' 'libwnck3' 'libnotify')
makedepends=('intltool')
optdepends=('xfce4-notifyd: show notifications on layout change')
install=$pkgname.install
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('f0bfe97875ef1ca0a3b6a6fac312663c9cada151cf1ac96071393d320cd04987')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
