# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hlint
pkgver=3.4.1
pkgrel=10
pkgdesc="Source code suggestions"
url="http://community.haskell.org/~ndm/hlint/"
license=("BSD")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-cmdargs' 'haskell-cpphs'
         'haskell-data-default' 'haskell-deriving-aeson' 'haskell-extra' 'haskell-file-embed'
         'haskell-filepattern' 'haskell-ghc' 'haskell-ghc-lib-parser-ex' 'haskell-hscolour'
         'haskell-refact' 'haskell-syb' 'haskell-uniplate' 'haskell-unordered-containers'
         'haskell-utf8-string' 'haskell-vector' 'haskell-yaml')
conflicts=('haskell-hlint')
replaces=('haskell-hlint')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6c41235c9c36588d2760af84a251b078ebc31762e8ad91c817a500c787866d34a0eae92298ebad7ed57ac9df678018c52cce990e767ea64ffebc3a6ff68b51d1')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    uusi -u ghc-lib-parser-ex
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fgpl -fthreaded -f-ghc-lib
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING.BSD3"
}
