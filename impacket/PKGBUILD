# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Paolo Giangrandi <peoro.noob@gmail.com>

pkgname=impacket
pkgver=0.11.0
pkgrel=1
pkgdesc='Collection of classes for working with network protocols'
url='https://github.com/CoreSecurity/impacket'
arch=(any)
license=(Apache-2.0)
depends=(
  python
  python-flask
  python-ldap3
  python-pcapy
  python-pyasn1
  python-pyasn1-modules
  python-pycryptodome
  python-pycryptodomex
  python-pyopenssl
  python-setuptools  # https://github.com/fortra/impacket/issues/885#issuecomment-1197218746
  python-charset-normalizer
  python-six
)
makedepends=(
  python-build
  python-installer
  python-wheel
)
checkdepends=(
  python-cryptography
  python-pytest
)
source=(https://github.com/CoreSecurity/impacket/archive/impacket_${pkgver//./_}.tar.gz)
sha256sums=('005b42b9c39515bfe15403b79d341b637dc621e327c2cabd859459b12618a010')
sha512sums=('bda52a44701af633b6ba5eab9f8fba120903398da40c17a3669332f71b883497ace7c5112dcf4d61ea40912323b7363662f8f36a904391faf2ead099863512f0')

build() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  pytest -m "not remote"
}

package() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 ChangeLog.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  ln -s "/usr/lib/python${PYTHONVERSION}/site-packages/impacket/examples" "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: ts=2 sw=2 et:
