# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='sqlite3'
pkgname="ruby-${_gemname}"
pkgver=1.6.9
pkgrel=1
pkgdesc='Ruby bindings for the SQLite3 embedded database'
url='https://github.com/luislavena/sqlite3-ruby'
arch=('loong64' 'x86_64')
license=('BSD')
options=('!emptydirs')
depends=('ruby' 'ruby-mini_portile2' 'sqlcipher' 'sqlite')
makedepends=('ruby-rake' 'ruby-rake-compiler' 'ruby-rake-compiler-dock')
source=("https://github.com/sparklemotion/sqlite3-ruby/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('d36afd980ece16d8ba6d1bcf9945a0cebf3bec84aa2e74d3ce74df1840b4d862c2fdd951549a985e4b20c91e3c3aab116a7d11eccae7fa410f114dc057f43dcc')
b2sums=('e518e5c5a82fa1b3c4c44f6b187997c5d4a38860009565fd828a8d42b15c72aa08e48687290eecc43f98905dc34415a1ab13be0f107044ac60814e6ea04f1273')

prepare() {
  cd "${_gemname}-ruby-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-ruby-${pkgver}"

  rake compile -- --enable-system-libraries --with-sqlcipher
  rake build
}

check() {
  cd "${_gemname}-ruby-${pkgver}"

  rake test
}

package() {
  cd "${_gemname}-ruby-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "pkg/${_gemname}-${pkgver}.gem" \
    -- \
    --enable-system-libraries \
    --with-sqlcipher

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "${pkgdir}/${_gemdir}/cache/" \
    "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "${pkgdir}/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "${pkgdir}/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete

  install --verbose -D --mode=0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
