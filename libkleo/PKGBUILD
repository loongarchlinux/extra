# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libkleo
pkgver=23.08.3
pkgrel=1
pkgdesc='KDE PIM cryptographic library'
arch=(loong64 x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(qgpgme-qt5 kitemmodels5 kpimtextedit)
makedepends=(extra-cmake-modules qt5-tools boost doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('0c37f32e128a5d33e9bf32d00cf9bd61efe56990284d2d3c864aab18d83e13e8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
