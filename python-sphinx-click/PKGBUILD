# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx-click
pkgname=python-sphinx-click
pkgver=6.0.0
pkgrel=1
pkgdesc="Sphinx plugin to automatically document click-based applications"
arch=(any)
url="https://github.com/click-contrib/sphinx-click"
license=(MIT)
depends=(
  python
  python-click
  python-docutils
  python-sphinx
)
makedepends=(
  python-build
  python-installer
  python-pbr
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-click
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('361cc829735ca9edfc4b83767a897eaeedc203be42ea5266a38e7a25bca3d9e8055622961ea3fa042f19997eedc546f37fd7e0482b95bd48f666defaa584e8ca')
b2sums=('eeadb6a034249a2eb594ace694bcbe9d72da002edc7b1dbda809132c123265508c08ee5765a178afb4e10514985f3af0aeb590f1d54bb0f63d754a96ce4ce952')

build() {
  cd $_name-$pkgver
  PBR_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 docs/*.rst -t "$pkgdir/usr/share/doc/$pkgname/docs/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
