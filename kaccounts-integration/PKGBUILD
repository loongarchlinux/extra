# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=kaccounts-integration
pkgver=24.02.2
pkgrel=1
pkgdesc='Online account management system and its Plasma integration components'
arch=(loong64 x86_64)
url='https://www.kde.org/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcmutils
         kconfig
         kcoreaddons
         kdbusaddons
         ki18n
         kio
         kirigami
         kwallet
         libaccounts-qt
         qt6-base
         signon-kwallet-extension
         signon-plugin-oauth2
         signon-ui
         signond)
optdepends=('qt6-declarative: QML plugin')
makedepends=(extra-cmake-modules
             kdoctools
             qcoro-qt6)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('d50b3d790d8df6a24d1afe7660fa7c6e61c38159d777380f87b813c91d06d307'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
