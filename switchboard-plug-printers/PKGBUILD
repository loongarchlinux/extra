# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-printers
pkgver=2.2.1.r157.6487945
pkgrel=1
pkgdesc='Switchboard Printers Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-printers
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libcups
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_commit=648794590e01ca383bf84cfa6cf500c8e610c2d8
source=(git+https://github.com/elementary/switchboard-plug-printers.git#commit=${_commit})
b2sums=('d6cc08e0a18a041438e192bfb2f85db3f825e6e5ae8218a2b40514dcfd3208f159df98c5add581637715afc7da34494f47df737806dbc320738ec7294759a3fe')

pkgver() {
  cd switchboard-plug-printers
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-printers build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
