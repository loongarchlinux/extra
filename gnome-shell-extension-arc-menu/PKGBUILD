# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: skrewball <aur at joickle dot com>
# Contributor: Christian Brassat <christian.brassat at gmail dot com>

pkgname=gnome-shell-extension-arc-menu
_pkgname=ArcMenu
pkgver=56
pkgrel=1
pkgdesc='Application menu extension for GNOME Shell'
arch=('any')
url='https://gitlab.com/arcmenu/ArcMenu'
license=('GPL-2.0-or-later')
depends=('gnome-menus' 'gnome-shell')
source=("https://gitlab.com/arcmenu/ArcMenu/-/archive/v$pkgver/$_pkgname-v$pkgver.tar.gz")
sha256sums=('dd7bfbec3b811e3a6340532b74d1a3a95db89cb76267ae4a7c8953ed0e3b4b9a')

build() {
  cd $_pkgname-v$pkgver
  make
}

package() {
  cd $_pkgname-v$pkgver
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
