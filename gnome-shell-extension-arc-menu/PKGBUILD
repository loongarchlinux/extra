# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: skrewball <aur at joickle dot com>
# Contributor: Christian Brassat <christian.brassat at gmail dot com>

pkgname=gnome-shell-extension-arc-menu
_pkgname=ArcMenu
pkgver=58
pkgrel=1
pkgdesc='Application menu extension for GNOME Shell'
arch=(any)
url='https://gitlab.com/arcmenu/ArcMenu'
license=(GPL-2.0-or-later)
depends=(
  gnome-menus
  gnome-shell
)
makedepends=(git)
source=("git+https://gitlab.com/arcmenu/$_pkgname.git#tag=v$pkgver")
b2sums=('0beb15a268ce331569b45fd0fd67b1879c22a82ef233ae57a18843177a29ab2c425c50245711fa022e345f2135a32031bc5516c311cc35e8838b99591dbe6da5')

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
