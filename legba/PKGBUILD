# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=legba
pkgver=0.9.0
pkgrel=1
pkgdesc="A multiprotocol credentials bruteforcer / password sprayer and enumerator"
url='https://github.com/evilsocket/legba'
arch=('loong64' 'x86_64')
license=(
  'AGPL-3.0-only'
  'GPL-3.0-only'
)
depends=(
  'gcc-libs'
  'glibc'
  'libssl.so'
  'smbclient'
)
makedepends=(
  'cargo'
  'cmake'
)
options=('!lto')
source=(https://github.com/evilsocket/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('4ec96a5602c2704cc911c3030aa2a8d9bb3a99dfbb9987861e34d1496e793ccc')
b2sums=('adc5e92eba95387e5c5096fd3d7ea73c99ec0eb7bfde6920f91ecd1f947d98d531d88b00bec0a4f1c5613cc8aafe28ab1a5a3fd791863bab016ea737b601111b')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/legba
}

# vim: ts=2 sw=2 et:
