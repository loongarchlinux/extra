# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Georg Vogetseder <georg.vogetseder@gmail.com>

pkgname=hamster-time-tracker
pkgver=3.0.3
pkgrel=3
pkgdesc="Time tracking application that helps you to keep track on how much time you have spent during the day on activities you choose to track"
arch=('any')
url="https://github.com/projecthamster/hamster"
license=('GPL')
depends=('gtk3' 'python-dbus' 'python-cairo' 'python-gobject' 'python-xdg')
makedepends=('intltool' 'itstool' 'waf')
source=("$pkgname-$pkgver.tar.gz::https://github.com/projecthamster/hamster/archive/v$pkgver.tar.gz")
sha256sums=('66e7c40d24bcd573ae8204a5d17773d4708a0c3a1069d20771f6cf8c8d102d45')

build() {
  cd hamster-$pkgver
  rm -r waf waflib
  waf configure --prefix=/usr --libexecdir=/usr/lib  --skip-icon-cache-update
  waf build
}

package() {
  cd hamster-$pkgver
  waf install --destdir="$pkgdir"
  rm "$pkgdir/usr/share/glib-2.0/schemas/gschemas.compiled"
}
