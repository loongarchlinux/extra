# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-chess
pkgver=46.0
pkgrel=1
pkgdesc="Play the classic two-player boardgame of chess"
url="https://wiki.gnome.org/Apps/Chess"
arch=(loong64 x86_64)
license=(GPL-3.0-or-later)
depends=(
  gtk4
  libadwaita
  librsvg
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
optdepends=('gnuchess: Play against computer')
groups=(gnome-extra)
_commit=c3625ee59ab3acfd7566ef04300b15ddbadaaac3  # tags/46.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-chess.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
