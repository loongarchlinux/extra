# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=chitose
pkgver=0.0.7
pkgrel=1
pkgdesc='An iftop alternative for cumulated network traffic monitoring'
arch=(loong64 x86_64)
url='https://github.com/taoky/chitose'
license=('GPL-2.0-only')
depends=('glibc' 'libpcap')
makedepends=('git' 'go')
source=("git+https://github.com/taoky/chitose.git#tag=v$pkgver")
sha512sums=('d20f0413ed5d58a43ce7b29b5d3d5b93edcd78399b1cbcfe7c0aea1b159a775e76400c59cae8e1b354793f821d9e165f75dab9cdd47b89c74739b6e2e3336da6')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  cd chitose
  go build
}

package() {
  cd chitose
  install -Dm755 chitose -t "$pkgdir"/usr/bin/
}
