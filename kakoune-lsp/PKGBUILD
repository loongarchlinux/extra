# Maintainer: Maxim Baz <archlinux at maximbaz dot com>

pkgname=kakoune-lsp
pkgver=17.0.1
pkgrel=2
pkgdesc='Kakoune Language Server Protocol Client'
arch=('loong64' 'x86_64')
url="https://github.com/${pkgname}/${pkgname}"
license=('custom:unlicense')
replaces=('kak-lsp')
conflicts=('kak-lsp')
provides=('kak-lsp')
depends=('kakoune')
makedepends=('rust')
optdepends=(
    'bash-language-server: language server for Bash'
    'gopls: language server for Go'
    'haskell-language-server: language server for Haskell'
    'lua-language-server: language server for Lua'
    'python-lsp-server: language server for Python'
    'rust-analyzer: language server for Rust'
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
b2sums=('f11927473f2efbc109f8b6db46fb03b0bb29e108ed12d324c590e9b3b4c55f8c3b3020d6caf37ddf05ee3b0cdba8a132144e44f9a7a174b6065d0bd910d8dad0')

build() {
    cd "${pkgname}-${pkgver}"
    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

# vim:set ts=4 sw=4 et:
