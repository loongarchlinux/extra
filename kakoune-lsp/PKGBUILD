# Maintainer: Maxim Baz <archlinux at maximbaz dot com>

pkgname=kakoune-lsp
pkgver=16.0.0
pkgrel=1
pkgdesc='Kakoune Language Server Protocol Client'
arch=('loong64' 'x86_64')
url="https://github.com/${pkgname}/${pkgname}"
license=('custom:unlicense')
replaces=('kak-lsp')
conflicts=('kak-lsp')
provides=('kak-lsp')
depends=('kakoune')
makedepends=('rust')
optdepends=(
    'bash-language-server: language server for Bash'
    'gopls: language server for Go'
    'haskell-language-server: language server for Haskell'
    'lua-language-server: language server for Lua'
    'python-lsp-server: language server for Python'
    'rust-analyzer: language server for Rust'
)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
b2sums=('45e47279797dd1a4a14d45c3d9a438b8b4c59cbbd181c09360f8a74596392ca6988936096d94113b1224fcd4702ccb9aacfbb65156b8b66c0e1904f3d95e021b')

build() {
    cd "${pkgname}-${pkgver}"
    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

# vim:set ts=4 sw=4 et:
