# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-screenshot
pkgver=41.0+r39+ga362197
pkgrel=2
pkgdesc="Take pictures of your screen"
url="https://gitlab.gnome.org/GNOME/gnome-screenshot"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(gtk3 libhandy)
makedepends=(git meson appstream-glib)
_commit=a36219781bd08201d0454bc6101ff9502430aea3  # master
source=("git+https://gitlab.gnome.org/GNOME/gnome-screenshot.git#commit=$_commit"
        0001-Fix-various-fallback-paths.patch)
b2sums=('5c388fc96c2a153877d1824308f792861f6f3488c983b7ef14651cf265adf838eafb6a92a06f1a30b0fd70d8e83483a26c827dcd9085a93f37026ac03099e34b'
        'b13dc278bf0d69d9bd450d43584dd7d7e4e3a34460f1cac0d33b0a497a1f12561b579842fa49767562a7ac245d9d3d58a15d837e80ba262e1fceaa4f4788e49f')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix various fallback paths
  # https://gitlab.gnome.org/GNOME/gnome-screenshot/-/merge_requests/72
  patch -Np1 -i ../0001-Fix-various-fallback-paths.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
