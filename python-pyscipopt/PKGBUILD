# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Maximilian Stahlberg <maximilian.stahlberg tu-berlin de>

pkgname=python-pyscipopt
pkgver=5.1.1
pkgrel=1
pkgdesc='A Python interface to the SCIP Optimization Suite'
arch=(loong64 x86_64)
url='https://www.scipopt.org/'
license=(MIT)
depends=(glibc
         python
         scip)
makedepends=(cython
             git
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(git+https://github.com/SCIP-Interfaces/PySCIPOpt#tag=v$pkgver)
sha256sums=('11e7a560a35999f4b4fb62ea5c4d1f79b19f59ec0c1ea7db12174abb10d327fe')

build() {
  cd PySCIPOpt
  python -m build --wheel --no-isolation
}

package() {
  cd PySCIPOpt
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
