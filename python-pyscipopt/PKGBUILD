# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Maximilian Stahlberg <maximilian.stahlberg tu-berlin de>

pkgname=python-pyscipopt
pkgver=5.0.0
pkgrel=2
pkgdesc='A Python interface to the SCIP Optimization Suite'
arch=(loong64 x86_64)
url='https://www.scipopt.org/'
license=(MIT)
depends=(glibc
         python
         scip)
makedepends=(cython0
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(https://github.com/SCIP-Interfaces/PySCIPOpt/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('2109b24c81887523b2cde1cb450af3ae5b8e2e96604234c255315fab3cada6be')

build() {
  cd PySCIPOpt-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd PySCIPOpt-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
