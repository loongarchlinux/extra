# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=rshijack
pkgver=0.5.1
pkgrel=1
pkgdesc='TCP connection hijacker, rust rewrite of shijack'
url='https://github.com/kpcyrd/rshijack'
depends=(
  'gcc-libs'
  'glibc'
)
makedepends=('cargo')
arch=('loong64' 'x86_64')
license=('GPL-3.0-or-later')
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha256sums=('5e159096c69ac3bd169d337730b52c5fd177dc159128b9ed34cca2d43aadc349'
            'SKIP')
b2sums=('3964bdf46a48c4c44a9a2e69f47f9dca0bb06da6997f6b0f02f5471361acb28371bd0778e40c25bf26c1fe480d973bc83a1287fb7da57dcb038cccb24376e28e'
        'SKIP')

validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
