# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Robin Candau <antiz@archlinux.org>
# Contributor: Matrix <thysupremematrix a_t tuta d_o_t io>
# Contributor: Yamada Hayao <hayao@fascode.net>

pkgname=gnome-shell-extension-desktop-icons-ng
_pkgname=desktop-icons-ng
pkgver=47.0.9
pkgrel=1
pkgdesc='Desktop icons extension for GNOME Shell'
arch=('any')
url='https://www.rastersoft.com/programas/ding.html'
license=('GPL-3.0-only')
depends=('cairo' 'gdk-pixbuf2' 'glib2' 'gjs' 'gnome-autoar' 'gnome-desktop' 'gnome-shell' 'gtk3'
         'nautilus' 'pango')
makedepends=('meson')
source=("https://gitlab.com/rastersoft/$_pkgname/-/archive/$pkgver/$_pkgname-$pkgver.tar.gz")
sha256sums=('3fe94f0dc4d0cdcd84b586908d6ec285065c046229ea5c73ceb4b4f3ea9c50db')

build() {
  arch-meson $_pkgname-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
