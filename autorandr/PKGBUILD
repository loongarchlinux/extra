# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=autorandr
pkgver=1.15
pkgrel=1
pkgdesc="Auto-detect connected display hardware and load appropiate X11 setup using xrandr"
arch=(any)
url="https://github.com/phillipberndt/autorandr"
license=(GPL-3.0-or-later)
depends=(
  python
  xorg-xdpyinfo
  xorg-xrandr
)
makedepends=(
  bash-completion
  desktop-file-utils
  systemd
)
optdepends=(
  'bash-completion: auto-completion for autorandr in Bash'
  'zsh-completions: auto-completion for autorandr in ZSH'
)
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
sha512sums=('2837348b755f5c4b30ad2b68e191e1969a467401cf32dda495dd19867e391f8b8b4a9c0b4958ef33f75c71aace46686995e27bab805e1e40cc4a1af44e9f0405')
b2sums=('b7200eb7a77230076f1a9f8b1a21be883ae363cc41cff7f1191e034359cc8ebb52e9dd19dd5f2e415a34b9a3a89fda3d0d43a946b2f3401de0d279fb9d5ba1a2')

package() {
  make DESTDIR="$pkgdir" PREFIX=/usr install -C $pkgname-$pkgver
  # zsh completion
  install -vDm 644 $pkgname-$pkgver/contrib/zsh_completion/_$pkgname -t "$pkgdir/usr/share/zsh/site-functions/"
  # docs
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
