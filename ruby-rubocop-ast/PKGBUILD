# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Mario Finelli <mario at finel dot li>

_gemname='rubocop-ast'
pkgname="ruby-${_gemname}"
pkgver=1.30.0
pkgrel=1
pkgdesc="RuboCop's AST extensions and NodePattern functionality"
arch=('any')
url="https://github.com/rubocop/${_gemname}"
license=('MIT')
depends=(
  ruby
  ruby-parser
)
makedepends=(
  ruby-bump
  ruby-bundler
  ruby-oedipus_lex
  ruby-rake
  ruby-rspec
  ruby-simplecov
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('203eb5a369cb8da2e3b40ed3856754a03139ab4395c9511250147155aed98cb86cf63bf9a7718cec8f7a33b9cd7f2995213ffc012b4237c0696c4861dbf55a34')
b2sums=('47774c812912e8f964f68931bd2a7b4d9612983e7f4d5fdd94eea25537483b2bbb5685a86a1ed2230958e77a0b09a12dedc795b75b10c3d87f38b34586e0ea46')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended "s|gem 'simplecov', '~> 0.10', '< 0.18'|gem 'simplecov', '>= 0.10'|g" Gemfile
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec" Gemfile

  # we don't build from a git checkout
  sed --in-place --regexp-extended 's|git ls-files|find|' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  RUBOCOP_VERSION=none rake generate
  gem build "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install/${_gemdir}" RUBOCOP_VERSION=none rake spec
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
