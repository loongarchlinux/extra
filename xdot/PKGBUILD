# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Randy Heydon <randy.heydon at clockworklab dot net>

pkgname=xdot
_pkgname=${pkgname}.py
pkgver=1.4
pkgrel=2
pkgdesc="Interactive viewer for graphs written in Graphviz's dot language"
url='https://github.com/jrfonseca/xdot.py'
arch=('any')
license=('LGPL2.1')
depends=('graphviz' 'gtk3' 'python' 'python-cairo' 'python-gobject' 'python-numpy' 'python-packaging')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('xorg-server-xvfb')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/jrfonseca/${_pkgname}/archive/${pkgver}.tar.gz
        xdot.desktop)
sha512sums=('36065ca840ea0f3ffeca3d7bb16aa3f68b79ef3c7d43f0ba80d100d198f89edee1ca9cb3baff378a63c53a527c37dcccbd7262fd1aa26b7c61bc9e2c55c6e3ed'
            '7bd7994f03c4d7634a9ab780423f1e9d647df0836fd6d8f825dc0bac6ad506ad44457ba473367d1a16b89d00f441c730cb2b66cb5c300542addcf622be82c867')

build() {
  cd ${_pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pkgname}-${pkgver}
  xvfb-run python test.py
}

package() {
  cd ${_pkgname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 ../xdot.desktop -t "${pkgdir}/usr/share/applications"
}

# vim: ts=2 sw=2 et:
