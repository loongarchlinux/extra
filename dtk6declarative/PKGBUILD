# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=dtk6declarative
pkgver=6.0.16
pkgrel=2
pkgdesc='Deepin Toolkit, declarative module for DDE look and feel'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dtk6declarative"
license=('LGPL3')
# qt6-5compat: used in QML
# qt6-shadertools: used in QML (optdep of qt6-5compat for Qt5Compat.GraphicalEffects)
depends=('dtk6core' 'dtk6gui' 'qt6-declarative' 'qt6-base' 'qt6-5compat'
         'qt6-shadertools' 'libgl' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'qt6-shadertools' 'doxygen' 'gtest' 'gmock')
source=("git+https://github.com/linuxdeepin/dtk6declarative.git#tag=$pkgver")
sha512sums=('b2fb855dec68a6526f9935e6a57e6cea0dd1065761bee454c1f2443868adcc54d79d3486a00d32ad65d3f3e545fa1159719ec79498ee89689b2b5dfca2989442')

build() {
  cd $pkgname
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDTK_VERSION=$pkgver \
    -DBUILD_DOCS=ON
  ninja
}

check() {
  cd $pkgname
  ninja test || echo "Tests failed"
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
