# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kweathercore
pkgver=0.7
pkgrel=3
pkgdesc='Library to facilitate retrieval of weather information including forecasts and alerts'
arch=(loong64 x86_64)
license=(GPL)
url='https://invent.kde.org/libraries/kweathercore'
depends=(gcc-libs
         glibc
         kholidays5
         ki18n5
         qt5-base
         qt5-location)
makedepends=(doxygen
             extra-cmake-modules
             qt5-tools)
source=(https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('04dde83ae543163735776dc50edab3510247d4825432840a78fade9b237a79fd'
            'SKIP')
validpgpkeys=(0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D) # Bhushan Shah (mykolab address) <bshah@mykolab.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
