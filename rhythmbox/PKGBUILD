# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rhythmbox
pkgver=3.4.7
pkgrel=2
pkgdesc="Music playback and management application"
url="https://wiki.gnome.org/Apps/Rhythmbox"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  at-spi2-core
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  grilo
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk3
  hicolor-icon-theme
  json-glib
  libgirepository
  libgudev
  libnotify
  libpeas
  libsecret
  libsoup3
  libx11
  libxml2
  media-player-info
  pango
  python
  python-gobject
  tdb
  totem-plparser
)
makedepends=(
  brasero
  git
  gobject-introspection
  gtk-doc
  libdmapsharing
  libgpod
  libmtp
  lirc
  meson
  vala
  yelp-tools
  zeitgeist
)
checkdepends=(
  check
  xorg-server-xvfb
)
optdepends=(
  'brasero: Audio CD Recorder plugin'
  'grilo-plugins: Grilo media browser plugin'
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
  'gvfs-mtp: Portable Players - Android plugin'
  'libdmapsharing: DAAP Music Sharing plugin'
  'libgpod: Portable Players - iPod plugin'
  'libmtp: Portable Players - MTP plugin'
  'lirc: LIRC plugin'
  'zeitgeist: Zeitgeist logging plugin'
)
source=("git+https://gitlab.gnome.org/GNOME/rhythmbox.git#tag=v$pkgver")
b2sums=('4c4d2bf06fbfe642ebc81829c1fc7bc4a4d61ee4687acfa253c758fc7ddd061eccf2aa8ce257a6e61a4a35194c40aea85f4f9113c65b2e9ac1484ed683029794')

prepare() {
  cd $pkgname

  # Python 3.12
  git cherry-pick -n ceeed5736c3e25d33fec5a100afde20ef485e07e
}

build() {
  local meson_options=(
    -D daap=enabled
    -D gtk_doc=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs -t 3
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
