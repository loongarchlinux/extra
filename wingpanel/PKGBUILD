# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel
pkgver=3.0.5
pkgrel=3
pkgdesc='The Pantheon Top Panel'
arch=(loong64 x86_64)
url=https://github.com/elementary/wingpanel
license=(GPL-3.0-or-later)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgala.so
  libgee
  libgranite.so
  mutter45
  pantheon-notifications
)
makedepends=(
  cmake
  git
  gala
  meson
  vala
)
provides=(libwingpanel.so)
_tag=23a3eb7c2448b4f35398116df7d01b075361ef1f
source=(git+https://github.com/elementary/wingpanel.git#tag=${_tag})
b2sums=('5ce742e5a52e9e404a070e7f4af13e4eef2ac26e09cf435d94de96eeacf7e26fba836e59ca017f99bcc9a45cda1004db1c4edf06243095eb907e655e690ae87e')

pkgver() {
  cd wingpanel
  git describe --tags
}

prepare() {
  cd wingpanel

  # mutter 45
  git cherry-pick -n d6009d9f0d5c7479172093447ccceccba86ff1f3
}


build() {
  arch-meson wingpanel build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
