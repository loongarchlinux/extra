# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Bastian Germann <bastian.germann[at]gmx[dot]de>
# Contributor: hcartiaux
# Contributor: Chris Mann <cshclm[at]gmail[dot]com>

pkgname=unrar-free
pkgver=0.3.1
pkgrel=1
epoch=1
pkgdesc="Free utility to extract files from RAR archives."
url="https://gitlab.com/bgermann/unrar-free"
arch=('loong64' 'x86_64')
license=('GPL-2.0-or-later')
depends=('libarchive' 'glibc')
source=("${url}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('16ecd4cf8b236d472413d179721e40b2270262f32844e10435fe45dd68053fb8')

prepare() {
	cd "${pkgname}-${pkgver}"
	autoreconf -i
}

build() {
	cd "${pkgname}-${pkgver}"
	export CFLAGS="${CFLAGS} -Wno-format-security"
	./configure --prefix=/usr
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm 644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
