# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arvedui <arvedui@posteo.de>

pkgname=python-retrying
pkgver=1.3.4
pkgrel=3
pkgdesc="A general-purpose retrying library"
arch=('any')
url="https://github.com/groodt/retrying"
license=('Apache-2.0')
depends=('python-six')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
source=("https://github.com/groodt/retrying/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('80bd19eefdbd8277ec0ba656d1b1d3589b2e3492cbb902c963376b3df475abbe4725c9902426a1784e4dddbd57ea8bc51ea5925a0a72a6f5c11333893314f2c5')

build() {
  cd retrying-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd retrying-$pkgver
  python -m unittest -v
}

package() {
  cd retrying-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
