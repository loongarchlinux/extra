# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-mouse-touchpad
pkgver=7.0.0.r230.bed5553d
pkgrel=1
pkgdesc='Switchboard Mouse & Touchpad Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-mouse-touchpad
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libgee
  libgranite-7.so
  libswitchboard-3.so
  libxml2
)
makedepends=(
  git
  meson
  vala
)
_commit=bed5553d8844932b402be51c22a6f9eb3fb17368
source=(git+https://github.com/elementary/switchboard-plug-mouse-touchpad.git#commit=${_commit})
b2sums=('f2ff2b6bfa611ebe02e760f573896b7f4dd3134da2eedbfdd7d511740517d058fb03b1e824307f924bdf01cfc8a7b55ebe9fc466cbb395e9aa141929b33a5c78')

pkgver() {
  cd switchboard-plug-mouse-touchpad
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-mouse-touchpad build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
