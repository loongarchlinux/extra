# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=6.2.0
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=(loong64 x86_64)
url=https://github.com/elementary/terminal
license=(GPL3)
depends=(
  glib2
  gtk3
  libgranite.so
  libhandy-1.so
  pango
  vte3
)
makedepends=(
  appstream
  git
  intltool
  meson
  vala
  xorg-server-xvfb
)
groups=(pantheon)
_tag=0500b6872e76d2f0bf6b529aac00a8bdcf08ccd9
source=(pantheon-terminal::git+https://github.com/elementary/terminal.git#tag=${_tag})
b2sums=('fe55f48612989f80c8f2cef500cfee0ad5cde1b636cb5ccffca80ebbaaa884788c85329b88962bde701caa9b84cdc318130d8d5259f1ad8f409c8850fa9ecd18')

pkgver() {
  cd pantheon-terminal
  git describe --tags
}

build() {
  arch-meson pantheon-terminal build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
