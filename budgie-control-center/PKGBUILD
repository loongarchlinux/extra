# Maintainer: Campbell Jones <serebit@archlinux.org>

pkgname=budgie-control-center
pkgver=1.4.0
pkgrel=1
pkgdesc="Budgie's main interface to configure various aspects of the system"
arch=(loong64 x86_64)
url="https://github.com/BuddiesOfBudgie/budgie-control-center"
license=("GPL-2.0-or-later")
groups=("budgie")
depends=(accountsservice gcr gnome-bluetooth gnome-settings-daemon ibus libgtop libpwquality smbclient
         libcheese libnma udisks2 libhandy gsound colord-gtk)
makedepends=(docbook-xsl modemmanager meson)
optdepends=("system-config-printer: Printer settings"
            "gnome-user-share: WebDAV file sharing"
            "rygel: Media sharing"
            "openssh: Remote login"
            "power-profiles-daemon: Power profiles support")
source=("https://github.com/BuddiesOfBudgie/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"{,.asc})
b2sums=('eeeff329c6e92db27224921805a9b581b6a5b5b4e727f3b4d252caf7d5a7f5d28eda9163aabb6ade07442b7d31689642dc9bbe0b8e392ad29d06c05707d25f08'
        'SKIP')
validpgpkeys=("1E1FB0017C998A8AE2C498A6C2EAA8A26ADC59EE" # David Mohammed <fossfreedom at ubuntu dot com>
              "0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B") # Joshua Strobl <me at joshuastrobl dot com>

build() {
    arch-meson "$pkgname-$pkgver" build -Ddocumentation=true
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
