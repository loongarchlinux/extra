# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=skladnik
pkgver=0.5.2
pkgrel=1
pkgdesc='An implementation of the Japanese warehouse keeper game Sokoban'
url='https://apps.kde.org/skladnik/'
arch=(loong64 x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kio
         kwidgetsaddons
         kxmlgui
         libkdegames
         qt6-base)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-games)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('9091b63ce516e4361ef88745869cb698078623eaae1e16097f4c5124d44adb88'
            'SKIP')
validpgpkeys=(E6EBD6EB0518FE5BCCA5F6A76AACDD263C2CEFD4) # Friedrich W. H. Kossebau <kossebau@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
