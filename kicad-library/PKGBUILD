# Maintainer: Filipe La√≠ns <lains@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgbase=kicad-library
pkgname=($pkgbase{,-3d})
pkgver=8.0.1
pkgrel=1
pkgdesc='Assorted libraries for KiCad'
arch=('any')
url='http://kicad.org/'
license=('GPL')
makedepends=('git' 'cmake')
options=('!strip')
source=(
  "git+https://gitlab.com/kicad/libraries/kicad-packages3D.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-symbols.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-templates.git#tag=$pkgver"
  "git+https://gitlab.com/kicad/libraries/kicad-footprints.git#tag=$pkgver"
)
sha512sums=('5035d352be3431b1a6fa8a6de32f65c4fe0dcab00072c47fc150ee771f04762e87f8cda3cfc9b9b485a4646f7faf6e7d3952a50b6a7fee5760ab3a695697c75a'
            '60aea592646d6c8533f606cd1c0f6acd0f0dbb3a07159cf2657b2756ed147c8b80b17792032191cff60ef9881c67cf78fbecffbb3d90855cc72c952fe34b7966'
            'cbb41f7f94038bc823b98fc6217ded09f0eda107a17a9769348cd034309b7a72541c14c6bf2efdc849b2ff6d9520fbdddbdce1c88ac59c0ddaa25d228ee74220'
            '174752323315b48ccf394af491dee1512012bdaad928b535e4fbd2fe7f77d3e777712b5c74da0f4cdac4a0ba656093769d1d6848517625006564e37f8377885f')

build() {
  for package in symbols footprints packages3D templates; do
     cmake \
       -B "build-$package" \
       -S "kicad-$package" \
       -DCMAKE_INSTALL_PREFIX=/usr

     cmake --build "build-$package"
   done
}

package_kicad-library() {
  pkgdesc="KiCad symbol, footprint and template libraries"

  for package in symbols footprints templates; do
    DESTDIR="$pkgdir" cmake --install "build-$package"
  done
}

package_kicad-library-3d() {
  pkgdesc="KiCad 3D model libraries"

  DESTDIR="$pkgdir" cmake --install build-packages3D
}

# vim:set ts=2 sw=2 et:
