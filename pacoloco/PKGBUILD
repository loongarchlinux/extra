# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=pacoloco
pkgver=1.7
pkgrel=1
pkgdesc='Pacman caching proxy server'
arch=(loong64 x86_64)
url='https://github.com/anatol/pacoloco'
backup=(etc/pacoloco.yaml)
license=(MIT)
depends=(glibc)
makedepends=(go)
source=(pacoloco-$pkgver.tar.gz::https://github.com/anatol/pacoloco/archive/$pkgver.tar.gz)
sha256sums=('7d8628d3b93de0ff5047fe3d61e12da0afca895d822e446235b1056639f088b2')

build() {
  cd pacoloco-$pkgver

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build
}

package() {
  cd pacoloco-$pkgver

  install -D -m755 pacoloco "$pkgdir"/usr/bin/pacoloco
  install -D -m644 pacoloco.yaml.sample "$pkgdir"/etc/pacoloco.yaml
  install -D -m644 pacoloco.sysusers.d "$pkgdir"/usr/lib/sysusers.d/pacoloco.conf
  install -D -m644 pacoloco.service "$pkgdir"/usr/lib/systemd/system/pacoloco.service
  install -D -m644 pacoloco.tmpfiles.d "$pkgdir"/usr/lib/tmpfiles.d/pacoloco.conf
}
