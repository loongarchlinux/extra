# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=persistent
pkgname=haskell-persistent
pkgver=2.14.5.1
pkgrel=10
pkgdesc="Type-safe, multi-backend data serialization"
url="http://www.yesodweb.com/book/persistent"
license=("MIT")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-base64-bytestring'
         'haskell-blaze-html' 'haskell-conduit' 'haskell-fast-logger' 'haskell-http-api-data'
         'haskell-lift-type' 'haskell-monad-logger' 'haskell-path-pieces' 'haskell-resource-pool'
         'haskell-resourcet' 'haskell-scientific' 'haskell-silently' 'haskell-th-lift-instances'
         'haskell-unordered-containers' 'haskell-unliftio' 'haskell-unliftio-core' 'haskell-vault'
         'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-hspec' 'haskell-quickcheck' 'haskell-quickcheck-instances'
             'haskell-shakespeare')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('a3969142f77d9fc54a0e73ac54b53a96de0b93fadf67326f6b8d9fdb668fc4acf78eece4502cc557873b011e2191fc1607836a5aa0d5d287e79820c0bf6e90a4')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u aeson $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-nooverlap
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
