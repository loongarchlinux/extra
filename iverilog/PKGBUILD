# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jared Casper <jaredcasper@gmail.com>
# Contributor: Paulo Matias <matias archlinux-br org>

pkgname=iverilog
pkgver=12.0
pkgrel=2
pkgdesc='Icarus Verilog compiler and simulation tool'
arch=('loong64' 'x86_64')
url='https://github.com/steveicarus/iverilog'
license=('GPL')
depends=('zlib' 'bzip2')
makedepends=('gperf' 'git')
options=('staticlibs')
source=("git+https://github.com/steveicarus/iverilog#tag=v${pkgver/./_}"
        fix-string-literal.patch::https://github.com/steveicarus/iverilog/commit/23e51ef7a8e8e4ba42208936e0a6a25901f58c65.patch)
b2sums=('e84a7ebcb525a4ad3013f371df8871adf98beb4056d4e4b3502d7171820f59594e3ce892f566a74689adf442ecb6d39597c91f4e3e14d1d0338073544eee8830'
        '1ec55d4ec133de284bcaa6c9cd9e6d90e6d9c65e499816826e1e8add5d02e25371cb065820179c66c50ca615176a6cb0952d0b86f25ef6d73db5645ef46da04f')

prepare() {
  cd "$pkgname"

  patch -Np1 < ../fix-string-literal.patch

  aclocal
  autoconf
}

build() {
  cd "$pkgname"

  CFLAGS+=' -ffat-lto-objects -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common

  CXXCPP=/usr/bin/cpp ./configure --prefix=/usr
  make
}

package() {
  make -C "$pkgname" -j1 prefix="$pkgdir/usr" install
}

# vim: ts=2 sw=2 et:
# getver: raw.githubusercontent.com/steveicarus/iverilog/master/scripts/MAKE_RELEASE.sh
