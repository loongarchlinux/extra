# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kyle Manna <kyle[at]kylemanna[d0t]com>

pkgname=python-can
pkgver=4.4.2
pkgrel=1
pkgdesc='Controller Area Network support for Python'
arch=(any)
url=https://github.com/hardbyte/python-can
license=(LGPL-3.0-only)
depends=(
  python
  python-setuptools
  python-typing_extensions
  python-wrapt
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
)
_tag=478f8b8749ce08906b0386d13db289cf98e3246e
source=(git+https://github.com/hardbyte/python-can.git#tag=${_tag})
b2sums=('SKIP')

pkgver() {
  cd python-can
  git describe --tags | sed 's/^v//'
}

build() {
  cd python-can
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" python-can/dist/*.whl
}

# vim: ts=2 sw=2 et:
