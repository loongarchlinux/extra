# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plfit
pkgver=0.9.6
pkgrel=1
pkgdesc='Fitting power-law distributions to empirical data'
arch=(loong64 x86_64)
url='https://github.com/ntamas/plfit'
license=(GPL-2.0-only)
depends=(glibc)
makedepends=(cmake)
source=(https://github.com/ntamas/plfit/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('b44e592c1b96abff1255a4d67bee52cfe2a6414f975fb42fc3ffd2547bc8a535')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
