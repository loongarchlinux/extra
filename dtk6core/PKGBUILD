# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtk6core
pkgver=6.0.16
pkgrel=2
pkgdesc='DTK core modules'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dtk6core"
license=('LGPL3')
depends=('deepin-desktop-base' 'dtkcommon' 'gcc-libs' 'glibc' 'icu' 'libsystemd' 'lshw' 'qt6-base'
         'uchardet' 'spdlog')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'gtest' 'doxygen')
source=("git+https://github.com/linuxdeepin/dtk6core.git#tag=$pkgver"
         qt-6.7.1.patch)
sha512sums=('d200df6c72101f2a63fddfbe2277609acd2e08a57506782a7f47019f5d34705d283e50e30be3611d4d3a5892af02989fdc3ce60395437e6397fbd049f8756c8e'
            '39130b23e52e864ad5ed47085af89c0f558f4b246073d1452b3f2ebfc4e8cdfc473e2e487215e72b49b883e361522c29bed67ec6084a43484fe797e1aed7f103')

prepare() {
  cd dtk6core
  sed -i 's|/etc/os-version|/etc/uos-version|' src/dsysinfo.cpp
  patch -p1 -i ../qt-6.7.1.patch
}

build() {
  cd dtk6core
  cmake . -GNinja \
      -DBUILD_DOCS=ON \
      -DBUILD_TESTING=ON \
      -DBUILD_WITH_SYSTEMD=ON \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_INSTALL_LIBEXECDIR=lib \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DDTK_VERSION=$pkgver
  ninja
}

check() {
  cd dtk6core
  ninja test
}

package() {
  cd dtk6core
  DESTDIR="$pkgdir" ninja install
}
