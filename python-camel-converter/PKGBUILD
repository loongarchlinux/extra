# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-camel-converter
pkgver=3.1.2
pkgrel=1
pkgdesc='Converts a string from snake case to camel case or camel case to snake case'
arch=(any)
license=('MIT')
url='https://github.com/sanders41/camel-converter'
depends=(python)
makedepends=(python-poetry-core)
makedepends=(python-build python-installer python-wheel python-poetry-core)
checkdepends=(python-pytest python-pydantic)
optdepends=('python-pydantic: provides a base class to automatically convert between snake and camel case')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sanders41/camel-converter/archive/refs/tags/v${pkgver}.tar.gz)
sha512sums=('998312c79cdf12217ea384300e99254ca8746558f7eb4cffb22fc5c36f29a65dbcde4a4b8c584ff4cfbbf1287f743eabc7054fefa50afa9769b2fba829160e95')

build() {
  cd "camel-converter-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "camel-converter-${pkgver}"
  pytest -o addopts='' tests
}

package() {
  cd "camel-converter-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
