# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com)
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: karnath <karnathtorjian@gmail.com>

pkgname=zathura-pdf-poppler
pkgver=0.3.2
pkgrel=5
pkgdesc="Adds pdf support to zathura by using the poppler engine"
url="https://pwmt.org/projects/zathura-pdf-poppler/"
arch=('loong64' 'x86_64')
license=('Zlib')
depends=('poppler-glib' 'zathura' 'desktop-file-utils')
makedepends=('meson' 'ninja')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pwmt/${pkgname}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('5e6d748f9dfcc9e318ee7947d3a4d9acbbb30d6b45810a7c2fd2d0572d98872f5d594add8683b6579791fb82846a3d7854e84239ac5167c0541e700518a31043')

build() {
        cd "${pkgname}-${pkgver}"
        arch-meson build
        ninja -C build
}

package() {
        cd "${pkgname}-${pkgver}"
        DESTDIR="${pkgdir}" ninja -C build install
        install -Dm 664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
