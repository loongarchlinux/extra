# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=evolution-ews
pkgver=3.52.1
pkgrel=1
pkgdesc="MS Exchange integration through Exchange Web Services"
url="https://wiki.gnome.org/Apps/Evolution/EWS"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  evolution
  evolution-data-server
  gcc-libs
  glib2
  glibc
  gtk3
  json-glib
  libical
  libmspack
  libsoup3
  libxml2
  pango
)
makedepends=(
  cmake
  git
  intltool
  ninja
  python
  systemd
)
source=("git+https://gitlab.gnome.org/GNOME/evolution-ews.git#tag=$pkgver")
b2sums=('50f4bba01c47d3268471e83bd60d0edb35cc905827718ed03c07516b3f4feb27489fc779060c744d2880bb337172eea5fa0a0e14134be674aa4549c207c9344e')

prepare() {
  cd $pkgname
}

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DLIBEXEC_INSTALL_DIR=/usr/lib
    -DSYSCONF_INSTALL_DIR=/etc
  )

  cmake -S $pkgname -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim:set sw=2 sts=-1 et:
