# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-caching
pkgver=2.1.0
pkgrel=2
pkgdesc="Adds caching support to your Flask application"
url="https://github.com/sh4nks/flask-caching"
license=('BSD-3-Clause')
arch=('any')
depends=('python-flask' 'python-cachelib')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-pytest-cov' 'python-pytest-xprocess'
              'python-pylibmc' 'python-redis' 'memcached' 'python-asgiref')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sh4nks/flask-caching/archive/v$pkgver.tar.gz")
sha512sums=('b55e5b5f0dbe5fcb850f7155df4ebc5eb1c19f69764d07593299453270a36fd2c443c4834323291859fe9a8c7f6619baf133c78606cecc2b1b4a621e54e60519')

prepare() {
  cd flask-caching-$pkgver
  sed -i 's/, < 0.10.0//' setup.py
}

build() {
  cd flask-caching-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd flask-caching-$pkgver
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd flask-caching-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
