# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-caching
pkgver=2.3.0
pkgrel=1
pkgdesc="A caching extension for Flask"
url="https://github.com/pallets-eco/flask-caching"
license=('BSD-3-Clause')
arch=('any')
depends=(
  'python'
  'python-cachelib'
  'python-flask'
  'python-jinja'
  'python-werkzeug'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'memcached'
  'python-asgiref'
  'python-pylibmc'
  'python-pytest'
  'python-pytest-xprocess'
  'python-redis'
  'redis'
)
optdepends=(
  'python-pylibmc: for memcached backend'
  'python-redis: for Redis backend'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v.$pkgver.tar.gz")
sha512sums=('ed6c73f4dc7008c317a245cd4f3619deedfa43f118e50bfcab329f6f70a4b1a7adb893e1b6f7fe194bcf64f215eb17dfba136f58d8f7af68615976c84f8a01d2')

_archive=flask-caching-v.$pkgver

prepare() {
  cd "$_archive"
  # Remove failing (faulty?) import that causes skipped tests.
  sed -i '/__import__("pytest_xprocess")/d' tests/conftest.py
}

build() {
  cd "$_archive"
  python -m build --wheel --no-isolation
}

check() {
  cd "$_archive"
  python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PYTHONPATH="$PWD/tmp_install/$site_packages"
  # Deselect tests failing due to:
  #   redis.exceptions.ResponseError: value is not an integer or out of range
  # Not sure why.
  pytest \
    --deselect tests/test_backend_cache.py::TestRedisCache::test_generic_inc_dec
}

package() {
  cd "$_archive"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
