# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Alex Gentilucci <alexander.gentilucci@gmail.com>

pkgname=ft2-clone
pkgver=1.84
pkgrel=1
pkgdesc='Portable Fasttracker II clone'
arch=('loong64' 'x86_64')
url='https://16-bits.org/ft2.php'
license=(
  'BSD-3-Clause'
  'MIT'
  'CC-BY-NC-SA-4.0'
)
depends=(
  'glibc'
  'gcc-libs'
  'alsa-lib'
  'sdl2'
  'flac'
  'hicolor-icon-theme'
)
makedepends=(
  'git'
  'libicns'
  'cmake'
  'gendesk'
)
source=(
  "$pkgname::git+https://github.com/8bitbubsy/ft2-clone#tag=v$pkgver"
  'templatify-build-date.patch'
)
sha512sums=('14cc558c41dbfeb37f1b48eef33897c1eb87f06711f9916aaacebf24a5c5c708c3343a4517a23c2402aa56550f52abe8a0d1e58d6bd6fe079358bb71f25f04f9'
            'bd9024bf8eb1f08a18079963eeba05ff3018cdf51c9912236e23b3a71625a96a322f75bf2ca510fcbbb759ba240dcaea480a67ce11912fef30db14f608757523')
b2sums=('e0c72dee00132de7993a42f6dded4e028382800c277238c4c65f4fb0632e304af3baff2458f960227403621869c44a8ca132da30f356d8cc50a4f9ecaca8c2d7'
        '240a718bae59b9d6b5cd2027c6906788007beab9181dae477fdcb7189cb77e3dcf2e3cb621219d6205edf06b9729036558ed86db54227b4f28a2b5525475a8c0')

prepare() {
  cd "$pkgname"

  # convert icons
  icns2png -x \
    release/macos/ft2-clone-macos.app/Contents/Resources/ft2-clone-macos.icns

  # generate desktop file
  gendesk -n \
    --exec "$pkgname" \
    --name 'Fasttracker II Clone' \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --icon "$pkgname" \
    --genericname 'Music Tracker'

  # attempt repro build by using commit date as build date
  local commit_date="$(git show --no-patch --format=%cd --date=format:'%d %B %Y')"

  # apply template
  patch -p1 -i "$srcdir/templatify-build-date.patch"

  # patch in commit date
  sed -e "s/@DATE@/$commit_date/" -i src/ft2_header.h
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -DEXTERNAL_LIBFLAC=ON \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # application icon
  install -vDm644 ft2-clone-macos_512x512x32.png "$pkgdir/usr/share/icons/hicolor/512x512/apps/$pkgname.png"

  # desktop file
  install -vDm644 -t "$pkgdir/usr/share/applications" ft2-clone.desktop

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licensing
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
