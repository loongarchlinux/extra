# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-editor
pkgver=1.0.35
pkgrel=1
pkgdesc='Public library for deepin-image-viewer and deepin-album'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/image-editor"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'qt5-svg' 'libtiff' 'ffmpegthumbnailer'
         'freeimage' 'libmediainfo' 'opencv' 'gcc-libs' 'deepin-file-manager')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("https://github.com/linuxdeepin/image-editor/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        remove-broken-flags.patch)
sha512sums=('a88e0b6ac4de7a8d916b242b932bb09534960bf59ddf640e568da7ce9ec4bc50ebe4efbbb8ea2f2d3b1fd04628bb9ec41ace4221d8679ec5dba02df88ef8573c'
            'bc72e7732ae045a10129336a770d04827c487fb57938c4711e57aeeccde369fca207913477a695cddffc7d0fb6d77add1f75b9b5ca784d0285b8f4f56c2376ad')

prepare() {
  patch -d image-editor-$pkgver -p2 < remove-broken-flags.patch
}

build() {
  cd image-editor-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd image-editor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
