# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=implicit-hie-cradle
pkgname=haskell-implicit-hie-cradle
pkgver=0.5.0.1
pkgrel=1
pkgdesc="Auto generate hie-bios cradles"
url="https://github.com/Avi-D-coder/implicit-hie-cradle#readme"
license=("BSD")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-extra' 'haskell-hie-bios' 'haskell-implicit-hie')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('0e03f72a5b3aeb2da349e8f64c0fa65616bc40174d931934d2a2bbda6c924f7a60da77ebbb433277f91c9a2bc5fd85e36857366af922b7bb18683751e9f70d67')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
