# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-openstackclient
pkgver=6.6.0
_commit=2f9a523765ca99c9ef9a6968e430bab102f3208d
pkgrel=2
pkgdesc="OpenStack Command-line Client"
arch=('any')
url="https://docs.openstack.org/python-openstackclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-cryptography' 'python-cliff' 'python-iso8601' 'python-openstacksdk'
         'python-osc-lib' 'python-oslo-i18n' 'python-keystoneclient' 'python-novaclient'
         'python-cinderclient' 'python-stevedore')
makedepends=('git')
checkdepends=('python-oslotest' 'python-requests' 'python-requests-mock' 'python-stestr'
              'python-testtools' 'python-wrapt' 'python-ddt')
source=("git+https://github.com/openstack/python-openstackclient.git#commit=$_commit")
sha512sums=('a7526db65b692fd33059698319e6d90ab214d39576be3154a880ca05c77dd63b99932025aa172bb2c0f7510430b4c42160c0a444459e9b70fb7eba26ed957e6b')

build() {
  cd python-openstackclient
  python setup.py build
}

check() {
  cd python-openstackclient
  # https://bugs.launchpad.net/python-openstackclient/+bug/2061220
  stestr run --exclude-regex 'TestModuleList.test_module_list*'
}

package() {
  cd python-openstackclient
  python setup.py install --root="$pkgdir" --optimize=1
}
