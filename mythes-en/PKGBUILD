# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=mythes-en
# https://cgit.freedesktop.org/libreoffice/dictionaries/log/en
_commit=7272a82714ca91947b4573c09bce4938861a9877 # last commit "en" subdir - 2024-05-19
pkgver=20240519
pkgrel=1
pkgdesc="English thesaurus"
arch=('any')
url="https://wiki.documentfoundation.org/Development/Dictionaries"
license=('GPL-2.0-or-later' 'LicenseRef-mythes-en')
makedepends=('git' 'libmythes')
optdepends=('libmythes: offers thesaurus library functions')
source=("git+https://anongit.freedesktop.org/git/libreoffice/dictionaries.git#commit=$_commit")
sha256sums=('SKIP')

package() {
  cd dictionaries/en
#return 1
  install -dm755 "${pkgdir}"/usr/share/mythes
  cp -p th_en_US_v2.* "$pkgdir"/usr/share/mythes

  pushd "$pkgdir"/usr/share/mythes/
  # create sorted index *.idx file from structured thesaurus file 
  for lang in `find -type f -name "*.dat" -printf '%P\n'`; do
    cat "${lang}" | /usr/bin/th_gen_idx.pl > "${lang/.dat/}".idx
  done

  en_US_aliases="en_AG en_AU en_BS en_BW en_BZ en_CA en_DK en_GB en_GH en_IE en_IN en_JM en_NA en_NG en_NZ en_PH en_SG en_TT en_ZA en_ZW"
  for lang in $en_US_aliases; do
        ln -s th_en_US_v2.idx th_"$lang"_v2.idx
        ln -s th_en_US_v2.dat th_"$lang"_v2.dat
  done
  popd

  # the symlinks
  install -dm755 "${pkgdir}"/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
    for file in "$pkgdir"/usr/share/mythes/*; do
      ln -sv /usr/share/mythes/$(basename $file) .
    done
  popd
  
  # docs
  install -dm755 "${pkgdir}"/usr/share/doc/$pkgname
  cp -p README_en_GB_thes.txt "$pkgdir"/usr/share/doc/$pkgname

  install -Dm644 {README_en_GB_thes.txt,WordNet_license.txt,license.txt} \
    -t "$pkgdir/usr/share/licenses/$pkgname/"
}
