# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgbase=pacman-bintrans
pkgname=('pacman-bintrans' 'pacman-bintrans-tools')
pkgver=0.5.0
pkgrel=1
pkgdesc='Binary transparency for pacman'
url='https://github.com/kpcyrd/pacman-bintrans'
arch=('loong64' 'x86_64')
license=('GPL-3.0-or-later')
depends=(
  'bzip2' 'libbz2.so'
  'gcc-libs'
  'glibc'
  'openssl' 'libssl.so'
  'rekor'
  'shared-mime-info'
  'xz' 'liblzma.so'
  'zstd' 'libzstd.so'
)
makedepends=(
  'cargo'
  'sqlite' 'libsqlite3.so'
)
source=(https://github.com/kpcyrd/${pkgbase}/archive/v${pkgver}/${pkgbase}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgbase}/releases/download/v${pkgver}/${pkgbase}-${pkgver}.tar.gz.asc)
sha512sums=('acfcfe601d1843c696f014d0a3776bb4e56e9cbcc9e37f459193b6aedb2fb5c21c62c7276934bb177b72b372af428b81ac7902c6c46ef5143e7a34b3e9c1f584'
            'SKIP')
b2sums=('615c824b2c159f91e7732d07d5264200f3d3824cd54c0c33eec426cbaae4049c84926eb9bb10b00fa1371e6872be0561bd0cbcdf98ae353553e26f0473f20246'
        'SKIP')

validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")


prepare() {
  cd "${pkgbase}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgbase}-${pkgver}"
  cargo build --frozen --release --workspace
}

check() {
  cd "${pkgbase}-${pkgver}"
  cargo test --frozen --workspace
}

package_pacman-bintrans() {
  cd "${pkgbase}-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/pacman-bintrans

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgbase}"
}

package_pacman-bintrans-tools() {
  pkgdesc='Binary transparency for pacman (sign/monitor tools)'
  depends=(
    'bzip2' 'libbz2.so'
    'gcc-libs'
    'glibc'
    'openssl' 'libssl.so'
    'rekor'
    'shared-mime-info'
    'sqlite' 'libsqlite3.so'
    'xz' 'liblzma.so'
    'zstd' 'libzstd.so'
  )

  cd "${pkgbase}-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/pacman-bintrans-sign \
    target/release/pacman-bintrans-monitor
}

# vim: ts=2 sw=2 et:
