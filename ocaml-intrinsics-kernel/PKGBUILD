# Maintainer:
# Contributor: Daniel Peukert <daniel@peukert.cc>

pkgname=ocaml-intrinsics-kernel
pkgver=0.17.0
pkgrel=1
pkgdesc='OCaml interface to operations that have dedicated hardware instructions on some micro-architectures'
arch=(loong64 x86_64)
url='https://github.com/janestreet/ocaml_intrinsics_kernel'
license=(MIT)
depends=(glibc
         ocaml)
makedepends=(dune
             git)
options=(!debug !strip)
source=(git+https://github.com/janestreet/ocaml_intrinsics_kernel#tag=v$pkgver)
b2sums=('b720e270eb4b0b67d67ed9d60f869e91a71cac47bc4e3c1522df9be0f6db97e30c99a8bb0f86a2663031177cb8970480af3d592ced8aa936a11e63599f400956')

build() {
  cd ocaml_intrinsics_kernel
  dune build --release --verbose
}

package() {
  cd ocaml_intrinsics_kernel
  DESTDIR="$pkgdir" dune install --prefix '/usr' --libdir '/usr/lib/ocaml' --docdir '/usr/share/doc' --mandir '/usr/share/man' --release --verbose

  for _folder in "$pkgdir/usr/share/doc/"*; do
    mv "$_folder" "$pkgdir/usr/share/doc/ocaml-$(basename "$_folder")"
  done

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  ln -sf /usr/share/doc/$pkgname/LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
