# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libblastrampoline
pkgver=5.11.0
pkgrel=1
pkgdesc='Using PLT trampolines to provide a BLAS and LAPACK demuxing library'
arch=(loong64 x86_64)
url='https://github.com/JuliaLinearAlgebra/libblastrampoline'
license=(MIT)
depends=(glibc)
makedepends=(git)
source=(git+https://github.com/JuliaLinearAlgebra/libblastrampoline#tag=v$pkgver)
sha256sums=('2d59a33c9561e7fbbd8257c633a86a34ece1051a4f9049abba49f215f6a2c40f')
options=(!lto)

build() {
  cd $pkgname/src
  make\
    COMPLEX_RETSTYLE_AUTODETECTION=0 \
    F2C_AUTODETECTION=0 \
    LBT_CFLAGS="$CFLAGS -fPIC -D_GNU_SOURCE -DARCH_x86_64" \
    LBT_LDFLAGS="-shared $LDFLAGS"
}

package() {
  cd $pkgname/src
  make F2C_AUTODETECTION=0 COMPLEX_RETSTYLE_AUTODETECTION=0 prefix="$pkgdir"/usr install
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
