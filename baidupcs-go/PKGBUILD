# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=baidupcs-go
pkgver=3.9.2
pkgrel=1
pkgdesc="Terminal utility for Baidu Network Disk"
arch=('loong64' 'x86_64')
url="https://github.com/qjfoidnh/BaiduPCS-Go"
license=('Apache')
options=('!lto')
depends=('glibc')
makedepends=('go' 'git')
conflicts=("baidupcs")
provides=("baidupcs")
source=("https://github.com/qjfoidnh/BaiduPCS-Go/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0b2ff6f35ed25acc670a5ab8e6f889929c7fd167633717d3f3c3af13d8aff54be0991d7560558be78d38dfe5868d107903da58a55c74156354e591c8cc3855e9')

build() {
  cd BaiduPCS-Go-$pkgver
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go mod edit -replace=golang.org/x/sys=github.com/golang/sys@v0.0.0-20220622161953-175b2fd9d664
  go mod tidy
  go build -o baidupcs-go
}

package() {
  cd BaiduPCS-Go-$pkgver
  install -Dm755 baidupcs-go "$pkgdir"/usr/bin/baidupcs
}
