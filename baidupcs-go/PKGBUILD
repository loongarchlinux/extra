# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=baidupcs-go
pkgver=3.9.5
pkgrel=1
pkgdesc="Terminal utility for Baidu Network Disk"
arch=('loong64' 'x86_64')
url="https://github.com/qjfoidnh/BaiduPCS-Go"
license=('Apache-2.0')
options=('!lto')
depends=('glibc')
makedepends=('go' 'git')
conflicts=("baidupcs")
provides=("baidupcs")
source=("https://github.com/qjfoidnh/BaiduPCS-Go/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c7bc9b4562eb7d6cdae40cdc4a799ada398c8209e06291fc52ff7a95747bc211feafdf469397703e106f83afc0b47075a5a361221e5e61049b3a23784f0f71f4')

build() {
  cd BaiduPCS-Go-$pkgver
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o baidupcs-go
}

package() {
  cd BaiduPCS-Go-$pkgver
  install -Dm755 baidupcs-go "$pkgdir"/usr/bin/baidupcs
}
