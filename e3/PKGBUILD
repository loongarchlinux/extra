# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=e3
pkgver=2.82
pkgrel=7
pkgdesc='Tiny text editor written in Assembly'
arch=(loong64 x86_64)
url='https://packages.debian.org/sid/e3'
license=(GPL-2.0-only)
makedepends=(setconf yasm)
options=(!strip)
source=("$pkgname-$pkgver.tar.gz::http://deb.debian.org/debian/pool/main/e/e3/${pkgname}_$pkgver+dfsg.orig.tar.gz")
b2sums=('22e78f7f1c11e003567719b111133de9f573523f26f5c8030e4c1a0212fb7652456956a54ecaa851ba2be6e26e8aae8f73b7777a66618979dd4809fc05d07aa1')

prepare() {
  cd "$pkgname-$pkgver+dfsg"
  setconf Makefile PREFIX /usr
}

build() {
  cd "$pkgname-$pkgver+dfsg"
  make clean
  make NASM_OR_YASM=yasm 64
}

package() {
  cd "$pkgname-$pkgver+dfsg"
  install -Dm755 e3 "$pkgdir/usr/bin/e3"
  for f in e3ws e3em e3pi e3vi e3ne; do
    ln -sf /usr/bin/e3 "$pkgdir/usr/bin/$f"
  done
  install -Dm644 e3.man "$pkgdir/usr/share/man/man1/e3.1"
}
