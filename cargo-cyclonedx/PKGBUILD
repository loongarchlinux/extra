# Maintainer: David Runge <dvzrv@archlinux.org>

_upstream_name=cyclonedx-rust-cargo
pkgname=cargo-cyclonedx
pkgver=0.5.5
pkgrel=1
pkgdesc="Creates CycloneDX Software Bill of Materials (SBOM) from Rust (Cargo) projects"
arch=(loong64 x86_64)
url="https://github.com/CycloneDX/cyclonedx-rust-cargo"
license=(Apache-2.0)
depends=(
  cargo
  gcc-libs
  glibc
)
makedepends=(rust)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgname-$pkgver.tar.gz
)
sha512sums=('e7aa93be8044712d4bcf2d68b2595012b228555cc3cdd0441087ccf4969a43cab15991899169f5ead2ce9e22f9e6414967a59e7edf4890460ec8cb38a9561a61')
b2sums=('0e15f62051aabc383929e3b3522068db3764aba45ce8e7cbd63fe0eade3046a4de218453dacf262f9329468151d9b8d9711ac2234435ea8d81b06de6c144fd70')

prepare() {
  cd $_upstream_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_upstream_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $_upstream_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  # NOTE: use different target dir to have reproducible executable
  export CARGO_TARGET_DIR=target_test
  cargo test --frozen --all-features --workspace
}

package() {
  cd $_upstream_name-$pkgname-$pkgver
  install -vDm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
