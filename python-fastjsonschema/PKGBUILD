# Maintainer: David Runge <dvzrv@archlinux.org>

_name=fastjsonschema
pkgname=python-fastjsonschema
pkgver=2.19.1
pkgrel=3
pkgdesc="Fast JSON schema validator for Python"
arch=(any)
url="https://github.com/horejsek/python-fastjsonschema"
license=(BSD-3-Clause)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('5121ccd0585ea8a78f44ceca95f3f2b5eb769ac3529998a3e54da9535bec96f7e47f1240b0eba63f6fef2dec91fa86fc6317d9da6eb54fed29076759897a42aa')
b2sums=('47486a172ee8153a78b7c05034dd2f4539f905d639594d42e6b6888a199bcc7d1b0cfc1c5b0a96e6e8d375b74c5fecad9b883f9b94fe992edd3727d63b3d24cf')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    -m "not benchmark"
    # broken with pytest 8
    --deselect tests/test_pattern_properties.py::test_pattern_with_escape_no_warnings
    --deselect tests/test_string.py::test_pattern_with_escape_no_warnings
  )

  cd $pkgname-$pkgver
  pytest "${pytest_options[@]}"
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
