# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Piotr Miller <nwg.piotr@gmail.com>

pkgname=nwg-icon-picker
pkgver=0.1.1
pkgrel=3
pkgdesc="GTK icon chooser with a text search option"
arch=('any')
url="https://github.com/nwg-piotr/nwg-icon-picker"
license=('MIT')
depends=('python' 'python-gobject' 'gtk3' 'glib2')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v"$pkgver".tar.gz")
sha256sums=('264df7a4fa3ab8e9bfeecb2505b5ef4c58d3b5f9d1e7d8e1255f95f4bc869ffc')

build() {
        cd "${pkgname}-${pkgver}"
        python -m build --wheel --no-isolation
}

package() {
        cd "${pkgname}-${pkgver}"
        python -m installer --destdir="${pkgdir}" dist/*.whl

	install -Dm 644 "${pkgname}.svg" "${pkgdir}/usr/share/pixmaps/${pkgname}.svg"
	install -Dm 644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
