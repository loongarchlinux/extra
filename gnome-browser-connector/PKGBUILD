# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Andrew Querol <andrew@querol.me>
# Contributor: Rafael Fontenelle <rafaelff@gnome.org>

pkgname=gnome-browser-connector
pkgver=42.1
pkgrel=4
pkgdesc='Native browser connector for integration with extensions.gnome.org'
arch=('any')
url='https://wiki.gnome.org/Projects/GnomeShellIntegration'
license=('GPL3')
depends=('gnome-shell' 'gobject-introspection-runtime' 'hicolor-icon-theme' 'python' 'python-gobject')
makedepends=('meson')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('bd9702ce1c163606ca32b8c13d1f3ba6e82b247cf87aac60610b411df1556212')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
