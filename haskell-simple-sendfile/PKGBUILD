# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=simple-sendfile
pkgname=haskell-simple-sendfile
pkgver=0.2.32
pkgrel=20
pkgdesc="Cross platform library for the sendfile system call"
url="https://github.com/kazu-yamamoto/simple-sendfile"
license=("BSD")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-network')
makedepends=('ghc' 'haskell-hunit' 'haskell-conduit' 'haskell-conduit-extra' 'haskell-easy-file'
             'haskell-hspec' 'haskell-hspec-discover' 'haskell-resourcet')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('a6daf74f32d33096409c1650310b79005823b2513531b01b80e8a495af736299fd430db86faf5822464dec5a52ee7d733d49410f106f96a361eceb29b9df570f')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -fallow-bsd -f-fallback
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
