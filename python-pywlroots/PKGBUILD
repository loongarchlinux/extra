# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywlroots
pkgname=python-pywlroots
pkgver=0.16.8
pkgrel=1
pkgdesc="Python binding to the wlroots library using cffi"
arch=(loong64 x86_64)
url="https://github.com/flacjacket/pywlroots"
license=(NCSA)
depends=(
  glibc
  python
  python-cffi
  python-pywayland
  python-xkbcommon
  wlroots0.16
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
provides=(pywlroots)
conflicts=(pywlroots)
replaces=(pywlroots)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('36a6e296fba3abc0733b8ccd76f09f826ce9bd32b7c360f7b331d64f640589113b37921ebe593b4cfcdaac18c56dbcfa96e56b8fad042d03e09073f55f0421bc')
b2sums=('a3bb0342b111e1f642878ab3d39710d2e417c1423beec87e1fc840be0a3ca96b7193efdfc78b6a78cb4dc0106732b0ada434ed86918798844906cbbf9dd8974d')

build() {
  export CFLAGS="$CFLAGS -I/usr/include/wlroots0.16"
  export LDFLAGS="$LDFLAGS -L/usr/lib/wlroots0.16"
  cd $_name-$pkgver
  python wlroots/ffi_build.py
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
