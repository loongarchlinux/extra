# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libinsane
pkgver=1.0.10
pkgrel=2
pkgdesc='Library to access scanners'
arch=(loong64 x86_64)
url='https://gitlab.gnome.org/World/OpenPaperwork/libinsane'
license=(LGPL-3.0-or-later)
depends=(
  glib2
  glibc
  sane
)
makedepends=(
  doxygen
  git
  glib2-devel
  gobject-introspection
  meson
  vala
)
checkdepends=(
  cunit
  valgrind
)
source=("git+https://gitlab.gnome.org/World/OpenPaperwork/$pkgname.git#tag=$pkgver")
b2sums=(e79489c83ad4f9125dd0c94b31187c37420b884047aba334bd737ccd45f53d77cde882b87e3ddc8f67e311dc54c9120746746358ca59c68ab7e196c2f0e70230)

build() {
  arch-meson $pkgname build
  meson compile -C build
  meson compile -C build doc
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  cp -r build/doc/html "$pkgdir/usr/share/doc/$pkgname/html"
}
