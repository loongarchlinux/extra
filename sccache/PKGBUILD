# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Chocobo1 <chocobo1@archlinux.net>
# Contributor: Jean Lucas <jean@4ray.co>

pkgname=sccache
pkgver=0.7.5
pkgrel=1
pkgdesc='Shared compilation cache'
arch=(loong64 x86_64)
url=https://github.com/mozilla/sccache
license=(Apache)
depends=(
  gcc-libs
  glibc
  openssl
  zlib
  zstd
)
makedepends=(
  git
  rust
)
optdepends=(
  'memcached: Memcached support'
  'redis: Redis support'
)
_tag=11703e09ab88a65ca5daba89df50844ce41f25ba
source=(git+https://github.com/mozilla/sccache.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd sccache
  git describe --tags | sed 's/^v//'
}

prepare() {
  cargo fetch \
    --locked \
    --manifest-path sccache/Cargo.toml
}

build() {
  export CFLAGS+=' -ffat-lto-objects'
  export LDFLAGS+=' -lzstd'
  cargo build \
    --release \
    --frozen \
    --manifest-path sccache/Cargo.toml \
    --features all,dist-server,native-zlib
}

#check() {
#  cargo test \
#    --release \
#    --frozen \
#    --manifest-path sccache/Cargo.toml \
#    --features all,dist-server,native-zlib
#}

package() {
  export CFLAGS+=' -ffat-lto-objects'
  export LDFLAGS+=' -lzstd'
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path sccache \
    --root "${pkgdir}"/usr \
    --features all,dist-server,native-zlib
}

# vim: ts=2 sw=2 et:
