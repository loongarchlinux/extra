# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=tamarin-prover-theory
pkgname=haskell-tamarin-prover-theory
pkgver=1.8.0
pkgrel=21
pkgdesc="Security protocol types and constraint solver library for the tamarin prover"
url="http://www.infsec.ethz.ch/research/software/tamarin"
license=("GPL")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-attoparsec' 'haskell-dlist'
         'haskell-fclabels' 'haskell-parallel' 'haskell-regex-pcre' 'haskell-regex-posix'
         'haskell-safe' 'haskell-split' 'haskell-uniplate' 'haskell-tamarin-prover-utils'
         'haskell-tamarin-prover-term')
makedepends=('ghc' 'uusi')
source=("https://github.com/tamarin-prover/tamarin-prover/archive/$pkgver/tamarin-prover-$pkgver.tar.gz")
sha512sums=('62d8da7a46d521f85fa4544c676b5c7a6d0f26770eb4169fc3aef66e9b1df61753fc1e1fca9531cbe29d971807cc04f7af80c4c9f9c4bb2bae632c6e1fb32662')

prepare() {
    cd tamarin-prover-$pkgver/lib/theory
    uusi -r regex-pcre-builtin:regex-pcre $_hkgname.cabal

    sed -i '/import qualified Data.Binary/a import Data.Foldable (asum)' src/Theory/Constraint/Solver/Sources.hs
    sed -i '/import qualified Data.Set/a import Data.Foldable (asum)' src/Theory/Text/Parser/Token.hs
    sed -i '/import           Control.Category/a import Data.Foldable (asum)' src/Theory/Text/Parser/Formula.hs
    sed -i '/import qualified Data.Text.Encoding/a import Data.Foldable (asum)' src/Theory/Text/Parser/Rule.hs
    sed -i '/import           Data.Either/a import Data.Foldable (asum)' src/Theory/Text/Parser/Signature.hs
    sed -i '/import           Control.Category/a import Data.Foldable (asum)' src/Theory/Text/Parser/Proof.hs
    sed -i '/import Data.Functor/a import Data.Foldable (asum)' src/Theory/Text/Parser/Lemma.hs
    sed -i '/import           Data.Label/a import Data.Foldable (asum)' src/Theory/Text/Parser.hs
}

build() {
    cd tamarin-prover-$pkgver/lib/theory
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd tamarin-prover-$pkgver/lib/theory
    runhaskell Setup test --show-details=direct
}

package() {
    cd tamarin-prover-$pkgver/lib/theory
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
