# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Matthias Ellmer <ellmer.matthias@gmail.com>
# Contributor: Julien Morin <jmorin@cp.dias.ie>

pkgbase=python-basemap
pkgname=('python-basemap' 'python-basemap-common')
pkgver=1.4.1
pkgrel=4
arch=('loong64' 'x86_64')
url="https://matplotlib.org/basemap/"
license=('MIT')
makedepends=('cython' 'python-setuptools' 'python-matplotlib' 'geos' 'shapelib'
             'python-numpy' 'python-pyproj')
source=($pkgname-$pkgver.tar.gz::https://github.com/matplotlib/basemap/archive/v${pkgver}.tar.gz
        LICENSE)
sha256sums=('730b1e2ff5eb31c73680bd8ebabc6b11adfc587cfa6832c528a8a82822e5a490'
            '0deac85afb0740df7626b6d8d7aa4b8814bbe9a412fb1a0e5962895d17e63add')

prepare() {
  pushd "${srcdir}"/basemap-${pkgver}/packages/basemap
  sed -i "s/lib64/lib/g" setup.py
  popd
}

build() {
  cd "${srcdir}"/basemap-${pkgver}/packages/basemap
  python setup.py config
  python setup.py build
  cd "${srcdir}"/basemap-${pkgver}/packages/basemap_data
  python setup.py config
  python setup.py build
}

package_python-basemap() {
  pkgdesc="Toolkit for plotting data on map projections"
  depends=('python-basemap-common' 'python-matplotlib' 'geos' 'shapelib' 'python-numpy' 'python-pyproj')
  optdepends=('python-owslib: needed for the BaseMap.wmsimage function'
              'python-pillow: needed for Basemap warpimage, bluemarble, shadedrelief and etop methods')

  cd "${srcdir}"/basemap-${pkgver}/packages/basemap

  python setup.py install -O1 --skip-build --root="${pkgdir}" --prefix=/usr
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python-basemap-common() {
  pkgdesc="Data files for python-basemap"
  license=('GPL2')

  cd "${srcdir}"/basemap-${pkgver}/packages/basemap_data

  python setup.py install -O1 --skip-build --root="${pkgdir}" --prefix=/usr
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
