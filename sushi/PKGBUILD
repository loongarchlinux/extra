# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=sushi
pkgver=46.0
pkgrel=1
pkgdesc="A quick previewer for Nautilus"
url="https://gitlab.gnome.org/GNOME/sushi"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  evince
  freetype2
  gcc-libs
  gdk-pixbuf2
  gjs
  glib2
  glibc
  gst-plugin-gtk
  gst-plugins-base-libs
  gstreamer
  gtk3
  gtksourceview4
  harfbuzz
  libepoxy
  libsoup3
  pango
  webkit2gtk-4.1
)
makedepends=(
  git
  gobject-introspection
  meson
)
optdepends=('libreoffice: OpenDocument formats')
groups=(gnome)
source=("git+$url.git?signed#tag=$pkgver")
b2sums=('fdc05965a8e4b2de9e406ad12133cc2d54c439e48adb138f5f71b18f4ede706bc67f6e9e48eac3f600819ea49d099bb13102b0628be1aefa155da686884c7aa1')
validpgpkeys=(
  550660707A6F40376B9B9F8D504A78811E6160CC # Corey Berla <corey@berla.me>
)

prepare() {
  cd sushi
}

build() {
  arch-meson sushi build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
