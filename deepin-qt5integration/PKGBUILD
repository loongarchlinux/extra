# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=5.6.26
pkgrel=2
pkgdesc='Qt platform theme integration plugins for DDE'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'libqt5xdg' 'qt5-x11extras' 'qt5-base' 'libx11' 'glibc'
         'gcc-libs')
makedepends=('cmake' 'ninja' 'qt5-tools' 'xcb-util-renderutil' 'gtest')
groups=('deepin')
source=("https://github.com/linuxdeepin/qt5integration/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d0e7ee1fe0de7d22313530d540580ac0fa91e3afba3d3cf65811672ed1a6e08e0bf05ea7cd1c8ee254dd6867dcac7bc871bba35f66e287701761ad872c315441')

build() {
  cd qt5integration-$pkgver
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPLUGIN_INSTALL_BASE_DIR=lib/qt/plugins
  ninja
}

package() {
  cd qt5integration-$pkgver
  DESTDIR="$pkgdir" ninja install
}
