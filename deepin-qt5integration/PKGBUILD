# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=5.6.28
pkgrel=1
pkgdesc='Qt platform theme integration plugins for DDE'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'libqt5xdg' 'qt5-x11extras' 'qt5-base' 'libx11' 'glibc'
         'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'xcb-util-renderutil' 'gtest')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt5integration.git#tag=$pkgver")
sha512sums=('65751e731e0f94052e8c8f826404e9adf0f55d2b796180a374b6bc4c609fdf75abd3d723057e7893cfc5f8ae0467db1b76c863b7c031033c60f00dd319b1e567')

build() {
  cd qt5integration
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPLUGIN_INSTALL_BASE_DIR=lib/qt/plugins
  ninja
}

package() {
  cd qt5integration
  DESTDIR="$pkgdir" ninja install
}
