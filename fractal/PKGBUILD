# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=fractal
pkgver=7
pkgrel=1
pkgdesc="Matrix group messaging app"
arch=(loong64 x86_64)
url="https://gitlab.gnome.org/World/fractal"
license=(GPL-3.0-only)
depends=(
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gst-plugins-bad-libs
  gst-plugins-base-libs
  gstreamer
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libpipewire
  libshumate
  openssl
  pango
  sqlite
)
makedepends=(
  clang
  cmake
  meson
  rust
  xdg-desktop-portal
)
optdepends=('org.freedesktop.secrets: password storage')
source=(
  $url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz
)
sha512sums=('389d9c51068fbfe5b1f2ee3591a3fead4e94cf8a525d262ad89701c023d21ff8621d055ab07ffd785bcfd77df05234536e0d0848866da979f5a7a1cb875cdf4a')
b2sums=('f0a20c5062f97f6cbf952923204832e5e570a8ce0cc422830d7a8cbca4e2efc7816c17d7f6a86c2e4499b81f53b7bee16e22d402cf8e2f583c7dc2ea6cff0289')

prepare() {
  # NOTE: usptream uses a custom CARGO_HOME from within meson
  export CARGO_HOME="$(pwd)/build/cargo-home"
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  CFLAGS+=' -ffat-lto-objects'
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  # NOTE: explicitly prevent rebuild: https://gitlab.gnome.org/GNOME/fractal/-/issues/1327
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
