# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=fractal
pkgver=8
pkgrel=1
pkgdesc="Matrix group messaging app"
arch=(loong64 x86_64)
url="https://gitlab.gnome.org/World/fractal"
license=(GPL-3.0-only)
depends=(
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  graphene
  gst-plugins-bad-libs
  gst-plugins-base-libs
  gstreamer
  gtk4
  gtksourceview5
  hicolor-icon-theme
  libadwaita
  libpipewire
  libshumate
  openssl
  pango
  sqlite
)
makedepends=(
  clang
  cmake
  meson
  rust
  xdg-desktop-portal
)
optdepends=('org.freedesktop.secrets: password storage')
source=(
  $url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz
)
sha512sums=('6a77843ff0fd3171ed674de59d074df04de335310a0c4aff96da04860bf003c76b12fb424e75af69deb6bd0a4770867e4c9cdba9191c7646cfa183b06bde4faf')
b2sums=('d20f1be6d9f5be61f4eeff0dfe805f2878412dea7bda59cba048b00916d2d291d4bafcd6ca7d046e13662a9b669fd521983c6502fa36c3c83a896521eb9aa065')

prepare() {
  # NOTE: usptream uses a custom CARGO_HOME from within meson
  export CARGO_HOME="$(pwd)/build/cargo-home"
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  CFLAGS+=' -ffat-lto-objects'
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  # NOTE: explicitly prevent rebuild: https://gitlab.gnome.org/GNOME/fractal/-/issues/1327
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
