# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Pi-Yueh Chuang <pychuang@pm.me>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=python-imaplib2
pkgver=3.6
pkgrel=6
pkgdesc="Threaded Python IMAP4 client"
arch=('any')
url="https://github.com/jazzband/imaplib2"
license=('MIT')
depends=('python')
makedepends=('git' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel')
provides=("${pkgname}")
conflicts=("${pkgname}-git")
source=("${pkgname}::git+https://github.com/jazzband/imaplib2.git#tag=v3.6")
sha256sums=('SKIP')

build() {
    cd "${pkgname}"
    python -m build --wheel --no-isolation
}

package() {
    cd "${pkgname}"
    python -m installer --destdir="$pkgdir" dist/*.whl

    # install README
    install -Dm644 "README.md" -t "$pkgdir/usr/share/doc/$pkgname"

    # install license
    install -Dm644 "LICENCE" -t "$pkgdir/usr/share/licenses/$pkgname"
}
