# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: bekks <eduard.warkentin@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Wang Haoyu

pkgname=upx
pkgver=4.2.4
pkgrel=1
pkgdesc='Extendable, high-performance executable packer for several executable formats'
arch=(loong64 x86_64)
url='https://github.com/upx/upx'
license=(custom GPL2)
makedepends=(cmake git perl)
# https://github.com/upx/upx/tree/v4.2.4/vendor
# b='v4.2.4'; for x in doctest lzma-sdk ucl valgrind zlib; do echo "git+\$url-vendor-$x#commit=$(git ls-remote https://github.com/upx/upx-vendor-$x $b-upx-vendor | cut -b-40)"; done
source=(
  "git+$url-vendor-doctest#commit=6a55e3403db0b1e7830ade943884916f32013660"
  "git+$url-vendor-lzma-sdk#commit=c5e918f7bfbea264baaf737546d87d3123c96898"
  "git+$url-vendor-ucl#commit=2c68ae51994892b9080328617a84296ee55f0c80"
  "git+$url-vendor-valgrind#commit=c36826ec65eb9381716af1d02518d1f1f913ffd3"
  "git+$url-vendor-zlib#commit=8dc83b7c51d5014de10e0196eac9b4dd8096fc1c"
  "git+$url#commit=3757579ffc6fa8710b4b7a1055529fea9dcaf149") # tag: v4.2.4
b2sums=('3cc69f814f0b01184f3a61de67311de81757be1731a8ba76c840f65084513f985612e2c374adb41b56459c57d93c6bf44b646fe8ce5cf255ddc7b892a8e6630a'
        '3898ed1f3d4784e6defb465d3927b4106fdc50b429812801a434c81ed9a1266df6c679c7deee1cc1707b65dd2b1c57a4e3fe40ab3096c51e524af4a1c2d7d6ab'
        '1b278981d40e2afef3dcb4aa8ba60c48072190a6d8ec909c8ec322007f1049fa17da945b1a2ec0dfcd762f8c1bb8c3a68355c675f969f9137ba8121f1faee15c'
        'd58ea351ed6a17ef5b72e02c7d66a5f12deffcef98ed4e64aa1ee0d12e4dc73baada3679cd8297413557a76742ef031e6f00b468184434d962c55391495898d1'
        'b50bbc526d59d7dbed761707c760e8e98546372951875b15dd90dc15604c5775946208c33ef355c23df0e18e575085d8189e83cc076523ae7cfbe92f72be299d'
        'f7a64c8d52b315e8538209bf04fc4ce4f992041e55980e9ee0aadf4212ebecbcecd5136c28a44fa5d64e6e74b325ade9a2c6c32263cabe4285fd2b4c7754b5c2')

prepare() {
  cd $pkgname
  # Set up symbolic links for the vendor repos.
  for x in doctest lzma-sdk ucl valgrind zlib; do
    rm -frv vendor/$x && ln -s "$srcdir/upx-vendor-$x" vendor/$x
  done
}

build() {
  make -C $pkgname \
    CHECK_WHITESPACE=/bin/true \
    CXXFLAGS_WERROR="" \
    UPX_CMAKE_CONFIG_FLAGS='-D UPX_CONFIG_DISABLE_GITREV=1 -D UPX_CONFIG_DISABLE_SANITIZE=1 -D UPX_CONFIG_DISABLE_WERROR=1' \
    UPX_LZMA_VERSION=0x465 \
    UPX_LZMADIR="$srcdir"
}

check() {
  # Check that the version has the proper version number and not something like "git-877911+".
  $pkgname/build/release/upx --help | grep -q "^UPX $pkgver" || exit 1
  # Run tests
  make -C $pkgname/build/release test
}

package() {
  cd $pkgname
  install -Dm755 build/release/upx "$pkgdir/usr/bin/upx"
  install -Dm644 doc/upx.1 "$pkgdir/usr/share/man/man1/upx.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
