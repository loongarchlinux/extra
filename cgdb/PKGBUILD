# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Marek Kubica <marek@xivilization.net>
# Contributor: Javier ‘Phrodo_00’ Aravena <Phrodo.00@gmail.com>

pkgname=cgdb
pkgver=0.8.0
pkgrel=2
pkgdesc="Curses-based interface to the GNU Debugger"
arch=('loong64' 'x86_64')
url="http://cgdb.github.io/"
license=('GPL-2.0-or-later')
depends=('readline>=5.1' 'ncurses' 'gdb')
makedepends=('help2man')
source=(
  "$pkgver-$pkgname.tar.gz::https://github.com/cgdb/cgdb/archive/v${pkgver}.tar.gz"
  "cgdb-0_8_0-2-fix-readline-version-detection.patch"
)
sha512sums=('e2b653db472de2014402dae6d13953ab811526a47ad5d7a05e34cfba09f44d6fea6b19304b9205ff0e2ac9549ffa5818f4282d7c03293b6c0ca9d563f9ae89e1'
            '15c367fa32af5b728edd4f31d4856c67e243dddc3775bb5970e74f3e6c887e6e220f50ba5ae35a748b6550150b6eedf6608d8406b6b5939e3f22a0e0ff6c5ed1')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 --input="${srcdir}/cgdb-0_8_0-2-fix-readline-version-detection.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install
}
