# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=piep
pkgdesc='Bringing the power of python to stream editing'
pkgver=0.10.0
pkgrel=2
arch=("any")
url="https://github.com/gfxmonk/piep"
license=("GPL")
depends=('python-pygments')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("https://github.com/gfxmonk/$pkgname/archive/version-$pkgver.tar.gz")
sha512sums=('c81d89b1d8881a4bc577ca7c0d764f2c31d8579d0f7f6ba7ce68c98c929f5e215032f99c0ac0da432022bfdedb3d2b441ce16cebd59340a5b11ab92dfddbac1f')

build() {
  cd $pkgname-version-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-version-$pkgver
  python setup.py nosetests
}

package() {
  cd $pkgname-version-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
