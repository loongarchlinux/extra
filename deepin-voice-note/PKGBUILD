# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-voice-note
pkgver=6.0.17
pkgrel=1
pkgdesc='A lightweight memo tool to make text notes and voice recordings'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-voice-note"
license=('GPL3')
replaces=('deepin-voice-recorder')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base'
         'qt5-svg' 'qt5-multimedia' 'qt5-declarative' 'qt5-webchannel' 'qt5-location' 'qt5-webengine'
         'gstreamer' 'vlc' 'glib2' 'gcc-libs' 'deepin-movie')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-voice-note/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d89f5071c6aec513d4c94d9a7dbaca24a9611a2eaf5b9578d81cade5f1192c2f29b13dcf7674346d5188b6d78aeb0278cb83d897efbd7de8e58f2449311f3f45')

prepare() {
  cd deepin-voice-note-$pkgver
  sed -i '/include <QSurfaceFormat>/a #include <QStandardPaths>' src/main.cpp
  sed -i '/include <QMimeData>/a #include <QStandardPaths>\n#include <QThreadPool>' src/views/middleview.cpp
}

build() {
  cd deepin-voice-note-$pkgver
  cmake -GNinja .
  ninja
}

check() {
  cd deepin-voice-note-$pkgver
  ninja test || :
}

package() {
  cd deepin-voice-note-$pkgver
  DESTDIR="$pkgdir" ninja install
}
