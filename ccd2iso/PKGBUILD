# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ccd2iso
pkgver=0.3
pkgrel=12
pkgdesc="Converts CCD/IMG CloneCD images to ISO format"
arch=('loong64' 'x86_64')
url="https://sourceforge.net/projects/ccd2iso"
license=('GPL')
depends=('glibc')
source=(https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
	"ccd2iso-0.3-compilerWarnings.patch")
md5sums=('a7df1c46cb710f2fc8ebd88c2e64ce65'
         'be135089c0b34b04ed7db2ef39fd56a4')
sha256sums=('f874b8fe26112db2cdb016d54a9f69cf286387fbd0c8a55882225f78e20700fc'
            '25f64fccf562797096dbf0b306b0d33fffafcc1b67bce693453704f00465f370')

prepare() {
	cd "${srcdir}"/${pkgname}-${pkgver}/src
	patch -Np1 -i "${srcdir}/ccd2iso-0.3-compilerWarnings.patch"
}

build() {
	cd "${srcdir}"/${pkgname}-${pkgver}

	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}"/${pkgname}-${pkgver}

	make DESTDIR="${pkgdir}" install
}
