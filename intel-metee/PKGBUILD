# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgbase=intel-metee
pkgname=(intel-metee intel-metee-doc)
pkgver=4.0.0
pkgrel=1
pkgdesc="Access library for Intel CSME HECI interface"
arch=(loong64 x86_64)
url="https://github.com/intel/metee/"
license=(Apache-2.0)
makedepends=(cmake doxygen graphviz)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('173cd10bc8bbdd85b360c5bbdb1fd8b3df23b35ba1c23c33e061a95b5f452d87')

build() {
  cmake -B build -S metee-${pkgver} \
    -G 'Unix Makefiles' \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DOCS=ON \
    -DBUILD_SHARED_LIBS=ON \
    -Wno-dev
  cmake --build build
}

package_intel-metee() {
  depends=(glibc)
  
  DESTDIR="${pkgdir}" cmake --install build
  mv "${pkgdir}"/usr/share/doc .
}

package_intel-metee-doc() {
  pkgdesc+=' (documentation)'
    
  install -dm755 "${pkgdir}"/usr/share
  mv doc "${pkgdir}"/usr/share
}
