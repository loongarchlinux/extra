# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=merkuro
pkgver=24.05.2
pkgrel=1
pkgdesc='A calendar application using Akonadi to sync with external services'
arch=(loong64 x86_64)
url='https://apps.kde.org/merkuro.calendar/'
license=(GPL-2.0-or-later)
depends=(akonadi-calendar
         akonadi-contacts
         akonadi-mime
         gcc-libs
         glibc
         gpgme
         kcalendarcore
         kcodecs
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kdbusaddons
         kdeclarative
         kdepim-runtime
         ki18n
         kidentitymanagement
         kio
         kirigami
         kirigami-addons
         kitemmodels
         kmailtransport
         kmbox
         kmime
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         libakonadi
         libkleo
         libplasma
         mailcommon
         messagelib
         mimetreeparser
         prison
         qqc2-desktop-style 
         qt6-5compat
         qt6-base
         qt6-declarative
         qt6-location
         qt6-positioning)
makedepends=(extra-cmake-modules)
conflicts=(kalendar)
replaces=(kalendar)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('d0d174fc0a79466c87ba1fd0a33c2230e89e781c7303af8ce9b493670052d658'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
