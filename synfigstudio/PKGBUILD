# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfigstudio
pkgver=1.4.5
pkgrel=1
pkgdesc='Professional vector animation program (GUI)'
arch=('loong64' 'x86_64')
url='https://www.synfig.org/'
license=('GPL3')
depends=('gtkmm3' 'synfig')
makedepends=('etl' 'intltool')
optdepends=('python: for the plugins')
source=("https://downloads.sourceforge.net/synfig/$pkgname-$pkgver.tar.gz")
sha256sums=('82a2d8a6d03f72443f8ddeb9d7ea38214313c1ac56f4ddba3e872464e8f91568')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|appdatadir = $(datadir)/appdata|appdatadir = $(datadir)/metainfo|' Makefile.{am,in}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-update-mimedb
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
