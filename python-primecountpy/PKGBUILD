# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=primecountpy
pkgname=python-$_pyname
pkgver=0.1.0
pkgrel=4
pkgdesc='Cython interface to the C++ library primecount'
arch=(loong64 x86_64)
url='https://github.com/dimpase/primecountpy'
license=(GPL3)
depends=(gcc-libs
         glibc
         primecount
         python
         python-cysignals)
makedepends=(cython
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(https://github.com/dimpase/primecountpy/archive/v$pkgver/$_pyname-$pkgver.tar.gz)
sha256sums=('42835b1ddfc2e95c2a48118c3a871d650146aae92a63dd4564dd1be171d09c15')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_pyname-$pkgver 
  python -m installer --destdir="$pkgdir" dist/*.whl
}
