# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-music
pkgver=46.1
pkgrel=1
epoch=1
pkgdesc="Music player and management application"
url="https://apps.gnome.org/Music/"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  graphene
  grilo
  grilo-plugins
  gst-plugins-base
  gst-plugins-base-libs
  gst-plugins-good
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita
  libgirepository
  libmediaart
  libsoup3
  python
  python-cairo
  python-gobject
  tracker3
  tracker3-miners
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  yelp-tools
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-music.git#tag=${pkgver/[a-z]/.&}")
b2sums=('1cb6110e135dd1a07f7ae1cacbeb212c1139108d2d7d8a63d51affc0df5ab0167b070ab417190f24a81fc535df47f1fc2525c63d99df7de648d24cfd6847a0b2')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
