# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-music
pkgver=46.0
pkgrel=2
epoch=1
pkgdesc="Music player and management application"
url="https://wiki.gnome.org/Apps/Music"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  grilo
  grilo-plugins
  gst-plugins-base
  gst-plugins-good
  gtk4
  libadwaita
  libmediaart
  python-cairo
  python-gobject
  tracker3-miners
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  yelp-tools
)
optdepends=(
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
)
groups=(gnome)
_commit=1748d79846bfde7b9234f14da99c98eaf21e1e69  # tags/46.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-music.git#commit=$_commit")
b2sums=('d94428118d36a79408af0eed873131e738b2a7923c0106c6e7a1c7e9703bd0f5af0fc2f105c1df91c4c8f3ea8c17ec0046286d8af29ff20a4d9f59763cdbf9bf')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}

# vim:set sw=2 sts=-1 et:
