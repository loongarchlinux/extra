# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=passt
pkgver=2024_04_26.d03c4e2
pkgrel=1
pkgdesc="Plug A Simple Socket Transport"
arch=(loong64 x86_64)
url="https://passt.top/passt/about/"
license=(
  BSD-3-Clause
  GPL-2.0-or-later
)
depends=(glibc)
optdepends=(
  'sh: for demo script'
)
source=(https://passt.top/$pkgname/snapshot/$pkgname-$pkgver.tar.zst)
sha512sums=('4d96c4a331636009532e792bd89b1c976d08d84162ae691507b088f7211f90702009690aede3b404827b55b44d91d51af1ec82901a3fff940afc58b1d9b223d6')
b2sums=('6d954c276169cf73a7b2b7ea047005bdace445e3f900fd309ab47e68d69dc00a1867279bb97da118d3fb99a6be6c5a27434ef6053ef031378cb2c395f8af37f3')

build() {
  make VERSION="$pkgver" -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" prefix=/usr install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/LICENSES/* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
