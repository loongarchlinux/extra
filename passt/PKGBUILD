# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=passt
pkgver=2024_08_06.ee36266
pkgrel=1
pkgdesc="Plug A Simple Socket Transport"
arch=(loong64 x86_64)
url="https://passt.top/passt/about/"
license=(
  BSD-3-Clause
  GPL-2.0-or-later
)
depends=(glibc)
optdepends=(
  'sh: for demo script'
)
source=(https://passt.top/$pkgname/snapshot/$pkgname-$pkgver.tar.zst)
sha512sums=('98a7441199bfe951a52f87e8f217240056039e5ccd271123641758ec8bbe2c986347bf3df7cb6a1793f5a1a7a7990f8b38059ad2b6cdae51674022a1f49e9a43')
b2sums=('e1f942b91539591b8e15f2bbb61e19a0c31ddcad2ca682b3b1fe2caa5fe5a07e2000c135b68c5bbc3a37f446e4e5cdd10fb62412051776b32bbd25232952feba')

build() {
  make VERSION="$pkgver" -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir/" prefix=/usr install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/LICENSES/* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
