# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=pipe-rename
pkgver=1.6.5
pkgrel=2
pkgdesc='Rename your files using your favorite text editor'
arch=('loong64' 'x86_64')
url='https://github.com/marcusbuffett/pipe-rename'
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('cargo' 'git')
options=('!lto')
source=("git+$url#tag=$pkgver"
        'LICENSE')
sha256sums=('7f19f03aae18641ca8b52528eba6779ef11dc59b0948d5c6386d59b9a4417128'
            '9d548c1d09d68ad9e6275dfa9574e4c09b7d5c29d8436d883692c1ddd5155772')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/renamer

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/LICENSE"
}
