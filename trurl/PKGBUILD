# Maintainer: Christian Heusel <gromit@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Michael Lass <bevan@bi-co.net>

pkgname=trurl
pkgver=0.14
pkgrel=1
pkgdesc="Command line tool for URL parsing and manipulation"
arch=(loong64 x86_64)
url="https://github.com/curl/trurl"
license=(custom)
depends=(curl glibc)
checkdepends=(python valgrind)
source=("https://github.com/curl/$pkgname/archive/refs/tags/$pkgname-$pkgver.tar.gz")
sha256sums=('5080ce71984fc620c1d12010c70c22e8020aeeba7009afcdfce7a9ea40caa4d2')

build() {
	cd "$pkgname-$pkgname-$pkgver"
	make
}

check() {
	cd "$pkgname-$pkgname-$pkgver"
	make test
	make test-memory
}

package() {
	cd "$pkgname-$pkgname-$pkgver"

	make PREFIX=/usr DESTDIR="$pkgdir/" install
	install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
