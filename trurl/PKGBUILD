# Maintainer: Christian Heusel <gromit@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Michael Lass <bevan@bi-co.net>

pkgname=trurl
pkgver=0.12
pkgrel=1
pkgdesc="Command line tool for URL parsing and manipulation"
arch=(loong64 x86_64)
url="https://github.com/curl/trurl"
license=(custom)
depends=(curl glibc)
checkdepends=(python valgrind)
source=("https://github.com/curl/$pkgname/archive/refs/tags/$pkgname-$pkgver.tar.gz")
sha256sums=('67a1620ebb0392a9cdd8e46bc44a14e0a5d8c1a9112859fbbb525ec919d2fa9b')

build() {
	cd "$pkgname-$pkgname-$pkgver"
	make
}

check() {
	cd "$pkgname-$pkgname-$pkgver"
	make test
	make test-memory
}

package() {
	cd "$pkgname-$pkgname-$pkgver"

	make PREFIX=/usr DESTDIR="$pkgdir/" install
	install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
