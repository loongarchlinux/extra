# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=firefox-tridactyl
pkgver=1.24.1
pkgrel=1
pkgdesc="Replace Firefox's control mechanism with one modelled on Vim"
arch=('any')
url=https://github.com/tridactyl/tridactyl
license=('Apache')
groups=('firefox-addons')
makedepends=('git' 'node-gyp' 'python' 'strip-nondeterminism' 'yarn')
source=("git+$url.git#commit=$pkgver?signed")
b2sums=('183d6cb70e5bd05b68fdf7c15380852712e2fbfe54909964b0b8e47f82fc0e3c25ce98a176b71b74823f1718275978d622f4426bebc43567d1e9b8ec73eb781b')
validpgpkeys=('15D03C86837936A0A638AA2D2BB8C36BB504BFF3') # Oliver Blanthorn <freedom4cows@gmail.com>

prepare() {
  cd tridactyl
  yarn install --frozen-lockfile --non-interactive
}

build() {
  cd tridactyl
  scripts/sign nosignstable
  strip-nondeterminism -t zip web-ext-artifacts/tridactyl-$pkgver.xpi
}

package() {
  cd tridactyl
  install -Dm644 web-ext-artifacts/tridactyl-$pkgver.xpi \
    "$pkgdir"/usr/lib/firefox/browser/extensions/tridactyl.vim@cmcaine.co.uk.xpi
}
