# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: ObserverOfTime <chronobserver@disroot.org>

pkgname=tree-sitter-vimdoc
pkgver=3.0.0
pkgrel=1
pkgdesc='Vim help file grammar for tree-sitter'
arch=(loong64 x86_64)
url=https://github.com/neovim/tree-sitter-vimdoc
license=(Apache-2.0)
groups=(tree-sitter-grammars)
makedepends=(
  git
  tree-sitter-cli
)
optdepends=('tree-sitter: core library')
provides=("lib$pkgname.so")
source=("git+$url.git#tag=v$pkgver")
b2sums=('1ac4f94ca15e67b204f3d866d6ed1df8464c5fec29796b67d4bdab6a7be60651004e7970c3112a27206624291129d2a99be23f43d42580586b5abf1f6131abab')

build() {
  cd $pkgname
  make PREFIX=/usr PARSER_URL="$url"
}

package() {
  install -d "$pkgdir"/usr/lib/tree_sitter
  ln -s /usr/lib/lib$pkgname.so \
    "$pkgdir"/usr/lib/tree_sitter/${pkgname#tree-sitter-}.so

  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
