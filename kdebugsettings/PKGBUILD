# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdebugsettings
pkgver=23.08.3
pkgrel=1
pkgdesc='An application to enable/disable qCDebug'
arch=(loong64 x86_64)
url='https://apps.kde.org/kdebugsettings/'
license=(GPL)
depends=(kdbusaddons5 ki18n5 kitemviews5 kcompletion5 kcoreaddons5 kxmlgui5)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('71cd3c4a9fa437106bc0ba879918b57e043eb79bf4820e53da019497df5fa4ba'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

