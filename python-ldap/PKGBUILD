# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Maikel Wever <maikelwever@gmail.com>

pkgname=python-ldap
pkgver=3.4.4
pkgrel=2
pkgdesc="LDAP client API for Python"
url="https://python-ldap.readthedocs.io/"
arch=('loong64' 'x86_64')
license=('MIT')
depends=('python-pyasn1' 'python-pyasn1-modules' 'libldap')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('openldap' 'python-pyasn1' 'python-pyasn1-modules' 'python-pytest')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/python-ldap/python-ldap/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('98c03c14724636351964606a307bf946fa6248630c2d6b89938a6911b6b84c99')

prepare() {
  cd "$pkgbase-$pkgbase-$pkgver"

  # slapd fails to start because -F points to a non-empty directory.
  # Simply don't pass both -f and -F to slapd, it is only done when
  # converting slapd.conf to LDIF and pointless here.
  sed -i "/'-F', self.testrundir,/d" Lib/slapdtest/_slapdtest.py
}

build(){
  cd "$pkgbase-$pkgbase-$pkgver"
  python -m build --wheel --no-isolation
}

check(){
  cd "$pkgbase-$pkgbase-$pkgver"
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')

  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-${python_version}" pytest . 
}

package() {
  cd "$pkgbase-$pkgbase-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
