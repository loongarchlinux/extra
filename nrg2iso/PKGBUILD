# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Pointer <pointer@linux-blog.de>
# Contributor Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=nrg2iso
pkgver=0.4.1
pkgrel=3
pkgdesc="Utility for converting CD or DVD image generated by Nero Burning Rom to ISO format"
arch=('loong64' 'x86_64')
url="http://gregory.kokanosky.free.fr/v4/linux/nrg2iso.en.html"
license=('GPL3')
depends=('glibc')
source=(http://gregory.kokanosky.free.fr/v4/linux/${pkgname}-${pkgver}.tar.gz)
sha256sums=('3be36a416758fc1910473b49a8dadf2a2aa3d51f1976197336bc174bc1e306e5')

build() {
	cd ${pkgname}-${pkgver}

	sed -i -e 's|gcc|gcc ${CFLAGS}|g' Makefile
	make
}

package() {
	cd ${pkgname}-${pkgver}

	install -D nrg2iso "${pkgdir}"/usr/bin/nrg2iso
}

