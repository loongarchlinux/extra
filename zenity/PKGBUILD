# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=zenity
pkgver=4.0.2
pkgrel=1
pkgdesc="Display graphical dialog boxes from shell scripts"
url="https://gitlab.gnome.org/GNOME/zenity"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
)
makedepends=(
  git
  glib2-devel
  help2man
  meson
  util-linux
  yelp-tools
)
source=(
  # unknown public key E520923A38618D6C (Logan Rathbone <poprocks@gmail.com>)
  "git+$url.git#tag=$pkgver"
)
b2sums=('5d99c70fec39102d93a33832615a2b58107792ad2478b20d5e75642b3c9baff6f80df8c4e8e75ecf749513758c6208cf586b4b0251e1caf446b8b1ea6452600f')

prepare() {
  cd zenity
}

build() {
  arch-meson zenity build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  hardlink -c "$pkgdir/usr/share/help"
}

# vim:set sw=2 sts=-1 et:
