# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-photos
pkgver=8.0.0
pkgrel=1
pkgdesc='The Pantheon Photos Manager'
arch=(loong64 x86_64)
url=https://github.com/elementary/photos
license=(GPL-3.0-only)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  geocode-glib
  glib2
  glibc
  gst-plugins-base-libs
  gstreamer
  gtk3
  hicolor-icon-theme
  libexif
  libgee
  libgexiv2
  libgphoto2
  libgranite.so
  libgudev-1.0.so
  libhandy-1.so
  libportal
  libportal-gtk3
  libraw
  libwebp
  pango
  sqlite
)
makedepends=(
  git
  intltool
  meson
  vala
  webkit2gtk
)
optdepends=('webkit2gtk: Publishing plugins')
groups=(pantheon)
_tag=aaa83606295dded21b9eb17a569f20a61c53cad1
source=(pantheon-photos::git+https://github.com/elementary/photos.git#tag=${_tag})
sha256sums=('c2a85272def00190715d509c30e81095736b055644c78536d7a1a3c5b9ff7f96')

prepare() {
  cd pantheon-photos
}

pkgver() {
  cd pantheon-photos
  git describe --tags
}

build() {
  arch-meson pantheon-photos build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
