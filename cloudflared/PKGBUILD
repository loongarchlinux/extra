# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=cloudflared
pkgver=2024.4.0
pkgrel=1
pkgdesc='Command-line client for Cloudflare Tunnel'
arch=('loong64' 'x86_64')
url='https://github.com/cloudflare/cloudflared'
license=('Apache-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+$url.git#tag=$pkgver")
sha512sums=('9803502c911453fe2b83b5be3d5f7f04eb269c7616d809e2f0ef9820556c3d3f759c052023b5314437425559933491140cc24b284d9e02f287391b4310ff8899')
b2sums=('895277eadcebf02e2a7bad9744de57a409d0a60bb8f54976edd5a15fa9c9a3bb149896dcb3d545f9b16d398009edd875222ba8df64d2f4f252b999ec88a61a6a')

pkgver() {
  cd "$pkgname"

  git describe --tags
}

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build
}

build() {
  cd "$pkgname"

  # define buildtime for use in man page & binary
  local build_time="$(date -d@"$SOURCE_DATE_EPOCH" +%Y%m%d-%H:%M:%S)"

  # generate man page
  sed -e "s/\${VERSION}/${pkgver}/" \
      -e "s/\${DATE}/${build_time}/" \
    cloudflared_man_template > "build/$pkgname.1"

  # verify go modules
  go mod verify

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # build cloudflared
  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
      -X main.Version=${pkgver} \
      -X main.BuildTime=${build_time} \
      -X github.com/cloudflare/cloudflared/cmd/cloudflared/updater.BuiltForPackageManager=pacman" \
    -o build \
    ./cmd/...
}

check() {
  cd "$pkgname"

  go test -v -mod=vendor -race ./...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "build/$pkgname"

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" "build/$pkgname.1"
}

# vim:set ts=2 sw=2 et:
