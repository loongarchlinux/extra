# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=vector-sized
pkgname=haskell-vector-sized
pkgver=1.6.1
pkgrel=8
pkgdesc="Size tagged vectors"
url="https://github.com/expipiplus1/vector-sized#readme"
license=("BSD")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-adjunctions' 'haskell-comonad' 'haskell-distributive'
         'haskell-finite-typelits' 'haskell-hashable' 'haskell-indexed-list-literals'
         'haskell-indexed-traversable' 'haskell-primitive' 'haskell-vector')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('d7bee38a6857e84b9d34194536d7a2937d518d6c58640ca06ae614ca32c94cf59c18e04de4ab557dfc3f830b7e36a72f120a41c7982b22be3f0e502554e16dca')

prepare() {
  cd $_hkgname-$pkgver
  uusi -u vector $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
