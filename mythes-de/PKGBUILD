# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=mythes-de
_date=20240703
pkgver=${_date}
pkgrel=1
pkgdesc="German thesaurus"
arch=(any)
url="https://www.openthesaurus.de/"
license=('LGPL-2.1-or-later OR CC-BY-SA-4.0')
optdepends=('libmythes: offers thesaurus library functions')
source=(# this one changes every day - use it only to get the checksums 
        # and build it once offload with that source -then switch 
        # to the AL source and build again
        #Deutscher-Thesaurus-${_date}.oxt::http://www.openthesaurus.de/export/Deutscher-Thesaurus.oxt
        https://sources.archlinux.org/other/$pkgname/Deutscher-Thesaurus-${_date}.oxt
        # alternative source with most times older snapshot
        #http://extensions.libreoffice.org/extension-center/german-de-de-frami-dictionaries/releases/2011.06.03/dict-de_de-frami_2011-06-03.oxt
)
sha256sums=('b2b1cd8d44552b830b6943f079f7a55b2db2e46472bc661ed0e60d72227a3cad')

# source PKGBUILD && mksource
mksource() {
	mkdir /tmp/$pkgname-${_date}
	pushd /tmp/$pkgname-${_date}
	wget http://www.openthesaurus.de/export/Deutscher-Thesaurus.oxt
	mv Deutscher-Thesaurus.oxt Deutscher-Thesaurus-${_date}.oxt
	popd
}

package() {
  cd "$srcdir"
  install -dm755 "${pkgdir}"/usr/share/mythes
  cp -p th_de_DE_v2.* "$pkgdir"/usr/share/mythes

  pushd $pkgdir/usr/share/mythes/
  de_DE_aliases="de_AT de_BE de_CH de_LI de_LU"
  for lang in $de_DE_aliases; do
        ln -s th_de_DE_v2.idx "th_"$lang"_v2.idx"
        ln -s th_de_DE_v2.dat "th_"$lang"_v2.dat"
  done
  popd

  # the symlinks
  install -dm755 "${pkgdir}"/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
    for file in "$pkgdir"/usr/share/mythes/*; do
      ln -sv /usr/share/mythes/$(basename $file) .
    done
  popd

  install -Dm644 README.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
