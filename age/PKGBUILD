# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Tharre <tharre3@gmail.com>

pkgname=age
pkgver=1.2.0
pkgrel=1
pkgdesc='A simple, modern and secure file encryption tool'
arch=('loong64' 'x86_64')
url="https://github.com/FiloSottile/$pkgname"
license=('BSD-3-Clause')
depends=('glibc')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('fddc7e6a68115d52f4b98957ef87a82cbf634ba38a028fb113370392d88af3e8b32991baabc1595280ecbd8e44bbc88dcea93e7336e7ffa2cbccfea0c953842f')

build() {
    cd "$pkgname-$pkgver"
    go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-X main.Version=$pkgver -linkmode external -extldflags \"$LDFLAGS\"" \
      -o . ./...
}

check() {
    cd "$pkgname-$pkgver"
    go test ./...
}

package() {
    cd "$pkgname-$pkgver"
    install -Dm755 -t "$pkgdir/usr/bin/" age age-keygen
    install -Dm644 -t "$pkgdir/usr/share/man/man1/" doc/age.1 doc/age-keygen.1
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
