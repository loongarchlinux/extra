# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=miller
pkgver=6.12.0
pkgrel=1
pkgdesc='Name-indexed data processing tool'
arch=('loong64' 'x86_64')
url='https://github.com/johnkerl/miller'
license=('BSD-2-Clause')
depends=('glibc')
makedepends=('go' 'git')
options=('!lto')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('1c01595c8f2ab65814cc5b4b7616d4a014756340e5fa9a86dbbeaaf674b442e0eed72aa5881b98531b88b9986653dc3692e704c2dcabe35d2c50fd203375f732')
b2sums=('fa6a509d4a92584484fecb97c214c4fbba6154ff618a4367568e20a61f72e35cec1c393416c67ed6fecf411cd7b7d193e3e29e6ff93d1f954843974d1691f6b7')

prepare() {
  cd "$pkgname"

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    ./cmd/mlr
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" mlr

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/mlr.1
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
