# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Corey Mwamba <contact.me@coreymwamba.co.uk>

pkgname=rtirq
pkgver=20240220
pkgrel=1
pkgdesc="Realtime IRQ thread system tuning."
arch=(any)
url="https://www.rncbc.org/archive/#rtirq"
license=(GPL-2.0-or-later)
groups=(realtime)
depends=(bash)
backup=(etc/rtirq.conf)
source=(https://www.rncbc.org/archive/$pkgname-$pkgver.tar.gz{,.sig})
sha512sums=('a6b44f09e158e71bc5697b313d2c7b75097b6f669be4bd7c5c3cee2e59ab38c5751dbd222a28fe7e1bd340d2b7ce5dc7277559c41ad25f9d7b19f180ce337978'
            'SKIP')
b2sums=('08af8d371e4ace57dc18276748b00328e7ca1476ae46226ea85ba0c345c7f15b81e412bed8ac30768c5bedffc87887f60d647cd0ec2e82d3751654030c7937f1'
        'SKIP')
validpgpkeys=('0E89694D1D4F7883B98DEFA19D0514152A8E5EC4') # Rui Nuno Capela <rncbc@rncbc.org>

package() {
  cd $pkgname-$pkgver
  install -vDm 755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  install -vDm 644 $pkgname.conf -t "$pkgdir/etc/"
  install -vDm 644 ./*.service -t "$pkgdir/usr/lib/systemd/system/"
}
