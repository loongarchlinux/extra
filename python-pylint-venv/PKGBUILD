# Maintainer:
# Contributor: Stijn Seghers <stijnseghers at gmail dot com>

pkgname=python-pylint-venv
_pyname=pylint-venv
pkgver=3.0.3
pkgrel=2
pkgdesc='Make pylint respect virtualenvs'
arch=(any)
url="https://github.com/jgosmann/$_pyname"
license=(MIT)
depends=(python-pylint python-virtualenv)
makedepends=(python-build python-installer python-wheel python-poetry-core)
source=(https://github.com/jgosmann/$_pyname/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('4c85eee2830e12432dbb7570b0cefc046552283bf8a8658d86e7a505a8a51e88')

build() {
    cd $_pyname-$pkgver
    python -m build --wheel --no-isolation
}

package() {
    cd $_pyname-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
