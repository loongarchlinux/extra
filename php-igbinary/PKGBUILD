# Maintainer: David Runge <dvzrv@archlinux.org>

_name=igbinary
pkgbase=php-igbinary
pkgname=(php-igbinary php-legacy-igbinary)
pkgver=3.2.15
pkgrel=4
pkgdesc="A drop in replacement for the standard php serializer"
arch=(loong64 x86_64)
url="https://github.com/igbinary/igbinary"
license=(BSD-3-Clause)
depends=(glibc)
makedepends=(php-legacy php)
source=($pkgname-$pkgver.tar.gz::https://github.com/$_name/$_name/archive/$pkgver.tar.gz)
sha512sums=('285adae14c3104c4c53eec7b7e9b96e097096a06e2517ac554a4ecf5066f9b75318b6e29486cc41aa89a21ef46980285d1fa0d09a1f2b750253951c6d0480869')
b2sums=('affe47386e65576ee44fc7616a3c1c923a5af4b50308caa02f6c75ea9a2ba483ab90a9c656f5631fb5bee7360a1505525dfadb4083e668b58d149dfc7379bd4e')

prepare() {
  mv -v $_name-$pkgver $pkgbase-$pkgver
  # disable the extension by default
  sed 's/extension/;extension/g' -i $pkgbase-$pkgver/$_name.php.ini

  cp -av $pkgbase-$pkgver ${pkgname[1]}-$pkgver
  (
    cd $pkgbase-$pkgver
    phpize
  )
  (
    cd ${pkgname[1]}-$pkgver
    phpize-legacy
  )
}

build() {
  local configure_options=(
    --prefix=/usr
    --enable-igbinary
  )

  (
    cd $pkgbase-$pkgver
    ./configure "${configure_options[@]}"
    make
  )
  (
    cd ${pkgname[1]}-$pkgver
    ./configure "${configure_options[@]}"
    make
  )
}

check() {
  NO_INTERACTION=1 make -k test -C $pkgbase-$pkgver
  NO_INTERACTION=1 make -k test -C ${pkgname[1]}-$pkgver
}

package_php-igbinary() {
  depends+=(php)
  backup=(etc/php/conf.d/$_name.ini)

  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -vDm 644 $_name.php.ini "$pkgdir/etc/php/conf.d/$_name.ini"
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CREDITS,NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_php-legacy-igbinary() {
  depends+=(php-legacy)
  backup=(etc/php-legacy/conf.d/$_name.ini)

  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -vDm 644 "$_name.php.ini" "$pkgdir/etc/php-legacy/conf.d/$_name.ini"
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CREDITS,NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
