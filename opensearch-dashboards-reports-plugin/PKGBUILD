# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname='opensearch-dashboards-reports-plugin'
_pluginname='dashboards-reports'
pkgver=2.13.0.0
_dashboardsver=2.13.0
pkgrel=1
pkgdesc='OpenSearch Dashboards Reports Plugin'
url='https://opensearch.org/docs/latest/dashboards/reporting'
arch=('loong64' 'x86_64')
license=('Apache-2.0')
depends=("opensearch-dashboards=${_dashboardsver}" 'coffeescript')
makedepends=('yarn' 'npm' 'python' 'git')
options=('!strip' 'emptydirs')
source=(
  "git+https://github.com/opensearch-project/dashboards-reporting.git#tag=${pkgver}"
  "git+https://github.com/opensearch-project/OpenSearch-Dashboards.git#tag=${_dashboardsver}"
)
sha256sums=('8c6dbec82063d4c7283f9273fa3bf704e18f20aa4d3708fdca7c1381d4df5a37'
            '78a715eef9b363ef03bd2518016ae8af430aba8925fe03a0c47699435b5e23a6')

prepare() {
  nodeVersion="$(node -v)"
  # Yes, you support this version. You just don't know it yet.
  sed -i "s/    \"node\": \"[0-9\.]*\",/    \"node\": \"${nodeVersion:1}\",/" "OpenSearch-Dashboards/package.json"
}

build() {
  mv "dashboards-reporting" "OpenSearch-Dashboards/plugins/${_pluginname}"
  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
  yarn osd bootstrap
  yarn build --skip-archive
}

check() {
  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
  yarn test
}

package() {
  cd "${srcdir}/OpenSearch-Dashboards/plugins/${_pluginname}"

  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -dm755 "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  cp -r "build/opensearch-dashboards/"* "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  find "${pkgdir}/usr/share/opensearch-dashboards/plugins" -type d -empty -delete
}

# vim: ts=2 sw=2 et:
