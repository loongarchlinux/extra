# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-fiber-annotation
pkgver=0.2.0
pkgrel=1
pkgdesc='A mechanism for annotating fibers'
arch=(any)
url='https://github.com/ioquatix/fiber-annotation'
license=(MIT)
depends=(ruby)
checkdepends=(ruby-bake-modernize ruby-bake-test ruby-bake-test-external ruby-sus ruby-covered)
options=(!emptydirs)
source=(https://github.com/ioquatix/fiber-annotation/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('95d37669c3c3b13fccf2832ba30d8bafeee455e35fc7b263cedf2e0d377f33be')

prepare() {
  cd fiber-annotation-$pkgver
  sed '/signing_key/d' -i fiber-annotation.gemspec
  sed -i '/bake-gem/d;/utopia-project/d' gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd fiber-annotation-$pkgver
  gem build fiber-annotation.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    fiber-annotation-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd fiber-annotation-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd fiber-annotation-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
