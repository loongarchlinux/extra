# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Paul-Louis Ageneau <paul-louis at ageneau dot org>
pkgname=libjuice
pkgver=1.4.1
pkgrel=1
pkgdesc="UDP Interactive Connectivity Establishment (ICE) library"
arch=('loong64' 'x86_64')
url="https://github.com/paullouisageneau/$pkgname"
license=('MPL2')
makedepends=('cmake')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/paullouisageneau/libjuice/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('88e24c0b8e45916780a47618e40adc5a2fcdea75efbcb97c2e1dcfc149069ef691ce62a7b723bb6671dcef4cae768b40e1e7d703e7da31b6953be8afe09a7c2d')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DNO_TESTS=1 \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir/" cmake --install build

  cd ${pkgname}-${pkgver}
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
