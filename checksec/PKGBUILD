# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Lubomir Krajcovic <lubomir.krajcovic(AT)gmail(DOT)com>

pkgname=checksec
_pkgname=checksec.sh
pkgver=2.7.0
pkgrel=1
pkgdesc='Tool designed to test which standard Linux OS and PaX security features are being used'
url='https://github.com/slimm609/checksec.sh'
arch=('any')
license=('BSD')
depends=('bash' 'binutils' 'coreutils' 'findutils' 'procps-ng' 'awk' 'openssl'
         'file' 'grep')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('194a6f33042fcec94bbc463659066abdda644da42f68dd8001ba263a2679bd49a4443229cd5ddffd75afe693d255dc7f081c95eeeff940ac8a0e53313972f03b')
b2sums=('c44bcc1a5090a29563ce7e8694ead74230d43f469a71e8388c06138afe5192280366315f3921961adddb367cbab81a1948f45096cbb64a7d8fdb6de849dc1698')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -r 's|(pkg_release=)false|\1true|' -i checksec
}

package() {
  cd ${_pkgname}-${pkgver}
  install -Dm 755 checksec -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 ChangeLog README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 extras/man/checksec.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 extras/zsh/_checksec -t "${pkgdir}/usr/share/zsh/site-functions"
}

# vim: ts=2 sw=2 et:
