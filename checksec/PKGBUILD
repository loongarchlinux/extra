# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Lubomir Krajcovic <lubomir.krajcovic(AT)gmail(DOT)com>

pkgname=checksec
_pkgname=checksec.sh
pkgver=2.7.1
pkgrel=1
pkgdesc='Tool designed to test which standard Linux OS and PaX security features are being used'
url='https://github.com/slimm609/checksec.sh'
arch=(any)
license=(BSD-3-Clause)
depends=(
  awk
  bash
  binutils
  coreutils
  file
  findutils
  grep
  openssl
  procps-ng
)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('a9e7a59f1d4b1f836806ef404718dd91140617caec4c906c9fb523073560bdc513608a9bbdeeb6d4bb2a15e623b9f3e1eaa53a0b979a039125b2be20b110e40e')
b2sums=('a02dc30eb9e2176dbb336739b4def662ab7341e19a8715e322fa00ea509c7847ac28f23e20a005b94f66259fa18466fb2bbed5e410bdbd56e8e732b44a52565c')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -r 's|(pkg_release=)false|\1true|' -i checksec
}

package() {
  cd ${_pkgname}-${pkgver}
  install -Dm 755 checksec -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 ChangeLog README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 extras/man/checksec.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 extras/zsh/_checksec -t "${pkgdir}/usr/share/zsh/site-functions"
}

# vim: ts=2 sw=2 et:
