# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Lubomir Krajcovic <lubomir.krajcovic(AT)gmail(DOT)com>

pkgname=checksec
_pkgname=checksec.sh
pkgver=2.7.1
pkgrel=1
pkgdesc='Tool designed to test which standard Linux OS and PaX security features are being used'
url='https://github.com/slimm609/checksec.sh'
arch=(any)
license=(BSD-3-Clause)
depends=(
  awk
  bash
  binutils
  coreutils
  file
  findutils
  grep
  openssl
  procps-ng
)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('877a08cc2e335dc6378d52fc2e1ff39860dcf646e17be04addc50de99a3c68c1eaba0f843d6844ec9d3c687d7245379a008a2230838dc7a48e5b28893eacdee0')
b2sums=('1325f3b1496cbc7cab76eb28773be36c52e7069bab5c62579058130d12dfde1361efc086d04c8dc2da3105359f8c7b4844aae7e01ae14dae3a5b013db3e1170e')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -r 's|(pkg_release=)false|\1true|' -i checksec
}

package() {
  cd ${_pkgname}-${pkgver}
  install -Dm 755 checksec -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 ChangeLog README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 extras/man/checksec.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 extras/zsh/_checksec -t "${pkgdir}/usr/share/zsh/site-functions"
}

# vim: ts=2 sw=2 et:
