# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=hamlib
pkgver=4.5.5
pkgrel=4
pkgdesc='Ham radio equipment control libraries'
arch=('loong64' 'x86_64')
url='https://hamlib.github.io'
license=('GPL' 'LGPL')
depends=('perl' 'python' 'lua' 'tcl' 'libxml2' 'libusb-compat' 'libtool' 'libuhd' 'libindi')
makedepends=('swig')
#source=("https://github.com/Hamlib/Hamlib/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc})
source=(https://github.com/Hamlib/Hamlib/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('6b39109a6f65a96f15508ccda2b8aa0b208fd8eab0166cc871494e46f4f04c736ac4415e7717e6f196a8ccba3b25e1ffe95815eecd1fd569d9834e489a79d3b4')
validpgpkeys=('82D64F6B0E67CD41F689BBA6FB2C5130D55A8819') # Nate Bargmann <n0nb@n0nb.us>

build() {
  cd Hamlib-$pkgver

  ./bootstrap
  ./configure \
  	--prefix=/usr \
  	--sbindir=/usr/bin \
  	--with-perl-binding \
  	--with-python-binding \
  	--with-tcl-binding \
  	--with-lua-binding \
  	--with-xml-support \
  	--disable-uhd  # https://github.com/Hamlib/Hamlib/issues/355

  make
}

package() {
  cd Hamlib-$pkgver

  make DESTDIR="$pkgdir" install
}
