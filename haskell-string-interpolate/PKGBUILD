# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=string-interpolate
pkgname=haskell-string-interpolate
pkgver=0.3.4.0
pkgrel=3
pkgdesc="Haskell string/text/bytestring interpolation that just works"
url="https://gitlab.com/williamyaoh/string-interpolate/blob/master/README.md"
license=("BSD")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-src-exts' 'haskell-src-meta' 'haskell-split' 'haskell-text-conversions'
         'haskell-utf8-string')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-hspec' 'haskell-hspec-core'
             'haskell-quickcheck-instances' 'haskell-quickcheck-text' 'haskell-quickcheck-unicode'
             'haskell-unordered-containers')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('88838540b080308174d4fa51f20f15f46ed928bf1cf664f533f9bda5ae1e0b8e')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -f-bytestring-builder -f-extended-benchmarks -f-text-builder

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
