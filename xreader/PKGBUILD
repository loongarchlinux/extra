# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xreader
pkgver=4.2.2
pkgrel=1
pkgdesc="Document viewer for files like PDF and Postscript. X-Apps Project."
arch=(loong64 x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
groups=(x-apps)
depends=(poppler-glib webkit2gtk-4.1 xapp)
optdepends=('djvulibre: support for djvu files'
            'libgxps: support for xps files'
            'libspectre: support for dvi and ps files'
            'mathjax2: support for math in epub files'
            'texlive-bin: support for dvi files')
makedepends=(meson samurai intltool itstool gobject-introspection djvulibre
             libgxps libspectre texlive-bin glib2-devel)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('1392286e58408c40b35afd3f8d187f4c0b575d89ff86985f334e89109b6283e6')
b2sums=('ea2a6455e5a0a19b48e382838e839aa793943f02106da1d50ed5d36b4f9b242a475ecb7ce8cb80d82941b41a9a8ad4e408785f676b21483308060aeea0b00c18')

build() {
  arch-meson ${pkgname}-${pkgver} build \
    --libexecdir=lib/${pkgname} \
    -Dmathjax-directory=/usr/share/mathjax2 \
    -Dcomics=true \
    -Ddjvu=true \
    -Ddvi=true \
    -Dt1lib=true \
    -Dpixbuf=true \
    -Dhelp_files=true \
    -Dintrospection=true
  samu -C build
}

package(){
  DESTDIR="${pkgdir}" samu -C build install
}
