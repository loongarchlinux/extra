# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Rein Fernhout <me at levitati.ng>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: djnm <nmihalich [at} gmail dott com>

pkgname=dwarffortress
pkgver=50.11
_pkgver=50_11
pkgrel=1
pkgdesc="A single-player fantasy game in which you build a dwarven outpost or play an adventurer in a randomly generated world"
arch=('loong64' 'x86_64')
url="https://www.bay12games.com/dwarves/"
license=('custom:dwarffortress')
depends=('libice' 'gtk3' 'glu' 'sdl' 'sdl_image' 'sdl_ttf' 'libsndfile'
         'libsm'  'openal' 'glew' 'gcc-libs' 'glib2')
makedepends=('git' 'cmake')
optdepends=('nvidia-utils: If you have nvidia graphics'
            'alsa-lib: for alsa sound'
            'libpulse: for pulse sound')
options=('!strip' '!buildflags')
install=$pkgname.install
source=(dwarffortress
        dwarffortress.desktop
        dwarffortress.png
        https://www.bay12games.com/dwarves/df_${_pkgver}_linux.tar.bz2)
sha256sums=('c23b984face14541e48af1b6845c318fae74800b9774c97a1d5b8aa4bfa19b6a'
            'e79e3d945c6cc0da58f4ca30a210c7bf1bc3149fd10406d1262a6214eb40445a'
            '83183abc70b11944720b0d86f4efd07468f786b03fa52fe429ca8e371f708e0f'
            'a0b70323d60ef67579bbb1caf7a8801b7245e4082ea54cbbdcc0eeff106ae247')

noextract=(df_${_pkgver}_linux.tar.bz2)

prepare() {
    # tar archive has no root directory
    mkdir -p df_${_pkgver}_linux
    tar -xf df_${_pkgver}_linux.tar.bz2 -C df_${_pkgver}_linux
}

package() {
  install -dm755 "$pkgdir"/opt/
  cp -r "$srcdir"/df_${_pkgver}_linux "$pkgdir"/opt/$pkgname

  # set correct permissions
  find "$pkgdir"/opt/$pkgname -type d -exec chmod 755 {} +
  find "$pkgdir"/opt/$pkgname -type f -exec chmod 644 {} +
  find "$pkgdir"/opt/$pkgname -type f -name "*.so*" -exec chmod 755 {} +
  chmod 755 "$pkgdir"/opt/$pkgname/{*.so*,run_df,dwarfort}

  install -Dm755 "$srcdir"/dwarffortress "$pkgdir"/usr/bin/$pkgname

  # Desktop launcher with icon
  install -Dm644 "$srcdir"/dwarffortress.desktop "$pkgdir"/usr/share/applications/"$pkgname".desktop
  install -Dm644 "$srcdir"/dwarffortress.png     "$pkgdir"/usr/share/pixmaps/"$pkgname".png

  install -Dm644 "$srcdir"/df_${_pkgver}_linux/{readme.txt,"release notes.txt"} --target-directory "$pkgdir"/usr/share/$pkgname/
}

# vim:set ts=2 sw=2 et:
