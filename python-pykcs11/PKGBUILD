# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=python-pykcs11
pkgver=1.5.16
pkgrel=1
pkgdesc='PKCS#11 Wrapper for Python'
arch=('loong64' 'x86_64')
url='https://github.com/LudovicRousseau/PyKCS11'
license=('GPL-2.0-or-later')
depends=(
  gcc-libs
  glibc
  opensc
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  softhsm
  swig
)
source=("git+https://github.com/LudovicRousseau/PyKCS11.git/?signed#tag=$pkgver")
b2sums=('f556da591cece315d73ccb408249e7b951fbf3f5636740eebd7946cc78d9a85160463883feca989178c245641577fe8b0dcf2c7e55d893b8d2907ddc5b6f06f1')

validpgpkeys=(F5E11B9FFE911146F41D953D78A1B4DFE8F9C57E) # Ludovic Rousseau <ludovic.rousseau@free.fr>

build() {
  cd PyKCS11
  python -m build
}

package() {
  cd PyKCS11
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}
