# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=python-pykcs11
pkgver=1.5.15
pkgrel=2
pkgdesc='PKCS#11 Wrapper for Python'
arch=('loong64' 'x86_64')
url='https://github.com/LudovicRousseau/PyKCS11'
license=('GPL-2.0-or-later')
depends=(
  gcc-libs
  glibc
  opensc
  python
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  softhsm
  swig
)
source=("git+https://github.com/LudovicRousseau/PyKCS11.git/?signed#tag=$pkgver")
b2sums=('0b7e4bf308f0a3e2770efe90b0fb0e163cf6fe2ea7852cb920802ac6fcedb877a3d2ea320a9a52db92bcd995de63f5c83947062ab6f24cec29be3ffc669cac4e')

validpgpkeys=(F5E11B9FFE911146F41D953D78A1B4DFE8F9C57E) # Ludovic Rousseau <ludovic.rousseau@free.fr>

build() {
  cd PyKCS11
  python -m build
}

package() {
  cd PyKCS11
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}
