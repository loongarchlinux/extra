# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-dominate
pkgver=2.9.1
pkgrel=2
pkgdesc="A Python library for creating and manipulating HTML documents using an elegant DOM API"
url="https://github.com/Knio/dominate"
license=('LGPL')
arch=('any')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Knio/dominate/archive/$pkgver.tar.gz")
sha512sums=('d13b3fcc22176b2ad0c393887b6b3fa896252f9c4126a017eb696a973c05090cbbf0b0437495761987e1f08b76d0e2901d06c61b02b6b20a4aac652ec2867297')

build() {
  cd dominate-$pkgver
  python -m build -nw
}

check() {
  cd dominate-$pkgver
  pytest
}

package() {
  cd dominate-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
}
