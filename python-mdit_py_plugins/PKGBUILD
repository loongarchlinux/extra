# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_pkgname=mdit-py-plugins
pkgname=python-mdit_py_plugins
pkgver=0.4.1
pkgrel=1
pkgdesc='Collection of core plugins for markdown-it-py'
arch=('any')
url='https://github.com/executablebooks/mdit-py-plugins'
license=('MIT')
depends=('python-markdown-it-py')
makedepends=('python-build' 'python-installer' 'python-flit-core')
checkdepends=('python-pytest' 'python-pytest-regressions')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('d2f5c59bf2a17ce441de18f815624cb6f61202f111176c5f1c7745e430d59e8690ce8a5f9da2402bd34dcc5cf4dc38377990df649fbf3256216bc82cdfc6333a')

build() {
  cd $_pkgname-$pkgver

  python -m build -wn
}

check() {
  cd $_pkgname-$pkgver

  python -m pytest
}

package() {
  cd $_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"${_pkgname//-/_}"-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
