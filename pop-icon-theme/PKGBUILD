# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Christopher Reimer <mail@c-reimer.de>

pkgname=pop-icon-theme
pkgver=3.5.0
pkgrel=1
pkgdesc='System76 Pop Icon Theme'
arch=(any)
url=https://github.com/pop-os/icon-theme
license=(CCPL)
depends=(adwaita-icon-theme)
makedepends=(
  git
  meson
)
_tag=3126c6a3f605923c648e74e7276b9de59d4ab67a
source=(pop-icon-theme::git+https://github.com/pop-os/icon-theme.git#tag=${_tag})
b2sums=('f94d60b0ae862b9ec1496e0441021367112b2db790c7299db0046b551817ce56c033fae8d66e25ea95bab68c18e80dc61d984d4ae00742232f549e90705445af')

pkgver() {
  cd pop-icon-theme
  git describe --tags | sed 's/^v//;s/-/.r/;s/-g/./'
}

build() {
  arch-meson pop-icon-theme build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
