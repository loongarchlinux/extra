# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=incremental-parser
pkgname=haskell-incremental-parser
pkgver=0.5.1
pkgrel=2
pkgdesc="Generic parser library capable of providing partial results from partial input."
url="https://hackage.haskell.org/package/${_hkgname}"
license=('GPL')
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-logict' 'haskell-input-parsers' 'haskell-monoid-subclasses'
         'haskell-parsers' 'haskell-rank2classes')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-checkers' 'haskell-tasty'
             'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('85119178cc29d4ffb162bfd5aed4441bbc2d2a842a69d5071d3452613a24538893941aa695d88dc0571cab2725001e49c64c8733842e0427a6bdbba06ddc5e20')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.txt"
}
