# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=firecracker
pkgname=('firecracker' 'firecracker-docs')
pkgver=1.7.0
pkgrel=1
pkgdesc='Secure and fast microVMs for serverless computing'
arch=('loong64' 'x86_64')
url='https://firecracker-microvm.github.io'
license=('Apache-2.0' 'BSD-3-Clause') # firecracker is Apache, Chromium components is BSD
makedepends=('git' 'rust' 'clang' 'cmake')
options=('!lto' '!buildflags')
source=("$pkgname::git+https://github.com/firecracker-microvm/firecracker.git#tag=v$pkgver")
sha512sums=('4895565cc1cd59a0b012b15607550155aa76ccfd623acb1de0d012470f74c341d5b0ea9539963650111bf20902a90256b20925a764ed77568f180601e3c2f09c')
b2sums=('c75d568bcd39fe369e851b2479016e2d67050f698f39c9b3db0a722d29b48a71c8646bef0b8ba70bbebb81bff8e0e3261416892e615cef693aa47bbfc13101a0')

_cargo_target="$CARCH-unknown-linux-gnu"

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target="$_cargo_target"
}

build() {
  cd "$pkgbase"

  cargo build \
    --package firecracker \
    --package jailer \
    --package seccompiler \
    --package rebase-snap \
    --release \
    --frozen \
    --all-features \
    --target-dir=target \
    --target="$_cargo_target"
}

package_firecracker() {
  depends=('glibc' 'gcc-libs')

  cd "$pkgbase"

  # binaries
  find "target/$_cargo_target/release" \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin/" {} +

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}

package_firecracker-docs() {
  #arch=('any')
  pkgdesc+=" (documentation)"

  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgbase"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE NOTICE THIRD-PARTY
}
