# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordian Edenhofer <gordian.edenhofer@gmail.com>

pkgname=routersploit
pkgver=3.4.4
pkgrel=1
pkgdesc='Open-source exploitation framework dedicated to embedded devices'
url='https://github.com/threat9/routersploit'
arch=(any)
license=(BSD-3-Clause)
depends=(
  python
  python-bluepy
  python-cryptography
  python-future
  python-paramiko
  python-pycryptodome
  python-pysnmp
  python-requests
  python-setuptools
  python-urllib3
)
makedepends=(
  python-build
  python-installer
  python-wheel
)
checkdepends=(
  flake8
  python-pytest
  python-pytest-forked
  python-pytest-xdist
  python-threat9-test-bed
)
source=(https://github.com/threat9/routersploit/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('7813881a7f59e043736fc61db3871b69c961f69a75e824025e934d394d114c110c15d5dd98e4ce42e2a5441632c4dd8066646f241ceaa87808a5932708bacf32')
b2sums=('be47c728c89cf87c0e013e2af8aed63b5e408483b5d670fa632202d35825612f3c41025171c2565613f15e3e0d931ec26a00870450adf4762b12b0cadf3b524a')

prepare() {
  cd $pkgname-$pkgver

  # Remove failing tests
  rm tests/exploits/misc/miele/test_pg8528_path_traversal.py
  rm tests/exploits/cameras/xiongmai/test_uc_httpd_path_traversal.py
  rm tests/exploits/routers/dlink/test_dns_320l_327l_rce.py
  rm tests/exploits/routers/tplink/test_wdr740nd_wdr740n_path_traversal.py
  rm tests/exploits/routers/3com/test_officeconnect_rce.py
  rm tests/exploits/cameras/multi/test_jvc_vanderbilt_honeywell_path_traversal.py
  rm tests/exploits/routers/linksys/test_1500_2500_rce.py
}

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}

  python -m installer --destdir="${pkgdir}" dist/*.whl
  rm -rf "${pkgdir}"/usr/lib/python*/site-packages/tests
  mv "${pkgdir}/usr/bin/rsf"{.py,}

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/* "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
