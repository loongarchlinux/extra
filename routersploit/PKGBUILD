# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordian Edenhofer <gordian.edenhofer@gmail.com>

pkgname=routersploit
pkgver=3.4.2
pkgrel=2
pkgdesc='Open-source exploitation framework dedicated to embedded devices'
url='https://github.com/threat9/routersploit'
arch=('any')
license=('BSD-3-Clause')
depends=('python' 'python-future' 'python-requests' 'python-paramiko' 'python-pysnmp' 'python-pycryptodome')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-forked' 'python-pytest-xdist' 'flake8' 'python-threat9-test-bed')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/threat9/routersploit/archive/v${pkgver}.tar.gz)
sha512sums=('74186272f84f33e66d3235b849ae9bac496c5b2174d8b441848681a272f0e2c11ed585b87049a82a792429b36b6dfa7e985370a3d2c2bfb6bca189ba96d6dcd1')

prepare() {
  cd $pkgname-$pkgver

  # Remove failing tests
  rm tests/exploits/misc/miele/test_pg8528_path_traversal.py
  rm tests/exploits/cameras/xiongmai/test_uc_httpd_path_traversal.py
  rm tests/exploits/routers/dlink/test_dns_320l_327l_rce.py
  rm tests/exploits/routers/tplink/test_wdr740nd_wdr740n_path_traversal.py
  rm tests/exploits/routers/3com/test_officeconnect_rce.py
  rm tests/exploits/cameras/multi/test_jvc_vanderbilt_honeywell_path_traversal.py
  rm tests/exploits/routers/linksys/test_1500_2500_rce.py
}

check() {
  cd ${pkgname}-${pkgver}
  make tests
}

package() {
  cd ${pkgname}-${pkgver}

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  rm -rf "${pkgdir}"/usr/lib/python*/site-packages/tests
  mv "${pkgdir}/usr/bin/rsf"{.py,}

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/* "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
