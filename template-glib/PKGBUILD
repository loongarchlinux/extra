# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=template-glib
pkgver=3.36.2
pkgrel=1
pkgdesc="A templating library for GLib"
url="https://gitlab.gnome.org/GNOME/template-glib"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  glib2
  gobject-introspection-runtime
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  meson
  vala
)
provides=(libtemplate_glib-1.0.so)
_commit=51ab89b83f47e6435df454699bcde6816372b9d3  # tags/3.36.2^0
source=("git+https://gitlab.gnome.org/GNOME/template-glib.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
