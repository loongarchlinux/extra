# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>
# Contributor: Denis Zawada <deno@bajtogrod.pl>

pkgname=igraph
pkgver=0.10.13
pkgrel=1
pkgdesc='A library for creating and manipulating (un)directed graphs'
arch=(loong64 x86_64)
url='https://igraph.org/c/'
license=(GPL-2.0-only)
depends=(arpack
         blas
         gcc-libs
         glibc
         glpk
         gmp
         lapack
         libxml2
         plfit)
makedepends=(cmake
             git)
source=(git+https://github.com/igraph/igraph#tag=$pkgver)
sha256sums=('7fcf4c0fd8511ac30395467e0304d4b88cfde14e8f6ee3e0d446095dfbb01b68')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

check() {
  cmake --build build --target check
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
