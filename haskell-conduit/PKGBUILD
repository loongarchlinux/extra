# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=conduit
pkgname=haskell-conduit
pkgver=1.3.5
pkgrel=5
pkgdesc="Streaming data processing library."
url="https://github.com/snoyberg/conduit"
license=("MIT")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-mono-traversable' 'haskell-primitive'
         'haskell-resourcet' 'haskell-unliftio-core' 'haskell-vector')
makedepends=('ghc' 'haskell-hspec' 'haskell-quickcheck' 'haskell-safe' 'haskell-silently'
             'haskell-split' 'haskell-unliftio')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('a123d093e7896a2999bb6006a8d8b1a4ef18c5b1682d5588d85fc81496a9fd9f979d2087531e55d8f2aa22221301264ba700cbbe1f0731437f162fea0ff612e5')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
