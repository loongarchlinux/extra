# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kopeninghours
pkgver=24.05.2
pkgrel=1
pkgdesc='Library for parsing and evaluating OSM opening hours expressions'
arch=(loong64 x86_64)
url='https://www.kde.org'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kholidays
         ki18n
         qt6-base)
makedepends=(boost
             doxygen
             extra-cmake-modules
             python
             qt6-declarative
             qt6-doc
             qt6-tools)
optdepends=('boost-libs: Python bindings'
            'python: Python bindings'
            'qt6-declarative: QML bindings')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('648ecd8b22b4f0909674ce3a991f73167e6648b8ae35e689f816d1b0f583518c'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON \
    -DQT_MAJOR_VERSION=6
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
