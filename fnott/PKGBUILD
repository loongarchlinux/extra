# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: Daniel Ekl√∂f <daniel at ekloef dot se>

pkgname=fnott
pkgver=1.6.0
pkgrel=1
pkgdesc='Keyboard driven and lightweight Wayland notification daemon'
url="https://codeberg.org/dnkl/$pkgname"
arch=(loong64 x86_64)
license=(MIT)
depends=(dbus
         fcft
         wayland)
makedepends=(meson
             tllist
             scdoc
             wayland-protocols)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('fc8a0a9b75995a10afeaf3a670fb30986b21a4f48bd67a7018802de10debc83f')

build() {
	cd "$pkgname"
	arch-meson . build
	meson compile -C build
}

package() {
	cd "$pkgname"
	meson install -C build --destdir "$pkgdir"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
