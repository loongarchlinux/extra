# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpublictransport
pkgver=23.08.3
pkgrel=1
pkgdesc='Library to assist with accessing public transport timetables and other data'
arch=(loong64 x86_64)
url='https://www.kde.org'
license=(LGPL)
depends=(qt5-declarative networkmanager-qt5 ki18n5)
makedepends=(extra-cmake-modules protobuf qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('e7033bc8e5a1667aac3f26361c72f3be55388f9df24b64701d09ff5dbe4b93fb'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
