# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-cramjam
_pkgname=${pkgname#python-}
pkgver=2.8.3
pkgrel=3
pkgdesc="Thin Python bindings to de/compression algorithms in Rust"
arch=(loong64 x86_64)
url="https://github.com/milesgranger/cramjam"
license=(MIT)
depends=(
  bzip2
  gcc-libs
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-maturin
  python-wheel
)
checkdepends=(
  python-hypothesis
  python-numpy
  python-pytest
)
options=(!lto)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('54c5d19876f63bc0955f919d9479145ca9f142f48a18db0cd560e590a6eca01a')

build() {
  cd "$_pkgname-$pkgver/cramjam-python"

  export RUSTUP_TOOLCHAIN=stable
  python -m build --wheel --no-isolation
}

check() {
  cd "$_pkgname-$pkgver/cramjam-python"

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m installer --destdir=tmp_install dist/*.whl
  PYTHONPATH="$PWD/tmp_install/$site_packages" pytest \
    --ignore=benchmarks/
}

package() {
  cd "$_pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" cramjam-python/dist/*.whl
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
