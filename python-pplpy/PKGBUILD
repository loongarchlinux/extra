# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=pplpy
pkgname=python-pplpy
pkgver=0.8.10
pkgrel=2
pkgdesc='Python wrapper to the C++ Parma Polyhedra Library (PPL)'
arch=(loong64 x86_64)
url='https://gitlab.com/videlec/pplpy/'
license=(GPL-3.0-or-later)
depends=(gcc-libs
         glibc
         gmp
         ppl
         python
         python-cysignals
         python-gmpy2)
makedepends=(cython
             python-build
             python-installer
             python-setuptools
             python-sphinx
             python-wheel)
source=(https://github.com/sagemath/pplpy/archive/$pkgver/$_pyname-$pkgver.tar.gz)
sha256sums=('9f01ddd28e0b7de9ca9599d03d00d261e395a161a592aca4ef8fb79a487505e6')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation

  _pythonver=`python -c "from sysconfig import get_python_version; print(get_python_version())"`
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-${_pythonver/./}" make -C docs html
}

package() {
  cd $_pyname-$pkgver 
  python -m installer --destdir="$pkgdir" dist/*.whl

  mkdir -p "$pkgdir"/usr/share/doc/pplpy
  cp -r docs/build/html/* "$pkgdir"/usr/share/doc/pplpy
}
