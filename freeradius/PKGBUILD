# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jason R Begley (jayray@digitalgoat.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=freeradius
pkgver=3.2.5
pkgrel=1
pkgdesc='The premier open source RADIUS server'
arch=('loong64' 'x86_64')
url='https://freeradius.org/'
license=('GPL')
depends=('krb5' 'net-snmp' 'postgresql-libs' 'mariadb-libs' 'talloc'
         'libpcap' 'libxcrypt' 'libcrypt.so' 'smbclient')
makedepends=('curl' 'freetds' 'hiredis' 'json-c' 'libmemcached' 'perl'
             'python' 'samba' 'sqlite' 'unixodbc' 'yubico-c-client')
optdepends=('curl: for REST'
            'freetds: for Sybase and MS SQL'
            'hiredis: for redis support'
            'json-c: rlm_rest module'
            'libmemcached: for memcached'
            'perl: for Perl'
            'python: for Python'
            'sqlite: for sqlite'
            'unixodbc: for ODBC'
            'yubico-c-client: for yubicloud')
options=('!makeflags')
validpgpkeys=('BF2384EC6938B9744B03E2A620E37C25995B4F85') # FreeRADIUS - Package Signing [...] <packages@freeradius.org>
source=("https://github.com/FreeRADIUS/freeradius-server/releases/download/release_${pkgver//./_}/freeradius-server-${pkgver}.tar.bz2"{,.sig}
        'freeradius-sysusers.conf'
        'freeradius-tmpfiles.conf'
        'freeradius.service')
sha256sums=('0fe4f57b28b942c5e5955f48a88769817ca287a830b939d7120ffcff3fcdba88'
            'SKIP'
            '8ecaca94c7d0f4806b326685312dd4e543ce9c6c183d3d7ad01c1a0197bdfb94'
            'f536a9aa972e3e42a6b1a6d8ee17166eb721c7cba2e80f60473811497c7bd8bc'
            'ff9e31232122d2982fae5466969f45f9f46396c307754aa10b582e5cab19758f')

build() {
  cd "$srcdir"/freeradius-server-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libdir=/usr/lib/freeradius \
    --localstatedir=/var \
    --enable-heimdal-krb5 \
    --enable-reproducible-builds \
    --without-rlm_python \
    --with-regex=false \
    --with-rlm_python3 \
    --with-system-libltdl \
    --with-system-libtool \
    --with-udpfromto
  make PACKAGE='archlinux'
}

package() {
  cd "$srcdir"/freeradius-server-$pkgver

  make install R="$pkgdir" PACKAGE='archlinux'
  chmod o+r "$pkgdir"/etc/raddb/*
  mv "$pkgdir"/etc/raddb "$pkgdir"/etc/raddb.default
  rm -rf "$pkgdir"/var/run

  install -D -m0644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
  install -D -m0644 "$srcdir"/$pkgname-sysusers.conf "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
  install -D -m0644 "$srcdir"/$pkgname-tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf
}
