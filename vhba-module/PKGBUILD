# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgbase=vhba-module
pkgname=(
  vhba-module
  vhba-module-dkms
)
pkgver=20240202
pkgrel=20
pkgdesc="Kernel module that emulates SCSI devices"
url="https://cdemu.sourceforge.io/"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
makedepends=(linux-headers)
source=(
  "https://downloads.sourceforge.net/cdemu/$pkgbase-$pkgver.tar.xz"
  dkms.conf
)
b2sums=('7974f60b73c634528afdd44e94c6da34bdc481d874f19845efc0464435a7e560494dd703b0fb1df34c96fce5f1afb300a1e6e2b3b9730e91c956816905eb2f44'
        'f1f3d97fc450de4a0257fa4c864eb7245f6c9657472eec67949b1335681ba86a40273b7fb11d023e406f792709252d80e635791674bdf79153f53161b4c5d871')

# https://sourceforge.net/projects/cdemu/files/vhba-module/
md5sums=('ba66d25b257daba715d4870addbd131c'
         '7da9314812d9ea665df0db33052c7452')
sha1sums=('84f281612d18978959216a8b9eae6e5a94dcb6cf'
          'c3b46eeceef6124483020ae52ccb801c534996b2')

prepare() {
  cd $pkgbase-$pkgver
}

build() {
  cd $pkgbase-$pkgver
  make KERNELRELEASE="$(</usr/src/linux/version)"
}

package_vhba-module() {
  depends=(linux)
  provides=(VHBA-MODULE)

  local extradir="/usr/lib/modules/$(</usr/src/linux/version)/extramodules"
  install -Dt "$pkgdir$extradir" -m644 $pkgbase-$pkgver/*.ko

  find "$pkgdir" -name '*.ko' -exec strip --strip-debug {} +
  find "$pkgdir" -name '*.ko' -exec xz {} +
}

package_vhba-module-dkms() {
  depends=(dkms)
  provides=(VHBA-MODULE)
  conflicts=(vhba-module)

  install -Dt "$pkgdir/usr/src/$pkgbase-$pkgver" -m644 \
    dkms.conf $pkgbase-$pkgver/{Makefile,vhba.c}
}

# vim:set sw=2 sts=-1 et:
