# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgbase=vhba-module
pkgname=(
  vhba-module
  vhba-module-dkms
)
pkgver=20240202
pkgrel=42
pkgdesc="Kernel module that emulates SCSI devices"
url="https://cdemu.sourceforge.io/"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
makedepends=(
  git
  linux-headers
)
source=(
  "cdemu-code::git+https://git.code.sf.net/p/cdemu/code#tag=$pkgbase-$pkgver"
  dkms.conf
)
b2sums=('2c97148e88aac9281ffcaee579b0cd9f00117086e69f9035e44e4288f5ba28a6a3b1246c922647742dbed7666fd8158431d600387247132808097bdb33f74981'
        'f1f3d97fc450de4a0257fa4c864eb7245f6c9657472eec67949b1335681ba86a40273b7fb11d023e406f792709252d80e635791674bdf79153f53161b4c5d871')

prepare() {
  cd cdemu-code/$pkgbase
}

build() {
  cd cdemu-code/$pkgbase
  make KERNELRELEASE="$(</usr/src/linux/version)"
}

package_vhba-module() {
  depends=(linux)
  provides=(VHBA-MODULE)

  local extradir="/usr/lib/modules/$(</usr/src/linux/version)/extramodules"
  install -Dt "$pkgdir$extradir" -m644 cdemu-code/$pkgbase/*.ko

  # compress kernel modules
  find "${pkgdir}" -name '*.ko' -exec zstd --rm -19 {} +
}

package_vhba-module-dkms() {
  depends=(dkms)
  provides=(VHBA-MODULE)
  conflicts=(vhba-module)

  install -Dt "$pkgdir/usr/src/$pkgbase-$pkgver" -m644 \
    dkms.conf cdemu-code/$pkgbase/{Makefile,vhba.c}
}

# vim:set sw=2 sts=-1 et:
