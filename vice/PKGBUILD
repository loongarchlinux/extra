# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=vice
pkgver=3.8
pkgrel=1
pkgdesc='Versatile Commodore Emulator'
arch=(loong64 x86_64)
license=(GPL)
url='https://vice-emu.sourceforge.io/'
depends=(alsa-lib giflib glew gtk3 libjpeg-turbo libpng libpulse python sdl2 sdl2_image)
makedepends=(dos2unix ffmpeg4.4 libpcap libxaw texlive-bin texlive-core xa xorg-bdftopcf xorg-mkfontdir)
source=("https://downloads.sourceforge.net/project/vice-emu/releases/$pkgname-${pkgver%.0}.tar.gz"
        disable-fc-cache.patch)
b2sums=('709fd6eda874de542e5770a328b0c0308d04f755e3bf1fceba5147156eb894a95ce6f14e9f8848ca6992d4f958082f83a198d2176c1fe54e87027c7dfac8849c'
        'a27ff2a70537a57fd1c3be561181403fede8a6fd75ca8a31a4a4ed97b010cd8f8f0376a619be126ac22ad8c81bf0d1fd9c7e5e411bf94da32006c9ecdd10d32e')

prepare() {
  cd $pkgname-$pkgver
  rm -rf src/lib/{liblame,libx264,libffmpeg}
  patch -p1 -i ../disable-fc-cache.patch
  sed -i 's/lib64/lib/g' configure.ac
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  export CFLAGS="$CFLAGS -w -Wl,--allow-multiple-definition"
  PKG_CONFIG_PATH="/usr/lib/ffmpeg4.4/pkgconfig" \
  ./configure \
    --enable-ffmpeg \
    --enable-gtk3ui \
    --enable-sdl2ui \
    --enable-x64 \
    --libdir=/usr/lib \
    --prefix=/usr
  make
}

package() {
  make -C $pkgname-$pkgver \
    DESTDIR="$pkgdir" \
    realdocdir=/usr/share/doc/vice \
    install
}
