# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=csound-plugins
pkgver=1.0.2
pkgrel=11
pkgdesc="Extra opcodes for Csound"
arch=(loong64 x86_64)
url="https://github.com/csound/plugins"
license=('LGPL-2.1-or-later')
groups=('pro-audio')
depends=(
	'csound'
	'gcc-libs'
	'glibc'
	'hdf5'
	'libx11'
	'python'
	'stk'
)
makedepends=(
	'cmake'
	'eigen'
	'faust'
	'fltk'
	'fluidsynth'
	'git'
	'gmm'
	'jack'
	'lame'
	'libpng'
	'libwebsockets'
	'wiiuse'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/csound/plugins/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('8c2f0625ad1d38400030f414b92d82cfdec5c04b7dc178852f3e1935abf75d30')

build() {
        CFLAGS+=" -Wno-incompatible-pointer-types" \
	cmake -B build -S "plugins-$pkgver" \
		-DCMAKE_BUILD_TYPE='None' \
		-DCMAKE_INSTALL_PREFIX='/usr' \
		-Wno-dev
	make -C build
}

package() {
	depends+=(
		faust libfaust.so
		fltk libfltk.so
		fluidsynth libfluidsynth.so
		jack libjack.so
		lame libmp3lame.so
		libpng libpng16.so
		libwebsockets libwebsockets.so
		wiiuse libwiiuse.so
	)

	make -C build DESTDIR="$pkgdir/" install
}
