# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Taijian <taijian@posteo.de>
# Contributor: Sebastian Lau <lauseb644@gmail.com>
# Contributor: Damian01w <damian01w@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=plymouth
pkgver=24.004.60
pkgrel=7
pkgdesc='Graphical boot splash screen'
arch=('loong64' 'x86_64')
url='https://www.freedesktop.org/wiki/Software/Plymouth/'
license=('GPL-2.0-or-later')
depends=('bash' 'cairo' 'cantarell-fonts' 'filesystem' 'fontconfig' 'freetype2' 'glib2' 'glibc'
         'libdrm' 'libevdev' 'libpng' 'libx11' 'libxkbcommon' 'pango' 'systemd-libs'
         'xkeyboard-config')
makedepends=('gtk3' 'docbook-xsl' 'meson')
optdepends=('gtk3: x11 renderer')
backup=('etc/plymouth/plymouthd.conf')
install='plymouth.install'
source=("https://www.freedesktop.org/software/$pkgname/releases/$pkgname-$pkgver.tar.xz"
        '0001-label-freetype-fix-fallback-not-working-when-fc-matc.patch'
        '0001-renderers-Do-not-assume-all-keyboards-have-LEDs.patch'
        '0001-ply-boot-splash-Set-unbuffered-input-when-creating-a.patch'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'plymouth-shutdown.initcpio_install'
        'mkinitcpio-generate-shutdown-ramfs-plymouth.conf')
sha256sums=('f3f7841358c98f5e7b06a9eedbdd5e6882fd9f38bbd14a767fb083e3b55b1c34'
            '9d5feec6980fb878b827bf8b4df236783afacf9e0d1d47daaad915b8f9702441'
            '0c99366046ac68377ee0348263a535bb06a935cbe0457f254a9f8e2a422edf0c'
            'a261cf07ec57d7cb230b589e7f85a3080125b89248b1fb6191bc38aefc24076e'
            'de852646e615e06d4125eb2e646d0528d1e349bd9e9877c08c5d32c43d288b6f'
            'ecd979b70a613b6aea05443da735e95a8c7341fbc9f099da807bd82394f5d3cf'
            'd75b67c7c28698c1e27864a63f3f405897aea053c346c2478ee97d0caec241d5'
            '91df753b37fa33493a3fa4b966f5535e9694022a4ba083dcc504404ab25715b1')

prepare() {
  cd $pkgname-$pkgver
  # https://gitlab.freedesktop.org/plymouth/plymouth/-/merge_requests/302
  patch -Np1 -i ../0001-label-freetype-fix-fallback-not-working-when-fc-matc.patch

  # https://gitlab.freedesktop.org/plymouth/plymouth/-/merge_requests/304
  patch -Np1 -i ../0001-renderers-Do-not-assume-all-keyboards-have-LEDs.patch

  # https://gitlab.freedesktop.org/plymouth/plymouth/-/merge_requests/303
  patch -Np1 -i ../0001-ply-boot-splash-Set-unbuffered-input-when-creating-a.patch

  # Use mkinitcpio to update initrd
  sed -i 's/^dracut -f$/mkinitcpio -P/' scripts/plymouth-update-initrd

  # Change default theme
  sed -i 's/^Theme=spinner$/Theme=bgrt/' src/plymouthd.defaults
}

build() {
  arch-meson build $pkgname-$pkgver \
    -D logo=/usr/share/pixmaps/archlinux-logo.png
  meson compile -C build

  # Convert logo for the spinner theme
  rsvg-convert '/usr/share/pixmaps/archlinux-logo-text-dark.svg' -o archlinux-logo-text-dark.png
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir/run"

  # Install mkinitcpio hook
  install -Dm644 plymouth.initcpio_hook "$pkgdir/usr/lib/initcpio/hooks/$pkgname"
  install -Dm644 plymouth.initcpio_install "$pkgdir/usr/lib/initcpio/install/$pkgname"

  # Install mkinitcpio shutdown hook and systemd drop-in snippet
  install -Dm644 plymouth-shutdown.initcpio_install "$pkgdir/usr/lib/initcpio/install/$pkgname-shutdown"
  install -Dm644 mkinitcpio-generate-shutdown-ramfs-plymouth.conf "$pkgdir/usr/lib/systemd/system/mkinitcpio-generate-shutdown-ramfs.service.d/plymouth.conf"
  
  # Install logo for the spinner theme
  install -Dm644 archlinux-logo-text-dark.png "$pkgdir/usr/share/$pkgname/themes/spinner/watermark.png"
}
