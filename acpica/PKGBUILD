# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alessio 'mOlOk' Bolognino <themolok@gmail.com>

pkgname=acpica
pkgver=20240321
pkgrel=1
pkgdesc="ACPI tools, including Intel ACPI Source Language compiler"
arch=(loong64 x86_64)
url="https://acpica.org"
license=(GPL-2.0)
depends=(glibc)
makedepends=(flex bison)
provides=(iasl)
conflicts=(iasl)
replaces=(iasl)
options=(!makeflags)
# thanks Intel for making downloading sources so painful...
# https://github.com/acpica/acpica/issues/888
# https://github.com/acpica/acpica/issues/886
# https://github.com/acpica/acpica/issues/883
# https://github.com/acpica/acpica/issues/857
source=(https://downloadmirror.intel.com/819451/$pkgname-unix-$pkgver.tar.gz
        acpica-fix-build.patch)
sha512sums=('9d21cdbf4351f643b7694be0844d2102c9b1ca2afffd5b4453ef8387cc09812ffe2630f0b4489f679e92eab6e5923f306771b60bb2629fba6a1b2e326d52a11e'
            'f3d0e7e95f211e026440698fe08104878ab2d68af504827dba67c048a92da71026485133737f5a62589a913964e3b0cffd6385c65777bfc1454b0aea955bffb0')
b2sums=('ee8fea5bd99c6279843a02399760de6e42a7c13a68d4d9fbbf18cee806b2d69aad7b89647cd23db356927b589d2943348f0cf63c0b7d62ccc21c064bd542dd54'
        'f84e269288dd152d42a7ef7c5f1366fdd26a11654ce4183384786561e6367958ab572c9cad3664ac6fa17bcbb727fcb84954574e46b43da899457f7959a06543')

prepare() {
  cd acpica-unix-${pkgver}
  patch -p1 -i "$srcdir/acpica-fix-build.patch"
#patch -p1 -i "$srcdir/$pkgname-unix-la64.patch"
}

build() {
  NOWERROR=TRUE make -C $pkgname-unix-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-unix-$pkgver
}
