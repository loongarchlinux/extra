# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidictl
pkgver=1.0.0
pkgrel=1
pkgdesc="MIDI remote control application sending MIDI data over network using UDP/IP multicast"
arch=(loong64 x86_64)
url="https://qmidictl.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  qt6-base
  qt6-svg
)
makedepends=(
  cmake
  qt6-tools
)
optdepends=(
  'qt6-wayland: for native wayland support'
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('921af61503e2b0e0444c97dfcdf491886b8942b2110b0ac9075246f4cf7169187577c309eb247e1c2da54568d1b08344a68a02013213ef55ece598163300dbff')
b2sums=('18cebb239634bb0f84a49467513337ae59142fd3a59abdf652d3cf6e5604f2a17a62889b7970e3f844ebb1d9339af3205ccfe93f68890cd2ecf810a53ee8bb6a')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
