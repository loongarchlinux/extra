# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-music
pkgver=8.0.0
pkgrel=1
pkgdesc='The Pantheon Music Player'
arch=(loong64 x86_64)
url=https://github.com/elementary/music
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gst-plugins-base-libs
  gstreamer
  gtk4
  hicolor-icon-theme
  libadwaita-1.so
  libgranite-7.so
)
makedepends=(
  git
  gobject-introspection
  intltool
  meson
  vala
)
optdepends=(
  'gst-plugins-base: "Base" plugin libraries'
  'gst-plugins-good: "Good" plugin libraries'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
)
_tag=5712a4c4771fce300d31ca0ce5bc7edc87c14396
source=(pantheon-music::git+https://github.com/elementary/music.git#tag=${_tag})
b2sums=('3474c3daa83da6e6ef6c4e66258f718630552f40ba0b42384ea252f986bb696bbea7bc85aea1ac8eff686d326eac4df5acdd0c86d26a5f0011505861caf2ed69')

pkgver() {
  cd pantheon-music
  git describe --tags
}

build() {
  arch-meson pantheon-music build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
