# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=biblesync
pkgver=2.1.0
pkgrel=5
pkgdesc='multicast shared co-navigation library for Bible programs'
arch=('loong64' 'x86_64')
url="https://github.com/karlkleinpaste/biblesync"
license=('custom')
makedepends=('cmake' 'git')
depends=('gcc-libs' 'util-linux-libs')
provides=('libbiblesync.so')
source=("git+https://github.com/karlkleinpaste/biblesync.git#tag=${pkgver}")
sha256sums=('867f86996a63e2bac48507b1ca3e8f4498d64691f67f14e3c3610d497e5bfb4a')

build() {
  cmake -B build -S "$pkgname" \
    -DBUILD_SHARED_LIBS=TRUE \
    -DLIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 "$pkgname"/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}
