# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=nox
pkgname=python-$_pkgname
pkgver=2024.04.15
pkgrel=1
pkgdesc='Flexible test automation for Python'
arch=('any')
url='https://github.com/wntrblm/nox'
license=('Apache')
depends=('python' 'python-argcomplete' 'python-colorlog' 'python-virtualenv' 'python-packaging')
optdepends=('python-jinja: tox_to_nox'
            'python-tox: tox_to_nox')
makedepends=('python-build' 'python-installer' 'python-hatchling')
checkdepends=('python-pytest' 'python-jinja' 'python-tox')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('99143b4c372e40c5b436ef786e72582eae3182513e9763cb76eb123dcb4d16de8b0585a21119c3513fe9aae4bb6d9d19b16f3129a42f3addf2878e6e7f044ebb')

build() {
  cd $_pkgname-$pkgver

  python -m build -nw
}

check() {
  cd $_pkgname-$pkgver

  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
# tox 4 isn't supported https://github.com/wntrblm/nox/issues/673
  test-env/bin/python -m pytest -k 'not test_tox_to_nox'
}

package() {
  cd $_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
