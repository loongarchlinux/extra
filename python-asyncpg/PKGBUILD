# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_name=asyncpg
pkgname=python-$_name
pkgver=0.29.0
pkgrel=3
pkgdesc='A fast PostgreSQL Database Client Library for Python/asyncio'
arch=('loong64' 'x86_64')
url='https://github.com/MagicStack/asyncpg'
license=('Apache')
depends=('python' 'python-async-timeout')
makedepends=('cython0' 'python-build' 'python-installer' 'python-setuptools'
'python-wheel')
checkdepends=('python-pytest' 'python-uvloop' 'postgresql')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('5db979496323fcf4068d5de111b52f2ae1ad9efe68f80c28e6303ad936eb051d1792409f1e31fb86dc35caad5ef9b92c40e4f6dcbcccc3907ab2247f61db5504')
b2sums=('9bb5606dfb103e066dcfd15c327fe8a8135bf4433c816dc354d84227e40b9f18bb2788ead0dd25b087f97b3113645b6dcfd8991a7bb6e2e71528d122c41105d5')

prepare() {
  cd $_name-$pkgver

  sed -i "s|'Cython==.*'|'Cython'|" setup.py
}

build() {
  cd $_name-$pkgver

  python -m build -nw
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver

  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
  mv -v $_name $_name-off
  pytest -vv tests/
}

package() {
  cd $_name-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
