# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-asyncpg
_pkgname=${pkgname#python-}
pkgver=0.29.0
pkgrel=5
pkgdesc="A fast PostgreSQL Database Client Library for Python/asyncio"
arch=(loong64 x86_64)
url="https://github.com/MagicStack/asyncpg"
license=(Apache-2.0)
depends=(
  glibc
  python
  python-async-timeout
  python-typing_extensions
)
makedepends=(
  cython
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  postgresql
  python-pytest
  python-uvloop
)
source=(
  "git+$url.git#tag=v$pkgver"
  "git+https://github.com/MagicStack/py-pgproto"
)
sha256sums=(
  '57ce4ef0a1f6095b484fa608fda82e18b9b01c4257841b1044605cf256706de6'
  'SKIP'
)

_archive="$_pkgname"

prepare() {
  cd "$_archive"

  git submodule init
  git config submodule.asyncpg/pgproto.url "$srcdir/py-pgproto"
  git -c protocol.file.allow=always submodule update

  sed -e 's|>=0.29.24,<0.30.0|>=0.29.24|' -e 's|>=0.29.24,<3.0.0|>=0.29.24|' -i setup.py -i pyproject.toml # Drop Cython version constraints
}

build() {
  cd "$_archive"

  python -m build --wheel --no-isolation
}

check() {
  cd "$_archive"

  python -m installer --destdir=tmp_install dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PYTHONPATH="$PWD/tmp_install/$site_packages" pytest
}

package() {
  cd "$_archive"

  python -m installer -d "$pkgdir" dist/*.whl
}
