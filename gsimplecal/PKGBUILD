# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: DrZaius <lou at fakeoutdoorsman dot com>
# Contributor: Victor Feight <vrfeight3 at gmail dot com>

pkgname=gsimplecal
pkgver=2.5.1
pkgrel=2
pkgdesc="Simple and lightweight GTK calendar"
url="https://dmedvinsky.github.io/gsimplecal/"
arch=('loong64' 'x86_64')
license=('BSD-3-Clause')
depends=('gtk3')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dmedvinsky/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('1b7268959022cc7b8a405c9207b07258d2d1488192520baa15bbe9c0b0ff3f0f')

build() {
	cd "${pkgname}-${pkgver}"
	./autogen.sh
	./configure --prefix=/usr
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
