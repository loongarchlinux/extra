# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=luametatex
pkgver=2.11.03
pkgrel=2
pkgdesc='The lean and mean successor of the LuaTeX engine'
arch=(loong64 x86_64)
url='https://wiki.contextgarden.net/LMTX'
license=(GPL-2.0-or-later)
depends=(glibc)
makedepends=(cmake
             git)
source=(git+https://github.com/contextgarden/luametatex#tag=v$pkgver)
sha256sums=('56cb2c5f980e5fbbd39f22eef9d263b4fbe3533617e3c9b1de8d6e71e6137ef8')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  ln -s luametatex "$pkgdir"/usr/bin/context
  ln -s luametatex "$pkgdir"/usr/bin/mtxrun
}
