# vim:set ft=sh:
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: (sirocco AT ngs.ru)

pkgbase=doublecmd
pkgname=('doublecmd-gtk2' 'doublecmd-qt5' 'doublecmd-qt6')
pkgver=1.1.9
pkgrel=1
url="http://doublecmd.sourceforge.net/"
arch=('loong64' 'x86_64')
license=('GPL')
provides=("$pkgbase")
makedepends=('lazarus' 'gtk2' 'qt5pas' 'qt6pas')
optdepends=(
    'lua: scripting'
    'unzip: support extracting zip archives'
    'zip: suport packing zip archives'
    'p7zip: support for 7zip archives'
    'libunrar: support for rar archives'
)
source=(
    "https://downloads.sourceforge.net/project/$pkgbase/Double%20Commander%20Source/$pkgbase-$pkgver-src.tar.gz"
)
sha512sums=('7ac0aa64fb9fe4c3096fc7906fe3559c212be76f218a51a47c7a090f607aed450b66dc93eaf33a4531dbc0a8804f5d6e4615805c397fd1c1db5bed8b4127904d')

prepare() {
    cp -a /usr/lib/lazarus ./

    cd "$pkgbase-$pkgver"
    sed -e 's/LIB_SUFFIX=.*/LIB_SUFFIX=/g' -i install/linux/install.sh
    sed -e "s@=\$(which lazbuild)@=\"\$(which lazbuild) --lazarusdir=$srcdir/lazarus\"@" -i build.sh
    sed -e '/doublecmd.zdli/d' -i install/linux/install.sh
}

build() {
    cd "$srcdir/$pkgbase-$pkgver"
    ./build.sh components
    ./build.sh plugins

    cd "$srcdir"

    cp -a "$pkgbase-$pkgver" "$pkgbase-gtk"
    cp -a "$pkgbase-$pkgver" "$pkgbase-qt5"
    cp -a "$pkgbase-$pkgver" "$pkgbase-qt6"

    # build gtk
    cd "$srcdir/$pkgbase-gtk"
    /usr/bin/lazbuild --lazarusdir="$srcdir/lazarus" \
        src/doublecmd.lpi \
        --bm="NoDebug Full Optimizations" \
        --widgetset=gtk2
    #./build.sh beta gtk2

    # build qt5
    cd "$srcdir/$pkgbase-qt5"
    /usr/bin/lazbuild --lazarusdir="$srcdir/lazarus" \
        src/doublecmd.lpi \
        --bm="NoDebug Full Optimizations" \
        --widgetset=qt5
    #./build.sh beta qt5

    # build qt6
    cd "$srcdir/$pkgbase-qt6"
    /usr/bin/lazbuild --lazarusdir="$srcdir/lazarus" \
        src/doublecmd.lpi \
        --bm="NoDebug Full Optimizations" \
        --widgetset=qt6
    #./build.sh beta qt6
}

package_doublecmd-gtk2() {
    pkgdesc="twin-panel (commander-style) file manager (GTK3)"
    depends=('gtk2' 'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info')
    conflicts=('doublecmd-qt5' 'doublecmd-qt6')
    cd "$srcdir/$pkgbase-gtk"
    ./install/linux/install.sh --install-prefix="$pkgdir"
}

package_doublecmd-qt5() {
    pkgdesc="twin-panel (commander-style) file manager (Qt5)"
    depends=('qt5pas' 'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info')
    conflicts=('doublecmd-gtk2' 'doublecmd-qt6')
    replaces=('doublecmd-qt' 'doublecmd-qt4')
    cd "$srcdir/$pkgbase-qt5"
    ./install/linux/install.sh --install-prefix="$pkgdir"
}

package_doublecmd-qt6() {
    pkgdesc="twin-panel (commander-style) file manager (Qt6)"
    depends=('qt6pas' 'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info')
    conflicts=('doublecmd-gtk2' 'doublecmd-qt5')
    cd "$srcdir/$pkgbase-qt6"
    ./install/linux/install.sh --install-prefix="$pkgdir"
}
