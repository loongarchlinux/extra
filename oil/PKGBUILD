# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yigit Sever <yigit at yigitsever dot com>
# Contributor: Qontinuum <qontinuum@artixlinux.org>
# Contributor: timetoplatypus <timetoplatypus@protonmail.com>
# Contributor: andychu <andy@oilshell.org>

pkgname=oil
pkgver=0.22.0
pkgrel=1
pkgdesc='A new Unix shell'
arch=('loong64' 'x86_64')
url='https://www.oilshell.org'
license=('Apache-2.0')
depends=('glibc' 'gcc-libs' 'readline')
makedepends=('bash')
replaces=('osh')
install=oil.install
source=("$url/download/oils-for-unix-$pkgver.tar.gz")
sha256sums=('7ad64ad951faa9b8fd310fc17df0a93291e041ab75311aca1bc85cbbfa7ad45f')
b2sums=('a5cf432f0d75f6ec8ebc6d5277132bb25074e8bdb545b5fba0498d5665aef32956c30ce9fb3566a4879789c8eebca945be28744dabfa295505a4eea6d75761a7')
noextract=("oils-for-unix-$pkgver.tar.gz")
# https://www.oilshell.org/release/latest/

prepare() {
  # fix: LTO + strip
  mkdir -p path
  cat >path/c++ <<END
#!/bin/sh
exec g++ \$CXXFLAGS \$LDFLAGS -fPIC "\$@"
END
  cat >path/strip <<END
#!/bin/sh
exec cp -p "\$3" "\$2"
END
  chmod +x path/*

  # fix: bsdtar choking on hardlinked files pointing to self, e.g.:
  # _gen/frontend/option.asdl.h: Skipping hardlink pointing to itself: _gen/frontend/option.asdl.h: No such file or directory
  tar -xf "oils-for-unix-$pkgver.tar.gz"
}

build() {
  cd "oils-for-unix-$pkgver"

  ./configure \
    --prefix=/usr \
    --datarootdir=/usr/share

  PATH="$srcdir/path:$PATH" _build/oils.sh
}

package() {
  cd "oils-for-unix-$pkgver"

  DESTDIR="$pkgdir" ./install
}
