# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Nelson Menon <nelsonmenon@gmail.com insanatorium.wordpress.com>

pkgname=freecol
pkgver=1.2.0
pkgrel=2
pkgdesc="A turn-based strategy game based on Colonization"
arch=('any')
url="http://www.freecol.org/"
license=('GPL')
depends=('java-runtime>=11')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.zip
        freecol.sh)
sha512sums=('181a1b8de35d3514cca31c8436affe95190ee62378ac2de5845e25e1ce7c7a966ab50d6cb59566030b622efb3ba3fd2036135affa3fa1459564d38ad34994ff0'
            '90cf7eed5a81e5dd2bc9c609a18713422e98f0447841aaf650c9408f75d9405995cdfc0e471955c50d383de20c79e3454de139c3221cec8a16fbc0c0a9e79fab')

package() {
  cd "$srcdir/$pkgname"

  install -Dm644 FreeCol.jar "$pkgdir/usr/share/java/$pkgname/FreeCol.jar"
  install -Dm644 FreeCol.pdf "$pkgdir/usr/share/doc/$pkgname/FreeCol.pdf"
  install -Dm644 $pkgname.xpm "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
  install -Dm644 freecol.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm755 "$srcdir"/freecol.sh "$pkgdir/usr/bin/$pkgname"

  cp -r data jars "$pkgdir/usr/share/java/$pkgname/"
  find "$pkgdir/usr/share/java/$pkgname" -type d -exec chmod 755 '{}' \;
  find "$pkgdir/usr/share/java/$pkgname" -type f -exec chmod 644 '{}' \;
}
