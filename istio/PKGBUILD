# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Arturo Penen <apenen@gmail.com>
# Contributor: jtagcat <arch-3ff61201@jtag.cat>

pkgname=istio
pkgver=1.22.3
pkgrel=1
pkgdesc='Istio configuration command line utility for service operators to debug and diagnose their Istio mesh'
arch=('loong64' 'x86_64')
url='https://github.com/istio/istio'
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('8ed38bb4dac2258509757f924c54b8bc8352da096f10ce898fe8e54de7510970f59574b9503c23549121bd5f95dd98aa9d01f2b85d17c4b470b74ef4a6a80022')
b2sums=('1dfa924fbcd6e04123e58c93214ecc5bec4ba3e7b8978fa9b0d2c165ac164d27206414c6cba45aa7f230cba9cbbf7f3a72260443f9d6ce5e3562eba2b8ba6827')

prepare() {
  cd $pkgname-$pkgver

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname-$pkgver

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  # shellcheck disable=SC2153
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"

  mkdir -p build
  local ldflags=" \
    -compressdwarf=false  \
    -linkmode=external  \
    -X istio.io/istio/pkg/version.buildVersion=$pkgver \
  "
  go build -v -buildvcs=false -o build -ldflags="$ldflags" \
    ./istioctl/cmd/istioctl
}

check() {
  cd $pkgname-$pkgver

  # Test that the exectuble reports the correct version.
  test "$(./build/istioctl version --remote=false)" = "$pkgver"

  # Disable unit tests failing in a chroot - not sure why.
  local unit_tests=$(
    go list ./istioctl/... \
      | grep -v istio.io/istio/istioctl/pkg/multicluster \
      | grep -v istio.io/istio/istioctl/pkg/tag
  )
  # shellcheck disable=SC2086
  go test -v $unit_tests
}

package() {
  install -Dm 755 "$srcdir/$pkgname-$pkgver/build/istioctl" "$pkgdir/usr/bin/istioctl"

  # Populate bash and zsh completions.
  "$pkgdir/usr/bin/istioctl" collateral --bash
  "$pkgdir/usr/bin/istioctl" collateral --zsh
  install -Dm 644 istioctl.bash "${pkgdir}/usr/share/bash-completion/completions/istioctl"
  install -Dm 644 _istioctl "${pkgdir}/usr/share/zsh/site-functions/_istioctl"
}
