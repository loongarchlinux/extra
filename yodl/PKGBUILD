# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Controbutor: Baptiste Jonglez <archlinux at bitsofnetworks.org>
# Contributor: nl6720 <nl6720@gmail.com>
# Contributor: Andreas B. Wagner <AndreasBWagner@pointfree.net>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>

pkgname=yodl
pkgver=4.03.03
pkgrel=4
pkgdesc='Implements a pre-document language and tools to process it.'
arch=(loong64 x86_64)
url='https://fbb-git.gitlab.io/yodl/'
license=(AGPL-3.0-or-later)
depends=(bash)
makedepends=(icmake)
_archive="$pkgname-$pkgver"
source=("https://gitlab.com/fbb-git/$pkgname/-/archive/$pkgver/$_archive.tar.gz")
sha512sums=('5ba3a70152e3820dc08e2da821f357ec00c1f0f500e52bc4cc5a36f5d4ea8cdc13d564f5678a306cfb621d30bc53dd85f6756a6bbfd6825f63237d9217deaf3b')

build() {
	cd "$_archive/$pkgname"
        CXXFLAGS+=' --std=c++20'
        export CXXFLAGS
	./build programs
	./build macros
	./build man
	./build html
}
package() {
	cd "$_archive/$pkgname"
	./build install programs "$pkgdir"
	./build install macros "$pkgdir"
	./build install man "$pkgdir"
	./build install manual "$pkgdir"
}
