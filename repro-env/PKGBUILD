# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=repro-env
pkgver=0.4.1
pkgrel=1
pkgdesc="Dependency lockfiles for reproducible build environments"
url='https://github.com/kpcyrd/repro-env'
arch=('loong64' 'x86_64')
license=('GPL-3.0-or-later')
depends=(
  'catatonit'
  'gcc-libs'
  'glibc'
  'podman'
)
makedepends=(
  'cargo'
  'scdoc'
)
options=(!lto)
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")
sha256sums=('391f28817691e57f2635611cee3c4e0783b14fd08147d11b1093f061dfe597b1'
            'SKIP')
b2sums=('ff62d864f8c6f199f156db7f6b4334b9c4746095c953320db3ac30f66312e73dd95a541b27832057f038091479c1275e9b89c096c63cd48d17254cd24680e310'
        'SKIP')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
  make docs
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen
  target/release/repro-env --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/repro-env

  install -Dm 644 -t "${pkgdir}/usr/share/man/man1" \
    docs/repro-env.1

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completions bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
