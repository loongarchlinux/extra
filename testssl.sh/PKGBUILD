# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Johannes Schleifenbaum <johannes [at] js-webcoding [dot] de>

pkgname=testssl.sh
_pkgver=3.0.9
pkgver=${_pkgver/-/.}
pkgrel=1
pkgdesc='Testing TLS/SSL encryption'
url=https://github.com/drwetter/testssl.sh
arch=(any)
license=(GPL-2.0-only)
depends=(
  bash
  bind-tools
  coreutils
  openssl
  procps-ng
  util-linux
)
makedepends=(
  perl
  perl-data-dump
  perl-json
  perl-test-base
)
source=(
  "https://github.com/drwetter/testssl.sh/archive/v${_pkgver}/${pkgname}-${pkgver}.tar.gz"
  set-install-dir.patch
)
sha512sums=('006180d175492f1cba6d056a92a57247725d4b68cddbebeea80a5d339d542f5103ddc8226f3a554775cb83bbef836a5d5bfc1bcf4fba7b18b9541473122ff720'
            '82bb83a5553bdc8d02cb655c76163ac9c86b0fc1cff9c5627cb035eae016c5a8b76a3a9e278de4578779bf63a35548815a53911288872530d65c1100c065f621')
b2sums=('424569411022b68f105a20ad5b60db7c8994d1170482612ae7aed741230bd3fca43cd853dfb00660d5ba7cfdc717fa887a2fe9b6c59bbcef881f8f6473ccad3e'
        '26f90e385085e0c50bd877b37e85f489177bcef0be90baad096c6f3e95732542e2adcaf2aa03ba75d264607d869ea479b768834ba476e258eea291c452f4f3b9')

prepare() {
  cd ${pkgname}-${_pkgver}
  patch -p0 < "${srcdir}/set-install-dir.patch"
  rm bin/openssl.Linux*
}

#check() {
#  cd ${pkgname}-${_pkgver}
#  # the bundled openssl segfaults in the tests
#  TESTSSL_INSTALL_DIR="$(pwd)" prove -v
#}

package() {
  cd ${pkgname}-${_pkgver}
  install -Dm 755 testssl.sh "${pkgdir}/usr/bin/testssl"
  install -Dm 644 etc/* -t "${pkgdir}/usr/share/testssl.sh/etc"
  install -Dm 644 Readme.md doc/testssl.1.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/testssl.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
