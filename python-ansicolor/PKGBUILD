# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Darshit Shah <darnir@gmail.com>

pkgname=python-ansicolor
_name=${pkgname#python-}
pkgver=0.3.2
pkgrel=6
pkgdesc='Library to produce ansi color output and colored highlighting and diffing'
arch=('any')
url=https://github.com/numerodix/ansicolor
license=('Apache-2.0')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("git+$url.git#tag=$pkgver")
b2sums=('1826b94e1b970aa01263c974816047d7ac2cb8f175ff50cb7fa8c1cf53e3756e80b323a2944e14ccc77c4cd76500f5ca0d5972241ad4d90191059705287afc98')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl
}
