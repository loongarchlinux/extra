# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-calendar
pkgver=8.0.0
pkgrel=1
pkgdesc='The Pantheon Calendar'
arch=(loong64 x86_64)
url=https://github.com/elementary/calendar
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  clutter
  clutter-gtk
  dconf
  evolution-data-server
  folks
  gdk-pixbuf2
  geoclue
  geocode-glib-2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libchamplain
  libfolks.so
  libgee
  libgranite.so
  libhandy-1.so
  libical
  libportal
  libportal-gtk3
)
makedepends=(
  cmake
  git
  intltool
  meson
  vala
)
_tag=a8741655fef3ab18e57c2873e40fa6a9168447a8
source=(pantheon-calendar::git+https://github.com/elementary/calendar.git#tag=${_tag})
sha256sums=('c03ee2d9dca8dd288bf8d4a2030b307f1c1e7b7942df9477f641573764935353')

pkgver() {
  cd pantheon-calendar
  git describe --tags
}

build() {
  arch-meson pantheon-calendar build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
