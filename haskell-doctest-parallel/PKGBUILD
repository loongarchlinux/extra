# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=doctest-parallel
pkgname=haskell-doctest-parallel
pkgver=0.3.1
pkgrel=10
pkgdesc="Test interactive Haskell examples"
url="https://github.com/martijnbastiaan/doctest-parallel#readme"
license=("MIT")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-glob' 'haskell-base-compat' 'haskell-code-page'
         'haskell-ghc' 'haskell-ghc-paths' 'haskell-random' 'haskell-syb'
         'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'expac' 'haskell-hunit' 'haskell-quickcheck' 'haskell-hspec'
             'haskell-hspec-core' 'haskell-mockery' 'haskell-setenv' 'haskell-silently'
             'haskell-stringbuilder')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('0dc7dcfe7de99c68e8b91657914d7189c3957986fce93d2144a6192645e47151')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  # == An ugly hack to generate .ghc.environment without cabal-install ==
  # doctest-parallel relies on this cabal-install feature to configure GHCi.
  # https://github.com/martijnbastiaan/doctest-parallel/issues/22
  ghc_version=$(expac %v ghc | cut -d - -f 1)
  _ghc_env_filename=.ghc.environment.$CARCH-linux-$ghc_version
  echo -e "package-db dist/package.conf.inplace" > $_ghc_env_filename
  ls dist/package.conf.inplace/*.conf | sed 's|.*/\(.*\).conf$|package-id \1|' >> $_ghc_env_filename
  ls /usr/lib/ghc-$ghc_version/lib/package.conf.d/*.conf | sed 's|.*/\(.*\).conf$|package-id \1|' >> $_ghc_env_filename

  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
