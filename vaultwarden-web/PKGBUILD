# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
_pkgver=2024.6.2b
pkgver=2024.6.2.b
_upstreamver=${_pkgver%[[:lower:]]}
_patchver=$_upstreamver
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with Vaultwarden'
arch=(any)
url=https://github.com/dani-garcia/bw_web_builds
license=(GPL-3.0-only)
depends=(vaultwarden)
makedepends=(
  git
  nodejs
  npm
)
install=$pkgname.install
source=(
  "bitwarden-clients::git+https://github.com/bitwarden/clients.git#tag=web-v$_upstreamver"
  "git+https://github.com/dani-garcia/bw_web_builds#tag=v$_pkgver"
)
b2sums=('7fd09ee856e82fb1eaf8022b6ed6846819bc3f11b7bcdd684bb8e6cd53b2e98b4cc2e46f45fcb5b6e75dae3b68a05ae887a7e49fad7a91a1f2558a88d9aa7c1a'
        '4490f5dda526cd9f71923dd9fdd7e6f3976ca10676dc776ab240b2f2e815cffdf9bdbcbb3b5306c811eaa1ab4915bd3a3dfc05a8468967f20ef8f04b3124e43a')

# Appease vercmp by prepending trailing letter with a period
# shellcheck disable=SC2001
pkgver() {
  echo $_pkgver | sed 's/[[:lower:]]$/.&/'
}

prepare() {
  cd bitwarden-clients

  # Copy Vaultwarden images
  cp -vr ../bw_web_builds/resources/src/images/{logo-{dark,white}@2x,icon-white}.png \
    apps/web/src/images

  # Apply Vaultwarden patch
  patch --forward --strip=1 --input=../bw_web_builds/patches/v$_patchver.patch

  npm ci
}

build() {
  cd bitwarden-clients/apps/web

  # https://nodejs.org/api/cli.html#--max-old-space-sizesize-in-megabytes
  # Workaround for "JavaScript heap out of memory" on 32-bit systems
  if [[ $(getconf LONG_BIT) -eq 32 ]]; then
    export NODE_OPTIONS="--max-old-space-size=1536"
  fi
  npm run dist:oss:selfhost
}

package() {
  install -d "$pkgdir"/usr/share/webapps/$pkgname

  cp -R bitwarden-clients/apps/web/build/* "$pkgdir"/usr/share/webapps/$pkgname
}
