# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=topcom
pkgver=1.1.2
pkgrel=2
pkgdesc='A package for computing Triangulations Of Point Configurations and Oriented Matroids'
arch=(loong64 x86_64)
url='https://www.wm.uni-bayreuth.de/de/team/rambau_joerg/TOPCOM/index.html'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         gmp)
source=(https://www.wm.uni-bayreuth.de/de/team/rambau_joerg/TOPCOM-Downloads/TOPCOM-${pkgver//./_}.tgz)
sha256sums=('4fb10754ee5b76056441fea98f2c8dee5db6f2984d8c14283b49239ad4378ab6')

build() {
  cd $pkgname-$pkgver
  CXXFLAGS+=' -ffat-lto-objects' \
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
# Rename binaries with common names
  for _prog in binomial cross cube cyclic lattice; do
    mv "$pkgdir"/usr/bin/{,topcom-}$_prog
  done
  rm "$pkgdir"/usr/bin/check
}
