# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-applets
pkgver=3.52.0
pkgrel=1
pkgdesc='Small applications for the GNOME Panel'
arch=('loong64' 'x86_64')
url='https://wiki.gnome.org/Projects/GnomeApplets'
license=('GPL-2.0-or-later')
depends=('at-spi2-core' 'cairo' 'cpupower' 'dconf' 'gdk-pixbuf2' 'glib2' 'glibc' 'gnome-panel'
         'gsettings-desktop-schemas' 'gtk3' 'hicolor-icon-theme' 'libgtop' 'libgweather-4'
         'libnotify' 'libwnck3' 'libx11' 'libxml2' 'pango' 'polkit' 'tracker3' 'upower')
makedepends=('itstool')
optdepends=('tracker3-miners: Search bar')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
        'gnome-applets-98.patch')
sha256sums=('6f3d3b428656ff6d5b1d3ee5ccb7ece3d2b18b54bf0457acf43b711e55e2d62c'
            'a0ea3eeeda6c09e156c7713b45998de7674bcd4c720d1bb98c9ce97703bcf4db')

prepare() {
  cd $pkgname-$pkgver

  # Destroy preferences window on dispose
  # https://gitlab.gnome.org/GNOME/gnome-applets/-/merge_requests/98
  patch -Np1 -i ../gnome-applets-98.patch

  # Regenerate resources file
  rm gnome-applets/ga-resources.{c,h}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
