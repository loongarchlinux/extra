# Maintainer: David Runge <dvzrv@archlinux.org>

_name=link
pkgname=abletonlink
pkgver=3.1.2
pkgrel=1
pkgdesc="Synchronizes musical beat, tempo, and phase across multiple applications"
arch=(any)
url="https://github.com/ableton/link"
license=(GPL-2.0-or-later)
depends=(asio)
makedepends=(
  catch2
  cmake
  jack
  portaudio
  qt5-tools
  qt5-quickcontrols
)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/Ableton/$_name/archive/Link-$pkgver.tar.gz
  $pkgname-3.1.1-devendor_asio_and_catch2.patch
  $pkgname-3.0.5-cmake_system_paths.patch
)
sha512sums=('889aa8cf56df19631a15cc4e245f3b7165a1d08aa199446de3b209c5be58904c11776899e9202900e73cc90ea63d366c6c3b2628657dac96db5a16a5217b3df7'
            'a2135e854941b126d87210920d7c359f344d651cabeb81624f0b4afea1e54190f1c46bbe565a0d24546be5805238bd0fadc1bd3acd4916dd8fe56d0dc7ae722f'
            '826a6e83b6ad859197475551b82f8391844d8d3717a1f2df4a7b16d77d47a1a557ce4c7231a68fa11b7d1112a10746e23875d387efa50af8735df150bb26a6cd')
b2sums=('5c1e666f9e2ef01836fc1943bb4d41752aabd29aa1a81432bdc6f8dbc9c9178d0c894a4d36fdd0647cfff51a8703db3c92a40867367ba72fe842e898c5982a02'
        'b71e960fabc078ba298694557812944df4b62b384b02f0a9c8344d0ceee59b43a1266330efb6dede4f7453033df179f927536e5beb58752543b0d309f9435990'
        '63c23dddf60ebe055f9087f7fe5c7519773a3af055059025d283bb02581a25d3fd0ffec8be2aecde43fb1427400bdaadaee9d8387364d81725484ea5f38bd13a')

prepare() {
  # use system-wide provided versions of asio and catch2
  patch -Np1 -d $_name-Link-$pkgver -i ../$pkgname-3.1.1-devendor_asio_and_catch2.patch

  # the cmake integration is used to build tests and examples, so duplicating
  # before patching for later providing it system-wide
  cp -av $_name-Link-$pkgver $_name-Link-$pkgver-system

  # provide cmake integration with system-wide paths (for other projects)
  patch -Np1 -d $_name-Link-$pkgver-system -i ../$pkgname-3.0.5-cmake_system_paths.patch
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_INSTALL_PREFIX=/usr
    -D LINK_BUILD_ASIO=ON
    -D LINK_BUILD_JACK=ON
    -D LINK_BUILD_QT_EXAMPLES=ON
    -S $_name-Link-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  build/bin/LinkCoreTest
  build/bin/LinkDiscoveryTest
}

package() {
  (
  cd $_name-Link-$pkgver
  find include -type f \( -iname "*.ipp" -o -iname "*.hpp" \) -exec install -vDm 644 {} "$pkgdir/usr/"{} \;
  install -vDm 644 {{README,CONTRIBUTING}.md,*.pdf} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 cmake_include/AsioStandaloneConfig.cmake -t "$pkgdir/usr/lib/cmake/$pkgname/cmake_include"
  )

  # modified cmake integration for system-wide use
  install -vDm 644 $_name-Link-$pkgver-system/AbletonLinkConfig.cmake -t "$pkgdir/usr/lib/cmake/$pkgname/"
}
