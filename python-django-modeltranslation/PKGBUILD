# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-django-modeltranslation
pkgver=0.19.6
pkgrel=1
pkgdesc='Translates Django models using a registration approach'
arch=(any)
url=https://github.com/deschler/django-modeltranslation
license=(BSD)
depends=(python-django)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=28d58f8fa95aa0590b19aff686314593f89760b9
source=(git+https://github.com/deschler/django-modeltranslation.git#tag=${_tag})
sha256sums=('4c022307f8ee75d0b124c23a7d2426e47cb674d11c70d826ddf714d918e11f26')

pkgver() {
  cd django-modeltranslation
  git describe --tags | sed 's/^v//'
}

build() {
  cd django-modeltranslation
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" django-modeltranslation/dist/*.whl
  install -Dm 644 django-modeltranslation/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/python-django-modeltranslation/
}

# vim: ts=2 sw=2 et:
