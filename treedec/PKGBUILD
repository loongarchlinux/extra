# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=treedec
pkgver=0.9.3
pkgrel=1
pkgdesc='Algorithms for computing tree decompositions of graphs'
arch=(any)
url='https://github.com/freetdi/treedec/'
license=(GPL-2.0-only
         GPL-3.0-only)
depends=()
makedepends=(boost
             git)
conflicts=(tdlib)
provides=(tdlib)
replaces=(tdlib)
source=(git+https://gitlab.com/freetdi/treedec#tag=$pkgver
        no-stderr.patch)
sha256sums=('3e7d781d4922d7b15d34e5c0f8e108fe68f085e476d521260191a98d979c16ba'
            '361471a81d187978a8a1a86c2893f3db3706c17ca9fb50eac8e6f22296626737')

prepare() {
  cd $pkgname
  patch -p1 -i ../no-stderr.patch # Remove debug output
  ./bootstrap
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir"
}
