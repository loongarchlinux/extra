# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-kuwo
pkgver=0.2.2
pkgrel=3
pkgdesc="Kuwo music provider for FeelUOwn music player"
arch=('any')
url="https://github.com/feeluown/feeluown-kuwo"
license=('GPL3')
depends=('feeluown' 'python-marshmallow' 'python-requests')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
groups=('feeluown-full')
source=("https://github.com/feeluown/feeluown-kuwo/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('17695be095f7816bf59dae4b2ab6f5c589ffe3dcdc1bde541ae71d161db6428c2e0c5fcc330b61a53d756b5eaaa6447e84fda44ada28480bf536bb060a0d17e3')

build() {
  cd feeluown-kuwo-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd feeluown-kuwo-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
