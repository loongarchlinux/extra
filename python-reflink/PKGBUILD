# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-reflink
pkgver=0.2.2
pkgrel=3
pkgdesc='Reflink support for Python'
arch=(loong64 x86_64)
url=https://gitlab.com/rubdos/pyreflink
license=(MIT)
depends=(
  glibc
  python
  python-cffi
)
makedepends=(
  git
  python-setuptools
  python-wheel
)
_tag=f49e834dea6be63c7eb7d31e344232820514880d
source=(git+https://gitlab.com/rubdos/pyreflink.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  sed '/pytest-runner/d' -i pyreflink/setup.py
}

pkgver() {
  cd pyreflink
  git describe --tags
}

build() {
  cd pyreflink
  python setup.py build
}

package() {
  cd pyreflink
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-reflink/
}

# vim: ts=2 sw=2 et:
