# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=cosign
pkgver=2.2.4
pkgrel=1
pkgdesc="Container Signing with support for ephemeral keys and Sigstore signing"
arch=('loong64' 'x86_64')
url="https://github.com/sigstore/cosign"
license=('Apache')
depends=('pcsclite')
makedepends=('go' 'git')
checkdepends=('go-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('3a2e2e7ea3964428bc7c4adb9475fbb730cbe2b7dfa7a5eb5cf350d88a4f3a9aef64f00574c7f38bf0074cb3e386cd26e6cfe4558ce5989ea557b654aa82ef71')
b2sums=('22522fc70ff6b01957af2e294b7fdad740d55cc9710f9023246855dd9383be0688934f27e5b32cde3383cdfe3c02cd2de5547a515a14c1442ac85afc9e566b8f')
options=('!lto')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_ENABLED=1
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external -ldflags=-buildid='' -ldflags=-X=sigs.k8s.io/release-utils/version.gitVersion=v$pkgver"
  mkdir build
  go build -o build/ ./cmd/cosign
}

# Disabling tests, because of a segfault
#check() {
#  cd "${pkgname}-${pkgver}"
#  GODEBUG=x509sha1=1 make test # we have to allow sha1 certificates for tests
#}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/cosign "${pkgdir}/usr/bin/${pkgname}"

  # build bash completions
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  "$pkgdir"/usr/bin/cosign completion bash > "${pkgdir}/usr/share/bash-completion/completions/cosign"

  # build zsh completions
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions"
  "$pkgdir"/usr/bin/cosign completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_cosign"

  # build fish completions
  mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d/"
  "$pkgdir"/usr/bin/cosign completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/cosign.fish"
}

