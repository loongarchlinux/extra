# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgbase=lua-vstruct
_rockname=${pkgbase#lua-}
pkgname=("$pkgbase" "lua51-$_rockname" "lua52-$_rockname" "lua53-$_rockname")
pkgver=2.2.0
_rockrel=1
pkgrel=1
pkgdesc='Lua library to manipulate binary data'
arch=(any)
url="https://github.com/ToxicFrog/$_rockname"
license=(MIT)
makedepends=(lua
             lua51
             lua52
             lua53
             luarocks)
_archive="$_rockname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('54ade004665b62b2253d403f3e468631caa486a45a2b873818e55097e7b679bf')

_package() {
	cd "$_archive"
	depends=("${pkgname%-*}")
	luarocks --lua-version "$1" --tree "$pkgdir/usr/" \
		make --deps-mode none --no-manifest "$_archive-$_rockrel.rockspec"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" COPYING
}

package_lua-vstruct() {
	_package 5.4
}

package_lua51-vstruct() {
	_package 5.1
}

package_lua52-vstruct() {
	_package 5.2
}

package_lua53-vstruct() {
	_package 5.3
}
