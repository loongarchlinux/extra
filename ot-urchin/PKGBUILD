# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=ot-urchin
pkgname=(
  'ot-urchin-docs'
  'ot-urchin-standalone'
  'ot-urchin-clap'
  'ot-urchin-vst3'
)
pkgver=1.0.2
pkgrel=1
pkgdesc='A hybrid drum synth that models the gritty lo-fi sound of vintage beats'
arch=('loong64' 'x86_64')
url='https://punklabs.com/ot-urchin'
license=('GPL-3.0-or-later')
groups=('pro-audio')
_common_depends=(
  'glibc'
  'gcc-libs'
  'libx11'
  'libxcb'
  'xcb-util-wm'
  'libglvnd'
)
makedepends=(
  'rust'
  'libglvnd'
  'python'
  'alsa-lib'
  'jack'
  'libx11'
  'libxcb'
  'libxcursor'
  'xcb-util-wm'
)
source=(
  "$pkgbase-$pkgver.zip::https://punklabs.com/content/projects/ot-urchin/downloads/OneTrickURCHIN-Source-v$pkgver.zip"
  "$pkgbase-$pkgver-UserGuide.pdf::https://punklabs.com/content/projects/ot-urchin/downloads/OneTrick%20URCHIN%20User%20Guide.pdf"
)
sha512sums=('cd195bff490cc621479fdd93bb3fcf6c58b1b4c9dcf7730bea99a170121afd151f69d826cc962cb3b297c738497f22ec19b4ded99beeabebaa09fbfe4a8695e2'
            '10b9902c5c786c42573fe5402727b2582b87489f1482df923086541cc6b0258b5424965897dbe2a5d2baeb98db17f51b832bba8c75e0cea4dbb58c009fefda96')
b2sums=('81f7e04e1ac0f76a95273347abd3988002820cdad75d5b53b743890bbf63612d0d46346197be902461ba09cdfb5f88a319a43a4e27f018e7ac4adf52f3b20af7'
        'f6a51ace8c7279b488d33fa814ee42990e372bda2fbe0ebe9c9623823b1118cba447fee90559430e5c477f0103f48bd342843ceaf1788f917cb6754929943c93')

prepare() {
  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cargo xtask bundle onetrick_urchin --release
}

package_ot-urchin-docs() {
  pkgdesc+=' - documentation'

  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.txt
  install -vDm644 "$pkgbase-$pkgver-UserGuide.pdf" "$pkgdir/usr/share/doc/$pkgbase/UserGuide.pdf"
}

package_ot-urchin-standalone() {
  pkgdesc+=' - standalone'
  depends=("${_common_depends[@]}")
  optdepends=(
    'jack: audio backend'
    'alsa-lib: audio backend'
    'ot-urchin-docs: documentation'
  )

  # standalone
  install -vDm755 -t "$pkgdir/usr/bin" target/bundled/OneTrick\ URCHIN
}

package_ot-urchin-clap() {
  pkgdesc+=' - CLAP plugin'
  groups+=('clap-plugins')
  depends=(
    "${_common_depends[@]}"
    'clap-host'
  )
  optdepends=('ot-urchin-docs: documentation')

  # CLAP
  install -vDm644 -t "$pkgdir/usr/lib/clap" target/bundled/OneTrick\ URCHIN.clap
}

package_ot-urchin-vst3() {
  pkgdesc+=' - VST3 plugin'
  groups+=('vst3-plugins')
  depends=(
    "${_common_depends[@]}"
    'vst3-host'
  )
  optdepends=('ot-urchin-docs: documentation')

  # VST3
  install -vd "$pkgdir/usr/lib/vst3"
  cp -vr target/bundled/OneTrick\ URCHIN.vst3 "$pkgdir/usr/lib/vst3"
}
