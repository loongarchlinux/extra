# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sage-notebook-exporter
pkgver=3.4
pkgrel=3
pkgdesc='Jupyter extension to export notebooks from SageNB'
arch=(any)
url='https://github.com/vbraun/ExportSageNB'
license=(GPL3)
depends=(jupyter-nbconvert ipython python-six)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(https://github.com/vbraun/ExportSageNB/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('a996464922d4894c0dc5184a23c1ac95c087c401e6fb9861cf57021cc5515a4a')

build() {
  cd ExportSageNB-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd ExportSageNB-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  rm -r "$pkgdir"/usr/lib/python*/site-packages/test
}
