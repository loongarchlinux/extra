# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openpgp-card-tools
pkgver=0.11.3
pkgrel=1
pkgdesc="CLI tool to inspect, manage and use OpenPGP cards"
arch=(loong64 x86_64)
url="https://codeberg.org/openpgp-card/openpgp-card-tools"
license=('Apache-2.0 OR MIT')
depends=(
  ccid
  gcc-libs
  glibc
)
makedepends=(
  cargo
  pcsclite
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('68dd38a86f3e969a8c05f5d46704eb8b7e04d61af218f86c9ec9baba84ce01717d217be76e5cf642cba285946ea53ed27551cced3b18dac83bc2712bc52c95cf')
b2sums=('ae725e15a839055c37874a525f1ebf166ff2649154acdc3f9125515aed788b9daa91253edefb34ae753d06018832692a18f4cc4b9e4ae396f6d2eafc5ddd3c65')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release
  OCT_MANPAGE_OUTPUT_DIR="$PWD/../man/" cargo run
  OCT_COMPLETION_OUTPUT_DIR="$PWD/../completion/" cargo run
}

package() {
  depends+=(
    pcsclite libpcsclite.so
  )

  cd $pkgname
  install -vDm 755 target/release/oct -t "$pkgdir/usr/bin/"
  install -vDm 644 {scripting,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 ../man/*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 ../completion/oct.bash "$pkgdir/usr/share/bash-completion/completions/oct"
  install -vDm 644 ../completion/_oct -t "$pkgdir/usr/share/zsh/site-functions/"
  install -vDm 644 ../completion/oct.fish -t "$pkgdir/usr/share/fish/vendor_completions.d/"
}
