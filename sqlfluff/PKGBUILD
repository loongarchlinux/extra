# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Morteza NourelahiAlamdari <m@0t1.me>

pkgname=sqlfluff
pkgver=3.0.4
pkgrel=1
pkgdesc='A dialect-flexible and configurable SQL linter'
arch=('any')
url='https://www.sqlfluff.com'
license=('MIT')
depends=(
  'python'
  'python-appdirs'
  'python-chardet'
  'python-click'
  'python-colorama'
  'python-diff-cover'
  'python-jinja'
  'python-pathspec'
  'python-pytest'
  'python-pyyaml'
  'python-regex'
  'python-tblib'
  'python-toml'
  'python-tqdm'
  'python-typing_extensions'
)
makedepends=(
  'git'
  'python-build'
  'python-wheel'
  'python-installer'
  'python-setuptools'
)
checkdepends=('python-hypothesis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sqlfluff/sqlfluff/archive/refs/tags/$pkgver.tar.gz")
b2sums=('38763f5cf11cf3373cc78e92cfb5161f4f26776b96ee06b699633b0c732a8e37c88e0c0143029671d32801bd12bd31368dbf34072d824fa173f476cd5e1395c2')

build() {
  cd "$pkgname-$pkgver"

  python \
    -m build \
    --wheel \
    --no-isolation
}

check() {
  cd "$pkgname-$pkgver"

  # install to temporary directory for access to sqlfluff executable
  python \
    -m installer \
    --destdir "$PWD/tmpdir" \
    dist/*.whl
  local PATH="$PWD/tmpdir/usr/bin:$PATH"

  # skip failing tests for now
  PYTHONPATH="$PWD/src:$PYTHONPATH" pytest \
    --ignore test/core/plugin_test.py \
    --ignore plugins \
    --deselect test/test_testing.py::test_rules__test_helper_has_variable_introspection
}

package() {
  cd "$pkgname-$pkgver"

  python \
    -m installer \
    --destdir "$pkgdir" \
    dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
