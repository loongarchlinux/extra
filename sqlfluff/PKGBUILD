# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Morteza NourelahiAlamdari <m@0t1.me>

pkgname=sqlfluff
pkgver=3.1.0
pkgrel=1
pkgdesc='A dialect-flexible and configurable SQL linter'
arch=('any')
url='https://github.com/sqlfluff/sqlfluff'
license=('MIT')
depends=(
  'python'
  'python-appdirs'
  'python-chardet'
  'python-click'
  'python-colorama'
  'python-diff-cover'
  'python-jinja'
  'python-pathspec'
  'python-pluggy'
  'python-pytest'
  'python-yaml'
  'python-regex'
  'python-tblib'
  'python-toml'
  'python-tqdm'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-hypothesis')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
b2sums=('f6a378f59c43c6ae634099c8a19b78579c58dbd16e3a9521583e4984d53ff6213ef7fb0cdc075c061a908443401debce68bdc16015aa45a557c038d5617147b0')

build() {
  cd "$pkgname-$pkgver"

  python \
    -m build \
    --wheel \
    --no-isolation
}

check() {
  cd "$pkgname-$pkgver"

  # install to temporary directory for access to sqlfluff executable
  python \
    -m installer \
    --destdir "$PWD/tmpdir" \
    dist/*.whl

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PATH="$PWD/tmpdir/usr/bin:$PATH"
  export PYTHONPATH="$PWD/tmpdir/$site_packages"
  pytest \
    --deselect='test/core/plugin_test.py::test__plugin_default_config_read[Example_L001-forbidden_columns]' \
    --deselect='test/core/plugin_test.py::test__plugin_example_rules_returned[Rule_Example_L001]' \
    --deselect='test/core/plugin_test.py::test__plugin_manager_registers_example_plugin'
}

package() {
  cd "$pkgname-$pkgver"

  python \
    -m installer \
    --destdir "$pkgdir" \
    dist/*.whl

  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
