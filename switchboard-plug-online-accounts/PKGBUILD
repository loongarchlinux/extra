# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-online-accounts
pkgver=6.5.3.r66.2a1461a8
pkgrel=1
pkgdesc='Switchboard Online Accounts Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-onlineaccounts
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita-1.so
  libcamel-1.2.so
  libedataserver-1.2.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_commit=2a1461a88b107c0dd629c122fc06ed7ed5c1433a
source=(switchboard-plug-online-accounts::git+https://github.com/elementary/switchboard-plug-onlineaccounts.git#commit=${_commit})
b2sums=(SKIP)

pkgver() {
  cd switchboard-plug-online-accounts
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-online-accounts build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
