# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=7.1.3.r184.48249e8e
pkgrel=1
pkgdesc='The Pantheon Window Manager'
arch=(loong64 x86_64)
url=https://github.com/elementary/gala
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  graphene
  gtk3
  gtk4
  libcanberra
  libgee
  libgl
  libgranite.so
  libgranite-7.so
  libhandy-1.so
  libxfixes
  mutter
  sqlite
  systemd-libs
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
_commit=48249e8efc18ba9b057dbdbc8cc8d622638d3cb1
source=(git+https://github.com/elementary/gala.git#commit=${_commit})
b2sums=('4392067affe536dadd16e87c810fcedf9a755d85e8473444e2ec036a3a8685d3d390306d81f266fd25b7b7a799b8bfa5b69b3f4e76e709d7af182bfd6395824f')

pkgver() {
  cd gala
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson gala build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:
