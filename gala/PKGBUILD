# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=7.1.3
pkgrel=2
pkgdesc='The Pantheon Window Manager'
arch=(loong64 x86_64)
url=https://github.com/elementary/gala
license=(GPL-3.0-or-later)
groups=(pantheon)
depends=(
  atk
  bamf
  cairo
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  json-glib
  libcanberra
  libdrm
  libgee
  libgexiv2
  libgl
  libgranite.so
  libx11
  libxcomposite
  libxdamage
  libxext
  libxfixes
  libxi
  libxrandr
  libxtst
  mesa
  mutter45
  pango
  plank
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
_tag=167862ae223b3483336ad3ed45bc2326ac3c167a
source=(git+https://github.com/elementary/gala.git#tag=${_tag})
b2sums=('cfc8c09e56cd1b9645bc005c89ccb4239a3c4f7137b3ef3d09a355d251c46a7a8adbda10f88872116e843207985466be24029848019d7f0965c32743119928c6')

pkgver() {
  cd gala
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson gala build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:
