# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=cue
pkgver=0.8.1
pkgrel=1
pkgdesc="Validate and define text-based and dynamic configuration"
arch=('loong64' 'x86_64')
url="https://cuelang.org/"
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/cue-lang/cue/archive/v${pkgver}.tar.gz")
sha512sums=('540121083ec41403690def441cebb3f935f73e7e682adb1604e0a77af2a5d330c4d1a6e40ce5f7dd785fce59f8a1cda68be8feb7ac32cd9ebc2bc597b02308ce')

build() {
  cd "${pkgname}-${pkgver}/cmd/cue/"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags "-s -w -linkmode=external -X cuelang.org/go/cmd/cue/cmd.version=${pkgver}" -v .
  chmod +x cue
}

# check() {
#   cd "${pkgname}-${pkgver}"
#   go test -v ./...
# }

package() {
  cd "${pkgname}-${pkgver}/cmd/cue/"
  install -Dsm755 ./cue "${pkgdir}/usr/bin/cue"

  # build bash completions
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  ./cue completion bash  > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"

  # build zsh completions
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions"
  ./cue completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
