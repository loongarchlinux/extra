# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-applications-menu
pkgver=2.11.1.r147.98bd6e84
pkgrel=1
pkgdesc='The Pantheon Application Menu'
arch=(loong64 x86_64)
url=https://github.com/elementary/applications-menu
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  json-glib
  libgee
  libgranite.so
  libhandy-1.so
  libsoup
  libswitchboard-3.so
  wingpanel
  plank
  zeitgeist
)
makedepends=(
  git
  meson
  vala
)
_commit=98bd6e840fbf6991ad92c3d4a875a5a2d5d4f181
source=(pantheon-applications-menu::git+https://github.com/elementary/applications-menu.git#commit=${_commit})
b2sums=('f55eb653078d6e11f9c542253c0bb8a6d62f72e0fabc1782dfbbf122b27399bbe140fbe11c7d00ca706aab86bb96e38c3c16fa91f2a9d95cbd90e6b0f39b1f0c')

pkgver() {
  cd pantheon-applications-menu
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson pantheon-applications-menu build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
