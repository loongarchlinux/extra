# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=angelfish
pkgver=24.02.2
pkgrel=1
pkgdesc='Web browser for Plasma Mobile'
url='https://apps.kde.org/angelfish/'
arch=(loong64 x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(futuresql-qt6
         gcc-libs
         glibc
         kconfig
         kcoreaddons
         kdbusaddons
         ki18n
         kirigami
         kirigami-addons
         knotifications
         kwindowsystem
         purpose
         qcoro-qt6
         qqc2-desktop-style
         qt6-base
         qt6-declarative
         qt6-webengine)
makedepends=(corrosion
             extra-cmake-modules)
groups=(kde-applications
        kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('5d3ab074739428b6d30f6a12d683a05f10dff61f543b0539f5a19a15fc4c83f1'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(!lto)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
