# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Matthew Gamble <git@matthewgamble.net>

pkgname=python-poetry-plugin-up
pkgver=0.7.3
pkgrel=1
pkgdesc='Poetry plugin that updates dependencies and bumps their versions in pyproject.toml file'
url=https://github.com/MousaZeidBaker/poetry-plugin-up
license=(MIT)
depends=(
  python
  python-cleo
  python-poetry
  python-poetry-core
  python-tomlkit
)
makedepends=(
  git
  python-build
  python-installer
)
_tag=ca66272a4eea61d7a61aaf1a5c272c8702561461
source=(git+https://github.com/MousaZeidBaker/poetry-plugin-up.git#tag=${_tag})
b2sums=('d3eb84f61e0db6043495520687758f1699c1836149505b59b8e1ef82cdeb2f6801578dbd9a8661dfac8d6faa90d636db1c53eaefd2631a2be359b59ac85b92fb')
arch=(any)

pkgver() {
  cd poetry-plugin-up
  git describe --tags | sed 's/^v//'
}

build() {
  cd poetry-plugin-up
  python -m build --wheel --no-isolation
}

package() {
  python -m installer -d "${pkgdir}" poetry-plugin-up/dist/*.whl
  install -Dm 644 poetry-plugin-up/LICENSE -t "${pkgdir}"/usr/share/licenses/python-poetry-plugin-up/
}

# vim: ts=2 sw=2 et:
