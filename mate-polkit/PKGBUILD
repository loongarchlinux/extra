# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-polkit
pkgver=1.28.1
pkgrel=1
pkgdesc="PolicyKit integration for the MATE desktop"
url="https://mate-desktop.org"
arch=('loong64' 'x86_64')
license=('LGPL')
groups=('mate')
depends=('gtk3' 'polkit' 'gettext')
makedepends=('accountsservice' 'gobject-introspection')
conflicts=('mate-polkit-gtk3')
replaces=('mate-polkit-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('350a1dd0a8ed6a2c9c183cff2a21f394299e968fcc6ae2e8c93c565dae604ae8')

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
                --prefix=/usr \
                --libexecdir=/usr/lib/${pkgname} \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --enable-introspection

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
