# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Baptiste Grenier <baptiste@bapt.name>
# Contributor: Florian Fischer <florian.fischer@muhq.space>

pkgname=rubber
pkgver=1.6.6
pkgrel=1
pkgdesc="A wrapper for LaTeX and friends"
arch=('any')
url="https://launchpad.net/rubber/"
license=('GPL-2.0-only')
depends=('python')
makedepends=('texinfo' 'texlive-plaingeneric' 'python-hatchling' 'python-installer' 'python-build')
source=("https://gitlab.com/latex-rubber/rubber/-/archive/$pkgver/rubber-$pkgver.tar.bz2")
sha512sums=('0913a1c9ac012a8bd577e975aca92eae0965137fbc01b049b139f8f31bd85856503af7d760defc67f76894a74ca0616fcb887b1a9395be5a93ff0397ec6795ae')

build() {
  cd "${srcdir}/rubber-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}/rubber-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  mkdir -p "$pkgdir"/usr/share/bash-completion/completions/
  echo "complete -f -X '!*.tex' rubber" >"$pkgdir"/usr/share/bash-completion/completions/rubber
}
