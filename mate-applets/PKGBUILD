# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-applets
pkgver=1.28.0
pkgrel=1
pkgdesc="Applets for MATE panel"
arch=('loong64' 'x86_64')
url="https://mate-desktop.org"
license=('GPL')
depends=('cpupower' 'gtksourceview4' 'libgtop' 'libnotify' 'mate-panel' 'polkit' 'upower' 'wireless_tools' 'libnl')
makedepends=('intltool' 'itstool' 'gucharmap' 'yelp-tools')
optdepends=('fortune-mod: for displaying fortune cookies in the Wanda the Fish applet'
            'gucharmap: character picker applet')
groups=('mate-extra')
conflicts=('mate-applets-gtk3' 'mate-netspeed' 'mate-netspeed-gtk3')
replaces=('mate-applets-gtk3' 'mate-netspeed' 'mate-netspeed-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('1b6bef6bd5d326fb9dc828ff910e4b1b9294b4660c311dc1c90310fd9c356686')

build() {
    	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--sysconfdir=/etc \
        	--libexecdir=/usr/lib/${pkgname} \
        	--with-dbus-sys="/usr/share/dbus-1/system.d" \
        	--enable-polkit \
        	--enable-ipv6
    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
