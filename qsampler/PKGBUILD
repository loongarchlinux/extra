# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qsampler
pkgver=1.0.0
pkgrel=1
pkgdesc="A LinuxSampler Qt GUI Interface"
arch=(loong64 x86_64)
url="https://qsampler.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  linuxsampler
  qt6-base
  qt6-svg
)
makedepends=(
  cmake
  libgig
  liblscp
  qt6-tools
)
optdepends=(
  'qt6-wayland: for native wayland support'
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('3414da88a5924206017d1829928281c345bc036244c38ccce84de080ab2a8f992ab3dec7391f52334ed47fa122315f47562739e7a12c488d0e38e944fa79ef6a')
b2sums=('f5e66edb92d24208e4cce9efcc0b84166fdd23180e47c5b136d9749908565400d6b3d201eda7287e89153c98042d69b2e0e4742a1e7b39d9b104cc45d21e0d46')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    libgig libgig.so
    liblscp liblscp.so
  )
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
