# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qsampler
pkgver=0.9.12
pkgrel=3
pkgdesc="A LinuxSampler Qt GUI Interface"
arch=(loong64 x86_64)
url="https://qsampler.sourceforge.io/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  linuxsampler
  qt6-base
  qt6-svg
)
makedepends=(
  cmake
  libgig
  liblscp
  qt6-tools
)
optdepends=(
  'qt6-wayland: for native wayland support'
)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('089a177e2d6350880c96e17f7833ef80b94add3535b9cf0418453080c294c51a98b909a4072054c5f0b8ecdf014914966ac7d2e90add984225ec3c9a35a0ca0c')
b2sums=('c68fad51b37f4b471e1ffdcb4168487ef0069877ed2c13fd06f320c1a353c146ab493d94522d7ed86a8541646429550a4d02a89d5b37ae44847687acb5400fd1')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CONFIG_QT6=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    libgig libgig.so
    liblscp liblscp.so
  )
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
