# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cl-trivial-gray-streams
_pkgname="${pkgname#cl-}"
pkgver=2.1
pkgrel=1
pkgdesc='Compatibility layer for Gray Streams'
arch=('any')
url='https://trivial-gray-streams.common-lisp.dev'
license=('MIT')
depends=('common-lisp' 'cl-asdf')
makedepends=('git')
checkdepends=('sbcl')
_commit='a7ead683666849762ea657dac9137d693c5a4929'
source=(
  "$pkgname::git+https://github.com/trivial-gray-streams/trivial-gray-streams#commit=$_commit"
  'run-tests.lisp'
)
sha512sums=('2616b95278ea8e3d77611184c7d7789760ef03ef6c240b024db717a8b451a6943597c8dc21620f19a87b987bd4f116a76b89245a57349428db6b0d6dfa94c038'
            '8252eea45d95cdf66a924f2a7d357b43c3354888b519224b0e76147824d08e087ec17f88485962bb6433c1844724a53c17a7c42e4f25d3467a5c726c842ba1de')
b2sums=('d9ece3c7f6d070c0f176692236b3ab1f7b56bc00d5f494d0a54114ccb29f0f42a2ecc9b6b1266450ee9e939ae3cbbdca303ef2a467cd9aa927731f8e90919c1e'
        '7af6a2946854fcaa07f5ca24d3c1738b8a9eb6762c4fb8f900a3abc3b22913b50d90b29c9676837153ffc2cc8f14833718dd5e40cf8b72b47ff37e999a89efba')

pkgver() {
  cd "$pkgname"

  # first known commit to receive latest version
  local _pkgcommit='a7ead683666849762ea657dac9137d693c5a4929'
  local _pkgver='2.1'
  git tag "$_pkgver" "$_pkgcommit"

  git describe --tags | sed -e 's/^v//' -e 's/-/.r/' -e 's/-/./g'
}

check() {
  cd "$pkgname"

  sbcl --script ../run-tests.lisp
}

package() {
  cd "$pkgname"

  # create directories
  install -vd \
    "$pkgdir/usr/share/common-lisp/source/$_pkgname" \
    "$pkgdir/usr/share/common-lisp/systems"

  # library
  cp -vr test ./*.{lisp,asd} "$pkgdir/usr/share/common-lisp/source/$_pkgname"
  pushd "$pkgdir/usr/share/common-lisp/systems"
  ln -s "../source/$_pkgname/$_pkgname.asd" .
  ln -s "../source/$_pkgname/$_pkgname-test.asd" .
  popd

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
