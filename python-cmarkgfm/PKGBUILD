# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=python-cmarkgfm
_pyname=${pkgname#python-}
pkgver=2024.1.14
pkgrel=2
pkgdesc="Minimal bindings to GitHub's fork of cmark"
url="https://github.com/theacodes/cmarkgfm"
license=('MIT')
arch=('loong64' 'x86_64')
depends=('cmark-gfm' 'python-cffi')
makedepends=('cmark-gfm' 'cython' 'python-build' 'python-installer' 'python-setuptools' 'python-cffi' 'python-wheel')
checkdepends=('python-pytest')
# Upstream did not tag the version on GitHub to match PyPi, see https://github.com/theacodes/cmarkgfm/issues/51
# source=("https://github.com/theacodes/cmarkgfm/archive/$pkgver/$pkgname-$pkgver.tar.gz")
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_pyname-$pkgver.tar.gz")
sha512sums=('df014d92040e74943f1d448e3e333a67d9f3b4a0edede114ddb7be52dc13afc0dd57681b22bfb1f01976de84b6b5d278e9ec860cc349003875e6b9937adb3c06')

prepare() {
  cd cmarkgfm-$pkgver
  sed -e '/include_dirs=/a libraries=["cmark-gfm", "cmark-gfm-extensions"],' \
      -i src/cmarkgfm/build_cmark.py
}

build() {
  cd cmarkgfm-$pkgver
  python -m build -wn
}

check() {
  cd cmarkgfm-$pkgver
  local _pyver=cpython-$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-${_pyver}" pytest
}

package() {
  cd cmarkgfm-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm0644 -t "$pkgdir"/usr/share/licenses/$pkgname/ LICENSE.txt
}
