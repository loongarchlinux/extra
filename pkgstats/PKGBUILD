# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=pkgstats
pkgver=3.2.18
pkgrel=1
pkgdesc='Submit a list of installed packages to the Arch Linux project'
url='https://pkgstats.archlinux.de/'
arch=('loong64' 'x86_64')
license=('GPL')
depends=('pacman' 'systemd')
makedepends=('git' 'go' 'just' 'gcc')
source=("${pkgname}::git+https://github.com/archlinux-de/pkgstats-cli.git?signed#tag=${pkgver}")
sha256sums=('995a5e021fa602eda391007190ca358a8835dbb5cd418ede7e876165a20dd94a')
validpgpkeys=('3E80CA1A8B89F69CBA57D98A76A5EF9054449A5C')

prepare() {
	cd "${srcdir}/${pkgname}"

	export GOPATH="${srcdir}"
	just prepare
}

check() {
	cd "${srcdir}/${pkgname}"

	export GOPATH="${srcdir}"
	just test
}

build() {
	cd "${srcdir}/${pkgname}"

	export GOPATH="${srcdir}"
	just build
}

package() {
	cd "${srcdir}/${pkgname}"

	just install "${pkgdir}"
}
