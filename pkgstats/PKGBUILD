# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=pkgstats
pkgver=3.2.17
pkgrel=1
pkgdesc='Submit a list of installed packages to the Arch Linux project'
url='https://pkgstats.archlinux.de/'
arch=('loong64' 'x86_64')
license=('GPL')
depends=('pacman' 'systemd')
makedepends=('git' 'go' 'just' 'gcc')
source=("${pkgname}::git+https://github.com/archlinux-de/pkgstats-cli.git?signed#tag=${pkgver}")
sha256sums=('7515d5248a1794369d35a4de06427a870bba6cf5239b0a679a154c7b85adf7ac')
validpgpkeys=('3E80CA1A8B89F69CBA57D98A76A5EF9054449A5C')

prepare() {
	cd "${srcdir}/${pkgname}"

	export GOPATH="${srcdir}"
	just prepare
}

check() {
	cd "${srcdir}/${pkgname}"

	export GOPATH="${srcdir}"
	just test
}

build() {
	cd "${srcdir}/${pkgname}"

	export GOPATH="${srcdir}"
	just build
}

package() {
	cd "${srcdir}/${pkgname}"

	just install "${pkgdir}"
}
