# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-menu
pkgver=5.0.1
pkgrel=15
pkgdesc="Deepin menu service for building beautiful menus"
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-menu"
license=('GPL3')
depends=('deepin-qt5integration')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-menu/archive/$pkgver.tar.gz"
         missing-include.patch)
sha512sums=('0a23ce9a2272982355fa7f85c3d172ca96ec15cfd1a55551310b763bbe6fd7cf434cbc7385f8198983a34c6892ba9bb4b7bdf3555eaaf6a6afdc4c1a66485d8b'
            '7bf2d73c6e67c6688ab48d6a5cf3c4ca038473361846efe92349cb78421a26d6db02ddd51e2e441e387bea9dabca4c97093d021a3c527effe7de9fe611f79da4')

prepare() {
  patch -d deepin-menu-$pkgver -p1 < missing-include.patch # Add missing include
}

build() {
  cd deepin-menu-$pkgver

  qmake-qt5 DEFINES+=QT_NO_DEBUG_OUTPUT
  make
}

package() {
  cd deepin-menu-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
