# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-hookable
pkgver=6.0
pkgrel=1
pkgdesc="Represents the core of the Zope hookable Architecture"
arch=('loong64' 'x86_64')
url="https://github.com/zopefoundation/zope.hookable"
license=('ZPL')
depends=('python-setuptools')
checkdepends=('python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.hookable/archive/$pkgver.tar.gz")
sha512sums=('273fa07d0682ce0f97435221d99311467b8dfdea88c8ea50acec727d43f9a6ae2cd6f173c360d37024cd5067252442e6af52df9227395051c515e0c04c9c95f2')

build() {
  cd "$srcdir"/zope.hookable-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.hookable-$pkgver
  python setup.py nosetests
}

package() {
  cd "$srcdir"/zope.hookable-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
