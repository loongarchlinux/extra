# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: SaultDon <sault.don gmail>
pkgname=pdal
pkgver=2.7.2
pkgrel=1
pkgdesc="A C++ library for translating and manipulating point cloud data"
arch=('loong64' 'x86_64')
url="http://www.pdal.io"
license=('BSD')
depends=('gdal' 'libgeotiff' 'jsoncpp')
makedepends=('cmake' 'python' 'python-numpy' 'ninja' 'postgresql-libs')
optdepends=('python-numpy: for the Python plugin'
            'sqlite: for the sqlite plugin'
            'postgresql-libs: for the postgresql plugin')
provides=('pdal')
source=("https://github.com/PDAL/PDAL/releases/download/${pkgver}/PDAL-${pkgver}-src.tar.bz2")
sha512sums=('81e4d4b136e292f5a2e4145f730e00375a26a6a2aa52af679bea6d9d371b55c92641475ea332fc8301fe35d237415b70ea311da3fc1bc4ff69013a7448f66896')

build() {
  cd "PDAL-$pkgver-src/"

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_PLUGIN_PGPOINTCLOUD=ON \
    -DWITH_COMPLETION=ON

  ninja -C build
}

package() {
  cd "PDAL-$pkgver-src"

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
