# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: Quentin Stievenart <quentin.stievenart@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mk-configure
pkgver=0.39.3
pkgrel=1
pkgdesc="Lightweight replacement for GNU autotools"
arch=('any')
makedepends=('bmake' 'bmkdep')
url="https://github.com/cheusov/mk-configure"
license=('BSD')
depends=('bash' 'gawk')
source=("https://github.com/cheusov/$pkgname/archive/$pkgname-$pkgver.tar.gz")
sha256sums=('438ab3c524092982d1473d4019d119afa91cdb60138e1cad6e075f16fd73e3f1')

build() {
  cd $pkgname-$pkgname-$pkgver
  export USE_AWK=/usr/bin/gawk
  export USE_INSTALL=/usr/bin/install
  export PREFIX=/usr SYSCONFDIR=/etc MANDIR=/usr/share/man LIBEXECDIR=/usr/lib
  bmake CUSTOM.prog_nbmkdep=/usr/bin/bmkdep all
}

package() {
  cd $pkgname-$pkgname-$pkgver
  export PREFIX=/usr SYSCONFDIR=/etc MANDIR=/usr/share/man LIBEXECDIR=/usr/lib
  bmake CUSTOM.prog_nbmkdep=/usr/bin/bmkdep DESTDIR="$pkgdir" install
  install -Dm644 doc/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
