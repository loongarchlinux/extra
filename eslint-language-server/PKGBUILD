# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Dmytro Meleshko <qzlgeb.zryrfuxb@tznvy.pbz(rot13)>

pkgname=eslint-language-server
pkgver=3.0.10
pkgrel=1
pkgdesc='Language server for ESLint'
arch=(any)
url=https://github.com/Microsoft/vscode-eslint
license=(MIT)
depends=(nodejs)
makedepends=(
  git
  npm
)
conflicts=("vscode-$pkgname")
replaces=("vscode-$pkgname")
source=("git+$url.git#tag=release/$pkgver")
b2sums=('ba87ea2e11f1a8a7b5c4d97060cad7e5a17937f2e8f5b16d183c2d7375c147cde1584c7c1174fad96b32f5d78fcad85fedb20f099f5504ed236e36f32d937362')

prepare() {
  cd vscode-eslint
  npm ci
}

build() {
  cd vscode-eslint
  npm run compile:server
}

package() {
  local mod_dir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/{usr/bin,$mod_dir}
  printf '%s\n' '#!/usr/bin/env node' "require('$mod_dir/out/eslintServer.js');" >"$pkgdir"/usr/bin/$pkgname
  chmod +x "$pkgdir"/usr/bin/$pkgname
  ln -s $pkgname "$pkgdir"/usr/bin/vscode-$pkgname

  cd vscode-eslint
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname License.txt

  cd server
  cp -r node_modules out package.json "$pkgdir"/$mod_dir
}
