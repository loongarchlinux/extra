# Maintainer: Ray Rashif <schiv@archlinux.org>
# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>

pkgname=kbibtex
epoch=1
pkgver=0.10.0
pkgrel=5
pkgdesc='A BibTeX editor for KDE'
arch=(loong64 x86_64)
url='https://apps.kde.org/kbibtex/'
license=(GPL)
depends=(gcc-libs
         glibc
         icu
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kcrash5
         ki18n5
         kiconthemes5
         kitemviews5
         kio5
         kjobwidgets5
         kparts5
         kservice5
         ktexteditor5
         ktextwidgets5
         kwallet5
         kwidgetsaddons5
         kxmlgui5
         poppler-qt5
         qt5-base
         qt5-networkauth
         qt5-webengine
         qt5-xmlpatterns)
optdepends=('okularpart5: Document preview')
makedepends=(extra-cmake-modules
             kdoctools5)
source=(https://download.kde.org/stable/KBibTeX/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('b127b240a7cd77c538619dc882a399b3c6ccd77a04428a497af906f14d09b8c4'
            'SKIP')
validpgpkeys=('1808CB466328F4380685A3B1A264FD738D861F41') # Thomas Fischer <fischer@unix-ag.uni-kl.de>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
