# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=papilo
pkgver=2.2.0
pkgrel=1
pkgdesc='Parallel Presolve for Integer and Linear Optimization'
arch=(loong64 x86_64)
url='https://www.scipopt.org/'
license=(LGPL-3.0-only)
depends=(boost-libs
         gcc-libs
         glibc
         gmp
         tbb)
makedepends=(boost
             cmake)
source=(https://github.com/scipopt/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('4ed759e55fe1c74be779137e4e3cdae67e1b64bd62ca31793ca3b321509c27a8')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
