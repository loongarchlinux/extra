# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=papilo
pkgver=2.3.0
pkgrel=1
pkgdesc='Parallel Presolve for Integer and Linear Optimization'
arch=(loong64 x86_64)
url='https://www.scipopt.org/'
license=(LGPL-3.0-only)
depends=(boost-libs
         gcc-libs
         glibc
         gmp
         tbb)
makedepends=(boost
             cmake
             git)
source=(git+https://github.com/scipopt/papilo#tag=v$pkgver)
sha256sums=('19d5b3a284193740a3f8ad906461d3f3e461a75e8d79754784b7ccf125c29b91')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
