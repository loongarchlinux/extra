# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-user-accounts
pkgver=2.4.3.r163.87a5341
pkgrel=1
pkgdesc='Switchboard User Accounts Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-useraccounts
license=(LGPL-3.0-only)
groups=(pantheon)
depends=(
  accountsservice
  cairo
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop-4
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libpwquality
  libswitchboard-3.so
  polkit
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
)
_commit=87a53416484879b44e632562501b416674089f87
source=(switchboard-plug-user-accounts::git+https://github.com/elementary/switchboard-plug-useraccounts.git#commit=${_commit})
b2sums=('3314431daab2ebfeaa4ae781d7b0429851a2566afbd59de642636198ad4a1e55fa243ff9d0dba2839e40e41da5495d47808c668aa24cf8becd430e2b975f136b')

pkgver() {
  cd switchboard-plug-user-accounts
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-user-accounts build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
