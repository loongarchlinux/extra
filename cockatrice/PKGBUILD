# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=cockatrice
_pkgname=Cockatrice
pkgver=2.9.0
_date=2023-09-14
_tag=$_date-Release-$pkgver
pkgrel=5
pkgdesc='A cross-platform virtual tabletop for multiplayer card games'
arch=('loong64' 'x86_64')
url='https://cockatrice.github.io'
license=('GPL2')
depends=('qt5-base' 'protobuf' 'qt5-tools' 'qt5-svg' 'qt5-multimedia' 'qt5-websockets')
makedepends=('cmake' 'git' 'zlib' 'mariadb-libs')
checkdepends=('gtest' 'valgrind')
optdepends=('zlib: Load zipped files in Oracle'
	    'mariadb-libs: MySQL/MariaDB connection')
provides=('cockatrice-client' 'cockatrice-server')
conflicts=('cockatrice-client' 'cockatrice-server')
source=("git+https://github.com/Cockatrice/Cockatrice.git#tag=$_tag"
         protobuf-23.patch)
sha512sums=('SKIP'
            '42eb9d1dbceee5f4e39a9ed6fa7b7b3b72901b001975b81b629fe0d2d51aef0d1cd4e5d1519d240479679b2532cf0aae73340d5ac2afe3294177288c405c8acd')

prepare() {
  patch -d $_pkgname -p1 < protobuf-23.patch # Fix build with protobuf 23
}

build() {
  mkdir -p $_pkgname/build
  cd $_pkgname/build

  cmake .. \
  	-DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_CXX_STANDARD=17 \
  	-DWITH_ORACLE=ON \
  	-DWITH_CLIENT=ON \
  	-DWITH_SERVER=ON \
  	-DTEST=ON

  make
}

check() {
  cd $_pkgname/build

  make test
}

package() {
  cd $_pkgname/build

  make DESTDIR="$pkgdir" install
}
