# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=libtracefs
pkgname=(libtracefs libtracefs-docs)
pkgver=1.8.1
pkgrel=1
pkgdesc="Linux kernel trace file system library"
arch=(loong64 x86_64)
url="https://git.kernel.org/pub/scm/libs/libtrace/libtracefs.git/about/"
license=(
  GPL2
  LGPL2.1
)
makedepends=(
  asciidoc
  libtraceevent
  xmlto
)
source=(
  https://git.kernel.org/pub/scm/libs/libtrace/libtracefs.git/snapshot/$pkgname-$pkgver.tar.gz
  $pkgname-1.2.0-docs_makefile.patch
  $pkgname-1.6.1-ldflags.patch
)
sha512sums=('1d8775cf511982a99e2dd17afcf48da272ff8ec189710a27420befe8765155ed21fd62db8fb2b75a0965e0a52ef810459c826f9b4989093c6a64d0cb7894cfca'
            'bdfffba9957d889c60baea9c197709bf1a62d30ac4b8fd47d930b8d5b67fa040817b9a835e226dd1201f2ccdb7fcb43a12093c2b559fb6c0edc3e9ba167062c2'
            '3f83a85aa0be92ff4b1b5e9fe484f84305c149f0b4b7a2785aac25d3af098a2dbaaed72c470e65efe665be3eca4531ad5687f51688bc0ebb6b92f20da20b5d76')
b2sums=('cb4f491464d54f1271591cdc5ed61fa67f49e63b94ff91162e3b4f59387d3f27d866e288b69dacdbea7c145989d50e8c00e3f63b066eef93164a8748c535420d'
        '1340af2c65dc344912715c69827d5849fda83e4bcf136da3ccdbee4e3b508e9b557af0828914dff8d27abebbdbdab975d8800432d5252b6b1eb0da08b0cb4b02'
        '2663392d56cf1d811fdc8d117a5455c32c4c5f5a18a8e3aa4ee44df7b350bdd91fd512d4753ca34286ee59c4e7f4b8309bfc62b9711b1a01e60d90de91995418')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  patch -d $pkgname-$pkgver -Np 1 -i ../$pkgname-1.2.0-docs_makefile.patch
  patch -d $pkgname-$pkgver -Np 1 -i ../$pkgname-1.6.1-ldflags.patch
}

build() {
  make -C $pkgname-$pkgver
  make sqlhist -C $pkgname-$pkgver
  make doc -C $pkgname-$pkgver
}

package_libtracefs() {
  depends=(
    glibc
    libtraceevent libtraceevent.so
  )
  optdepends=('libtracefs-docs: for documentation')
  provides=(libtracefs.so)

  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C $pkgname-$pkgver/Documentation
  install -vDm 755 $pkgname-$pkgver/bin/sqlhist -t "$pkgdir/usr/bin/"

  (
    cd "$pkgdir"
    _pick libtracefs-docs usr/share/doc
  )
}

package_libtracefs-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
}
