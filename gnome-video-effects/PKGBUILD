# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-video-effects
pkgver=0.6.0
pkgrel=1
epoch=1
pkgdesc="Collection of GStreamer effects for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-video-effects"
arch=(any)
license=(GPL2)
depends=(
  frei0r-plugins
  gst-plugins-bad
  gst-plugins-good
)
makedepends=(
  git
  meson
)
_commit=e4ae5aa3ee136333c6481469ed1d8869307ef5e4  # tags/0.6.0^0
source=("git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
