# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mbuffer
pkgver=20240707
pkgrel=1
pkgdesc="A tool for buffering data streams"
arch=(loong64 x86_64)
url="https://www.maier-komor.de/mbuffer.html"
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
)
makedepends=(openssl)
source=(https://www.maier-komor.de/software/$pkgname/$pkgname-$pkgver.tgz)
sha512sums=('603877e86df666807c77e6147268d15242acbbc9787fbc9962af97367694a25d95eda1c66197975b858739ce4683e3c36cd1b442f65daae304f25056096319c6')
b2sums=('ae03f9a581ef37d7502d60df35ab7026987ca1e4cdd09b7bd0a2e90fa6f3b75aead9a6add7907f18c582ba40b110f24c9430a1e145772996641196de88a1470e')

prepare() {
  cd $pkgname-r$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-r$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  make -k check -C $pkgname-r$pkgver
}

package() {
  depends+=(
    openssl libcrypto.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-r$pkgver
}
