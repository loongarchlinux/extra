# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-icon-theme
pkgver=1.0.0.alpha.1
pkgrel=1
pkgdesc='System76 Cosmic icon theme'
arch=(any)
url=https://github.com/pop-os/cosmic-icons
license=(CC-BY-SA-4.0)
depends=(pop-icon-theme)
makedepends=(
  git
  just
)
options=(!strip)
_tag=f93dcdfa1060c2cf3f8cf0b56b0338292edcafa5
source=(git+https://github.com/pop-os/cosmic-icons.git#tag=${_tag})
b2sums=('SKIP')

pkgver() {
  cd cosmic-icons
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

package() {
  cd cosmic-icons
  just rootdir="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
