# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: jgmdev <jgmdev at gmail dot com>
# Contributor: TÃ©rence Clastres <t dot clastres at gmail dot com>
# Contributor: redtide <redtid3 at gmail dot com>

pkgname=lite-xl
pkgver=2.1.5
pkgrel=1
pkgdesc='A lightweight text editor written in Lua'
arch=(loong64 x86_64 aarch64)
url="https://$pkgname.com"
license=(MIT)
depends=(freetype2
         lua
         pcre2
         sdl2)
makedepends=(git # used by Meson to fetch submodule, TODO: move to source()
             meson)
conflicts=(lite)
provides=(lite)
replaces=("$pkgname-bin")
_archive="$pkgname-$pkgver"
source=("https://github.com/$pkgname/$pkgname/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('b1a4dc1ad8ed8e0c822a4381131a21db4f81edae84259da556a4435d0be12de9')

build() {
        arch-meson "$_archive" build -D use_system_lua=true
        meson compile -C build
}

package() {
        meson install --skip-subprojects -C build --destdir "$pkgdir"
        cd "$_archive"
        install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
