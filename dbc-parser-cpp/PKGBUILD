# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=dbc-parser-cpp
pkgver=0.4.0
pkgrel=1
pkgdesc='A DBC file parsing library written in C++'
arch=(loong64 x86_64)
license=(MIT)
url='https://github.com/LinuxDevon/dbc_parser_cpp'
depends=()
makedepends=(cmake
             fast_float)
source=(https://github.com/LinuxDevon/dbc_parser_cpp/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('af60cc8cfd7aa6345f724cc96e09d0b805df81797ba69e52faca6e6af4fde497')

build() {
  cmake -B build -S ${pkgname//-/_}-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_INCLUDEDIR=include \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects" \
    -DDBC_ENABLE_TESTS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 ${pkgname//-/_}-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
