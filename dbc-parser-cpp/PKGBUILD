# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=dbc-parser-cpp
pkgver=0.5.0
pkgrel=1
pkgdesc='A DBC file parsing library written in C++'
arch=(loong64 x86_64)
license=(MIT)
url='https://github.com/LinuxDevon/dbc_parser_cpp'
depends=()
makedepends=(cmake 
             fast_float
             git)
source=(git+https://github.com/LinuxDevon/dbc_parser_cpp#tag=v$pkgver)
sha256sums=('5c5a5bdf52c1976b9dce263634fe4b78ddb07dac766f51b008760f6d7f440de3')

build() {
  cmake -B build -S ${pkgname//-/_} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_INCLUDEDIR=include \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects" \
    -DDBC_ENABLE_TESTS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 ${pkgname//-/_}/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
