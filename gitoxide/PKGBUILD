# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=gitoxide
pkgver=0.33.0
pkgrel=1
pkgdesc="An idiomatic, lean, fast & safe pure Rust implementation of Git"
url='https://github.com/Byron/gitoxide'
arch=('loong64' 'x86_64')
license=('MIT' 'Apache-2.0')
makedepends=('cargo' 'cmake')
options=(!lto)
source=(https://github.com/Byron/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0fc1e0f9e36d2622f6a11925607090abf619c73551d2a8f5ea710e97ef1a9eb4')
b2sums=('319b4838b1555b308cac1f945ff648f29f485df387a10aac48d78d1b1bccb7585a328e32948822cb4c0cdc01e12b64c1896318b043bd849c8b6f02d819498d2d')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release --no-default-features --features max-pure
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/gix \
    target/release/ein
}

# vim: ts=2 sw=2 et:
