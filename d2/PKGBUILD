# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=d2
pkgver=0.6.5
pkgrel=1
pkgdesc='A modern diagram scripting language that turns text to diagrams'
arch=('loong64' 'x86_64')
url='https://d2lang.com'
license=('MPL-2.0')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/terrastruct/d2#tag=v$pkgver")
sha512sums=('1428752c29a797ceafc188f9364f29741dd2836c9693d4b285435e217c42b331421907e5def504b5d526308c26302c13e3f2c84d3e3c4e498011b8a9b7b9d8c5')
b2sums=('ef38ea49d4a0d75d4ae5345646fd69334a7143b91a0327ce41c3906454757a7fe4e3675affb36a8eacb581ae2a6bcd8d9dd85859de63f99b0ef0c20f7ef26480')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X oss.terrastruct.com/d2/lib/version.Version=$pkgver" \
    -o build \
    .
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/d2

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" ci/release/template/man/d2.1

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
