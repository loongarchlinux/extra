# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: AwesomeHaircut <jesusbalbastro@gmail.com>
# Contributor: jbouter <aur@kn0x.org>

pkgname=touchegg
pkgver=2.0.17
pkgrel=2
pkgdesc="Linux multi-touch gesture recognizer"
url="https://github.com/JoseExposito/touchegg"
arch=('loong64' 'x86_64')
license=('GPL-3.0-or-later')
depends=('libinput' 'cairo' 'systemd-libs' 'libx11' 'libxi' 'libxrandr' 'libxtst' 'pugixml' 'gtk3' 'glib2')
makedepends=('cmake')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b0c957003f0df1c3fdb750e0f3a253ed39014c900c82ffa7be254afe6c2a60949ea81d2628bf3d40ad4f3547093b274fb20831144c2427b417262812a10d563d')

build() {
	cmake -B build -S "${pkgname}-${pkgver}" \
	    -DCMAKE_BUILD_TYPE='None' \
	    -DCMAKE_INSTALL_PREFIX='/usr' \
	    -Wno-dev
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --install build
}
