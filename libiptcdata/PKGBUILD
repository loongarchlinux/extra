# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=libiptcdata
pkgver=1.0.5
pkgrel=2
pkgdesc='Library for manipulating the IPTC metadata'
arch=('loong64' 'x86_64')
url='https://libiptcdata.sourceforge.net/'
license=('LGPL')
depends=('glibc')
makedepends=('python')
optdepends=('python: Python bindings')
source=("https://github.com/ianw/libiptcdata/releases/download/release_${pkgver//./_}/$pkgname-$pkgver.tar.gz")
sha256sums=('c094d0df4595520f194f6f47b13c7652b7ecd67284ac27ab5f219bc3985ea29e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-python
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
