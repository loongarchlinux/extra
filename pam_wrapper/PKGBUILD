# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=pam_wrapper
pkgver=1.1.5
pkgrel=2
pkgdesc="Tool to test PAM applications and PAM modules"
url="https://cwrap.org/pam_wrapper.html"
arch=(loong64 x86_64)
license=(GPL-3.0-or-later)
depends=(
  glibc
  pam
  python
)
makedepends=(
  cmake
  cmocka
  doxygen
  git
  graphviz
  ninja
)
provides=(
  libpam_wrapper.so
  libpamtest.so
)
source=(
  "git+https://git.samba.org/pam_wrapper.git#tag=$pkgname-$pkgver"
  $pkgname-1.1.5-site-packages.patch
)
b2sums=('c147d5fc2aa8abc65ed18725e80ecaa2bbe2018fb58cf99ff81ce0ea095c029dd69049bfc06da0ff5f8a4e91dc0ca19976038203ea5580a700fdd75d0cb0dab7'
        '721fc27dc18ea2ad727486ee08a1acdf9e71c44cd5519d468c14efd5f165ae048aee2ccf352e174e590c50a5b1ccfe2891e601fa20a163f960f968c7f6a54e1a')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^pam_wrapper-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  # do not rely on distutils to get site-packages location (minus prefix)
  # (this is a bit hacky but works for our use-case)
  patch -Np1 -i ../$pkgname-1.1.5-site-packages.patch
}

build() {
  local cmake_options=(
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -D UNIT_TESTING=true
  )

  cmake -S $pkgname -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
  cmake --build build --target doc
}

check() {
  cd build
  ctest --output-on-failure --stop-on-failure -j$(nproc)
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  mkdir -p "$pkgdir/usr/share/doc"
  cp -a build/doc/html "$pkgdir/usr/share/doc/$pkgname"
  cp -a build/doc/man/man3 "$pkgdir/usr/share/man"
}

# vim:set sw=2 sts=-1 et:
