# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=pam_wrapper
pkgver=1.1.7
pkgrel=1
pkgdesc="Tool to test PAM applications and PAM modules"
url="https://cwrap.org/pam_wrapper.html"
arch=(loong64 x86_64)
license=(GPL-3.0-or-later)
depends=(
  glibc
  pam
  python
)
makedepends=(
  cmake
  cmocka
  doxygen
  git
  graphviz
  ninja
  python-setuptools
)
provides=(
  libpam_wrapper.so
  libpamtest.so
)
source=(
  "git+https://git.samba.org/pam_wrapper.git#tag=pam_wrapper-$pkgver"
)
b2sums=('4cf21b6021e43c32656376f43551dd19dcd2683f527cf9705af2befcc4d8d9cf8aa18d333bfa6ba3fe7d321853aed5809ba2389ef3cad56cc32cc0f2f72210d7')

prepare() {
  cd $pkgname
}

build() {
  local cmake_options=(
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -D UNIT_TESTING=true
  )

  cmake -S $pkgname -B build -G Ninja "${cmake_options[@]}"
  cmake --build build
  cmake --build build --target doc
}

check() {
  ctest --test-dir build --output-on-failure --stop-on-failure -j$(nproc)
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  mkdir -p "$pkgdir/usr/share/doc"
  cp -a build/doc/html "$pkgdir/usr/share/doc/$pkgname"
  cp -a build/doc/man/man3 "$pkgdir/usr/share/man"
}

# vim:set sw=2 sts=-1 et:
