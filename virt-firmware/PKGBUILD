# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=virt-firmware
pkgver=24.4
pkgrel=2
pkgdesc="Collection of tools for edk2 firmware images"
arch=(any)
url="https://gitlab.com/kraxel/virt-firmware"
license=(GPL-2.0-or-later)
depends=(
  python
  python-crc32c  # undeclared runtime dependency: https://gitlab.com/kraxel/virt-firmware/-/issues/4
  python-cryptography
  python-pefile
  python-setuptools
)
makedepends=(
  python-build
  python-installer
  python-wheel
)
checkdepends=(
  python-pytest
  systemd  # tests require systemd-detect-virt(?!)
)
source=($url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('a36506187a166b4f71bdcabb506333ccd1132b2a09123048aca6361bf106d885d2bad6f036e19ee4ffa132de161a2962eff2de64d3e30a3da902a569b481e5ff')
b2sums=('973abaafc9480c707b866e495b768363ffc7fb87e3267510f0aef744b017667c733b350d0b872a3ae58e2d9690f89aca9ac3f6d18a34b33cd59e247df4334443')

build() {
  cd $pkgname-v$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-v$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"

  pytest -vv tests/tests.py
}

package() {
  cd $pkgname-v$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 man/virt-*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
