# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=krusader
pkgver=2.8.1
pkgrel=1
pkgdesc='Advanced twin panel (commander style) file manager'
arch=(loong64 x86_64)
url='https://krusader.org/'
license=(GPL-2.0-or-later)
depends=(acl
         gcc-libs
         glibc
         karchive5
         kbookmarks5
         kcodecs5
         kcompletion5
         kconfig5
         kconfigwidgets5
         kcoreaddons5
         kguiaddons5
         ki18n5
         kiconthemes5
         kio5
         kitemviews5
         kjobwidgets5
         knotifications5
         kparts5
         kservice5
         ktextwidgets5
         kwallet5
         kwidgetsaddons5
         kwindowsystem5
         kxmlgui5
         qt5-base
         solid5
         zlib)
makedepends=(extra-cmake-modules
             kdoctools5)
optdepends=('xz: LZMA and XZ archive support'
            'unzip: ZIP decompression support'
            'zip: ZIP archive support'
            'lhasa: LHA archive support'
            'cpio: cpio archive support'
            'unrar: RAR decompression support'
            'arj: ARJ archive support'
            'unarj: ARJ decompression support'
            'unace: ACE decompression support'
            'p7zip: 7z archive support'
            # additional apps
            'keditbookmarks: Manage Bookmarks functionality'
            'kde-cli-tools: manage file associations, root mode'
            'kompare: file contents comparison'
            'kdiff3: file contents comparison'
            'krename: advanced rename tool'
            'konsolepart5: terminal'
            'ktexteditor5: file editing support')
source=(https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('37bf204519d0ab1ba40964af02741bc228f11e97f28c4c518ca05d358df18283'
            'SKIP')
validpgpkeys=(3381929FB59A539333042D57AD596556DFF2501F) # Nikita Melnichenko <nikita+kde@melnichenko.name>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DKDESU_PATH="/usr/lib/kf6/kdesu"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
