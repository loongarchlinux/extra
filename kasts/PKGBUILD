# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kasts
pkgver=24.02.2
pkgrel=1
pkgdesc='Kirigami-based podcast player'
url='https://apps.kde.org/kasts/'
arch=(loong64 x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kcolorscheme
         kconfig
         kcoreaddons
         ki18n
         kirigami
         kirigami-addons
         qt6-base
         qt6-declarative
         qt6-multimedia
         qtkeychain-qt6
         syndication
         taglib
         threadweaver
         vlc)
makedepends=(extra-cmake-modules
             python)
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('4dc7f65ef7e548163d57abc07d1492b6ed1c6b645080a268614b048c820208df'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
