# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-music
pkgver=7.0.4
pkgrel=1
pkgdesc='Awesome music player with brilliant and tweakful UI Deepin-UI based.'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-music"
license=('GPL3')
# ffmpeg, vlc: dlopen
# dtkdeclarative: qml: org.deepin.dtk
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'dtkdeclarative' 'qt5-base' 'qt5-declarative' 'glibc'
         'gcc-libs' 'kcodecs5' 'qt5-multimedia' 'qtmpris' 'ffmpeg' 'taglib' 'udisks2-qt5' 'vlc')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-music/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-ldflags.patch::https://github.com/linuxdeepin/deepin-music/pull/422.patch
        taglib-2.patch)
sha512sums=('f9f86152ab1853ebb2b0faa664e891e13bbd80ab87105ced745c4339fd23a85b0f781635cdc0e3d8383d5ce3d15bfed4c54e019137c4f2868e2e1242b1b486db'
            '7c90a9d5662f5de99d9a086576c8fbe1633596cc7f8819a5e83224e97a5171243a53fbf8d66053f76c67b19697fa2d33ca3072c9831b7821a9bcfbead4b6cd17'
            'a588a36096712bc87d5c87e5dc30b45b493c74bcc80690f2f588df23af799a31c86a63838922234f4da82296f95a265761bd84fb9e01fb9535fd151db837e48b')

prepare() {
  cd deepin-music-$pkgver
  patch -Np1 -i ../$pkgname-ldflags.patch
  sed -i 's/-fPIE//' CMakeLists.txt
  patch -p1 -i ../taglib-2.patch
}

build() {
  cd deepin-music-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DVERSION=$pkgver
  ninja
}

package() {
  cd deepin-music-$pkgver
  DESTDIR="$pkgdir" ninja install
}
