# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pytest-datafixtures
pkgname=python-pytest-datafixtures
pkgver=1.0.0
pkgrel=5
pkgdesc="Data fixtures for pytest made simple"
arch=(any)
url="https://github.com/idlesign/pytest-datafixtures"
license=(BSD-3-Clause)
depends=(
  python
  python-pytest
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('17e00ec4cdc61dc9e3f65066d13b031aad6a5838e3b2def25c353e1e3877d70d549078da6f1537ff7e88b4ca00c3a2bd4f3ee5f2c39ee398be27b5b8a5d58240')
b2sums=('2bc3b2efa3d19268b665d55c92bd6090cb5c7ece611e2a0559cdacfcd3ff1e22d4e144897c6fd2734f0a40b2c40766d6037c858d82a87a3f845ea0d0230780de')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build:$PYTHONPATH"
  pytest -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {AUTHORS,CHANGELOG,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
