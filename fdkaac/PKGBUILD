# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: PelPix <kylebloss@pelpix.info>

pkgname=fdkaac
pkgver=1.0.6
pkgrel=1
pkgdesc='Command line encoder frontend for libfdk-aac'
arch=(loong64 x86_64)
url=https://github.com/nu774/fdkaac
license=(custom)
depends=(
  glibc
  libfdk-aac.so
)
makedepends=(git)
_tag=35527e4041f30107f02519af920bcb3e91787329
source=(git+https://github.com/nu774/fdkaac.git#tag=${_tag})
sha256sums=('f596ce9d6076a494581277302973c128e8760fde8f028fe9a73716396365fd86')

prepare() {
  cd fdkaac

  autoreconf -fiv
}

pkgver() {
  cd fdkaac

  git describe --tags | sed 's/^v//'
}

build() {
  cd fdkaac

  ./configure \
    --prefix=/usr \
    --disable-rpath
  make
}

package() {
  make DESTDIR="${pkgdir}" -C fdkaac install
  install -Dm 644 fdkaac/COPYING -t "${pkgdir}"/usr/share/licenses/fdkaac/
}

# vim: ts=2 sw=2 et:
