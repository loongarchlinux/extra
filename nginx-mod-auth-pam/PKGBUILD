# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-auth-pam
pkgver=1.5.5
pkgrel=1

_modname=ngx_http_auth_pam_module

pkgdesc='Nginx module to use PAM for simple http authentication'
arch=('loong64' 'x86_64')
depends=("nginx" 'pam')
makedepends=("nginx-src")
url="https://github.com/sto/ngx_http_auth_pam_module"
license=('BSD')

source=(https://github.com/sto/$_modname/archive/v$pkgver/$_modname-$pkgver.tar.gz)
validpgpkeys=(B0F4253373F8F6F510D42178520A9993A1C052F8) # Maxim Dounin <mdounin@mdounin.ru>
sha256sums=('98a71617d9119ae784993e3789ce8766fdf2ff2479691f3dc6cf8d8763f8d364')

prepare() {
	cp -r /usr/src/nginx .
}

build() {
	cd "$srcdir"/nginx
	./configure --with-compat --add-dynamic-module=../$_modname-$pkgver
	make modules
}

package() {
	install -Dm644 "$srcdir"/$_modname-$pkgver/LICENSE \
	               "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	cd "$srcdir"/nginx/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done
}
