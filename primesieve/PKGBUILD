# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=primesieve
pkgver=12.4
pkgrel=1
pkgdesc='Fast prime number generator'
arch=(loong64 x86_64)
url='https://github.com/kimwalisch/primesieve'
license=(BSD-2-Clause)
depends=(gcc-libs
         glibc)
makedepends=(cmake
             git)
source=(git+https://github.com/kimwalisch/primesieve#tag=v$pkgver)
sha256sums=('00a985dca748b3ea0ce7841fe26e036a0a2d5b9daf36c6af27302497b95d3e23')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_STATIC_LIBS=OFF
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
