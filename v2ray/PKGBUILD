# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pandada8 <pandada8@gmail.com>

pkgname=v2ray
pkgver=5.16.1
_commit=346ca66c57ab1b6872908354d5f3e4152fff59da
pkgrel=1
pkgdesc="A platform for building proxies to bypass network restrictions"
arch=('loong64' 'x86_64')
url="https://github.com/v2fly/v2ray-core"
license=('MIT')
depends=('glibc' 'v2ray-domain-list-community' 'v2ray-geoip')
makedepends=('go' 'git')
backup=(etc/v2ray/config.json)
source=("git+https://github.com/v2fly/v2ray-core.git#commit=$_commit")
sha512sums=('e58fb62d3d63ef1dbc2655b4d03fd9cc148572dde13579a11a9a683f450ee33cbc83ab2b6135ef06de7975ae58e9dc106be3e2a6ffa997ef8be783fc23097b01')

prepare() {
  cd v2ray-core
  sed -i 's|/usr/local/bin|/usr/bin|;s|/usr/local/etc|/etc|' release/config/systemd/system/*.service
}

build() {
  cd v2ray-core
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o v2ray ./main
}

check() {
  cd v2ray-core
  go test -p 1 -tags json -v -timeout 30m ./...
}

package() {
  cd v2ray-core
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/v2ray/
  install -Dm644 release/config/systemd/system/v2ray.service -t "$pkgdir"/usr/lib/systemd/system/
  install -Dm644 release/config/systemd/system/v2ray@.service -t "$pkgdir"/usr/lib/systemd/system/
  install -Dm644 release/config/*.json -t "$pkgdir"/etc/v2ray/
  install -Dm755 v2ray -t "$pkgdir"/usr/bin/
}
