# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=audacious-plugins
pkgver=4.4
pkgrel=1
pkgdesc="Plugins for Audacious"
arch=('loong64' 'x86_64')
url="https://audacious-media-player.org/"
license=('BSD' 'GPL')
depends=('alsa-lib' 'curl' 'dbus-glib' 'faad2' 'ffmpeg' 'flac' 'fluidsynth'
         'jack' 'lame' 'libcdio-paranoia' 'libcue' 'libmms' 'libmodplug'
         'libmtp' 'libpulse' 'libnotify' 'libsamplerate' 'libsidplayfp'
         'libvorbis' 'lirc' 'mpg123' 'neon' 'wavpack' 'libbs2b' 'libopenmpt'
         'libcddb' 'opusfile' 'libpipewire')
makedepends=("audacious>=$pkgver" 'meson' 'glib2-devel')
source=(https://distfiles.audacious-media-player.org/$pkgname-$pkgver.tar.bz2
        sidplay-rom-paths.patch)
sha256sums=('3caf3a5fe5b6f2808f461f85132fbff4ae22a53ef9f3d26d9e6030f6c6d5baa2'
            'c32cd36f75dd18db082f9b9447f1c0982279703b9d648f5695295ff25c9b678d')

prepare() {
  cd $pkgname-$pkgver

  # set paths for ROM files from vice for sidplay
  patch -Np1 -i ../sidplay-rom-paths.patch
}

build() {
  arch-meson $pkgname-$pkgver build \
    -Dgtk=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
