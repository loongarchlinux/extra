# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=debian-ports-archive-keyring
pkgver=2024.02.02
pkgrel=1
pkgdesc="GnuPG archive keys of the debian-ports archive"
arch=('any')
url="https://packages.debian.org/sid/debian-ports-archive-keyring"
license=('GPL')
depends=('gnupg')
source=("https://mirrors.kernel.org/debian/pool/main/d/debian-ports-archive-keyring/debian-ports-archive-keyring_$pkgver.tar.xz")
sha512sums=('b5e75543fd45d58c43ef7e58ef8cf46f6cebf5b0aeb8ad6a13e663f8500fe00c0a6722208b664e8338878e9259325ae94f9bb649241754894de4cac12650b272')

build() {
  cd $pkgname-$pkgver
  # Translated from debian/rules

  # Build keyrings
  mkdir -p build/keyrings
  gpg --no-keyring --import-options import-export --import active-keys/* > build/keyrings/debian-ports-archive-keyring.gpg
  gpg --no-keyring --import-options import-export --import removed-keys/* > build/keyrings/debian-ports-archive-keyring-removed.gpg
  
  # Build fragment files
  mkdir -p build/trusted.gpg.d
  for k in $(ls active-keys); do
    gpg --no-keyring --import-options import-export --import active-keys/$k > build/trusted.gpg.d/${k%.key}.gpg
  done
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 build/trusted.gpg.d/*gpg -t "$pkgdir"/etc/apt/trusted.gpg.d/
  install -Dm644 build/keyrings/*gpg -t "$pkgdir"/usr/share/keyrings/
}

# vim:set ts=2 sw=2 et:
