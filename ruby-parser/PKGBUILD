# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-parser
pkgver=3.3.3.0
pkgrel=2
pkgdesc='A production-ready Ruby parser written in pure Ruby'
arch=(any)
url='https://github.com/whitequark/parser'
license=(MIT)
depends=(
  ruby
  ruby-ast
  ruby-racc
)
makedepends=(
  ragel
  ruby-bundler
  ruby-rake
  ruby-rdoc
)
checkdepends=(
  ruby-erb
  ruby-minitest
  ruby-simplecov
)
options=(!emptydirs)
source=(https://github.com/whitequark/parser/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('a519f16f288bc5558d467f8abaca59f1d01b4ccba971a4a83371ab94ba801d750018495aacbf07c2ad6da068e54b41b8c9cc1892128d3aa6ce038f972ea78eab')
b2sums=('dd730e39a82dfb5d97bc11ce9814a5106fc608e33d59ffc7a40cd03f549476bd6984a3a090b894d0bf2d67309c113bf0a79354e1dcb9c7c7026e6b69b500243f')

prepare() {
  cd parser-$pkgver
  sed 's/~>/>=/' -i parser.gemspec
  sed -i '/[cC]liver/d' Rakefile
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd parser-$pkgver
  rake generate
  gem build parser.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    parser-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd parser-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd parser-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
