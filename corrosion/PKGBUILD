# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Vaporeon <vaporeon@vaporeon.io>

pkgname=corrosion
pkgver=0.4.9
pkgrel=1
pkgdesc='Tool for integrating Rust into an existing CMake project'
arch=(any)
url='https://github.com/corrosion-rs/corrosion'
license=(MIT)
depends=(cmake
         rust)
makedepends=(git)
source=(git+https://github.com/corrosion-rs/corrosion#tag=v$pkgver)
sha256sums=('9e6a2a7a80ee956bcbf665849b389ae637413ad7ce171dd6676579b990b83b02')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCORROSION_BUILD_TESTS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
