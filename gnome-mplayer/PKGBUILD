# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=gnome-mplayer
pkgver=1.0.9
pkgrel=9
pkgdesc='GTK/Gnome interface around MPlayer'
arch=('loong64' 'x86_64')
url='https://github.com/kdekorte/gnome-mplayer'
license=('GPL-2.0-or-later')
depends=('alsa-lib' 'cairo' 'dconf' 'dbus' 'dbus-glib' 'gdk-pixbuf2' 'glib2' 'glibc' 'gmtk' 'gtk3'
         'hicolor-icon-theme' 'libnotify' 'libx11' 'libxss')
makedepends=('git')
source=("git+https://github.com/kdekorte/gnome-mplayer.git#tag=v$pkgver"
        'gnome-mplayer.appdata.xml')
b2sums=('eb4a2008ba86f9ba99aa5b96e590212c7d3d15594f618276da08e2c1434962a0d62ff5215ea7e88214565ca22b96fa3bbd33791be51044b000970871a4fccf8f'
        '63427949e71ce98e450cc854be1d79e8076eafc4a2d854a3b5edaf25482f37a8cf189782db84ff81ad8a32023d21b148913ac7b8e2f3d9a7de4ba53741c64909')

prepare() {
  cd $pkgname

  # Add support for mate screensaver
  git cherry-pick -n 626caf4cf2ea9c69ea27113d0087dc3871d10169

  # Fix incompatible pointer type
  sed -i 's/gtk_check_menu_item_get_active(menuitem_view_subtitles)/gtk_check_menu_item_get_active(GTK_CHECK_MENU_ITEM(menuitem_view_subtitles))/' src/gui.c

  # Remove leftover of gconf
  sed -i '/AM_GCONF_SOURCE_2/d' configure.in

  # Disable Wayland backend as it's not supported
  sed -i '/g_type_init();/a gdk_set_allowed_backends ("x11");' src/main.c

  autoreconf -fi
}

build() {
  cd $pkgname
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 ../gnome-mplayer.appdata.xml "$pkgdir/usr/share/metainfo/gnome-mplayer.appdata.xml"
}
