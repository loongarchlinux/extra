# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kwpolska <kwpolska@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Laszlo Papp <djszapi @ gmail at com>

pkgname=vim-nerdtree
pkgver=7.1.2
pkgrel=1
pkgdesc='Tree explorer plugin for navigating the filesystem'
url='https://github.com/preservim/nerdtree'
arch=('any')
license=('custom:WTFPL')
depends=('vim-plugin-runtime')
groups=('vim-plugins')
source=($url/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('877563f8200510512d11ab417d61d8b49a452a64b31d5cfe1fce37d3021c906a7dce6c9c88a81e176e390e667661e948fcc82a4cf022b9f32eea135c0aece59c')
b2sums=('fa1b184114a21d7ddd82bc88c5293010ba4274cf82787e8bfa66815e9d4bcb2d4bba38ecff752af39febc401485b460318de81e271c2bafa131ca058efa52efa')

prepare() {
  cd nerdtree-${pkgver}
  sed 's|\[s:rootNERDTreePath,|["/usr/share/doc/vim-nerdtree",|' -i autoload/nerdtree.vim
}

package() {
  cd nerdtree-${pkgver}
  local _installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -d "${_installpath}"
  cp -r -t "${_installpath}" autoload doc plugin syntax nerdtree_plugin lib
  install -Dm 644 CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.markdown CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
