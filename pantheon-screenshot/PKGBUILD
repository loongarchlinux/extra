# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-screenshot
pkgver=8.0.0
pkgrel=1
pkgdesc='The Pantheon Screenshot Tool'
arch=(loong64 x86_64)
url=https://github.com/elementary/screenshot
license=(GPL3)
groups=(pantheon)
depends=(
  gdk-pixbuf2
  glib2
  gtk3
  libcanberra
  libgranite.so
  libhandy-1.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=1f989fe300eabd89487fb06ff6891e44cd83113a
source=(pantheon-screenshot::git+https://github.com/elementary/screenshot.git#tag=${_tag})
b2sums=('85486d886572d9f8d7ee613f1c260ee4ccabd23aa9a1aa031a42fb0c0559365c5eff644f7d6d685cc28540b6efcaa869918836ac1d1ef06c25a50f1c1ed61c5e')

pkgver() {
  cd pantheon-screenshot
  git describe --tags
}

build() {
  arch-meson pantheon-screenshot build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
