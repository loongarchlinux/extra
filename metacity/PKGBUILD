# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=metacity
pkgver=3.52.0
pkgrel=1
pkgdesc='Window manager of GNOME Flashback'
arch=('loong64' 'x86_64')
url='https://wiki.gnome.org/Projects/Metacity'
license=('GPL-2.0-or-later')
depends=('at-spi2-core' 'cairo' 'dconf' 'gdk-pixbuf2' 'glib2' 'glibc' 'gsettings-desktop-schemas'
         'gtk3' 'libcanberra' 'libice' 'libgtop' 'libsm' 'libx11' 'libxcomposite' 'libxcursor'
         'libxdamage' 'libxext' 'libxfixes' 'libxinerama' 'libxpresent' 'libxrandr' 'libxres'
         'libxrender' 'pango' 'startup-notification' 'zenity')
makedepends=('python')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
        '0001-frames-Add-basic-color-scheme-support.patch')
sha256sums=('a72438ace6d5903ae79338c6098b1b35ab91c8a97c4323701d3bef1f3eeb191c'
            '66e54af887dbc41742f5845f195f202113b38861e5b481dc401f9e8b78d665fb')

prepare() {
  cd $pkgname-$pkgver
  # https://gitlab.gnome.org/GNOME/metacity/-/merge_requests/36
  patch -Np1 -i ../0001-frames-Add-basic-color-scheme-support.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-vulkan
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
