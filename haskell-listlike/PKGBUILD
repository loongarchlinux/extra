# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ListLike
pkgname=haskell-listlike
pkgver=4.7.8.2
pkgrel=5
pkgdesc="Generic support for list-like structures"
url="https://github.com/ddssff/listlike"
license=('BSD')
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-dlist' 'haskell-fmlist' 'haskell-utf8-string' 'haskell-vector')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-random')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('bba76a99d0b946c80a398d792036c91398db8004804e6267aa9aa3d3a66a76a8a513d4f40e74e93a59f7dbd342872adc7ef3102d32a0ff8e6dd762969ff50b12')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYRIGHT"
}
