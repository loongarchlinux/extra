# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=one_gadget
pkgname=${_gemname}
pkgver=1.9.0
pkgrel=2
pkgdesc='The best tool for finding one gadget RCE in libc.so.6'
url='https://github.com/david942j/one_gadget'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-elftools')
makedepends=('ruby-rake' 'ruby-rdoc' 'ruby-bundler' 'ruby-yard' 'ruby-rspec')
provides=('ruby-one_gadget')
options=('!emptydirs')
source=(${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz)
sha512sums=('42b957a6600746cb01263fd82448d33ac8037e9ebf84b2ec20f07d4bf6a3056b38c94248a9fe437ae21e5cc2b404ed53c51b74c481934c27b194ac0eb4fa2ed8')
b2sums=('b7ffdf6ea58096af1c16a2ecb4bc733ef3087299046d5a788703e06ed76b9ef7bd32d60f1b9b56677738a200a2a271026f2e7dde063213c30dbae139234de6b0')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
  sed -r "s|, '< .*'||g" -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    ${_gemname}-${pkgver}.gem

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so,man} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
