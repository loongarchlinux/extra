# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-sql-plugin
pkgver=2.15.0.0
pkgrel=1
_opensearchver=2.15.0
_jdkver=11
pkgdesc="OpenSearch SQL Plugin"
arch=('loong64' 'x86_64')
url="https://opensearch.org/docs/latest/search-plugins/sql"
license=('Apache-2.0')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/sql/archive/${pkgver}.tar.gz"
  "ignore-failed-tests.patch"
)
sha256sums=('5ec3eb36a9045fd07ed63df941d516395db3721e4b24e6f1d6680ed00ca33e29'
            'd915cd6d4f23a653f77d87b48b5532f71f57e43b0a615c8028cf5a30151e3247')

prepare() {
  cd "sql-${pkgver}"
  patch -Np1 < "${srcdir}/ignore-failed-tests.patch"
}

build() {
  cd "sql-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # :doctest requires python 3.7
  # :sql:build and :integ-test (reaper) require JDK 14
  ./gradlew build \
    --exclude-task ":doctest:bootstrap" \
    --exclude-task ":doctest:stopOpenSearch" \
    --exclude-task ":doctest:doctest" \
    --exclude-task ":jacocoTestReport" \
    --exclude-task ":sql:build" \
    --exclude-task ":integ-test:integTest"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-sql"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-sql"
  unzip "${srcdir}/sql-${pkgver}/plugin/build/distributions/opensearch-sql-${pkgver}.zip"

  install -Dm644 "${srcdir}/sql-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
