# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-rapidjson
pkgver=1.15
pkgrel=2
pkgdesc="Python wrapper around rapidjson"
arch=('loong64' 'x86_64')
url="https://pypi.python.org/pypi/python-rapidjson"
license=('MIT')
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel' 'rapidjson')
checkdepends=('python-pytest' 'python-pytest-benchmark' 'python-pytz')
source=("https://pypi.io/packages/source/p/python-rapidjson/python-rapidjson-$pkgver.tar.gz")
sha512sums=('a600c6131b334bcb8030309432ef5497590d180c179d41c5fd3bad656580e950be9e3a925bb21604ed830e52df5205a65c447410f937729d2f7ece24c86f898a')

prepare() {
  #rm -r python-rapidjson-$pkgver/rapidjson
  #sed -i 's/if not os.path.isdir/if os.path.isdir/' python-rapidjson-$pkgver/setup.py
  # Shared rapidjson fails tests.
  :
}

build() {
  cd python-rapidjson-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd python-rapidjson-$pkgver
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$python_version" pytest --ignore benchmarks/
}

package() {
  cd python-rapidjson-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
