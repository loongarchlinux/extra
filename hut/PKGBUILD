# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thorben GÃ¼nther <echo YWRtaW5AeGVucm94Lm5ldAo= | base64 -d>

pkgname=hut
pkgver=0.6.0
pkgrel=1
pkgdesc='A CLI tool for Sourcehut'
arch=('loong64' 'x86_64')
url='https://sr.ht/~xenrox/hut'
license=('AGPL-3.0-only')
depends=('glibc')
makedepends=('git' 'go' 'scdoc')
options=('!lto' '!debug')
source=("git+https://git.sr.ht/~xenrox/hut#tag=v$pkgver")
sha512sums=('db551e105d5585a058e79d396b0c368efc4695ef586837e8d781a56a0a283837c32434e7434975ca68523b666ea795c5defc4e0e70c1f54f3146c7c6f710e507')
b2sums=('225bc13b0e4f89ad4182223f8b84bb110f04608108b82aae9baf73fbd44a52480001023209bc0956327af7c1b7625c94708587c59f15d76732139e8aadf5f393')

prepare() {
  cd "$pkgname"

  # download dependencies
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  make --environment-overrides
}

check() {
  cd "$pkgname"

  go test -v ./...
}

package() {
  cd "$pkgname"

  make PREFIX=/usr DESTDIR="$pkgdir" install
}
