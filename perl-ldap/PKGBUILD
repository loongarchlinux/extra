# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Tom K <tomk@runbox.com>

pkgname=perl-ldap
pkgver=0.68
pkgrel=4
pkgdesc="Interface to LDAP Protocol (RFC1777)"
arch=('any')
url="https://search.cpan.org/dist/perl-ldap"
depends=('perl-convert-asn1' 'perl-xml-sax-writer' 'perl-text-soundex')
license=("GPL")
source=(https://search.cpan.org/CPAN/authors/id/M/MA/MARSCHAP/perl-ldap-$pkgver.tar.gz)
sha512sums=('9b6260ac997c3279585745571f7d825aac956f157aef5715f13efb118723379aa9c838fdc123e2932047b28b38ebc2261ebe82ae1ef6a1f383b775d748ff18c6')

build() {
  cd  "$srcdir"/perl-ldap-$pkgver
  PERL_USE_UNSAFE_INC=1 \
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/perl-ldap-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
