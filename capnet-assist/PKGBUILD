# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=capnet-assist
pkgver=2.4.4
pkgrel=1
pkgdesc='Captive Portal Assistant'
arch=(loong64 x86_64)
url=https://github.com/elementary/capnet-assist
license=(GPL3)
groups=(pantheon)
depends=(
  gcr
  glib2
  gtk3
  libgranite.so


  libhandy-1.so
  libsoup
  networkmanager
  webkit2gtk-4.1
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=3402c143f5487fecf2330f4af9c46032532c0a9c
source=(git+https://github.com/elementary/capnet-assist.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd capnet-assist
  git describe --tags
}

prepare() {
  cd capnet-assist
  sed -i '7d' data/meson.build
  sed -i '16d' data/meson.build
}

build() {
  arch-meson capnet-assist build \
    -D b_pie=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
