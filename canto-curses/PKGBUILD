# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=canto-curses
pkgver=0.9.9
pkgrel=6
pkgdesc="Next-gen console RSS/Atom reader"
arch=(loong64 x86_64)
url="https://github.com/themoken/canto-curses"
license=(GPL-2.0-only)
depends=(
  canto-daemon
  glibc
  python
)
makedepends=(
  ncurses
  python-setuptools
  python-build
  python-installer
  python-wheel
  readline
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('2a88631506c1fda7278bb3ac376c2b67b4f2e5e1f443e49fe3035f542b66ac4b232fa66e7ae1b40e86b0a5cde3c38d11a0953754e5c5c7ed572a0da74656d798')
b2sums=('c6154dfa71ec88b405fb89feec66a6bb922dfb93523c865c33fe0664ed7ea796d795027ff65a355fd3767342ffd5fa8cb01f48475c262ded4413924df6339509')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  depends+=(
    ncurses libncursesw.so
    readline libreadline.so
  )

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
