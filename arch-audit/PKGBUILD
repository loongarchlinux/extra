# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=arch-audit
pkgver=0.2.0
pkgrel=1
pkgdesc='A utility like pkg-audit based on Arch Security Team data'
url='https://gitlab.archlinux.org/archlinux/arch-audit'
depends=(
  'ca-certificates'
  'gcc-libs'
  'glibc'
  'pacman' 'libalpm.so'
)
makedepends=(
  'cargo'
  'pkg-config'
  'scdoc'
)
arch=('loong64' 'x86_64')
license=('MIT')
backup=('etc/arch-audit/settings.toml')
source=(https://gitlab.archlinux.org/archlinux/arch-audit/-/archive/v${pkgver}/arch-audit-v${pkgver}.tar.gz
        hook-depends.patch)
sha512sums=('353d220ffd553e42d3cfc9533eb5d904979d563871f4d7f24daf883f57f055861ff03b95dc89a0f3d8dc8772f793c929e3844bc03fa4be0522a2825811b4d9a3'
            'd1a5baefe57adca4e303c8abc2de9c4d146e9783d45e6c662867ccf90ddef6b041df8c5f067aa95e9a4d4f7efa59ea43fdf903d6edcc16b7e95a0bbd33bd596c')
b2sums=('0ad3ffbc1afdec3455c2d544b3d50d4a8852332141ca2b10601e01352e29ba96132362f793c75b397926c8582c3611661607b0637fb75132d6c181c5782a6f0e'
        '3b0616eb618dc62696cab13a996086117fe11d21eab0628e7d95972b9f6987ded4a6ba53be20ae0953fdb2abd4dea356563c203b7493416dac85e65154aad6c1')
options=(!lto)

prepare() {
  cd "${pkgname}-v${pkgver}"
  patch -Np1 -i "../hook-depends.patch"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "${pkgname}-v${pkgver}"
  cargo build --frozen --release
}

check() {
  cd "${pkgname}-v${pkgver}"
  cargo test --frozen --release
}

package() {
  cd "${pkgname}-v${pkgver}"

  make DESTDIR="${pkgdir}" PREFIX=/usr install

  install -Dm 644 contrib/hooks/arch-audit.hook -t "${pkgdir}/usr/share/${pkgname}"
  install -Dm 644 contrib/config/settings.toml -t "${pkgdir}/etc/arch-audit"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
