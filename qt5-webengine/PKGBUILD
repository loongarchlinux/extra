# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-webengine
_basever=5.15.10
pkgver=5.15.15
pkgrel=1
arch=('loong64' 'x86_64')
url='https://www.qt.io'
license=('LGPL3' 'LGPL2.1' 'BSD')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss' 'libxkbfile' 
         'libevent' 'snappy' 'nss' 'libxslt' 'minizip' 'ffmpeg' 'libvpx' 'libxtst' 'ttf-font')
makedepends=('git' 'python' 'gperf' 'jsoncpp' 'ninja' 'qt5-tools' 'poppler' 'pipewire' 'nodejs')
optdepends=('pipewire: WebRTC desktop sharing under Wayland')
groups=('qt' 'qt5')
_pkgfqn=${pkgname/5-/}
source=(git+https://code.qt.io/qt/qtwebengine.git#tag=v${pkgver}-lts
        git+https://code.qt.io/qt/qtwebengine-chromium.git
        http://loongarch.zhcn.cc/list/qtwebengine/qtwebengine-5.15.2-la64.patch/59168fd4d0e9a62535f8118511fb71fa/qtwebengine-5.15.2-la64.patch
        qt5-webengine-python3.patch
        qt5-webengine-chromium-python3.patch
        qt5-webengine-ffmpeg5.patch
        qt5-webengine-pipewire-0.3.patch)
sha256sums=('SKIP'
            'SKIP'
            'cace5fc41204e2679d577b6dd1030656826bbf69a6a4db126036fad47c7db2d1'
            '0ad5d1660886f7bbf5108b071bf5d7bbbabf1cd1258ce9d4587a01dec4a1aa89'
            'd8beb3d65a1aaf927285e6f055a9d1facd0f9c3fd851f91ba568389fb3137399'
            'c50d3019626183e753c53a997dc8a55938847543aa3178d4c51f377be741c693'
            '5e3a3c4711d964d5152a04059a2b5c1d14bb13dd29bce370120f60e85b476b6f')

prepare() {
  mkdir -p build

  cd ${_pkgfqn}
  git submodule init
  git submodule set-url src/3rdparty "$srcdir"/qtwebengine-chromium
  git submodule set-branch --branch 87-based src/3rdparty
  git -c protocol.file.allow=always submodule update

  patch -p1 -i "$srcdir"/qt5-webengine-python3.patch # Fix build with Python 3
  patch -p1 -d src/3rdparty -i "$srcdir"/qt5-webengine-chromium-python3.patch

  patch -p1 -d src/3rdparty -i "$srcdir"/qt5-webengine-ffmpeg5.patch # Fix build with ffmpeg 5
  patch -p1 -d src/3rdparty -i "$srcdir"/qt5-webengine-pipewire-0.3.patch # Port to pipewire 0.3
#patch -p1 -i $srcdir/qtwebengine-5.15.2-la64.patch
}

build() {
  cd build
  qmake ../${_pkgfqn} CONFIG+=force_debug_info QT_ARCH=loongarch64 -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker \
    -webengine-icu \
    -webengine-kerberos \
    -webengine-webrtc-pipewire
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -Dm644 "$srcdir"/${_pkgfqn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium

  # Fix cmake dependency versions
  sed -e "s|$pkgver\ |$_basever |" -i "$pkgdir"/usr/lib/cmake/*/*Config.cmake
}
