# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=repod
pkgver=0.3.0
pkgrel=2
pkgdesc="Tooling to maintain binary package repositories"
arch=(any)
url="https://repod.archlinux.page/"
license=(GPL3)
depends=(
  pyalpm
  python
  python-aiofiles
  python-email-validator
  python-jinja
  python-magic
  python-orjson
  python-pydantic
  python-pydantic-settings
  python-pyxdg
  python-pyzstd
  python-subprocess-tee
  python-tomli
)
makedepends=(
  python-build
  python-installer
  python-pdm-backend
  python-sphinx
  python-sphinx-argparse
  python-sphinx_rtd_theme
  python-sphinxcontrib-programoutput
)
checkdepends=(python-pytest python-pytest-asyncio python-pytest-lazy-fixture)
source=(
  https://gitlab.archlinux.org/archlinux/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz
  0001-add-loong64-support.patch
)
sha256sums=('1d7763d0234aeb64ba21772b9de33e4b05b575ff916c0bcf7055109f06168468'
            'c440e466ae1fb26a401d08b86611230574e478584af25a26420cce58f660b826')
b2sums=('ba17d42856ba032d564fe8bdc5083f6524bc2b2d47bb353c68fd10f36a5a55795eba479f10853937772f470abe44dd9c582b49c3d905feea349c363e9249baa0'
        '509f2db206e22df793e5f7d8dd052799f30e3853a9d41c3d278065e049e9ac6dcaafde1d512077cf91b798338a52f754ef2538b773b5e5da3131aff535130492')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i $srcdir/0001-add-loong64-support.patch
}

build() {
  export PDM_BUILD_SCM_VERSION=$pkgver
  cd $pkgname-$pkgver
  make build
  make system-docs
}

check() {
  cd $pkgname-$pkgver
  make test
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" make install
}
