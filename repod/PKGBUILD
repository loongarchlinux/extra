# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=repod
pkgver=0.3.1
pkgrel=2
pkgdesc="Tooling to maintain binary package repositories"
arch=(any)
url="https://repod.archlinux.page/"
license=(GPL-3.0-or-later)
depends=(
  pyalpm
  python
  python-aiofiles
  python-email-validator
  python-jinja
  python-magic
  python-orjson
  python-pydantic
  python-pydantic-settings
  python-pyxdg
  python-pyzstd
  python-subprocess-tee
  python-typing_extensions
)
makedepends=(
  python-build
  python-installer
  python-pdm-backend
  python-sphinx
  python-sphinx-argparse
  python-sphinx_rtd_theme
  python-sphinxcontrib-programoutput
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
  python-pytest-lazy-fixtures
)
source=(
  https://gitlab.archlinux.org/archlinux/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz
)
sha512sums=('fa7fea864686855e410da764c7303a1876fc86a9b59479855ae63d9e582ad52faea4bc33bf9fad170199de555298c57fb1ca5c089231fb0b423cfd06680c5956')
b2sums=('257287997bf1477ad92c856b3fc97e76cfb2f7996714618ff8a7f6e5e3afbc21190c672e3dc29458046924bf76c79d335079892ae80ca6e0230dc7cb94e62076')

build() {
  cd $pkgname-$pkgver
  PDM_BUILD_SCM_VERSION=$pkgver make build
  make system-docs
}

check() {
  local pytest_options=(
    -vv
    -k 'not (integration or regex)'
    # fail with Python 3.12 (unittest.mock.Mock.has_calls silently removed)
    --deselect tests/config/test_settings.py::test_usertomlconfig[True-False]
    --deselect tests/config/test_settings.py::test_usertomlconfig[False-False]
    --deselect tests/config/test_settings.py::test_usertomlconfig[True-True]
    --deselect tests/config/test_settings.py::test_usertomlconfig[False-True]
    --deselect tests/config/test_settings.py::test_systemtomlconfig[True-False]
    --deselect tests/config/test_settings.py::test_systemtomlconfig[False-False]
    --deselect tests/config/test_settings.py::test_systemtomlconfig[True-True]
    --deselect tests/config/test_settings.py::test_systemtomlconfig[False-True]
  )

  cd $pkgname-$pkgver
  pytest "${pytest_options[@]}"
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" make install
}
