# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=r2ghidra
_ghidra_native=21b6dbafee5b8265ee000827dd31f542ad46861d
pkgver=5.9.2
pkgrel=1
pkgdesc='Deep ghidra decompiler integration for radare2 and iaito'
url='https://github.com/radareorg/r2ghidra'
arch=(loong64 x86_64)
license=(LGPL-3.0-only)
depends=(
  radare2
  gcc-libs
  glibc
)
makedepends=(
  git
)
provides=(radare2-r2ghidra)
replaces=(r2ghidra-dec)
source=("git+https://github.com/radareorg/r2ghidra#tag=${pkgver}"
        "git+https://github.com/radareorg/ghidra-native.git#commit=${_ghidra_native}"
        git+https://github.com/zeux/pugixml.git)
sha512sums=('d55ab9abf23878d1a34392e9bfc4487879026749baa0339d2323597c1536423becad0107e4382a3c7f7a83c4159d9528d0ff265b332b30beadb9bbc92afa8da3'
            'b715599743e675ccf0925734c8fc876a2c3f0477d4b9d44b293ea152b4b3214bd552505e752277068d22be0f2aea79e323ff8b5dafa242423e68f56171db5c11'
            'SKIP')

prepare() {
  cd ${pkgname}
  git submodule init
  git config submodule."third-party/pugixml".url "${srcdir}/pugixml"
  git -c protocol.file.allow=always submodule update --recursive
  ln -sf "${srcdir}/ghidra-native" ghidra-native
}

build() {
  cd ${pkgname}
  make -C ghidra-native patch
  ./configure PREFIX=/usr
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="$pkgdir" install
  r2version=$(r2 -v|grep radare2|sed -E 's/radare2 ([0-9\.]+) .*/\1/')
  ln -s /usr/lib/radare2/${r2version} "${pkgdir}/usr/lib/radare2/last"
}

# vim: ts=2 sw=2 et:
