# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: aulonsal <aulonsal at gmail dot com>

pkgname=hck
pkgver=0.10.0
pkgrel=1
pkgdesc='A drop in replacement for cut that can use a regex delimiter instead of a fixed string'
arch=('loong64' 'x86_64')
url='https://github.com/sstadick/hck'
license=('Unlicense' 'MIT')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'rust' 'cmake')
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('ad2853a885a39afc1b1b924983194343d7dc837576cbe8a65f346f08b502e3e67a86a759847e86f5f26f0bd354ba708027645194f9a5a4b51199c2d87cc99656')
b2sums=('4955ac25a2f67550f454037529e56888e8209e17cddc923fc86e05c4886ad206c81619fa8c4eaa9095f85f6dce65760a96c505ea618b79d5702726874c959422')

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$pkgname"

	cargo build --release --frozen
}

check() {
	cd "$pkgname"

	cargo test --frozen
}

package() {
	cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/hck

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licenses
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE-MIT UNLICENSE
}
