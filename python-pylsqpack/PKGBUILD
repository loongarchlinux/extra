# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-pylsqpack
_name=pylsqpack
pkgver=0.3.18
pkgrel=2
pkgdesc='Python bindings for ls-qpack'
url='https://github.com/aiortc/pylsqpack'
arch=('loong64' 'x86_64')
license=('BSD-3-Clause')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
b2sums=('bb7320d9254feb12740536180aa065cdf38af42ed62772178213477fa48222cfb017e5d5c087862b223219ad8a2c1ef1ab65d44de183a906d96a2b11ae51995f')

build() {
  cd "${_name}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd ${_name}-${pkgver}
  python -m installer --destdir=test_dir dist/*.whl

  PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH" python -c 'import pylsqpack'
}

package() {
  cd "${_name}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
