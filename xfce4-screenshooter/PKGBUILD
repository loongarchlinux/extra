# Maintainer: Evangelos Foutras <foutrelis@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-screenshooter
pkgver=1.11.1
pkgrel=1
pkgdesc="An application to take screenshots"
arch=('loong64' 'x86_64')
url="https://docs.xfce.org/apps/xfce4-screenshooter/start"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'curl' 'jq' 'zenity' 'xclip' 'hicolor-icon-theme')
makedepends=('glib2-devel')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('d94c4a37ac9b26f6d73214bdc254624a4ede4e111bee8d34e689f8f04c37d34d')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
