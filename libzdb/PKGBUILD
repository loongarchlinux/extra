# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libzdb
pkgver=3.3.0
pkgrel=1
pkgdesc="Zild Database Library"
arch=('loong64' 'x86_64')
url="https://www.tildeslash.com/libzdb/"
license=('GPL')
depends=('postgresql-libs' 'sqlite' 'mariadb-libs')
source=(https://www.tildeslash.com/libzdb/dist/libzdb-$pkgver.tar.gz)
sha256sums=('b1f979f48f4c52f71821f4b2983ef26cf08fc58337effe5651155cfaa96ce95d')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '1,1i#include <cassert>' test/zdbpp.cpp
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
