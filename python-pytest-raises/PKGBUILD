# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-raises
pkgver=0.11
pkgrel=5
pkgdesc='An implementation of pytest.raises as a pytest.mark fixture'
arch=('any')
license=('MIT')
url='https://github.com/Lemmons/pytest-raises'
depends=('python-pytest')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("https://github.com/Lemmons/pytest-raises/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d4df7012941bd06d090fc26018a516c10329d96ba80f69ea9794cb17f632240a4747067cc146dd04c9c9a666a1e7be0458ccf1eb7c74b4bf6e2b73daa56f80ec')

build() {
  cd pytest-raises-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  # Hack entry points by installing it

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd pytest-raises-$pkgver
  python -m installer --destdir="$PWD/tmp_install" dist/*.whl
  # Appears to be 2 test runs. 1st run has 4 failures, 2nd has 3 failures
  PYTHONPATH="$PWD/tmp_install$site_packages:$PYTHONPATH" pytest || true
}

package() {
  cd pytest-raises-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/pytest_raises-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
