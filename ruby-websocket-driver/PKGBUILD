# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='websocket-driver'
pkgname="ruby-${_gemname}"
pkgver=0.7.6
pkgrel=1
pkgdesc='WebSocket protocol handler with pluggable I/O'
arch=('loong64' 'x86_64')
url='https://github.com/faye/websocket-driver-ruby'
license=('Apache')
options=(!emptydirs)
depends=(
  ruby
  ruby-websocket-extensions
)
makedepends=(
  ruby-rake
  ruby-rake-compiler
)
checkdepends=(
  ruby-bundler
  ruby-eventmachine
  ruby-permessage_deflate
  ruby-rspec
)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5444a0748821ab651e9e5ed40fd737c9af50eca1f8046b3110ab436bb8c0c56250ee8a8e762ae1668249d9ebfa2712b52802950529a949aba2574ffc66c1c58a')
b2sums=('e8da76846b519603f7eef9cef53afdff3d48176cba21de1ce6b17e3718dd33b8309aaca721d4d11480e34e04b1ed2bfbb8892af507fbc72c1ff846c800f46499')

prepare() {
  cd "${_gemname}-ruby-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-ruby-${pkgver}"

  rake gem
}

check() {
  cd "${_gemname}-ruby-${pkgver}"

  rspec
}

package() {
  cd "${_gemname}-ruby-${pkgver}"

  local _gemdir="$(gem env gemdir)"
  local _platform="$(gem env platform | cut -d':' -f2)"
  local _extension_api_version="$(ruby -e'puts Gem.extension_api_version')"

  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" "pkg/${_gemname}-${pkgver}.gem"

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem" \
      "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/ext/websocket-driver/Makefile" \
      "${pkgdir}/${_gemdir}/extensions/${_platform}/${_extension_api_version}/${_gemname}-${pkgver}/gem_make.out"
  rm -rf "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  install -Dm 644 CHANGELOG.md CODE_OF_CONDUCT.md README.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
