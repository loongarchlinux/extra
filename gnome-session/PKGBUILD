# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-session
pkgver=46.0
pkgrel=1
pkgdesc="The GNOME Session Handler"
url="https://gitlab.gnome.org/GNOME/gnome-session"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  bash
  dconf
  gcc-libs
  glib2
  glibc
  gnome-desktop
  gsettings-desktop-schemas
  gtk3
  json-glib
  libepoxy
  libgl
  libice
  libsm
  libx11
  libxcomposite
  libxtst
  systemd
  systemd-libs
)
makedepends=(
  docbook-xsl
  git
  gtk-doc
  mesa
  meson
  python
  python-packaging
  xmlto
  xtrans
)
groups=(gnome)
_commit=2437153bfd7cbebe62c73926bb85f88076313963  # tags/46.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-session.git#commit=$_commit")
b2sums=('509cd974134e0ff8f90cd039794eae3751d204e7f6056dd97d18ac141521026c016ee5407b8086b7a1036340e3bbf04acd629be67c125e7cd0df6dfe3ee5fb28')

pkgver() {
  cd $pkgname
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
