# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='marcel'
pkgname="ruby-${_gemname}"
pkgver=1.0.4
pkgrel=1
pkgdesc='Simple mime type detection using magic numbers, filenames, and extensions'
arch=('any')
url='https://github.com/rails/marcel'
license=('Apache' 'MIT')
options=(!emptydirs)
depends=('ruby')
makedepends=('ruby-bundler' 'ruby-rake')
checkdepends=('ruby-minitest' 'ruby-rack')
source=(
  "${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
  "${pkgname}_fix_tests.patch::${url}/commit/ccd4be5043628c4389230b4c012a7115b8e44c81.patch"
)
sha512sums=('68811c3116ecafdea322c97637b3566102c21363de05678862fe1d9f1300ff6c0056449440169a69a353d77b568d0401930fbeac1651e0fea8189ae4960ce189'
            '2d3fa29dca3c1ef01bbee7a8a1f9e67a18c934ac2e7e325f1dce94f85ce83c0668bb6df1bf70174e48fe668c1b4a748a819aad84bea609bba4ebb39c47f5fc4e')
b2sums=('6f5260408de9997f8515619a661ea5b5293182a73cb45671aa2b5302e99b73b0ecb148acbeca2b8afdab8991fdd7ff789d2d79075fb5d37b0166c6a776802d03'
        'eb60856fed963588f6a10825e14deace542bbdc67a5a67d2cc71c6d71345ddd72dee344388295e3d336c50370764a02090e848241f9e02143ad91dccad2457f1')

prepare() {
  cd "${_gemname}-${pkgver}"

  patch --verbose --strip=1 --input="../${pkgname}_fix_tests.patch"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  # we dont need debuggers for testing
  sed --in-place --regexp-extend '/byebug/d' "${_gemname}.gemspec"  test/test_helper.rb
}

build() {
  cd "${_gemname}-${pkgver}"

  rake build
}

check() {
  cd "${_gemname}-${pkgver}"

  rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" "pkg/${_gemname}-${pkgver}.gem"

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"

  install -Dm 644 APACHE-LICENSE MIT-LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
