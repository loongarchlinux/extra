# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=libpqxx
pkgver=7.9.0
pkgrel=1
pkgdesc='C++ client API for PostgreSQL'
url='http://pqxx.org/development/libpqxx/'
arch=('loong64' 'x86_64')
license=('custom')
depends=('postgresql-libs>=9.1')
makedepends=('python')
provides=("libpqxx-${pkgver%.*}.so")
source=(https://github.com/jtv/libpqxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a1fafd5f6455f6c66241fca1f35f5cb603251580b99f9a0cf1b5d0a586006f16')
b2sums=('ef64a970c6395ad5bdbf399d64c7dfce43b74f1597cceffe96813ca1806a616b68ae79317edf8ccb9dd6385e47ef6f78c498a210330b934440047b98bcce8b11')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-doc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix="${pkgdir}/usr" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
