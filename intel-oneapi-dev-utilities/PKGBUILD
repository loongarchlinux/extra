# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Contributor: Jingbei Li <i@jingbei.li>

pkgname=intel-oneapi-dev-utilities
_pkgver=2024.0
_debpkgrel=49320
pkgver=2024.0.0
pkgrel=1
pkgdesc="Intel oneAPI Dev Utilities"
arch=('any')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("LicenseRef-Intel-EULA-Developer-Tools")
source=("https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-eclipse-cfg-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb")
b2sums=('8728a9723b17200b9b052c151da151bdea62cd90e3cd8df3225eaae1c1183776736d24d3b863a7e50b59c346b3ab422f98ca0c7d85919a03f7089549cf7a563b'
        '203cb090e0c570c1868e2ba3252df2d9a4b9285534fe1ac2d1fa21629fd15ea415f4f043bb767179efe5c3312c71921758ee7c752a0e560f574b1eecc920db5f')
depends=('intel-oneapi-common')
provides=("${pkgname}-eclipse-cfg")
conflicts=('intel-oneapi-basekit')

noextract=(
	"${pkgname}-${pkgver}-${_debpkgrel}_amd64.deb"
	"${pkgname}-eclipse-cfg-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb"
)

package() {
  cd "${srcdir}"
  ar x ${pkgname}-${pkgver}-${_debpkgrel}_amd64.deb
  tar xvf data.tar.xz -C "${pkgdir}"
  rm data.tar.xz

  ar x ${pkgname}-eclipse-cfg-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb
  tar xvf data.tar.xz -C "${pkgdir}"
  rm data.tar.xz

  local _dev_path='/opt/intel/oneapi/dev-utilities'
  ln -s "${_dev_path}/${_pkgver}" "${pkgdir}/${_dev_path}/latest"

  install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}
