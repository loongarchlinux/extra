# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Contributor: Jingbei Li <i@jingbei.li>

pkgname=intel-oneapi-dev-utilities
_pkgver=2024.1
_debpkgrel=340
pkgver=2024.1.0
pkgrel=1
pkgdesc="Intel oneAPI Dev Utilities"
arch=('any')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("LicenseRef-Intel-EULA-Developer-Tools")
source=("https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-eclipse-cfg-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb")
b2sums=('e9b07c6d4b05795a061bc412244cb0d79283bb578ba181ba2d1725a65584a1ccf130faa1ff6070e6015efbc1d4c0180961586e26524130fc471a8eb166a38d72'
        '78b95f377504be99f6c45581f4b7b37b953204b1c061ac41e2919f0f9d5492eef8d51585842f4a1b8c084d32a794bcdec867736c4aa7dbe606bbdf3a3ba6babe')
depends=('intel-oneapi-common')
provides=("${pkgname}-eclipse-cfg")
conflicts=('intel-oneapi-basekit')

noextract=(
	"${pkgname}-${pkgver}-${_debpkgrel}_amd64.deb"
	"${pkgname}-eclipse-cfg-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb"
)

package() {
  cd "${srcdir}"
  ar x ${pkgname}-${pkgver}-${_debpkgrel}_amd64.deb
  tar xvf data.tar.xz -C "${pkgdir}"
  rm data.tar.xz

  ar x ${pkgname}-eclipse-cfg-${_pkgver}-${pkgver}-${_debpkgrel}_all.deb
  tar xvf data.tar.xz -C "${pkgdir}"
  rm data.tar.xz

  local _dev_path='/opt/intel/oneapi/dev-utilities'
  ln -s "${_dev_path}/${_pkgver}" "${pkgdir}/${_dev_path}/latest"

  install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}
