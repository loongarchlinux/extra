# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Dragutin Cirkovic <dragonmen@gmail.com>
# Contributor: heinrich5991 <heinrich5991@gmail.com>

pkgname=srs
pkgver=5.0_r3
pkgrel=1
pkgdesc="High performance realtime video server"
_pkgcommit=f32c8c31c0966fc37f9689000dd5ed5426af1fc0
arch=('loong64' 'x86_64')
url="https://github.com/ossrs/srs"
license=('MIT')
depends=('gcc-libs'
         'openssl'
         'srs-state-threads'
         'tcl')
makedepends=('zlib'
             'unzip'
             'net-tools'
             'python'
             'git'
             'cmake')
backup=(etc/srs/srs.conf)
options=('docs')
source=("git+https://github.com/ossrs/srs/#commit=${_pkgcommit}")
sha256sums=('67bb3a7241ba3773496b30fc7b833fbd3862da338c9951cac5fc42b920cc7677')

build() {
  cd "${srcdir}"/srs/trunk
  ./configure --prefix=/install --use-sys-ssl --use-shared-st --generic-linux=on
  make
  make DESTDIR="${srcdir}" install
}

package() {
  install -Dm644 "${srcdir}"/srs/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd "${srcdir}"/install
  install -d "${pkgdir}"/usr/bin "${pkgdir}"/etc/srs
  cp -r conf/. "${pkgdir}"/etc/srs
  install -m755 objs/srs "${pkgdir}"/usr/bin/srs
}
