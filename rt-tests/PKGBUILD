# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg at alchemy dot lu>

pkgname=rt-tests
pkgver=2.7
pkgrel=1
pkgdesc="A collection of latency testing tools for the linux(-rt) kernel"
arch=(loong64 x86_64)
url="https://git.kernel.org/pub/scm/utils/rt-tests/rt-tests.git/"
license=(GPL-2.0-or-later)
groups=(realtime)
depends=(glibc)
makedepends=(
  numactl
  python-setuptools
)
optdepends=(
  'bash: for determine_maximum_mpps.sh'
  'numactl: for cyclictest, oslat and signaltest'
  'python: for get_cyclictest_snapshot and hwlatdetect'
)
source=($url/snapshot/$pkgname-$pkgver.tar.gz)
sha512sums=('6882f36c45a2fc6b44e9ef590348ac9f8cf1d9005b7a36f2eb68c9fa5f883215c93ca1b36a3072a943b6a47e0c46d9e1b14108845e7b7e9b4382727f4fd00087')
b2sums=('062bea20689126571c0758fa16d33cd45f7308b1baf72d06d4daad8d698b44ccd7829418b364f7f1651097fbd2f9d8ed1ad313ee23cd54ac92c674cd181e4ffe')

build() {
  export PYLIB=$(python -c 'import distutils.sysconfig;  print(distutils.sysconfig.get_python_lib())')
  make -C $pkgname-$pkgver
}

package() {
  make prefix=/usr DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{MAINTAINERS,README.markdown} -t "$pkgdir/usr/share/doc/$pkgname/"
}
