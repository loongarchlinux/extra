# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: Parmjot Singh <parmjotsinghrobot at gmail dot com>

pkgname=waybar
pkgver=0.10.4
pkgrel=2
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors'
arch=('loong64' 'x86_64')
url="https://github.com/Alexays/Waybar/"
license=('MIT')
depends=(
    'atkmm'
    'cairomm'
    'fmt'
    'gcc-libs'
    'glib2'
    'glibc'
    'glibmm'
    'gtk-layer-shell'
    'gtk3'
    'gtkmm3'
    'jack'
    'jsoncpp'
    'libdbusmenu-gtk3'
    'libevdev'
    'libinput'
    'libmpdclient'
    'libnl'
    'libpipewire'
    'libpulse'
    'libsigc++'
    'libwireplumber'
    'libxkbcommon'
    'playerctl'
    'sndio'
    'spdlog'
    'systemd-libs'
    'upower'
    'wayland'
)
makedepends=(
    'catch2'
    'cmake'
    'meson'
    'glib2-devel' # gdbus-codegen
    'scdoc' # For generating manpages
    'wayland-protocols'
)
backup=(
    etc/xdg/waybar/config.jsonc
    etc/xdg/waybar/style.css
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/Alexays/Waybar/archive/$pkgver.tar.gz"
    "https://github.com/Alexays/Waybar/commit/a544f4b2cdcf632f1a4424b89f6e3d85ef5aaa85.patch"
)
b2sums=('907273e6f1371252aad05212b4c59eabe1662167659d22073ee3bf649d8ee43be852b996dd7b86eecd0384b833cd2d7b9eed69b6b3292cb0b9cdbeb88c9e2391'
        '4b6da361054ae5d0e3807cc1ef91250561f2e777a6fd0cc2f610145c2666cbd828351bcb27b68c0f121f414a253a7068836b42468f5d78e5ea9e83ade90597ca')

prepare() {
    cd "Waybar-$pkgver"
    # Fix regression of broken SIGUSR1 visibility toggle
    # https://github.com/Alexays/Waybar/issues/3455
    patch -p1 < ../a544f4b2cdcf632f1a4424b89f6e3d85ef5aaa85.patch
}

build() {
    cd "Waybar-$pkgver"
    meson --prefix=/usr \
          --buildtype=plain \
          --auto-features=enabled \
          --wrap-mode=nodownload \
          -Dcpp_std=c++20 \
          -Dexperimental=true \
          -Dcava=disabled \
          -Dtests=enabled \
          build
    meson compile -C build
}

check() {
    cd "Waybar-$pkgver"
    meson test -C build --print-errorlogs --no-rebuild --suite waybar
}

package() {
    depends+=(
      'libatkmm-1.6.so'
      'libcairomm-1.0.so'
      'libgtk-3.so'
      'libjack.so'
      'libjsoncpp.so'
      'libpipewire-0.3.so'
      'libsndio.so'
      'libspdlog.so'
      'libudev.so'
      'libupower-glib.so'
    )

    cd "Waybar-$pkgver"
    meson install -C build --destdir "$pkgdir"
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
