# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Sebastien Luttringer <seblu+arch@seblu.net>
# Contributor: Jochen Immend≈ìrfer <jochen.immendoerfer@gmail.com>

pkgname=alsa-tools
pkgver=1.2.11
pkgrel=1
pkgdesc="Advanced tools for certain sound cards"
arch=(loong64 x86_64)
url="https://alsa-project.org/"
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
)
makedepends=(
  alsa-lib
  fltk
  gtk2
  gtk3
)
optdepends=(
  'fltk: hdspconf, hdspmixer'
  'gtk2: envy24control, echomixer, rmeddigicontrol'
  'gtk3: hdajackretask'
  'python-pyalsa: hwmixvolume'
  'python-gobject: hwmixvolume'
)
provides=($pkgname-emu10k1 $pkgname-ice1712 liblo10k1.so)
source=($url/files/pub/tools/$pkgname-$pkgver.tar.bz2{,.sig})
sha512sums=('b5a29a83121a637eaa16d225875feeabaea893eabc61b97d0c18a5852d851ff2e6932bd732786bb43525b410c2bb9185261cc4509fd3ee5fd8536557f12b2154'
            'SKIP')
b2sums=('2c429cb0dceb1dcb186578e2109a1b3b6eece7ba5e8e1c6de14cc5e2f37ecc947469247e95fb894bb1171fa490deb000405135ec82e822d0fc24bf5c920ec724'
        'SKIP')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

_tools=(
  as10k1
  echomixer
  envy24control
  hda-verb
  hdajackretask
  hdajacksensetest
  hdspconf
  hdsploader
  hdspmixer
  hwmixvolume
  ld10k1
  mixartloader
  pcxhrloader
  # qlo10k1  # disabled, because build is broken
  rmedigicontrol
  sb16_csp
  seq/sbiload
  sscape_ctl
  vxloader
  us428control
  usx2yloader
)

prepare() {
  for tool in "${_tools[@]}"; do
    (
      cd $pkgname-$pkgver/$tool
      autoreconf -vfi
    )
  done
}

build() {
  # not using upstream's global makefile as it's trying to be too clever
  for tool in "${_tools[@]}"; do
    (
      cd $pkgname-$pkgver/$tool
      ./configure --prefix=/usr --sbindir=/usr/bin
      make
    )
  done
}

package() {
  depends+=(
    alsa-lib libasound.so
  )

  for tool in "${_tools[@]}"; do
    make DESTDIR="$pkgdir" install -C $pkgname-$pkgver/$tool
  done
}

# vim:set ts=2 sw=2 et:
