# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-xkbcommon
pkgver=1.5.1
pkgrel=1
pkgdesc="Python bindings for libxkbcommon using cffi"
arch=(loong64 x86_64)
url="https://github.com/sde1000/python-xkbcommon"
license=(MIT)
depends=(
  glibc
  python
  python-cffi
  libxkbcommon
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  libx11  # needed for X11 locales
  python-pytest
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz
)
sha512sums=('783706e822fce99c20f8c994a09a1e340aca65482c9ad63d0d0badbb74585811d23fd7ae6429e439b904afb7b45c78a90f6cc2a982992df684ba587b43f7b40d')
b2sums=('b2a35eac9d3628217c6e664ab0b86c8e409ce663731c5d2f5764d9e450d40ac1eae9ecb23908f2b6beecba4209a8e25028ce045620fe218bb5746f48dc6f5ff8')

build() {
  cd $pkgname-$pkgver
  python ${pkgname#python-}/ffi_build.py
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  export XLOCALEDIR=/usr/share/X11/locale/
  pytest -v
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
