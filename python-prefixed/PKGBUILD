# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Alan <alan1world@googlemail.com>

pkgname=python-prefixed
pkgver=0.8.0
pkgrel=1
pkgdesc='Prefixed alternative numeric library'
arch=(any)
url=https://github.com/Rockhopper-Technologies/prefixed
license=(MPL2)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=f6615ba74055680a8a2fd14554cf9819ef884aa3
source=(git+https://github.com/Rockhopper-Technologies/prefixed.git#tag=${_tag})
b2sums=('d4b2729b4f7052dfb7e58ef0e0dd161e8f1ffcfa1dbce9cd57e73f9955767516802be748d0f75db6cbe337d659faaef1d7d48e11a354c07f674f5514df8ab83f')

pkgver() {
  cd prefixed
  git describe --tags
}

build() {
  cd prefixed
  python -m build --wheel --no-isolation
}

package(){
  cd prefixed
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-prefixed/
}

# vim: ts=2 sw=2 et:
