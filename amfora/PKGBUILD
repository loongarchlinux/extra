# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Paul Boyd <boyd.paul2@gmail.com>

pkgname=amfora
pkgver=1.10.0
pkgrel=2
pkgdesc='Terminal browser for the Gemini protocol'
arch=(loong64 x86_64)
url='https://github.com/makeworld-the-better-one/amfora'
license=(GPL3)
makedepends=(git go imagemagick)
source=("git+$url#commit=2534983d96c431430e9263c27ce5bfa36b66c3d5") # tag: v1.10.0
b2sums=('7fc7424d5d436e7415dfa4825ff883dcbd0e6d695c822cd11e503dd0b39413ee70035cb300f0c180a8d5dfd5b8aca0bd05c57e743ecccddad7fcbb5e892bfa7b')

prepare() {
  cd $pkgname
  convert logo.png -colorspace sRGB \( +clone -crop 50%x100%+0+0 \) -delete 0 -fuzz 10% -transparent white \
    -background none -resize 48x48 -gravity center -extent 48x48 -strip amfora.png
  echo Icon=amfora >> amfora.desktop
}

build() {
  cd $pkgname
  go build -buildmode=pie -mod=readonly -modcacherw -trimpath -ldflags \
    "-s -w -linkmode=external -extldflags \"$LDFLAGS\" -X main.version=$pkgver -X main.builtBy=Arch_Linux -X main.commit=${source#*=}"
}

package() {
  cd $pkgname
  install -Dm755 amfora "$pkgdir/usr/bin/amfora"
  install -Dm644 amfora.png "$pkgdir/usr/share/pixmaps/amfora.png"
  install -Dm644 amfora.desktop "$pkgdir/usr/share/applications/amfora.desktop"
}
