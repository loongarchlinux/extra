# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-bilibili
pkgver=0.4.0
pkgrel=1
pkgdesc="Bilibili provider for FeelUOwn"
arch=('any')
url="https://github.com/feeluown/feeluown-bilibili"
license=('GPL3')
depends=('feeluown' 'python-cachetools' 'python-beautifulsoup4' 'python-pycryptodome')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core')
groups=('feeluown-full')
source=("git+https://github.com/feeluown/feeluown-bilibili.git#tag=v$pkgver")
sha512sums=('8ebc867119e577eb6a4be943219062ea19ab3581fc61a3977425110f659b6ee4d0c5ee7289b79a08c95943dfb17d57b639a4328ac563b7e4146ddb9c555d6605')

build() {
  cd feeluown-bilibili
  python -m build -nw
}

package() {
  cd feeluown-bilibili
  python -m installer --destdir="$pkgdir" dist/*.whl
}
