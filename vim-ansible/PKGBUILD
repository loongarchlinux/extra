# Maintainer: David Runge <dvzrv@archlinux.org>

_name=ansible-vim
pkgname=vim-ansible
pkgver=3.3
pkgrel=1
pkgdesc="A vim plugin for syntax highlighting Ansible's common filetypes"
arch=(any)
url="https://github.com/pearofducks/ansible-vim"
license=(
  BSD
  MIT
)
groups=(vim-plugins)
depends=(
  python-jinja
  vim
)
makedepends=(
  ansible
  python
)
optdepends=('vim-ultisnips: for ansible snippets')
source=($url/archive/$pkgver/$_name-$pkgver.tar.gz)
sha512sums=('7848e91ebf43d9581773436eca4310c5a26f743f2384d9cfc2e430a7b84fd945c4cd65aeb54947c47e3903e1fccb275d94bcfa22f6660e3438cc9785c38ca10f')
b2sums=('036cfdbdefb7d4992700476556a2805a3bc9cfba9e98d793eea7526fc2587a23ed34e64cfc141a54d9f435e957db7315b6549190de7dde3b619fe4360af238bb')

build() {
  cd $_name-$pkgver
  # generating a reproducible UltiSnips snippet file:
  # https://github.com/pearofducks/ansible-vim/pull/105
  python UltiSnips/generate.py --style=dictionary
}

package() {
  cd $_name-$pkgver
  install -vDm 644 ftdetect/*.vim -t "$pkgdir/usr/share/vim/vimfiles/ftdetect/"
  install -vDm 644 ftplugin/*.vim -t "$pkgdir/usr/share/vim/vimfiles/ftplugin/"
  install -vDm 644 indent/*.vim -t "$pkgdir/usr/share/vim/vimfiles/indent/"
  install -vDm 644 syntax/*.vim -t "$pkgdir/usr/share/vim/vimfiles/syntax/"
  install -vDm 644 ansible.snippets -t "$pkgdir/usr/share/vim/vimfiles/UltiSnips/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 syntax/*.vim_LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
