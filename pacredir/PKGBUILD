# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=pacredir
pkgver=0.4.7
pkgrel=3
pkgdesc='redirect pacman requests, assisted by avahi service discovery'
arch=('loong64' 'x86_64')
url='https://github.com/eworm-de/pacredir'
license=('GPL')
depends=('systemd-libs' 'avahi' 'curl' 'iniparser' 'darkhttpd' 'libmicrohttpd')
makedepends=('systemd' 'discount')
conflicts=('pacserve')
install=pacredir.install
backup=('etc/pacman.d/pacredir'
        'etc/pacredir.conf')
validpgpkeys=('BD84DE71F493DF6814B0167254EDC91609BC9183') # Christian Hesse <mail@eworm.de>
source=("https://www.eworm.de/download/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('af305e82ad1489086a9467cff2c55fc82130a686756ff930d766a1ef17a203ab'
            'SKIP')

prepare() {
  cd ${pkgname}-${pkgver}/

  sed -i 's|-liniparser|-I/usr/include/iniparser -liniparser|' Makefile
}

build() {
  cd ${pkgname}-${pkgver}/

  make
}

package() {
  cd ${pkgname}-${pkgver}/

  make DESTDIR="${pkgdir}" install
}

