# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname=ruby-pycall
pkgver=1.5.2
_commit=1f9e8f0d937ffc3a8b37e6b16aa3b407a5c74a4b
pkgrel=1
pkgdesc='Calling Python functions from the Ruby language'
arch=(loong64 x86_64)
url='https://github.com/mrkn/pycall.rb'
license=(MIT)
depends=(
  ruby
  python
)
makedepends=(
  git
  ruby-bundler
  ruby-pry
  ruby-pry-byebug
  ruby-rake
  ruby-rake-compiler
  ruby-rspec
  ruby-test-unit
)
checkdepends=(
  python-numpy
)
options=(!emptydirs)
source=(git+https://github.com/mrkn/pycall.rb.git#commit=$_commit)
sha512sums=('SKIP')

prepare() {
  cd pycall.rb
  sed -r -e '/rake-compiler-dock/d' -e '/launchy/d' -i pycall.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd pycall.rb
  rake build
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    pkg/pycall-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r "tmp_install/$_gemdir/"{cache,gems/pycall-$pkgver/ext}
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd pycall.rb
  GEM_HOME="tmp_install/$_gemdir" rake spec
}

package() {
  cd pycall.rb
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
