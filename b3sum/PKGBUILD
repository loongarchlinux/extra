# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=BLAKE3
pkgname=b3sum
pkgver=1.5.3
pkgrel=1
pkgdesc='Command line implementation of the BLAKE3 hash function'
arch=(loong64 x86_64)
url=https://github.com/BLAKE3-team/BLAKE3
license=('CC0-1.0 OR Apache-2.0 OR Apache-2.0 WITH LLVM-exception')
depends=(gcc-libs)
makedepends=(
  git
  rust
)
source=("git+$url.git#tag=$pkgver")
b2sums=('4f344d8bb2b28a79958b4d4cb6aa695d1bd179347a1b63263891445c47938843a45f0987bb6d792b6b8dc4a2ba4daebb0c3db08a38992f771cba3dd0cc742cfd')

prepare() {
  cd $_name/$pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name/$pkgname
  cargo build --release --locked --offline
}

check() {
  cd $_name/$pkgname
  cargo test --locked --offline
}

package() {
  cd $_name/$pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
}
