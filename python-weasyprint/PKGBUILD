# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Florijan Hamzic <florijanh at gmail dot com>

pkgname=python-weasyprint
pkgver=62.3
pkgrel=2
pkgdesc='Utility to render HTML and CSS to PDF'
arch=(any)
url='https://weasyprint.org/'
_url='https://github.com/Kozea/WeasyPrint'
license=(BSD-3-Clause)
_pydeps=(brotli
         cffi
         cssselect2
         fonttools
         html5lib
         pillow
         pydyf
         pyphen
         'tinycss2>=1.3' # so this doesn't leave testing out of order
         zopfli)
depends=(pango
         python
         "${_pydeps[@]/#/python-}"
         ttf-font)
makedepends=(git
             python-{build,installer,wheel}
             python-flit-core)
checkdepends=(python-pytest
              ghostscript
              ttf-dejavu)
source=("$pkgname::git+$_url.git#tag=v$pkgver")
sha256sums=('3b5d9010e02dba707553b6271cd13f6d57acfa8531c8cac5e57733c998fffd6b')

prepare() {
	cd "$pkgname"
	# thou shalt not lint coding style in integration tests
	sed -i '/^addopts/d' pyproject.toml
}

build(){
	cd "$pkgname"
	python -m build -wn
}

check() {
	cd "$pkgname"
	local deselected=(
		tests/draw/test_gradient.py::test_linear_gradients_5
		tests/draw/test_gradient.py::test_linear_gradients_12
	)
	pytest ${deselected[@]/#/--deselect }
}

package() {
	cd "$pkgname"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
