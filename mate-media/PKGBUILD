# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-media
pkgver=1.28.1
pkgrel=1
url="https://mate-desktop.org"
pkgdesc="MATE Media Tools"
arch=('loong64' 'x86_64')
license=('GPL')
depends=('libcanberra' 'libmatemixer' 'mate-desktop' 'mate-panel' 'gettext')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
groups=('mate-extra')
conflicts=('mate-media-gtk3')
replaces=('mate-media-gtk3')
sha256sums=('bcdc102e22f63f55e63166d5c708e91c113570e6a30a874345a88609e83a9912')

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
                --prefix=/usr \
                --sysconfdir=/etc \
                --libexecdir=/usr/lib/${pkgname} \
                --localstatedir=/var
    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
