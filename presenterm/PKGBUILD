# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=presenterm
pkgver=0.8.0
pkgrel=1
pkgdesc="A markdown terminal slideshow tool"
arch=('loong64' 'x86_64')
url="https://github.com/mfontanini/presenterm"
license=('BSD-2-Clause')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('65a4ab5b1a04d5833c40248c718c6779f49b1a766509f51a0f6f49909015566ae1af5d8ae918ae89e6f7961d14cf1a295b9a96216f814086a8067b0c80243fb6')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
