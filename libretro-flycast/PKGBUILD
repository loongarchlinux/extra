# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-flycast
pkgver=6323
pkgrel=1
pkgdesc='Sega Dreamcast core'
arch=(loong64 x86_64)
url=https://github.com/flyinghead/flycast
license=(GPL2)
groups=(libretro)
depends=(
  libgl
  libretro-core-info
  zlib
)
makedepends=(
  cmake
  git
  mesa
  ninja
)
_commit=3afa4322f10a3b60a4786d6e09c815eddf0a2bbb
source=(
  git+https://github.com/flyinghead/flycast.git#commit=${_commit}
  git+https://github.com/KhronosGroup/glslang.git
  git+https://github.com/rtissera/libchdr.git
  git+https://github.com/KhronosGroup/Vulkan-Headers.git
  git+https://github.com/GPUOpen-LibrariesAndSDKs/VulkanMemoryAllocator.git
)
b2sums=('684274243a81dc1f510ce37bebecb1fb1c9ed129ab8520c75840c98e92a41bd9120c7a0306357c89ede3cf1ff49a278b4ba2a73ee237d5d18481c3cbcd2f51e6'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
  cd flycast
  for submodule in core/deps/{glslang,libchdr,Vulkan-Headers,VulkanMemoryAllocator}; do
    git submodule init ${submodule}
    git submodule set-url ${submodule} "${srcdir}/${submodule##*/}"
    git -c protocol.file.allow=always submodule update ${submodule}
  done
}

pkgver() {
  cd flycast
  git rev-list --count HEAD
}

build() {
  cmake -S flycast -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DLIBRETRO=ON \
    -Wno-dev
  cmake --build build
}

package() {
  install -Dm 644 build/flycast_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
