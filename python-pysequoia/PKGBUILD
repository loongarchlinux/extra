# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pysequoia
pkgname=python-pysequoia
pkgver=0.1.24
pkgrel=2
pkgdesc="Provides OpenPGP facilities in Python through the Sequoia PGP library"
arch=(loong64 x86_64)
url="https://github.com/wiktor-k/pysequoia"
license=(Apache-2.0)
groups=(sequoia)
depends=(
  bzip2
  gcc-libs
  glibc
  gmp
  nettle
  pcsclite
  python
)
makedepends=(
  clang
  python-build
  python-installer
  python-maturin
  python-wheel
)
source=($_name-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('b7726993ef6ba588b8307b9d0cbd99f85f177eaa109c84e46d6e92f056842973edfc00097571ed211aa301671ed19418b86b4a2c718826be48acec2a679c934a')
b2sums=('4bf72e14d75bd1d2c98d2035d61a8bd3f6e9b9173233d0574ba8fb598f5df9fa7570b528af466f75b9ff6ccb93a73b49c5a2f08a0cd1d79b87c0d24b61bbe106')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgnam/"
}
