# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>

pkgname=trompeloeil
pkgver=48
pkgrel=1
pkgdesc="Thread-safe header-only mocking framework for C++11/14"
arch=('loong64' 'x86_64')
url="https://github.com/rollbear/trompeloeil"
license=('BSL-1.0')
makedepends=(cmake ninja)
checkdepends=(catch2)
source=("$pkgname-$pkgver.tar.gz::https://github.com/rollbear/trompeloeil/archive/v${pkgver}.tar.gz")
sha256sums=('eebd18456975251ea3450b815e241cccfefba5b883e4a36bd309f9cf629bdec6')
b2sums=('8f1a1214953f0acac49da8a211320fa76bc7176ff8163fae6f63b1e86bce213aab0e5ad7f62ae3e2d5bb2611a69a6d9fec0467d4c596021a47a280c43d1df714')

build() {
  cmake \
    -Bbuild \
    -GNinja \
    -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='Debug' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DTROMPELOEIL_INSTALL_TARGETS=ON \
    -DTROMPELOEIL_INSTALL_DOCS=OFF \
    -DTROMPELOEIL_BUILD_TESTS=yes \
    -Wno-dev
  cmake --build build --verbose --target self_test thread_terror custom_recursive_mutex
}

check() {
  ./build/test/self_test || exit -1
  ./build/test/thread_terror || exit -1
  ./build/test/custom_recursive_mutex || exit -1
}


package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE_1_0.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
