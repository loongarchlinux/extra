# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=meld
pkgver=3.22.2
pkgrel=3
pkgdesc="Compare files, directories and working copies"
url="https://meldmerge.org/"
license=(GPL-2.0-or-later)
arch=(any)
depends=(
  dconf
  gdk-pixbuf2
  glib2
  gsettings-desktop-schemas
  gtk3
  gtksourceview4
  hicolor-icon-theme
  pango
  python
  python-cairo
  python-gobject
)
makedepends=(
  git
  meson
  yelp-tools
)
checkdepends=(appstream-glib)
source=("git+https://gitlab.gnome.org/GNOME/meld.git#tag=$pkgver")
b2sums=('05edce3ee1b4bf14c494290f46a39fafc6683337ef71187bf4d1b6d4ce5e8fa8657e88f865ad159dfebc26aa6ea060f451869f989684cd411e19fa2153e1d5c8')

prepare() {
  cd meld
}

build() {
  arch-meson meld build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
