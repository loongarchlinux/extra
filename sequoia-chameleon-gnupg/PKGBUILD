# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sequoia-chameleon-gnupg
pkgver=0.9.0
pkgrel=1
pkgdesc="A re-implementation and drop-in replacement of gpg and gpgv"
arch=(loong64 x86_64)
url="https://gitlab.com/sequoia-pgp/sequoia-chameleon-gnupg"
license=(GPL-3.0-or-later)
groups=(sequoia)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  bzip2
  cargo
  clang
  git
  openssl
  sqlite
)
checkdepends=(
  gnupg
  sequoia-sq
)
source=(
  git+$url?signed#tag=v$pkgver
)
sha512sums=('dd443ada9fd539f8e4e7e89059e38ca3244e832c8d8ce401c4b278b3505adeff2670f4bf7972f46710c0acc68496075ce2eff2978aadf0ebc2f634ec2dcc9c21')
b2sums=('84c15659e38ed2d60082221d4eaeebca0c857c331ead974cdef48bd13ac4c4718361049f8f54e6adb9174c6f4923428d2079b8dbcc44038187eeefd3d9a22f43')
validpgpkeys=(
  CBCD8F030588653EEDD7E2659B7DD433F254904A  # Justus Winter <justus@sequoia-pgp.org>
)

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --no-default-features --features=crypto-openssl
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  RUST_BACKTRACE=1
  # skip broken test: https://gitlab.com/sequoia-pgp/sequoia-chameleon-gnupg/-/issues/58
  cargo test --frozen --no-default-features --features=crypto-openssl -- --skip password_store_git
}

package() {
  depends+=(
    bzip2 libbz2.so
    openssl libcrypto.so libssl.so
    sqlite libsqlite3.so
  )

  cd $pkgname
  install -vDm 755 target/release/gpg{,v}-sq -t "$pkgdir/usr/bin/"
  install -vDm 644 gpg-sq-parcimonie.service -t "$pkgdir/usr/lib/systemd/user/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
