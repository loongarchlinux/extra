# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=rkcommon
pkgver=1.13.0
pkgrel=1
pkgdesc="Intel RenderKit common C++/CMake infrastructure"
arch=(loong64 x86_64)
url="https://github.com/ospray/rkcommon/"
license=(Apache-2.0)
depends=(gcc-libs glibc onetbb)
makedepends=(cmake)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('8ae9f911420085ceeca36e1f16d1316a77befbf6bf6de2a186d65440ac66ff1f')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -G 'Unix Makefiles' \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
