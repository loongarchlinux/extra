# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-pyahocorasick
_name=pyahocorasick
pkgver=2.1.0
pkgrel=2
pkgdesc='Python module (C extension and plain python) implementing Aho-Corasick algorithm'
url='https://github.com/WojciechMula/pyahocorasick'
arch=('loong64' 'x86_64')
license=('BSD-3-Clause')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
b2sums=('f85f04e17fb4f5b66e4b1855dc83a0a2934742d52c1021218d6f4f3694c2427c4c6faf9582a474c432bda9e98710429017aa267e86a4008d8f27884ca1e4fc98')

build() {
  cd "${_name}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${_name}-${pkgver}"
  PYTHONPATH=. python -c 'import src'
}

package() {
  cd "${_name}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
