# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=audaspace
pkgver=1.5.0
pkgrel=2
pkgdesc="A high level and feature rich audio library written in C++ with language bindings"
arch=(loong64 x86_64)
url="https://audaspace.github.io/"
license=(Apache)
depends=(
  'ffmpeg'
  'fftw'
  'gcc-libs'
  'glibc'
  'jack'
  'libpulse'
  'libsndfile'
  'openal'
  'sdl2'
)
makedepends=(
  'cmake'
  'python'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
  'python-numpy'
)
optdepends=(
  'python: python language bindings'
  'python-numpy: python language bindings'
)
source=("https://github.com/neXyon/audaspace/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha512sums=('9c463c2ddfc2de10537590d678bcdee09e15f3f3ca386aebe8eb0a9f7bd9249b7a8cab51aabc9fd8fc640842bb78ae1ad229521144c3796894f0205217b23479')
b2sums=('7fae3f864675af38d3ec6402be2f6d08d9458ca0d4d27151b51a4fb556cde089a5892f5a66cd193b99b620826ee3ac5af33a10e7925971036cf893fd06f64349')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D DEFAULT_PLUGIN_PATH=/usr/lib/$pkgname/plugins
    -D WITH_JACK=ON
    -D WITH_PYTHON=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose

  cd build
  python -m build --wheel --no-isolation
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  python -m installer --destdir="$pkgdir" build/dist/*.whl
}
