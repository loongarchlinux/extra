# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: daurnimator <daurnimator@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Thomas Wei√üschuh <thomas_weissschuh lavabit com>

pkgname=httpie
pkgver=3.2.2
pkgrel=2
pkgdesc="human-friendly CLI HTTP client for the API era"
url="https://github.com/httpie/cli"
depends=('python-charset-normalizer'
         'python-defusedxml'
         'python-multidict'
         'python-pip'
         'python-pygments'
         'python-pysocks'
         'python-requests'
         'python-requests-toolbelt'
         'python-rich')
makedepends=('python-build'
             'python-installer'
             'python-setuptools'
             'python-wheel')
checkdepends=('python-pytest'
              'python-pytest-httpbin'
              'python-pytest-mock'
              'python-responses'
              'python-werkzeug')
conflicts=(python-httpie)
replaces=(python-httpie python2-httpie)
license=('BSD-3-Clause')
arch=('any')
source=($pkgname-$pkgver.tar.gz::"https://github.com/httpie/cli/archive/$pkgver.tar.gz"
        $pkgname-3.2.2-drop-python-lazy-fixture.patch
)
sha256sums=('01b4407202fac3cc68c73a8ff1f4a81a759d9575fabfad855772c29365fe18e6'
            'aca3cfda387c76a2ecac4d4db8b619f413194a5f8cf34c576a7fade4f8c7df5d')

prepare() {
  cd "cli-${pkgver}"
  patch -Np1 -i ../$pkgname-3.2.2-drop-python-lazy-fixture.patch
}

build() {
  cd cli-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd cli-$pkgver
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/httpie/LICENSE"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Fix upstream, include them in MANIFEST.in and use data_files in setup.py to install them automatically
  # TODO: add zsh support
  install -Dm644 extras/httpie-completion.bash "$pkgdir"/usr/share/bash-completion/completions/http
  install -Dm644 extras/httpie-completion.fish "$pkgdir"/usr/share/fish/vendor_completions.d/http.fish
}

check() {
  cd cli-$pkgver
  # https://github.com/httpie/cli/issues/1530
  python -m pytest \
    -m 'not requires_installation' \
    -k "not test_binary and not test_compress_form and not test_stdin_read_warning"
}
