# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-minio
pkgver=7.2.0
pkgrel=2
pkgdesc='MinIO Python Library for Amazon S3 Compatible Cloud Storage'
arch=('any')
url='https://github.com/minio/minio-py'
license=('Apache')
depends=(
  'python'
  'python-certifi'
  'python-urllib3'
  'python-argon2_cffi'
  'python-pycryptodome'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
checkdepends=(
  'minio'
  'python-pytest'
)
_commit='c37956b494a535429ff881171db0b4f6db1dc547'
source=("$pkgname::git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags
}

prepare() {
  cd "$pkgname"

  ln -sf /usr/bin/minio tests/functional/minio
  sed -i 's/assertEquals/assertEqual/g' tests/unit/crypto_test.py
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m pytest

  ./run_functional_tests.sh
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
