# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>

pkgname=python-minio
pkgver=7.2.7
pkgrel=1
pkgdesc='MinIO Python Library for Amazon S3 Compatible Cloud Storage'
arch=('any')
url='https://github.com/minio/minio-py'
license=('Apache-2.0')
depends=(
  'python'
  'python-argon2_cffi'
  'python-certifi'
  'python-pycryptodome'
  'python-typing_extensions'
  'python-urllib3'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=(
  'minio'
  'python-pytest'
)
source=("$pkgname::git+$url.git#tag=$pkgver")
b2sums=('9b912dc38d3f7933012b4f43d03b9cdd4583b798b675165ea72db7aa16355c4d9afdc2c0b83419a4e665210a8b44f7fc48b61b4788d938dbca0fcb4273af480e')

prepare() {
  cd "$pkgname"

  ln -sf /usr/bin/minio tests/functional/minio
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  python -m pytest

  ./run_functional_tests.sh
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
