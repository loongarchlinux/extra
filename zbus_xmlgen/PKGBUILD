# Maintainer: David Runge <dvzrv@archlinux.org>

_project_name=zbus
_name=zbus-xmlgen
pkgname=zbus_xmlgen
pkgver=4.1.0
pkgrel=1
pkgdesc="D-Bus XML interface code generator"
arch=(loong64 x86_64)
url="https://github.com/dbus2/zbus"
license=(MIT)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=(
  $url/archive/refs/tags/$pkgname-$pkgver.tar.gz
)
sha512sums=('6b8ca8a40af1aeaa23d26c1abed97601a1bc5e98bda490d27067c962b5dfb6a3e0c3a2d0e66f722a578b5980f2f888bacabf9aa7f8449559e24207feddb3fc1c')
b2sums=('43bb74780bea07325540a9bddfaa9fb0be77a90e06b442f2703061daf03cac72e64033ec0f7e328c77b3e0806d3244053adebdae303353592b91c067f3ceecfe')

prepare() {
  cd $_project_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_project_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features --package $pkgname
}

check() {
  cd $_project_name-$pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features --package $pkgname
}

package() {
  install -vDm 755 $_project_name-$pkgname-$pkgver/target/release/$_name -t "$pkgdir/usr/bin/"
  install -vDm 644 $_project_name-$pkgname-$pkgver/LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/MIT.txt"
}
