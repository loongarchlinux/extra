# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=toot
pkgver=0.41.1
pkgrel=2
pkgdesc='A Mastodon CLI client'
arch=('any')
url='https://github.com/ihabunek/toot'
license=('GPL3')
depends=(
  'python'
  'python-requests'
  'python-beautifulsoup4'
  'python-wcwidth'
  'python-urwid'
  'python-tomlkit'
  'python-click'
  'python-typing_extensions'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("$pkgname::git+$url#tag=$pkgver")
b2sums=('SKIP')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation

  # generate shell completions
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PATH="$(pwd)/tmp/usr/bin:$PATH"
  export PYTHONPATH="$(pwd)/tmp/$site_packages"
  for shell in bash fish zsh; do
  _TOOT_COMPLETE=${shell}_source toot > "$shell.completion"
  done
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # shell completion
  install -vDm644 bash.completion "$pkgdir/usr/share/bash-completion/completions/toot"
  install -vDm644 fish.completion "$pkgdir/usr/share/fish/vendor_completions.d/toot.fish"
  install -vDm644 zsh.completion "$pkgdir/usr/share/zsh/site-functions/_toot"
}
