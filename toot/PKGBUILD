# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=toot
pkgver=0.43.0
pkgrel=1
pkgdesc='A Mastodon CLI client'
arch=('any')
url='https://github.com/ihabunek/toot'
license=('GPL-3.0-only')
depends=(
  'python'
  'python-requests'
  'python-beautifulsoup4'
  'python-wcwidth'
  'python-urwid'
  'python-tomlkit'
  'python-click'
  'python-typing_extensions'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('72d706f4ee58610803f5b45d5188e19d13e4e90827d79f83ef4696fa3e899ecab1d500ae2152bbc1008422647e30806a0587e838d7a8a1ac57fe98882ca172e9')
b2sums=('3b482b2d3da55e61fb5f80941943ab079297832281deb59984507fd17af0981c395cf168937c97239cc720bfb83977b292d7999acb3da1f308e199ef41a96d89')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation

  # generate shell completions
  python -m installer --destdir="$(pwd)/tmp" dist/*.whl
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  export PATH="$(pwd)/tmp/usr/bin:$PATH"
  export PYTHONPATH="$(pwd)/tmp/$site_packages"
  for shell in bash fish zsh; do
  _TOOT_COMPLETE=${shell}_source toot > "$shell.completion"
  done
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # shell completion
  install -vDm644 bash.completion "$pkgdir/usr/share/bash-completion/completions/toot"
  install -vDm644 fish.completion "$pkgdir/usr/share/fish/vendor_completions.d/toot.fish"
  install -vDm644 zsh.completion "$pkgdir/usr/share/zsh/site-functions/_toot"
}
