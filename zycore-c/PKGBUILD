# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=zycore-c
pkgver=1.5.0
pkgrel=1
pkgdesc='Zyan Core Library for C'
arch=('loong64' 'x86_64')
url='https://github.com/zyantific/zycore-c'
license=('MIT')
depends=('glibc')
makedepends=('git' 'cmake')
provides=('libZycore.so')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('f1c58f6481bfda06f1aa6dcfa02cf51ddd031694f6807ffaaad5121facb8244557301548f71bad782663ce1e66767ee7fd5a8cabefef8578263f1cd0c6d09f5f')
b2sums=('e99648b347444524ef36c79b0a739047eb085da0008fe53ea0f569c93a3f958196cfbf6caa067d2ab52e30e9ad48e84d99d5a04e0a5b15d27333c04e1793557e')

prepare() {
  cmake \
    -S "$pkgname" \
    -B build \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D ZYCORE_BUILD_SHARED_LIB=ON
}

build() {
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE"
}
