# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=jellyfin-web
pkgver=10.9.9
pkgrel=1
pkgdesc='Web client for Jellyfin'
arch=('any')
url='https://jellyfin.org'
_url='https://github.com/jellyfin/jellyfin-web'
license=('GPL-2.0-or-later')
makedepends=('git' 'nodejs' 'npm')
source=("$pkgname::git+$_url#tag=v$pkgver")
sha512sums=('7ff75bd8fae4fa21e573b3da796012cb133022ddb149fa160b9d390d1291fd20c2579bb5b782ddae9af32dbb68a9268ce3ed183a26faf03cc2ad9c8f10d6d25c')
b2sums=('2ba983a03e605af15c3e3ee9282dc3751b5a9428f8e3b5e9536719a0f47bd26b5099e1bfd8de4fbe294fbd1c58427135795e770ac6bd78b0e7412b454689f82b')

prepare() {
  cd "$pkgname"

  # download dependencies
  # FS#79713 - remove environment variable with 10.9.x release
  SKIP_PREPARE=1 npm ci --no-audit --no-fund --no-update-notifier
}

build() {
  cd "$pkgname"

  npm run build:production
}

package() {
  cd "$pkgname"

  install -vd "$pkgdir/usr/share/jellyfin/web"
  cp -vr dist/* "$pkgdir/usr/share/jellyfin/web"
}

# vim: ts=2 sw=2 et:
