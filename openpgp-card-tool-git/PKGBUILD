# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openpgp-card-tool-git
pkgver=0.1.3
pkgrel=1
pkgdesc="A simple tool for Git signing and verification with a focus on OpenPGP cards"
arch=(loong64 x86_64)
url="https://codeberg.org/openpgp-card/oct-git"
_name="${url##*/}"
license=('Apache-2.0 OR MIT')
depends=(
  gcc-libs
  glibc
  openssl
  org.freedesktop.secrets
  sqlite
)
makedepends=(
  pcsclite
  rust
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('2f24a6e77ce2ab06836dccdca443ed13a2908dc5606abec49fc5e8e7bedab90a2e8b6417e0b601769cde6267aa6fdf0e11e43eb8688122410f0f2b61700191a1')
b2sums=('46614e83d0d3ba408439df1a1f47037ea3b97eb2922da331fe5f736395af65a1c0dc2e5bb7ed985b4506e7d9a80b9adea070e6cb3833ebfb262511396f312852')

prepare() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $_name
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  depends+=(
    pcsclite libpcsclite.so
  )

  cd $_name
  install -vDm 755 -t "$pkgdir/usr/bin/" target/release/oct-git
  install -vDm 644 README.md -t "$pkgdir/usr/lib/systemd/user/"
  install -vDm 644 LICENSES/*.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
