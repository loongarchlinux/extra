# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=opensnitch
pkgver=1.6.5.1
pkgrel=1
pkgdesc='A GNU/Linux application firewall'
arch=('loong64' 'x86_64')
url='https://github.com/evilsocket/opensnitch'
license=('GPL-3.0-or-later')
depends=(
  'glibc'
  'hicolor-icon-theme'
  'libnetfilter_queue'
  'libnfnetlink'
  'python'
  'python-grpcio'
  'python-protobuf'
  'python-slugify'
  'python-pyqt5'
  'python-pyinotify'
  'python-notify2'
)
makedepends=(
  'git'
  'go'
  'python-grpcio-tools'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
  'python-nspektr'
  'python-jaraco.text'
  'qt5-tools'
)
optdepends=(
  'logrotate: logfile rotation'
  'python-qt-material: extra ui themes'
  'python-pyasn: display network name of IP'
)
backup=(
  'etc/opensnitchd/default-config.json'
  'etc/opensnitchd/system-fw.json'
)
options=('!lto')
source=(
  "$pkgname::git+$url#tag=v$pkgver"
  'fix-systemd-service.patch'
  'remove-debian-path.patch'
  'fix-setup.py.patch'
  'add-generated-protobuf-files.patch'
  'use-system-python-packages.patch'
  'add-translations.patch'
  'template-version-strings.patch'
  'tmpfiles.conf'
)
sha512sums=('b987d99c231ba0dab437a5ffd845774184fe47c9b783943f36f40ae4dea6bba432bd01d5135668e9d77456a4bed096ded6e5c82ec6ccfd9dcaf265e27b1c2504'
            '05a0565346917491da3592fb23859a05b0a963932238754b07db7e1e0878437a99802f4e1ea7730ad19ab66207dd191c30d1f14f54c28e9ae842fe740b926d94'
            '09a8ae61e9dc12bf86741e67797b4e99abae89aee41e6fb60c957e6a88a5302c03dc1a9fd55a9b7432766063ce5f38c976f85eefe0c152a0f6d017afa1b923a0'
            '6573d1a5613f39e112f95d6b0ed5bdda6785595b4203d4013325bac2500f5c0122e50fd2c8df918c88825ae2ad341b1ac9e9ea84b0ec7bd079fb60370b8a81f6'
            'c89c7c20e6dc0cf1e4d9942190642abcc967ffdd884ed5d07fcc5b03344c88a0aa16b9e40198549d9e8438725fe60f2008fd2b582d222f6d92125c227b6fa412'
            '9e4f74ac8075b41007852b6518d6d8c25efd4f1f51d08d3269696d63fd72859990b30dab038fa4f1479496a4838c6ce7ffdbb54e01cbe0abebf2bca8e15a84fd'
            'b380e1d3ba8e967c937b576a66a40f5c2d129df325981454122c7235eb51d1349617dbf43e80c336f10a68b1707d666baa62c473eedf3527f1dd61a512841edd'
            '94e83cfd6957475c3667900ade91a69491c3e2b7c86f75540d13ebc84953d6bc63abf5e7f6b277a1ec4124593ab8e0222c2167b25a7939dc04e22ccc280f2f19'
            '11c20c25693bb5a41114e1a37ca159a6c89db70ecfdcc8b4ff0f4d1f1af1fd4fecf5315508261fa860663da4742f4115e10734ce42da2891db4df57e9c1ac79b')
b2sums=('a095ef78e0c515c5f381201d67447a7d77f0a11711c9d3c20e0c0e96ad87ed24e3842e1cfb25e7b12cd1c4d2a9742342fd05847bc726e23430f708e87eac787f'
        'a632dc8cc86a96e569a990ae1f869c3d03766828d7bfd672f2376bc0a5e8eacea8c09e3e889307ccc6138485dbde8a3e154103646cbaa80ffa4d9010abcc426d'
        '680aa7bf1470fac5c009cc5c8ea0f88dec7a8d5224be1239fcc1bbd041d724cc9e0d9389b95c4879cf16e80dc3ff7d09e472ee3c5c1585b8d6490d455df2500f'
        'd9ced1c2fd01d2d8f13c9173c7f0d70359d58cd9f9849c45b188117fd47722c8a0dbb65f6068c3e8d23cb19e6de496f845783b2ae35cb889f49b0558927e0a62'
        '9b186a31cd329ec8fcfc12c47ae8e9c3fe7a31f1fabe056bfcbd452eb69ec444ff9de51df0f0a18beb04fd246b8b93a4a3228cd0243ca5a970c702786dc68ae5'
        '00c0d5ecb2606d2b5b50c15b5cf561593629980d30a21d83d125161cd8a2a89dbca23d2eca6a1c519e8e499e5a4b669f70301c6a385fbba6832144665a5b47b8'
        '8c7de39f52acac87d11ef33816bd10ae26e91df092c7f4cfd5a48e9e5c65a1530afe065d312b8ae95639352ee613f49153315edebabcb2f388dd1c14ca71d8a3'
        '093ecf4398ffcd9f4077b41174e93707ba90cef717fa7e35a44557a52969d356216b08f6a4ce38fc93a7ba4e8cb14c7c2a4368ba0b023527d1ee93879728b30d'
        '81dbb6ba72d5bf5a0d9fa3dea33b87f03604c1a2537a4ca47b45923ab186e68d5c24b043691a857948ae404409dc3cc66a7ef004ee20dc2451aa5190fe6479bb')

prepare() {
  cd "$pkgname"

  # TODO file an upstream bug
  # * fix an issue with setup.py installing to python's site-packages instead
  # of /usr
  # * prefer scaled SVG instead of pixellated 48x48 PNG
  patch -p1 -i "$srcdir/fix-setup.py.patch"

  # add generated protobuf files
  # required for reproducible builds & to side-step
  # requirement of protoc-gen-go{,-grpc} binaries
  patch -p1 -i "$srcdir/add-generated-protobuf-files.patch"

  # use system python packages
  patch -p1 -i "$srcdir/use-system-python-packages.patch"

  # add generated translation files
  # required for reproducible builds
  git apply "$srcdir/add-translations.patch"

  # TODO file an upstream bug
  # fix a couple of issues with the systemd service
  patch -p1 -i "$srcdir/fix-systemd-service.patch"

  # TODO file an upstream bug
  # remove Debian-specific path from sys.path
  patch -p1 -i "$srcdir/remove-debian-path.patch"

  # version strings are currently out of date
  # template-ify version strings for easier sed invocation (1/2)
  #patch -p1 -i "$srcdir/template-version-strings.patch"

  # download dependencies
  cd daemon
  export GOPATH="${srcdir}"
  go mod download
}

build() {
  cd "$pkgname"

  # template-ify version strings for easier sed invocation (2/2)
  #sed -e "s/@VERSION@/$pkgver/" -i daemon/core/version.go
  #sed -e "s/@VERSION@/$pkgver/" -i ui/opensnitch/version.py

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # build daemon with debug info
  pushd daemon
  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\"" \
    -o opensnitchd \
    .
  popd

  # build python package
  pushd ui
  python -m build --wheel --no-isolation
  popd
}

package() {
  cd "$pkgname"

  # daemon
  install -vDm755 -t "$pkgdir/usr/bin" daemon/opensnitchd

  # systemd integration
  install -vDm644 utils/packaging/daemon/deb/debian/opensnitch.service "$pkgdir/usr/lib/systemd/system/opensnitchd.service"
  install -vDm644 "$srcdir/tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  # configuration
  install -vDm644 -t "$pkgdir/etc/opensnitchd" daemon/{default-config,system-fw}.json

  # logrotate
  install -vDm644 utils/packaging/daemon/deb/debian/opensnitch.logrotate "$pkgdir/etc/logrotate.d/$pkgname"

  # python ui
  python -m installer --destdir="$pkgdir" ui/dist/*.whl

  # TODO file an upstream bug
  # tests are in site-packages, big no-no
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  rm -rf "$pkgdir/$site_packages/tests"
}
