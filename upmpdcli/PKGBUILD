# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: AudioLinux  <audiolinux AT fastmail DOT fm>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Mansour Behabadi <mansour@oxplot.com>
# Contributor: Tilman Vatteroth <tilman.vatteroth@udo.edu>

pkgname=upmpdcli
pkgver=1.8.8
pkgrel=1
pkgdesc="A UPnP Media Renderer front-end for the Music Player Daemon (MPD)"
arch=(loong64 x86_64)
url="https://www.lesbonscomptes.com/upmpdcli/"
license=(LGPL-2.1-or-later)
depends=(python libmpdclient libupnpp libmicrohttpd jsoncpp)
makedepends=(python-setuptools python-requests python-bottle python-mutagen aspell-en id3lib python-waitress sqlite)
optdepends=(aspell-en id3lib sqlite
            'python-requests: enable search'
            'python-bottle: enable uprcl media server'
            'python-mutagen: enable uprcl media server'
            'python-waitress: enable uprcl media server'
            'recoll: enable uprcl media server')
source=("http://www.lesbonscomptes.com/upmpdcli/downloads/$pkgname-$pkgver.tar.gz"
        "upmpdcli-sysusers.conf"
        "upmpdcli-tmpfiles.conf"
        'upmpdcli.service')
sha256sums=('cb5278c7b6dd3ab1c17d8d03a05ad093bc6c8e64c1b9494151cb2c2a6723fd38'
            '91f22d20b1f1580d793d638818173d63700f1cd1cf674f8eabc63f5f2fc2a7f1'
            'eac843e7c34352e4c25df86b12d5412c6b031e9a520f94efba24cdd530cad07e'
            '53da5e4e40a1987e3c137270b315f8c0ba9521539c563e75e254f48449ce0ae9')
backup=('etc/upmpdcli.conf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --sysconfdir=/etc --prefix=/usr
  make
}

package() {
  depends+=(libupnpp.so)
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 systemd/upmpdcli.service "${pkgdir}"/usr/lib/systemd/system/upmpdcli.service
  sed '/\[Service\]/a User=upmpdcli' -i ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
  sed -i '/^After/ s/$/ mpd.service/' ${pkgdir}/usr/lib/systemd/system/upmpdcli.service
  install -Dm644 "${srcdir}"/upmpdcli.service "${pkgdir}"/usr/lib/systemd/user/upmpdcli.service
  install -vDm 644 "${srcdir}"/upmpdcli-sysusers.conf "${pkgdir}/usr/lib/sysusers.d/upmpdcli.conf"
  install -vDm 644 "${srcdir}"/upmpdcli-tmpfiles.conf "${pkgdir}"/usr/lib/tmpfiles.d/upmpdcli.conf
  chmod 644 "${pkgdir}"/etc/upmpdcli.conf
}
