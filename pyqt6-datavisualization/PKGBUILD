# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-datavisualization
pkgname=(python-pyqt6-datavisualization)
pkgver=6.7.0
pkgrel=2
pkgdesc='Python bindings for QtDataVisualization'
arch=(loong64 x86_64)
url='https://www.riverbankcomputing.com/software/pyqtdatavisualization/intro'
license=(GPL3)
groups=(pyqt6)
depends=(gcc-libs
         glibc
         python
         python-pyqt6
         qt6-base
         qt6-datavis3d)
makedepends=(pyqt-builder
             qt6-declarative
             sip)
source=(https://pypi.python.org/packages/source/P/PyQt6-DataVisualization/PyQt6_DataVisualization-$pkgver.tar.gz)
sha256sums=('8cbdd50326a2cda533bc5744c85a331c84047af121bdbd64f9c00dbc06588884')

build() {
  cd PyQt6_DataVisualization-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake6
  cd build
  make
}

package_python-pyqt6-datavisualization(){
  cd PyQt6_DataVisualization-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
