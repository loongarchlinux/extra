# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-notifications
pkgver=2.2.0.r150.12533fb
pkgrel=1
pkgdesc='Switchboard Notifications Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-notifications
license=(GPL-2.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_commit=12533fb6a3e6e65d3d1085dfcbd5f1a9f6f18531
source=(git+https://github.com/elementary/switchboard-plug-notifications.git#commit=${_commit})
b2sums=('5a4e52809f6c3c0660d88532805aba116a0dbd0d9fddf83e1b9b1e7bd9c8b712ed1871db7872ebdc14471829d1c1945caf0b6d56b018eba65dbd00458085da67')

pkgver() {
  cd switchboard-plug-notifications
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-notifications build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
