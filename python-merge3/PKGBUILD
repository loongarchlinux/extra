# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Luis Martinez <luis.martinez@disroot.org>
# Contributor: Mark Wagie <mark.wagie@tutanota.com>

pkgname=python-merge3
pkgver=0.0.14
pkgrel=3
pkgdesc='Python implementation of 3-way merge'
arch=(any)
url=https://github.com/breezy-team/merge3
license=(GPL2)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=d5e00212bc40620bc4a5fc43e603ddb63f2ed30a
#source=(git+https://github.com/breezy-team/merge3.git?signed#tag=${_tag})
source=(git+https://github.com/breezy-team/merge3.git#tag=${_tag})
b2sums=(SKIP)
validpgpkeys=(DC837EE14A7E37347E87061700806F2BD729A457) # Jelmer VernooÄ³ <jelmer@jelmer.uk>

pkgver() {
  cd merge3
  git describe --tags | sed 's/^v//'
}

build() {
  cd merge3
  python -m build --wheel --no-isolation
}

check() {
  cd merge3
  python -m unittest discover
}

package() {
  python -m installer --destdir="${pkgdir}" merge3/dist/*.whl
}

# vim: ts=2 sw=2 et:
