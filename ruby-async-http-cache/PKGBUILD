# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-async-http-cache
pkgver=0.4.3
pkgrel=3
pkgdesc='Standard-compliant cache for async-http'
arch=(any)
url='https://github.com/socketry/async-http-cache'
license=(MIT)
depends=(
  ruby
  ruby-async-http
)
checkdepends=(
  ruby-async-rspec
  ruby-bake-test
  ruby-bake-test-external
  ruby-bundler
  ruby-covered
  ruby-rspec
)
options=(!emptydirs)
source=(https://github.com/socketry/async-http-cache/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('66343e437de2b27288cce9fa4d1e1a3e8938aa4735f48a13600ac2a71cf748bf')

prepare() {
  cd async-http-cache-$pkgver
  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i async-http-cache.gemspec
  sed -i '/bake-gem/d;/bake-modernize/d' gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd async-http-cache-$pkgver
  gem build async-http-cache.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    async-http-cache-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd async-http-cache-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd async-http-cache-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
