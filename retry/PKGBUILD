# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Fernando Barillas < aur at fbis251 dot com >

pkgname=retry
pkgver=1.0.5
pkgrel=2
pkgdesc="Repeat a command until the command succeeds."
arch=("loong64" "x86_64")
url="https://github.com/minfrin/retry"
license=("Apache-2.0")
depends=('glibc')
source=("https://github.com/minfrin/retry/releases/download/retry-$pkgver/retry-$pkgver.tar.gz")
sha256sums=('09a75450153e65aca1cd5fa366d54a314050f8f19e8ba5747656c5b673b65fdf')

build() {
  cd retry-$pkgver
  autoreconf -fi
  ./configure --prefix=/usr
  make
}

check() {
  cd retry-$pkgver
  ./retry --version
}

package() {
  cd retry-$pkgver
  make DESTDIR="$pkgdir" install
}
