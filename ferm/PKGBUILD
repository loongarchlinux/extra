# Maintainer: SÃ©bastien Luttringer
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Manuel Mazzuola <origin.of@gmail.com>

pkgname=ferm
pkgver=2.7
pkgrel=2
pkgdesc='Tool to maintain complex firewalls'
arch=('any')
url='http://ferm.foo-projects.org/'
license=('GPL-2.0-only')
depends=('iptables' 'perl')
optdepends=('ebtables' 'arptables' 'ipset')
backup=("etc/$pkgname.conf")
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>
source=(
  "http://ferm.foo-projects.org/download/${pkgver:0:3}/$pkgname-$pkgver.tar.xz"{,.sig}
  "$pkgname.service"
  "reproducible-builds.patch")
sha256sums=('c00d910d5394e69675608eb5ee0f504d5cfda41e990a2d9a91baac6df93e3f92'
            'SKIP'
            'b091bdef4b42c74d162de43b017c71bc00d7969b1a4f2d9188df01354c36a78e'
            'c42896f9959b71497c2663fab81105caae282ded93657c92203ec730342443c5')
b2sums=('1c7b9e111b86cebc5c0f503e96422d3411b14eb18621268653262926be3e2493b6e66869d607feddbcdb945dada8960f40f166202e3b61a6e93007683fa3a38b'
        'SKIP'
        'f9d6600aa4370533b7f41f81127b98e1712da1bfe0ed293146d6e5b55eb64ca6d3fbc692237d883b6cf54b7e1da42728f790a940dc15325fee11bdeb595fd991'
        '7f070274168a8f2054e65bb6c7e8a598b141706c38413ae4865dcbc69c7c67c34880e57dbb3bdde9f3ce2a3ca143534fd30d499019b3ae0f1fb4dd8d69e5c0e9')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../reproducible-builds.patch
}

package() {
  # software setup
  cd $pkgname-$pkgver
  make PREFIX="$pkgdir/usr" install
  cd "$pkgdir"
  # setup default config
  install -D -m 644 usr/share/doc/ferm/examples/workstation.ferm etc/$pkgname.conf
  # remove fancy docs
  rm usr/share/doc/ferm/ferm.{html,txt}
  # /usr merge
  mv usr/sbin usr/bin
  # systemd
  install -Dm 644 "$srcdir/$pkgname.service" usr/lib/systemd/system/$pkgname.service
}

# vim:set ts=2 sw=2 et:
