# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Peter Wu <peter@lekensteyn.nl>
# Contributor: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: s1gma <s1gma@mindslicer.com>

pkgname=binwalk
pkgver=2.4.1
pkgrel=1
pkgdesc='Tool for searching a given binary image for embedded files'
url='https://github.com/OSPG/binwalk'
arch=(any)
license=(MIT)
depends=(
  python
  python-setuptools
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
)
optdepends=(
  'python-opengl: binviz module support'
  'python-capstone: disassembly support'
  'python-matplotlib: entropy plotting'
  'python-pyqtgraph: entropy plotting'
  'python-numpy: entropy module'
  'python-numba: entropy module'
  'python-gnupg: pgp plugin'
  'arj: ARJ decompression support'
  'cabextract: CAB archive support'
  'cpio: CPIO archive support'
  'gzip: GZIP decompression support'
  'mtd-utils: JFFS filesystem support'
  'p7zip: ZIP, LZMA and ISO decompression support'
  'squashfs-tools: squashfs support'
  'tar: TAR archive support'
  'bzip2: BZIP2 archive support'
  'unrar: RAR decompression support'
  'xz: XZ decompression support'
  'lhasa: LHA support'
  'sleuthkit: forensic analysis support'
)
source=(
  "${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
)
sha512sums=('f83d0ca09a39033944ce16345b7c2c8f3051a20f80315e4c797221df4e6844c7c89c0a49053d739156e2654b482d6ef93f0814fa588daab031dbeaed1423e6f2')
b2sums=('480f8ff06932c77ca5ab2857cddf7ecebb36b6f2e55e3ccf15a971fa0f4c59d57b652556fd9f0dd20e6fbd887fdef3c6da022f76787b2edf16b0d1714d291e8a')

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname}-${pkgver}
  python -m venv --clear --without-pip --system-site-packages .testenv
  .testenv/bin/python -m installer dist/*.whl
  .testenv/bin/python -m pytest
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 ./*.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
