# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Peter Wu <peter@lekensteyn.nl>
# Contributor: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: s1gma <s1gma@mindslicer.com>

pkgname=binwalk
pkgver=2.3.4
pkgrel=4
pkgdesc='Tool for searching a given binary image for embedded files'
url='https://github.com/ReFirmLabs/binwalk'
arch=('any')
license=('MIT')
depends=('python')
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
optdepends=(
  'python-opengl: binviz module support'
  'python-capstone: disassembly support'
  'python-matplotlib: entropy plotting'
  'python-pyqtgraph: entropy plotting'
  'arj: ARJ decompression support'
  'cabextract: CAB archive support'
  'cpio: CPIO archive support'
  'gzip: GZIP decompression support'
  'mtd-utils: JFFS filesystem support'
  'p7zip: ZIP, LZMA and ISO decompression support'
  'squashfs-tools: squashfs support'
  'tar: TAR archive support'
  'bzip2: BZIP2 archive support'
  'unrar: RAR decompression support'
  'xz: XZ decompression support'
  'lhasa: LHA support'
  'sleuthkit: forensic analysis support'
)
source=(
  "${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
  $pkgname-2.3.4-imp_module.patch
)
sha512sums=('5a5e16035dfc05b94ae4ee3969a337336c550606b71e20fcb1f150c4b38ef2084b6a823ce2a8050f4b41616b7d7c31cfb04bf43e7f2c977ab1a634aba9d67fec'
            'f474c437440f41c71bc5454a930e8abbfaa19c5989fd1453660021d777dfaab7ecf0650558a94c83e1502fb26d3e052989c0c7f388c62ad12c19058228fcdacb')
b2sums=('a5eac4242402c5034c293432131950bed9aff762c5db86b200a1f555bdfeeb1c6fda225393149f7beca2923141f2ae7930738038f2d039513cb06dfae9601b81'
        '4896dce8db043cd03997014aa4d37f1740bc8266728abc4ab740cd8fc3f9df2f496a2b7c5e27fcc9f068be96993d8f5aaf1682b9469ffedb5a31c84ea2ea4811')

prepare() {
  # replace use of imp module with importlab: https://github.com/ReFirmLabs/binwalk/issues/667
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-2.3.4-imp_module.patch
}

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 ./*.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
