# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: BrainDamage

_pkgname=python-readability
pkgname=python-readability-lxml
pkgver=0.8.1
pkgrel=9
pkgdesc='Fast html to text parser (article readability tool) python library'
arch=(any)
url='https://github.com/buriy/python-readability'
license=(Apache-2.0)
depends=(
  python
  python-chardet
  python-cssselect
  python-lxml
  python-lxml-html-clean
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("git+https://github.com/buriy/python-readability.git#tag=v$pkgver")
b2sums=(a1b05013ff0c7103e2d49ed748b338d38d1e7f5112e8f79442df87c56f38053fe79108149b9d4798ffba84df1275e38ecde515a21096b71cc6d2a1f724a195b2)

build() {
  cd $_pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $_pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
