# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown
pkgver=4.1.3
pkgrel=1
pkgdesc="FeelUOwn Music Player"
arch=('any')
url="https://github.com/feeluown/FeelUOwn"
license=('GPL3')
depends=('python-qasync' 'python-pyqt5' 'mpv' 'python-opengl' 'python-janus' 'python-requests'
         'python-tomlkit' 'python-packaging' 'python-pydantic' 'python-mutagen' 'qt5-svg'
         'xdg-user-dirs')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pycryptodome')
optdepends=('python-secretstorage: browser cookie extraction support'
            'python-pycryptodome: browser cookie extraction support'
            'dbus-python: mpris2 server support'
            'python-sanic: webserver support'
            'python-json-rpc: webserver support'
            'python-websockets: webserver support'
            'feeluown-netease' 'feeluown-qqmusic' 'feeluown-ytmusic' 'feeluown-bilibili')
conflicts=('feeluown-local')
provides=('feeluown-local')
replaces=('feeluown-local')
groups=('feeluown-full')
source=("https://pypi.io/packages/source/f/feeluown/feeluown-$pkgver.tar.gz"
        "feeluown.desktop"
	"pytest-skip-benchmark-cov-plugin.patch"
	"ordereddict-repr-change.patch")
sha512sums=('0a9d76d92ccb1329b8516d83b01c780314b5fa8b083caa4e23c7fea74469276b6b688c9f43a62f57675e919ea92c47a8f314de36db9b6ebe74bdd0cbc1b64997'
            '48882f7469c22e5db332663bc1aa8b398b0a10a0c929d4d7e3d7b8b91205d7d3070c5fa295cb9a14f8c352bff57978bfaad167343e0ddc51c92417eda07c8087'
            '031d4076970a91e135f1eb38abd4e749a38a4f47cb2a9ec6ed3c9b6b6c6cd3769f1914379aefc2dcd9debaad2ccac73d45bf5084b5d5cc64d1d6b77e79bca0be'
            'caf0ec4c621dd7c9d5eff3ad472dbcb6176c7b49984556df28450aa0e601c8142b8b251a5ad79a43790674c9323e11f745e557990d6e26c56439a18c9f06f3b3')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ${srcdir}/pytest-skip-benchmark-cov-plugin.patch
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  pytest -k 'not parse_lyric_text'
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm644 feeluown/gui/assets/icons/feeluown.png -t "$pkgdir"/usr/share/icons/hicolor/512x512/apps/
  install -Dm644 "$srcdir"/feeluown.desktop -t "$pkgdir"/usr/share/applications/
}
