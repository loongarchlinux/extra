# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Michael Schubert <mschu.dev at gmail>
# Contributor: alienzj <alienchuj@gmail.com>

pkgname=python-datrie
_pkgname=datrie
pkgver=0.8.2
pkgrel=5
pkgdesc="Fast, efficiently stored Trie for Python. Uses libdatrie"
arch=('loong64' 'x86_64')
url="https://github.com/pytries/datrie"
license=('LGPL-2.0-only')
depends=('libdatrie' 'glibc')
makedepends=('python-build' 'python-wheel' 'python-installer' 'python-setuptools' 'cython')
checkdepends=('python-pytest' 'python-hypothesis')
options=(!emptydirs)
source=($_pkgname-$pkgver.tar.gz::https://github.com/pytries/datrie/archive/$pkgver.tar.gz)
sha256sums=('79d9a2960749903d632812fcc0edd09d991774933c8c332b8a82b70097721923')

prepare() {
  cd "$srcdir"/$_pkgname-$pkgver
  sed -i 's/"pytest-runner", //' setup.py
  sed -i 's|../libdatrie/||g' src/cdatrie.pxd src/datrie.pyx
}

build() {
  cd "$srcdir"/$_pkgname-$pkgver
  ./update_c.sh
  python -m build --wheel --no-isolation
}

check() {
  cd "$srcdir"/$_pkgname-$pkgver
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="build/lib.linux-$CARCH-cpython-${python_version}" python -m pytest
}

package() {
  cd "$srcdir"/$_pkgname-$pkgver

  python -m installer --destdir="${pkgdir}" dist/*.whl
}
