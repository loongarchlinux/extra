# Maintainer: Campbell Jones <serebit at archlinux dot org>
# Contributor: Sandy Carter <aur at sandy dot mozmail dot com>
# Contributor: JstKddng <jk at vin dot ovh>

pkgname=argparse
pkgver=3.1
pkgrel=1
pkgdesc="Argument Parser for Modern C++"
arch=(any)
url="https://github.com/p-ranav/argparse"
license=("MIT")
makedepends=("cmake" "ninja")
source=("$pkgname-$pkgver.tar.gz::https://github.com/p-ranav/$pkgname/archive/v${pkgver}.tar.gz")
sha512sums=('31e03b7de44e091614c1680e76988e0f7f5bdc6baf0262ed0583c6311d6d0611b7e30fa73b4522fd99d8ea81e74e5a74a91888135a1352d9ab08bf4a5467d32d')

build() {
    cmake -B build \
        -G Ninja \
        -H"$pkgname-$pkgver" \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -Wno-dev

    cmake --build build
}

check() {
    ./build/test/tests
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    install -Dm 0644 "$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
