# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>

pkgname=curtail
pkgver=1.9.1
pkgrel=1
pkgdesc='Image compressor GUI, supporting PNG and JPEG file types, both lossless and lossy compression mode'
arch=('any')
url='https://github.com/Huluti/Curtail'
license=('GPL3')
depends=('gtk4' 'libadwaita' 'jpegoptim' 'oxipng' 'pngquant' 'python-gobject')
makedepends=('appstream-glib' 'meson')
source=("https://github.com/Huluti/Curtail/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('0ba1e52d9a772d93386a2dbde8468d7b67c5a8d9d40797dd07740be95732998a')

build() {
  arch-meson Curtail-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs || true
}

package() {
  meson install -C build --destdir "$pkgdir"
}
