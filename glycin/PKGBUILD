# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>

pkgname=glycin
pkgver=1.0.1
pkgrel=2
pkgdesc="Sandboxed and extendable image decoding"
arch=(loong64 x86_64)
url="https://gitlab.gnome.org/sophie-h/glycin"
license=(
  'MPL-2.0 OR LGPL-2.1-or-later'
)
depends=(
  bubblewrap
  cairo
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk4
  libjxl
  librsvg
  libseccomp
  libxml2
  pango
)
makedepends=(
  clang
  git
  libheif
  meson
  rust
)
optdepends=(
  'libheif: heif/heic file support'
)
checkdepends=(
  dav1d
  ffmpeg
  rav1e
  svt-av1
)
source=(
  "git+https://gitlab.gnome.org/sophie-h/glycin.git#tag=${pkgver/[a-z]/.&}"
  "git+https://gitlab.gnome.org/sophie-h/test-images.git"
)
b2sums=('238aec6a21c6c0d041f9fa3d71792b578f62df312eeddaa52c4b9d3784da3763dbd6d3cd25d2f73898348b11751a31195b7c85af3192f503d0805f9b2145641e'
        'SKIP')

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url tests/test-images "$srcdir/test-images"
  git -c protocol.file.allow=always submodule update
}

build() {
  local meson_options=(
    -D test_skip_install=false
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  RUST_BACKTRACE=full meson test -v -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
}
