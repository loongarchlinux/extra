# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Daniel Seymour <dannyseeless@gmail.com>

pkgname=emby-server
pkgver=4.8.8.0
pkgrel=1
pkgdesc='Bring together your videos, music, photos, and live television'
arch=(loong64 x86_64)
url=https://emby.media
license=(custom)
depends=(
  aspnet-runtime-6.0
  dotnet-runtime-6.0
  emby-ffmpeg
  libvips
  skia-sharp
  sqlite
)
optdepends=(
  'intel-compute-runtime: Intel OpenCL tone-mapping support (Premiere)'
  'intel-media-driver: Intel VAAPI support for Broadwell and newer (Premiere)'
  'intel-media-sdk: Intel QuickSync support (Premiere)'
  'libva-intel-driver: Intel VAAPI support for Comet Lake and older (Premiere)'
  'libva-mesa-driver: AMD VAAPI support (Premiere)'
  'nvidia-utils: Nvidia NVDEC/NVENC support (Premiere)'
  'onevpl-intel-gpu: Intel QuickSync support for Alder Lake and newer (Premiere)'
)
source=(
  https://github.com/MediaBrowser/Emby.Releases/releases/download/${pkgver}/embyserver-netcore_${pkgver}.zip
  emby-server
  emby-server.conf
  emby-server.service
  license.docx
  Permission_to_distribute.mbox
)
noextract=(license.docx)
backup=(etc/conf.d/emby-server)
b2sums=('7b79faea6f2ff550af6ccc2dc4db2023c67ae30d0e7696a6c7d13c85bb59b52856818fa3e1a6e7315384b442479e9273aa6187dba8dfe23e23286193d9ee5aa6'
        '48c11601d00121e725951fd48ba34137804d4a67aa12528a29ca4d43d301d19311182421e96bef7b7ddbc4929dc9d5b577b1659cb0b716b1c1f6938283465c46'
        '49c2e6a27f36a2e44a6b988573ee7d08bacb036008f5360ad1cb617b3d1577bdcd48a323ebb882e0f9ec4483962a96d8bd9af44385feb5245b49dfc191d73352'
        '11cc26f44f81823bcd50ee16344aff94eb0a64d740a2e489d832bc8d48d11f3065bf683da243409bab8e3d8a201083596a27983cc11a90688128442e0116430c'
        'e490c2ec7aff3deb9874a80345273c2aa435624b914a13fe8cba8f07ee44938699a024d0c6784fe68820d3a848cccf5af3f7120c7906356ed17d306fc31b6490'
        '61ca54af95278f498b21b424b4d4bae98660b4b7898bfd9752fe21da20d7c508502014bc1970a60920f3064ca20b31b46ba2a788b3b1371f75b5278add70ffc4')

prepare() {
  rm -rf system/{electron,runtimes/{osx,tizen*,win*}}
}

package() {
  install -dm 755 "${pkgdir}"/usr/lib
  cp -dr --no-preserve=ownership system "${pkgdir}"/usr/lib/emby-server
  install -Dm 755 emby-server -t "${pkgdir}"/usr/bin/
  install -Dm 644 emby-server.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm 644 emby-server.conf "${pkgdir}"/etc/conf.d/emby-server
  install -Dm 644 license.docx -t "${pkgdir}"/usr/share/licenses/emby-server/
}

# vim: ts=2 sw=2 et:
