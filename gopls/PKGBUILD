# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Chad Kunde <Kunde21@gmail.com>

pkgname=gopls
pkgver=0.16.1
pkgrel=1
pkgdesc='Language server for Go programming language'
arch=('loong64' 'x86_64' 'aarch64' 'armv7h' 'armv7l')
url='https://github.com/golang/tools/tree/master/gopls'
license=('BSD-3-Clause')
depends=('go')
makedepends=('git')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/golang/tools/archive/gopls/v${pkgver}.tar.gz")
sha512sums=('3a6f43a44c7135cacbb8827edd01064b660169761e78c6f4dbbfa6c430e31c45d976b6aa2cb19c5ab3a69137ef461459cea959262ab76b6eed5ce4575edba6fc')
b2sums=('f79bc35d1748695c96ec8c55c2a7277e8ec4081c9e1b65f623d32c61b38d767f302b4b34e12f922afc8c4c8ebcc79da44fea01e1bbdafde11b9970e39121201b')

build() {
  cd "tools-${pkgname}-v${pkgver}/${pkgname}"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\"" \
    -o "${pkgname}"
}

package() {
  cd "tools-${pkgname}-v${pkgver}"
  install -Dm755 "${pkgname}/${pkgname}" -t "${pkgdir}"/usr/bin/
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ts=2 sw=2 et:
