# Maintainer: David Runge <dvzrv@archlinux.org>

# WARNING: Always check whether the upgrade requires a data migration and if so upgrade stalwart-mail-old-upgrade in tandem accordingly!
pkgbase=stalwart-mail-server
_name=${pkgbase#stalwart-}
pkgname=(stalwart-cli stalwart-mail)
pkgver=0.9.1
pkgrel=1
pkgdesc="Secure & Modern All-in-One Mail Server (IMAP, JMAP, SMTP)"
arch=(loong64 x86_64)
url="https://stalw.art/"
_url="https://github.com/stalwartlabs/mail-server"
license=(AGPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cargo
  clang
)
options=(!lto)
source=(
  $pkgname-$pkgver.tar.gz::$_url/archive/refs/tags/v$pkgver.tar.gz
  config.toml
  stalwart-mail.service
  stalwart-mail.sysusers
  stalwart-mail.tmpfiles
)
sha512sums=('97193a3350d43c6b760a9b06a13d13397bbad241931a353db1ad15e3dfe83cc36fbd14456869257d479030fa7bb65362845c7aa9c6d8e70d2e19024c1876a24b'
            '6e3a80e4ddc8b6d668711c1d907295f43da0ceb51017e31a321e443d91fbf11c9636f5837460414ca64e49b31d42cbbb6f07d52d5522675e33ae237289f37583'
            '446fc808519be91cc07441fdde83c746d4d9a7439c79b126f8299c50fc04de2d1fb46ee8736d8ee9c40b6a8c458f1fbf64565375578b699b48ce44505c3864d3'
            '49a52e18e529997b2fb03aae7bbf64b6493ceda0d53c3a59a1b95b4dceab2ec700e3e358015404be74ac13e354b57d17358b22f31ee9866e60a79b6922808268'
            'fd8de5d9e0e0aa136d352908d2803e9e58ba251730fae791d51393aafbd8a4bcf6c205ec983c69c22c5eec6074ea05d97d7455feedce44ec4fd75a599cfef75e')
b2sums=('c69f877fa16e21ee133fe3791eed339388ce8065e59815a191e514b8f1fb5d2bd7459ab12594bfbd566337b402b5b5f78a7fe1e540f0a8052f3f0456c21ff027'
        '094d5495c7f9c0f8d3c3b483e34f0f5ce06d9ba604292b22d9acf99c136850861e43f878c852049f52d5be7453f109baaa724417b73ef7ea0394d49f5b785ca2'
        '7341acaf454712d0062911369ff45efaa10652f15d426d778825b42c48a73248fbfdb8c7eab0fe068cd48144c769e55a9b75d2d91d710e18110a44d34ff425d1'
        '034f1896519a8d2be2ccf5a9861ac7d5f051b3336e24d01134465c1d49aff30ab662bf9d698b3c726df41e3399f87d20f45b7cfc7db98916e3a443a0a87730ce'
        '03b261831cd96c29963e12816d6dfd3cf74eb5e1c4e2c838bb09785210c61f4c3f6f6c6cf48c90ead1c06440b1ffab2d05fee4d15b949eb9510c4f8af11f790d')

prepare() {
  cd $_name-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $_name-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release -p mail-server -p stalwart-cli
}

check() {
  cd $_name-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen -p mail-server -p stalwart-cli
}

package_stalwart-cli() {
  pkgdesc="Stalwart Mail Server CLI"

  cd $_name-$pkgver
  install -vDm 755 target/release/stalwart-cli -t "$pkgdir/usr/bin/"
  install -vDm 644 {CHANGELOG,README,UPGRADING}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_stalwart-mail() {
  pkgdesc="Stalwart Mail Server"
  optdepends=(
    'mariadb: for using MariaDB as storage backend on the same host'
    'postgresql: for using PostgreSQL as storage backend on the same host'
    'redis: for using Redis as lookup storage backend on the same host'
    'sqlite: for using SQLite as storage backend on the same host'
    'stalwart-cli: for using the CLI to the mail server on the same host'
    'stalwart-mail-old-upgrade: for exporting data when migrating from an older version'
  )
  backup=(etc/$pkgname/config.toml)
  install=$pkgname.install

  cd $_name-$pkgver
  install -vDm 755 target/release/stalwart-mail -t "$pkgdir/usr/bin/"
  install -vDm 640 ../config.toml -t "$pkgdir/etc/$pkgname/"
  install -vDm 644 ../stalwart-mail.service -t "$pkgdir/usr/lib/systemd/system/"
  install -vDm 644 ../stalwart-mail.sysusers "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm 644 ../stalwart-mail.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  install -vDm 644 resources/config/{build.py,spamfilter.toml} -t "$pkgdir/usr/share/$pkgname/"
  install -vDm 644 resources/config/spamfilter/maps/* -t "$pkgdir/usr/share/$pkgname/spamfilter/maps/"
  install -vDm 644 resources/config/spamfilter/scripts/* -t "$pkgdir/usr/share/$pkgname/spamfilter/scripts/"
  install -vDm 644 {CHANGELOG,README,UPGRADING}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
