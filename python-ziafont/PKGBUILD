# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ziafont
pkgver=0.7
pkgrel=3
pkgdesc="Convert TTF/OTF font glyphs to SVG paths"
url="https://ziafont.readthedocs.io/"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-nbval' 'python-pytest')
source=("https://github.com/cdelker/ziafont/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d0c526c7d3367f895b0248b1cdaf23f6bf583b7ec8273f23c3bd683a41890bf726f2957023e5ea2e956ba2f1d9b8fc2a8fba513cabac09ab634cd2c1e1ade851')

build() {
  cd ziafont-$pkgver
  python -m build -nw
}

check() {
  cd ziafont-$pkgver
  PYTHONPATH="$PWD" pytest --nbval || echo "Tests failed"
}

package() {
  cd ziafont-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
