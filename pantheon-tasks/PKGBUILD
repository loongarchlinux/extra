# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-tasks
pkgver=6.3.2
pkgrel=2
pkgdesc='The Pantheon tasks'
arch=(loong64 x86_64)
url=https://github.com/elementary/tasks
license=(GPL3)
groups=(pantheon)
depends=(
  clutter-gtk
  geoclue
  geocode-glib
  glib2
  gtk3
  libchamplain
  libclutter-1.0.so
  libecal-2.0.so
  libedataserver-1.2.so
  libgdata.so
  libgranite.so
  libhandy-1.so
  libical
  libportal-gtk3
)
makedepends=(
  cmake
  git
  intltool
  meson
  vala
)
_tag=91b9bc1250a159457ae7431d9e0d2d5c04650064
source=(pantheon-tasks::git+https://github.com/elementary/tasks.git#tag=${_tag})
b2sums=('ca613c4c9cc15984851926ed15adb91a2366667dc786fc3b74c8e52c5469c3febee3362b6152fc1a01566fd8c0140b18905a04358424ba7bd04129c9a11aa36f')

pkgver() {
  cd pantheon-tasks

  git describe --tags
}

build() {
  arch-meson pantheon-tasks build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
