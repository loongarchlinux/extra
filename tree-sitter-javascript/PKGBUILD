# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=tree-sitter-javascript
pkgver=0.21.4
pkgrel=1
pkgdesc='JavaScript grammar for tree-sitter'
arch=('loong64' 'x86_64')
url=https://github.com/tree-sitter/tree-sitter-javascript
license=('MIT')
groups=('tree-sitter-grammars')
makedepends=('git' 'tree-sitter-cli')
optdepends=('tree-sitter: core library')
provides=("lib$pkgname.so")
source=("git+$url.git#tag=v$pkgver")
b2sums=('5fb24f2276bbfd8c97b4f1757167fde14f65a3d4f50016fc7d65d5e54ce2b123571a5fd34f9bd1b14f7df26639453ab81600a4e52c7c211188ec6e2f156c7df5')

prepare() {
  cd $pkgname
  tree-sitter generate --no-bindings src/grammar.json
}

build() {
  cd $pkgname
  make PREFIX=/usr PARSER_URL="$url"
}

package() {
  install -d "$pkgdir"/usr/lib/tree_sitter
  ln -s /usr/lib/lib$pkgname.so \
    "$pkgdir"/usr/lib/tree_sitter/${pkgname#tree-sitter-}.so

  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
