# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=wl-mirror
pkgver=0.16.5
pkgrel=1
pkgdesc="a simple Wayland output mirror client"
url="https://github.com/Ferdi265/wl-mirror"
arch=(loong64 x86_64)
license=(GPL-3.0-or-later)
depends=(
  bash
  glibc
  libglvnd
  wayland
)
makedepends=(
  cmake
  ninja
  scdoc
)
optdepends=(
  'dmenu: for interactive selection of options and wl-present'
  'pipectl: for wl-present'
  'rofi: for interactively selecting options and wl-present'
  'slurp: for selecting regions and outputs and wl-present'
)
source=($pkgname-$pkgver.tar.gz::$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('f4260ac0798a25709676be8e6158b0cd06a4043f12fbe712ed53513b6791f402c38bc64d200e285edce04e18cf00f00ad36037ad5ea3af48243b7084dd73def5')
b2sums=('2ea41eeb35aec5bc745af107d56d9b3fee276adc3fa0955e6eb22bef80cfef34520289faf5e864e3444988b9757842f46d0a58ec957db89bca8d10ab333304b0')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D INSTALL_EXAMPLE_SCRIPTS=ON
    -D INSTALL_DOCUMENTATION=ON
    -G Ninja
    -S $pkgname-$pkgver
    -W no-dev
    )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
