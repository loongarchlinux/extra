# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pynormaliz
pkgname=(python-pynormaliz)
pkgver=2.19
pkgrel=2
pkgdesc='A Python interface to Normaliz'
arch=(loong64 x86_64)
url='https://github.com/Normaliz/PyNormaliz'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         normaliz
         python)
makedepends=(boost
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(https://github.com/Normaliz/PyNormaliz/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('99b25a5089bac5a56a85905f5a1b6a2ee9c6b96c4bb11b1db06a0a0f39642206')

build() {
  cd PyNormaliz-$pkgver
  python -m build --wheel --no-isolation
}

package_python-pynormaliz() {
  cd PyNormaliz-$pkgver
  
  python -m installer --destdir="$pkgdir" dist/*.whl
}
