# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zita-jacktools
pkgname=python-zita-jacktools
pkgver=1.7.2
pkgrel=3
pkgdesc="Audio tools running as a Jack client"
arch=(loong64 x86_64)
url="https://kokkinizita.linuxaudio.org/linuxaudio"
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  python
  python-zita-audiotools
)
makedepends=(
  fftw
  jack
  libsndfile
  python-build
  python-installer
  python-setuptools
  python-wheel
  zita-convolver
  zita-jclient
  zita-resampler
)
optdepends=(
  'python-numpy: for examples'
  'python-pyqt5: for examples'
  'python-pyqt6: for examples'
)
source=(https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$_name-$pkgver.tar.bz2)
sha512sums=('5be7abfd337099f55c2c54ffac710b932766bebaeeec8b184b1d5b376cdfce12083ea12a747c2f3b3a3a031f1efc9c53cc43a41b93ab3f2c4a6ccac8f2ace9d3')
b2sums=('3c0672c1d29484d7ad61d3f94b594e4c770c540f740bd3d823f0349754c74e2e984d8467264a70fd63a8cbbd586b548e4b28592e224afa84cef1862eea09841e')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  depends+=(
    fftw libfftw3f.so
    jack libjack.so
    libsndfile libsndfile.so
    zita-convolver libzita-convolver.so
    zita-jclient libzita-jclient.so
    zita-resampler libzita-resampler.so
  )

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # examples
  find examples -type f \( -iname "*.py" -or -iname "*README" -or -iname "*presets" \) -exec install -vDm 644 {} "$pkgdir/usr/share/doc/$pkgname/"{} \;
}
