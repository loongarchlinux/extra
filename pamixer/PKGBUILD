# Maintainer: Anatol Pomozov
# Contributor: Clément Démoulins <clement@archivel.fr>

pkgname=pamixer
pkgver=1.6
pkgrel=3
pkgdesc='Pulseaudio command-line mixer like amixer'
arch=(loong64 x86_64)
url='https://github.com/cdemoulins/pamixer'
license=(GPL-3.0-or-later)
depends=(
  gcc-libs
  glibc
  libpulse
)
makedepends=(
  cxxopts
  meson
  ninja
)
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
sha256sums=('8b7199e495dc19b190f8f02ace8782f533266a4bd7c7d3cf6f4cf09b2de13e71')
b2sums=('f7951102b2605d31886a7d76f608e3a349ab760a0995e81514dfc978d92e322ce83abb733364169317b996494bc898c1a81efc71805a62b61f9817c743a31e79')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir="$pkgdir"
}
