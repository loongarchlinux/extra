# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=caja-actions
pkgver=1.28.0
pkgrel=1
pkgdesc="Extension for Caja which allows the user to add arbitrary program to be launched"
url="https://mate-desktop.org"
arch=('loong64' 'x86_64')
license=('GPL')
depends=('caja' 'libgtop')
makedepends=('itstool')
groups=('mate-extra')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('310d39488e707fad848959a0a800b6154f4498dfddaeff5af49e4db35d0bea4d')

build() {
	cd "$pkgname-$pkgver"
    	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/$pkgname 

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    	make
}

package() {
    	cd "$pkgname-$pkgver"
    	make DESTDIR="${pkgdir}" install
}
