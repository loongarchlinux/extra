# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=gcolor3
pkgname=('gcolor3' 'gcolor3-xorg')
pkgver=2.4.0
pkgrel=7
pkgdesc='Simple color chooser written in GTK'
arch=('loong64' 'x86_64')
url='https://www.hjdskes.nl/projects/gcolor3/'
license=('GPL-2.0-or-later')
makedepends=('at-spi2-core' 'cairo' 'gdk-pixbuf2' 'glibc' 'glib2' 'gtk3' 'hicolor-icon-theme'
             'libhandy' 'libportal' 'libportal-gtk3' 'appstream-glib' 'meson')
source=("https://gitlab.gnome.org/World/$pkgbase/-/archive/v$pkgver/$pkgbase-v$pkgver.tar.gz"
        'gcolor3-2.4.0-libportal-0.5.patch'
        'gcolor3-2.4.0-dark-mode.patch'
        'portal.patch')
sha256sums=('d6071390a0980fb8eb8418750766c744cf0bca56f24ab4dbe3f23cb1ffd1973d'
            'def9e8f3135ef9cf3b4c9946302da8e617e853dc79bbde2719924c3230521934'
            'ef49025620bfc66ab87b7ad3bd087822ef803677b11de194c7728ecc0d9b432c'
            '8dcb21d421eef8d0bc95c62fee860a82fe5d8ae90a5c8d93b8e62444bf9c1152')

prepare() {
  cp -a $pkgbase{-v$pkgver,-xorg}
  cd $pkgbase-v$pkgver
  # Fix build with libportal 0.5
  patch -Np1 -i ../gcolor3-2.4.0-libportal-0.5.patch
  # Support dark mode
  patch -Np1 -i ../gcolor3-2.4.0-dark-mode.patch

  cd ../$pkgbase-xorg
  # Revert Portal support
  patch -RNp1 -i ../portal.patch
}

build() {
  # Portal version
  arch-meson $pkgbase-v$pkgver build -D werror=false
  meson compile -C build

  # Xorg version
  arch-meson $pkgbase-xorg build-xorg -D werror=false
  meson compile -C build-xorg
}

check() {
  meson test -C build
  meson test -C build-xorg
}

package_gcolor3() {
  pkgdesc+=" (with Portal support)"
  depends=('at-spi2-core' 'cairo' 'gdk-pixbuf2' 'glibc' 'glib2' 'gtk3' 'hicolor-icon-theme'
           'libhandy' 'libportal' 'libportal-gtk3')

  meson install -C build --destdir "$pkgdir"
}

package_gcolor3-xorg() {
  pkgdesc+=" (with Xorg support)"
  depends=('at-spi2-core' 'cairo' 'gdk-pixbuf2' 'glibc' 'glib2' 'gtk3' 'hicolor-icon-theme')
  conflicts=('gcolor3')

  meson install -C build-xorg --destdir "$pkgdir"

  # Ignore package by AppStream to avoid duplicated IDs
  rm -r "$pkgdir/usr/share/metainfo/"
  echo 'X-AppStream-Ignore=true' >> "$pkgdir/usr/share/applications/nl.hjdskes.gcolor3.desktop"
}
