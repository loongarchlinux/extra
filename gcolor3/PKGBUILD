# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gcolor3
pkgver=2.4.0
pkgrel=8
pkgdesc='Simple color chooser written in GTK'
arch=('loong64' 'x86_64')
url='https://www.hjdskes.nl/projects/gcolor3/'
license=('GPL-2.0-or-later')
depends=('at-spi2-core' 'cairo' 'gdk-pixbuf2' 'glibc' 'glib2' 'gtk3' 'hicolor-icon-theme'
         'libhandy' 'libportal' 'libportal-gtk3')
makedepends=('git' 'glib2-devel' 'appstream-glib' 'meson')
conflicts=('gcolor3-xorg')
replaces=('gcolor3-xorg')
source=("git+https://gitlab.gnome.org/World/$pkgname.git#tag=v$pkgver"
        'gcolor3-156.patch'
        'gcolor3-157.patch'
        'gcolor3-158.patch'
        'gcolor3-159.patch'
        'gcolor3-160.patch')
b2sums=('9208f76f6679dc5afbb805e074cf60c0d34ad0e84eea3f963010526322f20e53e8b5ddfda1b8588f431fcddd38783c7ad4c0caa2e8b8b40c5a84e19750b5ee17'
        '440dc524a4efaebd69b2bc2393c653b6b55a48e1b601950cdaf6eec18836d438e33502aea1d8089091e58de3438224188439db92016ece1a24b35c43dd40d161'
        '14e26a09613e2c1df501b86b96e3caef1989040698888744e2ff62a6b97ff6d3e08801b17f6eaabfe650f5c5e20b9884cb4142a43bdb53bc0f3354efbe5ad710'
        'e97892e4f28f385431c099d7e45b937abed056e51dcd0d7c2107f84abe0e6ac980ef538044fa7d5bf598a1560b2863ea6233eb307fa6a3b7549b2a221c7933af'
        '09c506cca0b6db1b218f0ac07ab8fb9f1cacd03ee64ff51e21b691fcf60719702644d1ff4dd48b1f640aa75c94da125257138620ced5e03562fa6d3018c4e566'
        'c73a69e6e3758665c699c39d5d69cb84a126409d91b872ee2c3b4e7d768fc6df658b614ee7e822049f96cb692cc96bba97a22c19a07645b38633dc32186364d7')

prepare() {
  cd $pkgname

  # Fix build with libportal 0.5
  git cherry-pick -n 1750369a3fd922aa9db6916207dc460c6f885e14

  # Support dark mode
  git cherry-pick -n fb003ceae476e8d33c5146db2e30b569e4f34426

  # https://gitlab.gnome.org/World/gcolor3/-/merge_requests/156
  git apply -3 ../gcolor3-156.patch

  # https://gitlab.gnome.org/World/gcolor3/-/merge_requests/157
  git apply -3 ../gcolor3-157.patch

  # https://gitlab.gnome.org/World/gcolor3/-/merge_requests/158
  git apply -3 ../gcolor3-158.patch

  # https://gitlab.gnome.org/World/gcolor3/-/merge_requests/159
  git apply -3 ../gcolor3-159.patch

  # https://gitlab.gnome.org/World/gcolor3/-/merge_requests/160
  git apply -3 ../gcolor3-160.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
