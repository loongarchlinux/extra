# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-flask-marshmallow
pkgver=1.2.1
pkgrel=2
pkgdesc='Flask + marshmallow for beautiful APIs'
url=https://flask-marshmallow.readthedocs.io/
arch=(any)
license=(MIT)
depends=(
  python-flask
  python-marshmallow
)
makedepends=(
  git
  python-build
  python-flit-core
  python-installer
)
_tag=7ea089d21d39f1d4abc549a4e7c8622c872c152b
source=(git+https://github.com/marshmallow-code/flask-marshmallow.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd flask-marshmallow
  git describe --tags
}

build() {
  cd flask-marshmallow
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" flask-marshmallow/dist/*.whl
  install -Dm 644 flask-marshmallow/LICENSE -t "${pkgdir}"/usr/share/licenses/python-flask-marshmallow/
}

# vim: ts=2 sw=2 et:
