# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com

_pyname=pcapy-ng
pkgname=python-pcapy
pkgver=1.0.9
pkgrel=1
pkgdesc='Extension module that interfaces with the libpcap packet capture library'
url='https://github.com/stamparm/pcapy-ng'
arch=('loong64' 'x86_64')
license=('Apache')
depends=('python' 'libpcap')
makedepends=('python' 'python-setuptools')
source=(https://github.com/stamparm/pcapy-ng/archive/${pkgver}/${_pyname}-${pkgver}.tar.gz)
sha256sums=('386a7dedb385154212fd4130632da397e77313b6d6fb3097508a18c99ff56931')
sha512sums=('4db77ceda661fa63befecc291f5ab231dce74e8b63dc25a9a355d63158101b0ed4f0ff181046597f8ec4e24e4e8aabcd8abddf96b2e6f0d537ed575257dc2432')

prepare() {
  cd ${_pyname}-${pkgver}
  sed -i '/self.assertEqual(refNone, sys.getrefcount(None))/d' tests/pcapytests.py
}

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pyname}-${pkgver}/tests
  local PYTHONVERSION="$(python -c 'import sys; print("{}{}".format(sys.version_info.major, sys.version_info.minor))')"
  PYTHONPATH="../build/lib.linux-${CARCH}-cpython-${PYTHONVERSION}" \
    python pcapytests.py
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 pcapy.html ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}/usr/share/doc/${_pyname}"
}

# vim: ts=2 sw=2 et:
