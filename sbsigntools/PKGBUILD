# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Mico Tischler <mt-ml at gmx dot net>
# Contributor: Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

pkgname=sbsigntools
pkgver=0.9.5
_commit=8445b18f64e60137408a4867fbcbb9ba112c583a  # refs/tags/v0.9.5
pkgrel=2
pkgdesc="Tools to add signatures to EFI binaries and Drivers"
arch=(loong64 x86_64)
url="https://git.kernel.org/pub/scm/linux/kernel/git/jejb/sbsigntools.git/about"
license=(GPL-3.0-or-later)
depends=(
  glibc
  openssl
)
makedepends=(
  git
  gnu-efi
  help2man
  util-linux
  sed
)
source=(
  git+https://git.kernel.org/pub/scm/linux/kernel/git/jejb/sbsigntools.git#tag=$_commit?signed
  git+https://git.ozlabs.org/ccan
)
sha256sums=('18d476d5497cab8f7f8acb8710481bdd3de887f6109f2deeed9e6bd2571d3f9d'
            'SKIP')
validpgpkeys=('D5606E73C8B46271BEAD9ADF814AE47C214854D6') # James Bottomley <jejb@kernel.org>

prepare() {
  cd $pkgname
  git submodule init
  git config submodule."lib/ccan.git".url "$srcdir/ccan"
  git -c protocol.file.allow=always submodule update
  ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=/usr
    --bindir=/usr/bin
    --sbindir=/usr/bin
    --libexecdir=/usr/lib
    --mandir=/usr/share/man
    --sysconfdir=/etc
  )

  cd $pkgname
  CFLAGS="${CFLAGS} -Wno-error" ./configure "${configure_options[@]}"
  make
}

package() {
  depends+=(
    util-linux-libs libuuid.so
  )

  make DESTDIR="$pkgdir" install -C $pkgname
  install -vDm 644 $pkgname/{AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
