# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=tecla
pkgver=46.0
pkgrel=1
pkgdesc="Keyboard layout viewer"
url="https://gitlab.gnome.org/GNOME/tecla"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libxkbcommon
  pango
  wayland
)
makedepends=(
  git
  meson
)
groups=(gnome)
_commit=828a9d7083f9ca5f4e5710333d4ed9acdfa71438  # tags/46.0^0
source=("git+https://gitlab.gnome.org/GNOME/tecla.git#commit=$_commit")
b2sums=('0d8e742c047b4b03097231d6f7249d82e56bdba5cf19dfc81ac75531fbc99ca5638352163e7e67b5bc63635d3859cb51c5ff6d648bdbc7a505ab625d8e725018')

pkgver() {
  cd tecla
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd tecla
}

build() {
  arch-meson tecla build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
