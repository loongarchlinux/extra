# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=jc
pkgver=1.25.2
pkgrel=1
pkgdesc='Converts the output of popular command-line tools and file-types to JSON'
arch=('any')
url='https://github.com/kellyjonbrazil/jc'
license=('MIT')
depends=(
  'python'
  'python-pygments'
  'python-ruamel-yaml'
  'python-xmltodict'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
b2sums=('2c7ff002d45ed587e1d6578a8f4be9f25a3e1f182e2652faceaa513684b5a02616708f0225217942ba9a20df54675d7cee792ad11bdfdef5bd89f87e53d2df86')

build(){
  cd "$pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname-$pkgver"

  python -m unittest -v
}

package() {
  cd "$pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md EXAMPLES.md
  cp -vr docs "$pkgdir/usr/share/doc/$pkgname"

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/jc.1

  # license
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/$pkgname-$pkgver.dist-info/LICENSE.md" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
