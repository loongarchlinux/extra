# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Gerardo Exequiel Pozzi <djgera@archlinux.org>

pkgname=archiso
pkgver=79
pkgrel=1
pkgdesc="Tools for creating Arch Linux live and install iso images"
arch=(any)
url="https://gitlab.archlinux.org/archlinux/archiso"
license=(GPL-3.0-or-later)
depends=(
  arch-install-scripts
  bash
  dosfstools
  e2fsprogs
  erofs-utils
  libarchive
  libisoburn
  mtools
  squashfs-tools
)
makedepends=(
  git
  python-docutils
)
checkdepends=(shellcheck)
optdepends=(
  'edk2-ovmf: for emulating UEFI with run_archiso'
  'gnupg: for PGP signature verification of rootfs over PXE'
  'grub: for grub support in the ISO'
  'openssl: for CMS signature verification of PXE artifacts and rootfs over PXE'
  'qemu-desktop: for run_archiso'
)
source=(git+$url.git?signed#tag=v$pkgver)
sha512sums=('b6113b5ec1f69b7a596d3a5724ad33a37ece47197de686f04847bd2f2a5ee70593f619481b21ca96297c755c894c5f1ae2068ba04b99cd567443d87722d73bca')
b2sums=('e9338b791ab9b0a9f387ca21cc52de7349be6e4de36d12d26c85f6172ce72d933be3fff11c26435dd5905434c2447635b50c0e4db6c699973f9a292279ef75ef')
validpgpkeys=(
  '991F6E3F0765CF6295888586139B09DA5BF0D338' # David Runge <dvzrv@archlinux.org>
  'BB8E6F1B81CF0BB301D74D1CBF425A01E68B38EF' # nl6720 <nl6720@archlinux.org>
)

check() {
  make -k check -C $pkgbase
}

package() {
  make DESTDIR="$pkgdir" PREFIX=/usr install -C $pkgbase
}
