# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pyalsa
pkgname=python-pyalsa
pkgver=1.2.12
pkgrel=1
pkgdesc="Python binding for the ALSA library"
arch=(loong64 x86_64)
url="https://www.alsa-project.org"
license=(LGPL-2.1-or-later)
depends=(
  alsa-lib
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=(https://www.alsa-project.org/files/pub/$_name/$_name-$pkgver.tar.bz2{,.sig})
sha512sums=('9073cb9a1fc6ad04bafa368435e45dd6c8506e14f0e594f02f7d2b7e06c72b8af9babf08cfead57a940a9ebde4548edf608196fea56089fe8d1300a04f573151'
            'SKIP')
b2sums=('50e4cff2d4fccdf8fd62cb379c08f55f8229b20cb51ac8c89c6ba84214663992748fc9c3555b4242457348dfc877280754f1574eaffb8c9cbc4e82d376d6660c'
        'SKIP')
validpgpkeys=(F04DF50737AC1A884C4B3D718380596DA6E59C91)  # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
