# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kevin Sopp

pkgname=zutty
pkgver=0.15
pkgrel=2
pkgdesc='Terminal emulator'
arch=(loong64 x86_64)
url='https://tomscii.sig7.se/zutty'
license=(GPL3)
depends=(freetype2 libglvnd libxmu xorg-fonts-misc)
makedepends=(git python waf)
source=('git+https://git.hq.sig7.se/zutty.git#commit=7e481c04507e9b5cacfe67fe2b96bdb449b08726') # tag: 0.15
b2sums=('a298699acf09ad599f0a7c921b546f398416698d9d92765667fa3b280c00e3fa745300d45dfaab0bc2aa468837cfc0a22da4c4bdc5188542c2daebdab73de8bb')

prepare() {
  cd $pkgname
  rm -rv waf
}

build() {
  cd $pkgname
  waf configure --prefix=/usr
  waf -v build
}

package() {
  cd $pkgname
  waf --destdir="$pkgdir" install
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -v doc/* "$pkgdir/usr/share/doc/$pkgname"
  cd icons
  install -Dm644 zutty.desktop "$pkgdir/usr/share/applications/zutty.desktop"
  install -Dm644 zutty.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/zutty.svg"
  for res in 16x16 32x32 48x48 64x64 96x96 128x128; do
    install -Dm644 zutty_$res.png "$pkgdir/usr/share/icons/hicolor/$res/apps/zutty.png"
  done
}
