# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kevin Sopp

pkgname=zutty
pkgver=0.16
pkgrel=1
pkgdesc='Terminal emulator'
arch=(loong64 x86_64)
url='https://tomscii.sig7.se/zutty'
license=(GPL3)
depends=(freetype2 libglvnd libxmu xorg-fonts-misc)
makedepends=(git python waf)
source=('git+https://git.hq.sig7.se/zutty.git#commit=33dcbd24ae9ab4f8005ce7ba9fad9821091bb8bc') # tag: 0.16
b2sums=('54f44c0c6dba677a533e02cc806e923126ea60fdd3c583803d88a5937244b54b8564117f29586e4bcac2605571eff3290bfae16482c1d0e161172f14a4a0b0d6')

prepare() {
  cd $pkgname
  rm -rv waf
}

build() {
  cd $pkgname
  waf configure --prefix=/usr
  waf -v build
}

package() {
  cd $pkgname
  waf --destdir="$pkgdir" install
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -v doc/* "$pkgdir/usr/share/doc/$pkgname"
  cd icons
  install -Dm644 zutty.desktop "$pkgdir/usr/share/applications/zutty.desktop"
  install -Dm644 zutty.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/zutty.svg"
  for res in 16 32 48 64 96 128; do
    install -Dm644 zutty_${res}x$res.png "$pkgdir/usr/share/icons/hicolor/${res}x$res/apps/zutty.png"
  done
}
