# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=cvxpy
pkgname=python-$_pyname
pkgver=1.5.0
pkgrel=1
pkgdesc='A Python-embedded modeling language for convex optimization problems'
url='https://www.cvxpy.org/'
license=(Apache-2.0)
arch=(loong64 x86_64)
depends=(gcc-libs
         glibc
         python
         python-clarabel
         python-ecos
         python-numpy
         python-osqp
         python-scipy
         python-scs)
makedepends=(git
             pybind11
             python-build
             python-installer
             python-setuptools
             python-wheel)
optdepends=('python-cvxopt: GLPK solver'
            'python-cylp: CBC solver'
            'python-pyscipopt: SCIP solver')
checkdepends=(python-ecos
              python-osqp
              python-pytest
              python-scs)
source=(git+https://github.com/cvxpy/cvxpy#tag=v$pkgver)
sha256sums=('d310fd3fac686b2b9dc81179f73ce767a5d91c175fc97621b600f915375aff3d')

prepare() {
  sed -e 's|,<1.24||' -e '/oldest-supported-numpy/d' -i $_pyname/pyproject.toml # Drop version constraints
}

build() {
  cd $_pyname
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v -k 'not test_scipy_mi_lp_2'
}

package() {
  cd $_pyname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
