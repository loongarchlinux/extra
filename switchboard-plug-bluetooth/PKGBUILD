# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-bluetooth
pkgver=2.3.6.r148.34e1d73
pkgrel=1
pkgdesc='Switchboard Bluetooth Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-bluetooth
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_commit=34e1d73379e8d05f2d57760a1974cd41f7584d0e
source=(git+https://github.com/elementary/switchboard-plug-bluetooth.git#commit=${_commit})
b2sums=('2345bb6ecd457729187dce98327b94f68d3b451fd0cd3c1ff37f8acbf2c6276aefaf9341c7e062b0d9c9f4514cd428a9dd8a358300c763c033c4931320db1cb2')

pkgver() {
  cd switchboard-plug-bluetooth
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-bluetooth build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
