# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>

pkgname=routino
pkgver=3.4.1
pkgrel=2
pkgdesc='Router for OpenStreetMap Data'
arch=('loong64' 'x86_64')
url='https://www.routino.org/'
license=('AGPL3')
depends=('zlib' 'bzip2')
source=(https://www.routino.org/download/$pkgname-$pkgver.tgz)
sha256sums=('0baa8d2a58d1755d0fae86e04b17eb64b8191fe3e5f247292a64dbf3718b852b')

prepare() {
  cd "$pkgname-$pkgver"

#paths fix
  sed -i 's|prefix=/usr/local|prefix=/usr|' Makefile.conf
  sed -i 's|/doc/routino|/share/doc/routino|' Makefile.conf

#RPATH fix
  sed -i 's|LDFLAGS_LDSO=-Wl,-R.||' Makefile.conf
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install
}
