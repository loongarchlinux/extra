# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

_name=zeitwerk
pkgname=ruby-zeitwerk
pkgver=2.6.13
pkgrel=2
pkgdesc="Efficient and thread-safe code loader for Ruby"
arch=(any)
url="https://github.com/fxn/zeitwerk"
license=(MIT)
depends=(ruby)
makedepends=(
  ruby-rake
  ruby-rdoc
)
checkdepends=(
  ruby-minitest
  # ruby-minitest-focus  # TODO: package
  ruby-minitest-proveit
  ruby-minitest-reporters
  ruby-warning
)
options=(!emptydirs)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('4ce3406cc77123439f3499d3f60030d043e9fca6ab1921ec95a5be58a0d4f2af22de06c1e0f48ad9a463f194f2f851eb0d815a1d7f35d9f6a6c0cb2ab3bd403d')
b2sums=('434da46147eb90d171a93a415dc3cca9c326136efd29a9fa46794dfc5bfcb5fc424fa5f64215281c36473d857c81895758129a0f779f13f6dc536839e240d455')

prepare() {
  cd $_name-$pkgver

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed -i -e 's|~>|>=|g' $_name.gemspec
  # remove ruby-minitest-focus requirement
  sed '/minitest\/focus/d' -i test/test_helper.rb
}

build() {
  local gemdir="$(gem env gemdir)"
  local gem_install_options=(
    --local
    --verbose
    --ignore-dependencies
    --no-user-install
    --install-dir tmp_install/$gemdir
    --bindir tmp_install/usr/bin
    $_name-$pkgver.gem
  )
  local unrepro_files=(
    tmp_install/$gemdir/cache/
    tmp_install/$gemdir/gems/$_name-$pkgver/vendor/
    tmp_install/$gemdir/doc/$_name-$pkgver/ri/ext/
  )

  cd $_name-$pkgver

  gem build $_name.gemspec
  gem install "${gem_install_options[@]}"

  # remove unrepreducible files
  rm -frv "${unrepro_files[@]}"

  find tmp_install/$gemdir/gems/ \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find tmp_install/$gemdir/extensions/ \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  local gemdir="$(gem env gemdir)"

  cd $_name-$pkgver

  GEM_HOME="tmp_install/$gemdir" rake test
}

package() {
  cd $_name-$pkgver

  mv -v tmp_install/* "$pkgdir/"

  install -vDm 644 MIT-LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
