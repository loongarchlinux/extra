# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_hkgname=alsa-mixer
pkgname=haskell-${_hkgname}
pkgver=0.3.0.1
pkgrel=1
pkgdesc='Bindings to the ALSA simple mixer API'
url='https://hackage.haskell.org/package/alsa-mixer'
license=('BSD')
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'glibc' 'gmp' 'alsa-lib' 'haskell-alsa-core' 'haskell-extensible-exceptions')
makedepends=('ghc' 'c2hs')
source=(https://github.com/ttuegel/alsa-mixer/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('3cc1c063945064c4ec625d4a38f4da51e7c80c622b045dc2425784752b1d8f4b')
sha512sums=('ac7970c8be871ff8ead1c866b360fe5170647cb0f91246e2488ec43f64b1cc7a785b2b680f35322bd73ad9f57df480ce3c32861d71e45d387b7fd8b976bae44c')

build() {
  cd ${_hkgname}-${pkgver}
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
        --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
        --ghc-option='-pie'
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd ${_hkgname}-${pkgver}
  runhaskell Setup test --show-details=direct
}

package() {
  cd ${_hkgname}-${pkgver}
  install -Dm 744 register.sh "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -Dm 744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -dm 755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
  runhaskell Setup copy --destdir="$pkgdir"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s /usr/share/doc/${pkgname}/html "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
}

# vim: ts=2 sw=2 et:
