# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=jupyter-sphinx
pkgname=python-$_pyname
pkgver=0.5.3
pkgrel=2
pkgdesc='Jupyter Sphinx Extensions'
arch=(any)
url='https://github.com/jupyter/jupyter-sphinx/'
license=(BSD)
depends=(ipython
         jupyter-nbclient
         jupyter-nbconvert
         jupyter-nbformat
         python
         python-docutils
         python-ipywidgets
         python-jupyter-client
         python-sphinx
         python-traitlets)
makedepends=(python-build
             python-hatchling
             python-installer)
checkdepends=(python-pytest)
source=(https://github.com/jupyter/jupyter-sphinx/archive/v$pkgver/$_pyname-$pkgver.tar.gz)
sha256sums=('1fe59e9b842b5da5e1d901e9c27b5c22990fac74a02931f3bfb8e4e1df463ebc')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver
  PYTHONPATH="$PWD" \
  pytest -v \
    -W ignore::DeprecationWarning
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
