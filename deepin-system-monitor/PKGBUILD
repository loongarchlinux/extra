# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=6.0.16
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
# startdde: org.deepin.dde.Display1
depends=('dtkcore' 'dtkgui' 'dtkwidget' 'dwayland' 'qt5-base' 'qt5-svg' 'gsettings-qt'
         'icu' 'dconf' 'libxcb' 'xcb-util-wm' 'gcc-libs' 'libnl' 'libcap' 'libpcap' 'startdde')
makedepends=('cmake' 'deepin-gettext-tools' 'gtest' 'ninja' 'qt5-tools' 'deepin-dock')
groups=('deepin')
install=deepin-system-monitor.install
source=("https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('802b73068151344d09f67cda14d28b0d0655d31497ce844eff1039ef087359c0c3a4cc3fa8ad188ce0cc752f8398e536b37a76f3aa78bba50b3a8959b65e98bb')

build() {
  cd deepin-system-monitor-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

package() {
  cd deepin-system-monitor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
