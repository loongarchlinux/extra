# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: sracker <smb.sac@gmail.com>

pkgname=xdebug
pkgver=3.3.2
pkgrel=1
pkgdesc="PHP debugging extension"
arch=('loong64' 'x86_64')
url="https://www.xdebug.org"
license=('GPL')
depends=('php')
backup=('etc/php/conf.d/xdebug.ini')
source=("https://xdebug.org/files/$pkgname-${pkgver}.tgz"
	      'xdebug.ini')
sha256sums=('15545b8311bedb5e7df08fd90d6653d9bf33629791e8119e70350475df932c55'
            '2d0227f47d844cec9f3513a8d91c14f4f34d7c634e86c2f79eab4e3017cc95a5')

build() {
  cd "$srcdir"/$pkgname-${pkgver}
  phpize
  ./configure --prefix=/usr --enable-xdebug
  make
}

package() {
  cd "$srcdir"/$pkgname-${pkgver}
  make INSTALL_ROOT="$pkgdir" install
  install -D -m 644 "$srcdir"/xdebug.ini "$pkgdir"/etc/php/conf.d/xdebug.ini
}
