# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libyuv
pkgver=r2426+464c51a0
pkgrel=1
pkgdesc="Library for YUV scaling"
arch=(loong64 x86_64)
url="https://chromium.googlesource.com/libyuv/libyuv/"
license=(custom)
depends=(gcc-libs libjpeg)
makedepends=(cmake git)
# Version required by libavif
_commit=464c51a0353c71f08fe45f683d6a97a638d47833
source=(git+${url}#commit=${_commit}
    0001-fix-build-error.patch)
sha512sums=('SKIP'
            '74e4503371ea7fbb054b18990dccc708d97612c03821d6ca3f19bf26ca8bbabfb7978f61b6a738b17c820bb5fd278a746dba5f0e9ae8e0d2a8833f6d5b7feaea')

pkgver() {
  cd ${pkgname}
  printf "r%s+%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  sed -i 's|yuvconvert ${JPEG_LIBRARY}|${ly_lib_shared} ${JPEG_LIBRARY}|' ${pkgname}/CMakeLists.txt
  patch -d ${pkgname} -p1 -i $srcdir/0001-fix-build-error.patch
}

build() {
  cmake -B build -S ${pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
