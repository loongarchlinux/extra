# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>

pkgname=i3lock
pkgver=2.15
pkgrel=2
pkgdesc='Improved screenlocker based upon XCB and PAM'
url='https://i3wm.org/i3lock/'
arch=('loong64' 'x86_64')
license=('MIT')
groups=('i3')
depends=(
  'cairo' 'libcairo.so'
  'glibc'
  'libev' 'libev.so'
  'libxcb'
  'libxkbcommon' 'libxkbcommon.so'
  'libxkbcommon-x11' 'libxkbcommon-x11.so'
  'pam' 'libpam.so'
  'xcb-util'
  'xcb-util-image'
  'xcb-util-xrm'
)
makedepends=(
  'meson'
)
options=('docs')
backup=('etc/pam.d/i3lock')
source=(https://i3wm.org/i3lock/$pkgname-$pkgver.tar.xz{,.asc})
sha512sums=('6079f5233c0820c6e38f3cd8f3732d585e95e7d670408e4fb18ab580a2e1face8f3d6ea13eaaeedc8671f74def917183667e736b35306bf7c630d40625b41ac2'
            'SKIP')
validpgpkeys=('424E14D703E7C6D43D9D6F364E7160ED4AC8EE1D') # Michael Stapelberg

prepare() {
  cd ${pkgname}-${pkgver}

  # Fix ticket FS#31544, sed line taken from gentoo
  sed -i -e 's:login:system-auth:' pam/i3lock
}


build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  cd ${pkgname}-${pkgver}
  install -Dm 644 i3lock.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
