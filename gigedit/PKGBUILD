# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=gigedit
pkgver=1.2.1
pkgrel=1
pkgdesc="Gigasampler instrument editor"
arch=(loong64 x86_64)
url="https://www.linuxsampler.org/"
license=(GPL-2.0-or-later)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  atkmm
  cairomm
  docbook-xsl
  gendesk
  glib2
  glibmm
  gtk3
  gtkmm3
  intltool
  libgig
  libsigc++
  libsndfile
  libxslt
  linuxsampler
  pangomm
)
provides=(libgigedit.so)
source=(
  https://download.linuxsampler.org/packages/$pkgname-$pkgver.tar.bz2
  $pkgname-1.2.0-libdir.patch
)
sha512sums=('f70b1887ea66acc26e8a1becabb424e66ee2dcdc726805b0e2f42fa28b5ff1a54e7b6bab8808ce84a02bcf8ea1aea13ab2cd891848d9bcc711944dcd312509b2'
            'd701ee5fb013c08f3f9bb9a85464c82aaa1200ca14f8876dfd5d4835f41c1325728c07279528fb22737d79e41b33f931628637c62458e5f49aaa77f6b5dd5fe9')
b2sums=('b448650a6200fd0f87d636518a61ef5a98601c0a6627e4a2cfd8658ad67c5f4f17b64f1913bb7b77d59874018ca24aceb590862dfc0b343a7111f50f357557b2'
        'c752d259e42c47bfd4e819daee271a565dbd30aa5806151be028eb333ec22f40b862529fb40c69fc14f5cd455efc52d43c959501512dd72ad71b48527e7fee3c')

prepare() {
  # generate XDG desktop file
  gendesk -n \
          --pkgname "$pkgname" \
          --name "$pkgname" \
          --genericname "Instrument Editor" \
          --categories "AudioVideo;Audio"

  # install shared object to global namespace, so no ld.so.conf is required
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-1.2.0-libdir.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  depends+=(
    atkmm libatkmm-1.6.so
    cairomm libcairomm-1.0.so
    glib2 libgobject-2.0.so libglib-2.0.so
    glibmm libglibmm-2.4.so
    gtk3 libgtk-3.so
    gtkmm3 libgtkmm-3.0.so
    libgig libgig.so
    libsigc++ libsigc-2.0.so
    libsndfile libsndfile.so
    linuxsampler liblinuxsampler.so
    pangomm libpangomm-1.4.so
  )

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}

# vim:set ts=2 sw=2 et:
