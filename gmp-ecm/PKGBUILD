# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@phare.normalesup.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=gmp-ecm
pkgver=7.0.6
pkgrel=1
pkgdesc='Elliptic Curve Method for Integer Factorization'
url='https://github.com/sethtroisi/gmp-ecm'
license=(GPL-3.0-or-later
         LGPL-3.0-or-later)
depends=(gcc-libs
         glibc
         gmp)
makedepends=(git)
arch=(loong64 x86_64)
source=(git+https://gitlab.inria.fr/zimmerma/ecm#tag=git-$pkgver)
sha256sums=('cec9bacbc6cfaeae0a094ea91451b5343b87519309855e732566ee5cdb7c14ea')

prepare() {
  cd ecm
  autoreconf -vi
}

build() {
  cd ecm
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --enable-openmp
  make
}

check() {
  cd ecm
  make check
}

package() {
  cd ecm
  make DESTDIR="$pkgdir" install
}
