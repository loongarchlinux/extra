# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=ana
pkgname=python-${_pyname}
pkgver=0.06
pkgrel=4
pkgdesc='Python module to provide easy distributed data storage'
url='https://github.com/zardus/ana'
arch=('any')
license=('custom:BSD2')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz"
        "drop-future-dependency.patch")
sha512sums=('d4944f96379d399040e191d3058d35e5e7ca2cd7193b8dfb3b7b78e0dbf26c8c5dc48acb7c570e51fead0387ee708246de8cf9231ef35028bdd7686f732df875'
            '035c21317a5383aeea5822ea3db464c7f4ab7152dcaaf03556008d06430c45f4ee8cfe3576df29ef37753dd1ace4877764d0456915111042571ed80c3c0cc558')
b2sums=('a94c37c2bd845b4f2f5402296487c4c29ff5f7f3a1b88b3a77a24add48efc5364bf0f07705a9db72e6d34579bece535ac3b230f173da3b262a50f4bf48dcc02c'
        '324faf19458ccd4a4085f9a91148d898160f0fb80e363211d04253b7ef2f293d557600a602f50e6a6e3a3fbb3b22e8f1a688fb246e7ade0b9ea7a7a0b523e1a5')

prepare() {
  cd "${_pyname}-${pkgver}"
  patch -Np1 -i ${srcdir}/drop-future-dependency.patch
}

build() {
  cd "${_pyname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
