# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libgit2-glib
pkgname=(
  libgit2-glib
  libgit2-glib-docs
)
pkgver=1.2.0
pkgrel=3
pkgdesc="GLib wrapper for libgit2"
url="https://gitlab.gnome.org/GNOME/libgit2-glib"
license=(LGPL-2.1-or-later)
arch=(loong64 x86_64)
makedepends=(
  gi-docgen
  git
  glib2
  gobject-introspection
  libgit2
  meson
  python-gobject
  vala
)
source=(
  "git+$url.git#tag=v$pkgver"
  $pkgname-1.2.0-libgit2_1.8.patch
)
b2sums=('d31e3edff3b663b0ba754868b719afd231067dd55aef05d4deec995dfab7eb7ba45c16ef2094dfa0f0c605731732b78e78bfa283a70fb18ab7d24a6f12fb00a8'
        '412dc5ae24a29789d1f8433c2dd3a1eaebb2087311be2019a2170c32d913ee9310e62e292fdd51d1a9eb5c4f9b1dc19deefb4734ca40940523e17cb76eecb834')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//g;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  # add compatibility for libgit2 1.8: https://gitlab.gnome.org/GNOME/libgit2-glib/-/merge_requests/40
  patch -Np1 -d $pkgname -i ../$pkgname-1.2.0-libgit2_1.8.patch
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libgit2-glib() {
  depends=(
    glib2 libgio-2.0.so libglib-2.0.so libgobject-2.0.so
    glibc
    libgit2 libgit2.so
  )
  optdepends=(
    'libgit2-glib-docs: for developer documentation'
    'python-gobject: for Python integration'
  )
  provides=(libgit2-glib-1.0.so)

  meson install -C build --destdir "$pkgdir"
  (
    cd "$pkgdir"
    _pick libgit2-glib-docs usr/share/gtk-doc
  )

  install -vDm 644 $pkgname/{AUTHORS,ChangeLog,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_libgit2-glib-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
