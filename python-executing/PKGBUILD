# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-executing
_name=${pkgname#python-}
pkgver=2.0.0
pkgrel=3
pkgdesc='Get the currently executing AST node of a frame, and other information'
arch=('any')
url=https://github.com/alexmojaki/executing
license=('MIT')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools-scm'
  'python-wheel'
)
checkdepends=('ipython' 'python-asttokens' 'python-littleutils' 'python-pytest')
source=("git+$url.git#tag=v$pkgver")
b2sums=('4d4c42729494d3900b59ef8c2c3cb5a53fdd609103337ae501f190a61fc0bc356efb575dd987f26eac4ef5ced3cb33b33946e73d7152ac540ef820c7142f011b')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  PYTHONPATH="$PWD/$_name:$PYTHONPATH" pytest
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"$_name"-$pkgver.dist-info/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
