# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: neodarz <neodarz at neodarz dot net>

pkgname=python-flake8-isort
pkgver=6.1.1
pkgrel=3
pkgdesc='Flake8 plugin to validate annotations complexity'
arch=('any')
url='https://github.com/gforcada/flake8-isort'
license=('GPL-2.0-only')
depends=(
  'python'
  'flake8'
  'python-isort'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-hatchling'
)
checkdepends=('python-pytest' 'python-toml')
_commit='3b2f1dd9a67c385f140b503a714e07ca3483f4c2'
source=("$pkgname::git+$url#commit=$_commit")
b2sums=('d21369c31a11221655972c30dfef91c16d6a2b393fd12d46b1f9370b5a2c86828cabf09938258fa8d9c9582255e50df1f4c989ddfdb04e821396287d1ca46ccc')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest run_tests.py
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
