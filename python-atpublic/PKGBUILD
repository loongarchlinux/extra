# Maintainer: David Runge <dvzrv@archlinux.org>

_name=public
pkgname=python-atpublic
pkgver=4.1.0
pkgrel=2
pkgdesc="@public decorator for Python"
arch=(any)
url="https://gitlab.com/warsaw/public"
license=(Apache-2.0)
depends=(python)
makedepends=(
  python-build
  python-hatchling
  python-installer
)
checkdepends=(
  python-pytest
  python-sybil
)
source=($url/-/archive/$pkgver/$_name-$pkgver.tar.gz)
sha512sums=('e6608572a6dfff0b9fa9039c6f7aea4488caabb89a2dcafc19435693b861336cd100da16c8f4bde0b8b454eea0354f0baedf3bf1ccdc8536b1dc11129e972f22')
b2sums=('c2584a6574094755d8d06984d42deecbb2ab6c26e9b3e7c1cdad1831189be7c594edd427ecc7d7468f6c855d8042b8ceffd4dff6bdbd12dfaa6cbd92c0743be8')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
