# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libndp
pkgver=1.9
pkgrel=1
pkgdesc="Library for Neighbor Discovery Protocol"
url="http://libndp.org/"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(glibc)
makedepends=(git)
source=("git+https://github.com/jpirko/libndp#tag=v$pkgver")
b2sums=('150f2bc4d0e67c44a694f6c6d0fb7f6927f7af2a8467b39fd77a41c0f5abb9df28d99432e444cff7328d0c8cd514516216d777cc416cfed3bb4cfa62007d6173')

prepare() {
  cd $pkgname
  ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libexecdir=/usr/lib
    --disable-static
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
