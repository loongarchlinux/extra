# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=live-media
pkgver=2024.04.19
pkgrel=1
pkgdesc='Set of C++ libraries for multimedia streaming'
url='http://live555.com/liveMedia'
arch=('loong64' 'x86_64')
license=('LGPL')
depends=('gcc-libs' 'openssl')
source=(http://live555.com/liveMedia/public/live.$pkgver.tar.gz)
sha256sums=('e6c2ebfec6772c1f37084271e4e5236abc7f0e1fc4489f9869763440200ddcc2')
sha512sums=('62aeacea8f987731237c5924097a2aa4f650629f44d190637b2c960425f295b1360fa27ecdfe96636e7f29a23385ed872f54b66b402c38e3e76f6564f3cdc38f')

prepare() {
  cd live
  find . -name Makefile.head | xargs sed -e 's|/local||g' -i # Fix prefix
}

build() {
  cd live
  sed -E 's|(-DSOCKLEN_T=socklen_t)|\1 -fPIC -DRTSPCLIENT_SYNCHRONOUS_INTERFACE=1|g' -i config.linux-with-shared-libraries
  ./genMakefiles linux-with-shared-libraries
  CXXFLAGS+=' -std=c++20' \
  make
}

package() {
  cd live
  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
