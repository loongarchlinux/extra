# Maintainer: David Runge <dvzrv@archlinux.org>

_name=fastnumbers
pkgname=python-fastnumbers
pkgver=5.1.0
pkgrel=1
pkgdesc="Super-fast and clean conversions to numbers"
arch=(loong64 x86_64)
url="https://github.com/SethMMorton/fastnumbers"
license=(MIT)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-hypothesis
  python-numpy
  python-pytest
  python-typing_extensions
)
optdepends=(
  'python-numpy: for conversion to numpy arrays'
)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('d2a1e1430f1ec5f2540c4ab1ddd2e855fdeb98079389849d33289505af2542631f2690769ec4ca92a079650947fcb564dff84c399eae2d1034b266df25a8851f')
b2sums=('905200063bcb1d053420b54d580db795714fac7da4fe981cbe0f559fb6eaf9861349effee72533442f1afb4811d79e9cd2534f207c15b253bfce349bec2371ba')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
