# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: Mohammad AlSaleh <CE.Mohammad.AlSaleh at gmail dot com>

pkgname=lua-language-server
pkgver=3.10.3
pkgrel=1
pkgdesc='Lua Language Server coded by Lua'
arch=(loong64 x86_64)
url=https://luals.github.io
license=(MIT)
depends=(
  bash
  gcc-libs
)
makedepends=(
  git
  ninja
  rsync
)
source=(
  "git+https://github.com/LuaLS/lua-language-server.git#tag=$pkgver"
  git+https://github.com/actboy168/luamake.git
  git+https://github.com/actboy168/bee.lua.git
  git+https://github.com/sqmedeiros/lpeglabel.git
  git+https://github.com/love2d-community/love-api.git
  git+https://github.com/bjornbytes/lovr-docs.git
  git+https://github.com/CppCXY/EmmyLuaCodeStyle.git
  git+https://github.com/actboy168/json.lua.git
  git+https://github.com/LuaCATS/OpenResty.git
  git+https://github.com/LuaCATS/bee.git
  git+https://github.com/LuaCATS/busted.git
  git+https://github.com/LuaCATS/Cocos4.0.git
  git+https://github.com/LuaCATS/Defold.git
  git+https://github.com/LuaCATS/Jass.git
  git+https://github.com/LuaCATS/luafilesystem.git
  git+https://github.com/LuaCATS/love2d.git
  git+https://github.com/LuaCATS/lovr.git
  git+https://github.com/LuaCATS/luaecs.git
  git+https://github.com/LuaCATS/luassert.git
  git+https://github.com/LuaCATS/skynet.git
  git+https://github.com/LuaCATS/ffi-reflect.git
  git+https://github.com/LuaCATS/luv.git
  wrapper
)
b2sums=('21e81df7d27cad8c626b60da26e8c4bb4768791e921309d2f96f4e615e5be1152bf32b57278b11d199713b8bff48f10786d4276a2067bf7440e74334ef071d1d'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        '8fabeb8f601c7577891119168b69561cba58c12fbc0db909a5fb26b1f696b881cee00d264d20fa818ae46421c8ef7bae980c3033ca14420cbaa4884a4b697d28')

prepare() {
  cd $pkgname
  git submodule init
  for i in ../{luamake,bee.lua,lpeglabel,love-api,EmmyLuaCodeStyle,json.lua}; do
    git config submodule.3rd/"${i##../}".url "$i"
  done
  for i in ../{OpenResty,bee,busted,Cocos4.0,Defold,Jass,love2d,lovr,luaecs,luassert,skynet,ffi-reflect,luv}; do
    git config submodule.meta/3rd/"${i##../}".url "$i"
  done
  git config submodule.3rd/lovr-api.url ../lovr-docs
  git config submodule.meta/3rd/lfs.url ../luafilesystem
  git -c protocol.file.allow=always submodule update
  cd 3rd/luamake
  git submodule init
  git config submodule.3rd/bee.lua.url ../../../bee.lua
  git -c protocol.file.allow=always submodule update
}

build() {
  cd $pkgname
  ninja -C 3rd/luamake -f compile/ninja/linux.ninja
  ./3rd/luamake/luamake rebuild
}

package() {
  install -D wrapper "$pkgdir"/usr/bin/$pkgname
  cd $pkgname
  install -Dt "$pkgdir"/usr/lib/$pkgname/bin bin/$pkgname
  install -m644 -t "$pkgdir"/usr/lib/$pkgname/bin bin/main.lua
  install -m644 -t "$pkgdir"/usr/lib/$pkgname {debugger,main}.lua
  rsync -r --exclude=.git locale meta script "$pkgdir"/usr/lib/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
