# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=diskus
pkgver=0.7.0
pkgrel=4
pkgdesc="Minimal, fast alternative to 'du -sh'"
arch=(loong64 x86_64)
url=https://github.com/sharkdp/diskus
license=('Apache-2.0 OR MIT')
depends=(gcc-libs)
makedepends=(
  git
  rust
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('706a2a58b58c2051142a984de31cfce46b25d4bf3c2152a5644d65a3130506eff3d6f471a1765b60b6c64f0db89240c249f6a323992dd281c0301ae22eaca743')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

check() {
  cd $pkgname
  cargo test --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 doc/$pkgname.1
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
