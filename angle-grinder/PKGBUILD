# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=angle-grinder
pkgver=0.19.4
pkgrel=1
pkgdesc='Slice and dice logs on the command line'
arch=('loong64' 'x86_64')
url='https://github.com/rcoh/angle-grinder'
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'rust')
options=('!lto')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('205be76f3f0b7ffacafde20b91f97d8f134dbf4d375bf07782b00b7dedb896d7fac9a218e30b3cd07e9081d67419bc900ba91fcff43f0b22b72c9c0caaff558e')
b2sums=('77e13798d5906018cb6c4c9c5e97a2731d0f8bd3e07ea7dd4ab79114ddb0bf93b79bb3a84dc27bcfe460771c794ab5b6651fc10f40f56f81272aa534c8f5a320')

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release
}

check() {
  cd "$pkgname"

  cargo test --frozen --release
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/agrind

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
