# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.15.9
pkgrel=1
pkgdesc="Library to aggregates people into metacontacts"
url="https://wiki.gnome.org/Projects/Folks"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  evolution-data-server
  libgee
  libxml2
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  libsysprof-capture
  meson
  python-dbusmock
  vala
)
provides=(
  libfolks.so
  libfolks-{dummy,eds}.so
)
options=(
  !lto # LTO copies some GType constructors
)
_commit=f57424e8411bb0b005483593ed11025a838ab19f  # tags/0.15.9^0
source=("git+https://gitlab.gnome.org/GNOME/folks.git#commit=$_commit")
b2sums=('f86c2302139f244f6d8ff3b7207104dc7e81aed6f51fd450f2fdcd6bbcef7866cc8d0d4764c21c06bacc8946714589955e598c79b0d4d50fbc11c0628a7d5410')

pkgver() {
  cd folks
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd folks
}

build() {
  local meson_options=(
    -D docs=true
    -D profiling=true
    -D telepathy_backend=false
  )

  arch-meson folks build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs -t 4
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
