# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contributor: Jochen Schalanda <jochen+aur@schalanda.name>

_gemname='faraday'
pkgname="ruby-${_gemname}"
pkgver=2.10.1
pkgrel=2
pkgdesc='HTTP/REST API client library.'
arch=('any')
url="https://github.com/lostisland/${_gemname}"
license=('MIT')
depends=(
  ruby
  ruby-faraday-net_http
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-bundler
  ruby-pry
  ruby-rack
  ruby-rake
  ruby-rspec
  ruby-webmock
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz")
sha512sums=('21a1777e1bb2f7b62963ad9063bc5ac373a9d1c6e827cf346630ea9dc1bd8f09009a554363dc92e030ce07bca002a24e10dce2736f43414e4e20bd394e55cdb4')
b2sums=('2df625d91cedcfc3f78b97570e0a87404db74728f82e7c77776f806a0e388d629cb1fe0b35157c4b300db93db056a9c2bd1aff90f39a9dc326c6cae76e37fc0e')

prepare() {
  cd "${_gemname}-${pkgver}"

  # allow latest dependencies
  sed --in-place --regexp-extended \
    --expression 's|~>|>=|g' \
    --expression "s|, '< [0-9\.]+'||g" \
    "${_gemname}.gemspec"

  # we don't need test coverage
  sed --in-place --regexp-extended \
    --expression '/SimpleCov\.start/,/end/d' \
    --expression '/simplecov/Id' \
    --expression '/coveralls/d' \
    spec/spec_helper.rb
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake spec
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
