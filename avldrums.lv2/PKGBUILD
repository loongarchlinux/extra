# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=avldrums.lv2
pkgver=0.7.3
pkgrel=1
pkgdesc="A simple Drum Sample Player Plugin, dedicated to the AVLinux Drumkits"
arch=(loong64 x86_64)
url="https://x42-plugins.com/x42/x42-avldrums"
license=(GPL-2.0-or-later)
groups=(
  lv2-plugins
  pro-audio
)
depends=(
  cairo
  glibc
  libx11
  lv2-host
)
makedepends=(
  fluidsynth
  git
  glib2
  glu
  libglvnd
  lv2
  pango
)
checkdepends=(lv2lint)
source=(
  git+https://github.com/x42/$pkgname.git#tag=v$pkgver
  git+https://github.com/x42/robtk
  $pkgname-0.6.0-use-system-fluidsynth.patch
)
sha512sums=('db18307cdcfaf1382854528b483c5ad1188f00d63326f59caf4223dd85d64571f6f1627599ee8c4599717a8f96b6b2570d9b7ddca601c374723a69f152ad5040'
            'SKIP'
            'fdf48b606630d8c17df0e5b7cfda5b580c77d0237c5c76bdf6c831f693a70401ad66a94a2e4dc2ec04636654bf65256a27b985a963c5ad4c8948baa61a726f4d')
b2sums=('2322616c238a46cabaeae74129fb24cc19a10a34d79ac6eacaea8ff33b52bffcc7e5219b7cc958780320a92d678447021f9e6186e87f35102e2410bd474f4c4b'
        'SKIP'
        'b9a492b48ed34c0705e8e75a3ce4c971bd61e2efe93432c81785cb314815995edb6144a311841025acdbaa0ceef2684cc2f1d872a53a191518909f263c88571a')

prepare() {
  (
    cd $pkgname
    git submodule init
    git config submodule.robtk.url "$srcdir/robtk"
    git -c protocol.file.allow=always submodule update
  )

  # build against system fluidsynth
  patch -Np1 -d $pkgname -i ../$pkgname-0.6.0-use-system-fluidsynth.patch
}

build() {
  make -C $pkgname
}

check() {
  for _name in BlackPearl BlackPearlMulti RedZeppelin RedZeppelinMulti; do
    lv2lint -Mpack -I "$pkgname/build/" "http://gareus.org/oss/lv2/avldrums#$_name"
  done
}

package() {
  depends+=(
    fluidsynth libfluidsynth.so
    glib2 libgobject-2.0.so
    libglvnd libGL.so
    pango libpango-1.0.so libpangocairo-1.0.so
  )

  make DESTDIR="$pkgdir" PREFIX=/usr install -C $pkgname
}
