# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-display
pkgver=7.0.0.r66.39d2ae5
pkgrel=1
pkgdesc='Switchboard Display Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-display
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_commit=39d2ae5375afb8ed2f7b5cac6c746f6d3f354772
source=(git+https://github.com/elementary/switchboard-plug-display.git#commit=${_commit})
b2sums=(SKIP)

pkgver() {
  cd switchboard-plug-display
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-display build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
