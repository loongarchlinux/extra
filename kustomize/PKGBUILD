# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=kustomize
pkgver=5.4.1
pkgrel=1
pkgdesc="Template-free customization of Kubernetes YAML manifests"
arch=('loong64' 'x86_64')
url="https://github.com/kubernetes-sigs/kustomize"
license=('Apache-2.0')
makedepends=('go')
depends=('glibc')
optdepends=('helm')
options=('!lto')
source=("https://github.com/kubernetes-sigs/kustomize/archive/kustomize/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('60c890e789a9f8e37370b14d0d83b310c9b20e85cf4088399fdf8726bc80d210143a8a14109c629eccc2ada8da9620b74ea499c2feccb663d7845156994344d4')
b2sums=('22cd3bb72b7d2d2994062acafd63deacaba86f29b28c1c01c0c2d39d532f6773b7f877e0df804bae9ac04e66d34bd2ae898f6fbd708a7abeaf2bf5408825c2b1')

prepare() {
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  GOPATH="${srcdir}"
  go mod download
}

build(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  GOPATH="${srcdir}"
  export GOPROXY=off
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags " \
      -X sigs.k8s.io/kustomize/api/provenance.version=${pkgver} \
      -X sigs.k8s.io/kustomize/api/provenance.buildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH}) \
      -compressdwarf=false \
      -linkmode external \
      -extldflags \"${LDFLAGS}\"" \
    .
}

check(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  go test ./...
}

package(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  # install completions
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  ./kustomize completion bash >  "${pkgdir}/usr/share/bash-completion/completions/kustomize"
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions/"
  ./kustomize completion zsh >  "${pkgdir}/usr/share/zsh/site-functions/_kustomize"
  mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d"
  ./kustomize completion fish >  "${pkgdir}/usr/share/fish/vendor_completions.d/kustomize.fish"

  # install binary
  install -D kustomize "${pkgdir}/usr/bin/kustomize"
}

# vim: ts=2 sw=2 et:
