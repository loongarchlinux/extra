# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Letu Ren <fantasquex at gmail dot com>

_pyname=pypcode
pkgname=python-${_pyname}
pkgver=2.1.0
pkgrel=1
pkgdesc="Python bindings to Ghidra's SLEIGH library for disassembly and lifting to p-code IR"
url='https://github.com/angr/pypcode'
license=('custom')
arch=('loong64' 'x86_64')
depends=('python' 'python-cffi' 'nanobind')
makedepends=('cmake' 'python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('1ef4f0808d652e37c594aa13c1cc9bab7c9695f43616130b9e0eeb06c1b23947fe43b7ee4f682bec8d847950b5b92e86ec6cc3d6cd6b7eee6f36f9f98ea082ef')
b2sums=('bb2833112b7ec9f4eacf1c822b3b41125473bb7a02574e0e3bad15618cfdaf6467c80204715e61569c77d8fb99c8e8fce5ddd4a69f63340e9f866a2c1c09140b')

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
