# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=bytestring-strict-builder
pkgname=haskell-bytestring-strict-builder
pkgver=0.4.5.7
pkgrel=4
pkgdesc="An efficient strict bytestring builder"
url="https://github.com/nikita-volkov/bytestring-strict-builder"
license=('MIT')
arch=('loong64' 'x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'uusi' 'haskell-tasty' 'haskell-tasty-quickcheck' 'haskell-quickcheck-instances'
             'haskell-rerebase')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('83e141b5fbd666cdc72157936ebdb80a2a1deef968725818703b34bd930ee717da2334194cfe39c76a55b7182ab0ddbdc7e193c5fb90e9e9d3d21f9e597986cc')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
