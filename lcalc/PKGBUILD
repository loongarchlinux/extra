# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=lcalc
pkgver=2.0.5
pkgrel=3
pkgdesc='C++ L-function class library and command line interface'
arch=(loong64 x86_64)
url='https://gitlab.com/sagemath/lcalc'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         pari)
makedepends=(gengetopt
             git)
source=(git+https://gitlab.com/sagemath/lcalc#tag=$pkgver)
sha256sums=('aa07efdb738be9b1b3c8f96ea57a2647a6685c93e197779bf37b3eb062be76df')

prepare() {
  cd $pkgname
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --with-pari
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
