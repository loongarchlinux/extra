# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>

pkgname=kooha
pkgver=2.3.0
pkgrel=5
pkgdesc="Elegantly record your screen"
arch=('loong64' 'x86_64')
url="https://github.com/SeaDve/Kooha"
license=('GPL-3.0-only')
depends=('gst-plugin-pipewire'
         'glib2>=2.80'
         'gst-plugins-base'
         'gst-plugins-ugly'
         'gstreamer-vaapi'
         'gtk4'
         'libadwaita>=1:1.5.0'
         'libpulse'
         'x264'
         'xdg-desktop-portal')
makedepends=('cargo'
             'meson'
             'pango')
checkdepends=('appstream-glib'
              'xorg-server-xvfb')
optdepends=('gst-plugins-good: Encode captures as mp4')
sha256sums=('a1aec108b44483664a6c680c5351c32de98862f5df4503a915d5f23e8ae24636')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")

prepare() {
  cd Kooha-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  arch-meson Kooha-$pkgver build
  meson compile -C build
}

check() {
  find -name hooha -executable
  # https://github.com/SeaDve/Kooha/issues/197
  # xvfb-run meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
