# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=libqxp
pkgver=0.0.2
pkgrel=12
pkgdesc="a library that parses the file format of QuarkXPress documents"
arch=('loong64' 'x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libqxp"
license=('MPL2')
depends=('librevenge' 'icu' 'gcc-libs' 'glibc')
makedepends=('boost' 'cppunit' 'doxygen')
source=("https://dev-www.libreoffice.org/src/$pkgname/$pkgname-$pkgver.tar.xz")
sha256sums=('e137b6b110120a52c98edd02ebdc4095ee08d0d5295a94316a981750095a945c')

build() {
	cd "$pkgname-$pkgver"
	./configure --prefix=/usr
        sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
}
