# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-minitest-hooks
pkgver=1.5.1
pkgrel=2
pkgdesc='Around and before_all/after_all/around_all hooks for Minitest'
arch=(any)
url='https://github.com/jeremyevans/minitest-hooks'
license=(MIT)
depends=(
  ruby
  ruby-minitest
)
makedepends=(
  ruby-minitest-global_expectations
  ruby-rake
  ruby-rdoc
  ruby-sequel
  ruby-sqlite3
)
options=(!emptydirs)
source=(https://github.com/jeremyevans/minitest-hooks/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('8c78e96c238f15c739f9b5e0259366054da4df864434c48688421fdd3b30b936')

build() {
  local _gemdir="$(gem env gemdir)"
  cd minitest-hooks-$pkgver
  gem build minitest-hooks.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    minitest-hooks-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd minitest-hooks-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd minitest-hooks-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 MIT-LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
