# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-network
pkgver=2.5.0.r158.63d08c3e
pkgrel=2
pkgdesc='Switchboard Network Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-network
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libnm
  libnma-gtk4
  libswitchboard-3.so
  networkmanager
  polkit
)
makedepends=(
  cmake
  git
  gobject-introspection
  meson
  vala
)
_commit=63d08c3e84f30c09150a1328b3103fef63496749
source=(git+https://github.com/elementary/switchboard-plug-network.git#commit=${_commit})
b2sums=(SKIP)

pkgver() {
  cd switchboard-plug-network
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-network build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
