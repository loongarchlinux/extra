# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-timecop
pkgver=0.9.8
pkgrel=2
pkgdesc='A gem providing "time travel" and "time freezing" capabilities, making it dead simple to test time-dependent code'
arch=(any)
url='https://github.com/travisjeffery/timecop'
license=(MIT)
depends=(
  ruby
)
makedepends=(
  ruby-rdoc
)
checkdepends=(
  ruby-activesupport
  ruby-minitest-rg
  ruby-mocha
  ruby-pry
  ruby-rake
)
options=(!emptydirs)
source=(https://github.com/travisjeffery/timecop/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('3abb18214bc93fbbf93500b529c95307598911164f87cc32c4380bc819abeb89')

prepare() {
  cd timecop-$pkgver
  sed '/bundler/d' -i Rakefile test/*.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd timecop-$pkgver
  gem build timecop.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    timecop-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd timecop-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd timecop-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
