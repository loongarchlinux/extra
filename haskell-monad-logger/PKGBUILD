# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=monad-logger
pkgname=haskell-monad-logger
pkgver=0.3.40
pkgrel=32
pkgdesc="A class of monads which can log messages."
url="https://github.com/snoyberg/monad-logger"
license=("MIT")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-conduit' 'haskell-conduit-extra'
         'haskell-fast-logger' 'haskell-lifted-base' 'haskell-monad-control' 'haskell-monad-loops'
         'haskell-resourcet' 'haskell-stm-chans'
         'haskell-transformers-base' 'haskell-transformers-compat' 'haskell-unliftio-core')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('a4dd32624a9feb399cf30f107b591742ac558180cb3220acb91f833459c9c525947efd5e8c5961a079c8a5b06263ff7a9a8a9a73067031f4551d6dec4a1db218')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -ftemplate_haskell
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
