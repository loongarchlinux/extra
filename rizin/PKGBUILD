# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=rizin
pkgver=0.7.3
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://rizin.re'
arch=('loong64' 'x86_64')
license=(
  'Apache-2.0'
  'BSD-2-Clause'
  'BSD-3-Clause'
  'CC-PDDC'
  'CC0-1.0'
  'GPL-1.0-or-later'
  'GPL-2.0-only'
  'GPL-2.0-or-later'
  'GPL-3.0-only'
  'GPL-3.0-or-later'
  'LGPL-2.0-or-later'
  'LGPL-2.1-only'
  'LGPL-2.1-or-later'
  'LGPL-3.0-only'
  'MIT'
)
depends=(
  glibc
  sh
  openssl
  zlib
  lz4
  xz
)
makedepends=(
  capstone
  file
  libmspack
  libzip
  meson
  ninja
  python
  python-yaml
  tree-sitter
  xxhash
)
optdepends=('rz-ghidra: ghidra decompiler plugin')
options=('!emptydirs')
source=(https://github.com/rizinorg/rizin/releases/download/v${pkgver}/rizin-src-v${pkgver}.tar.xz)
sha512sums=('bad89888b2cb1ce1227ce629ff1209f18d1afb9da55685f0ea4ead7dd1b4dfbf8ed72c173048b2ee47362a10d5e20a6c34e09d0bbaa210f3873073c10ebc4dc1')
b2sums=('9bbfbd8cf86839dcfdc30beb12d4472abf68e7d8c81561f6124b4d8ac781242bbbe8b62e06d2873928830d2524203c18ab1c17fa35855ac2d344af7649b64b3c')

build() {
  cd ${pkgname}-v${pkgver}
  arch-meson build \
    -D use_sys_capstone=enabled \
    -D use_capstone_version=v5 \
    -D use_sys_magic=enabled \
    -D use_sys_libzip=enabled \
    -D use_sys_lz4=enabled \
    -D use_sys_lzma=enabled \
    -D use_sys_zlib=enabled \
    -D use_sys_xxhash=enabled \
    -D use_sys_openssl=enabled \
    -D use_sys_tree_sitter=enabled \
    -D use_sys_libmspack=enabled \
    -D use_zlib=true \
    -D use_lzma=true \
    -D local=disabled
  ninja -C build
}

package() {
  depends+=(
    capstone libcapstone.so
    file libmagic.so
    libmspack libmspack.so
    libzip libzip.so
    tree-sitter libtree-sitter.so
    xxhash libxxhash.so
  )

  cd ${pkgname}-v${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -d "${pkgdir}/usr/share/doc/rizin"
  cp -r doc/* "${pkgdir}/usr/share/doc/rizin"
  rm "$pkgdir"/usr/lib/pkgconfig/libzstd.pc
}

# vim: ts=2 sw=2 et:
