# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=rizin
pkgver=0.7.2
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://rizin.re'
arch=('loong64' 'x86_64')
license=(
  'Apache-2.0'
  'BSD-2-Clause'
  'BSD-3-Clause'
  'CC-PDDC'
  'CC0-1.0'
  'GPL-1.0-or-later'
  'GPL-2.0-only'
  'GPL-2.0-or-later'
  'GPL-3.0-only'
  'GPL-3.0-or-later'
  'LGPL-2.0-or-later'
  'LGPL-2.1-only'
  'LGPL-2.1-or-later'
  'LGPL-3.0-only'
  'MIT'
)
depends=(
  glibc
  sh
  openssl
  zlib
  lz4
  xz
)
makedepends=(
  capstone
  file
  libmspack
  libzip
  meson
  ninja
  python
  python-yaml
  tree-sitter
  xxhash
)
optdepends=('rz-ghidra: ghidra decompiler plugin')
options=('!emptydirs')
source=(https://github.com/rizinorg/rizin/releases/download/v${pkgver}/rizin-src-v${pkgver}.tar.xz)
sha512sums=('e99a4c14511d689e7e2fb9af09f010fcc4d155a189a1e613191b646b1d80e04722c2da6ccd60e5c0cd89933f83484ce11362b04ab2b659594aad6f2057a97019')
b2sums=('192dce69231fd89fc829cf95b1b7d5dba38f5f6b46413fb187b989848139d15c0abc652083ec3b09df12bdb80b0c732e1c8b18bd33341678b69e3643067d4fd8')

build() {
  cd ${pkgname}-v${pkgver}
  arch-meson build \
    -D use_sys_capstone=enabled \
    -D use_capstone_version=v5 \
    -D use_sys_magic=enabled \
    -D use_sys_libzip=enabled \
    -D use_sys_lz4=enabled \
    -D use_sys_lzma=enabled \
    -D use_sys_zlib=enabled \
    -D use_sys_xxhash=enabled \
    -D use_sys_openssl=enabled \
    -D use_sys_tree_sitter=enabled \
    -D use_sys_libmspack=enabled \
    -D use_zlib=true \
    -D use_lzma=true \
    -D local=disabled
  ninja -C build
}

package() {
  depends+=(
    capstone libcapstone.so
    file libmagic.so
    libmspack libmspack.so
    libzip libzip.so
    tree-sitter libtree-sitter.so
    xxhash libxxhash.so
  )

  cd ${pkgname}-v${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -d "${pkgdir}/usr/share/doc/rizin"
  cp -r doc/* "${pkgdir}/usr/share/doc/rizin"
  rm "$pkgdir"/usr/lib/pkgconfig/libzstd.pc
}

# vim: ts=2 sw=2 et:
