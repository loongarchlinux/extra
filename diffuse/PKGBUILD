# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=diffuse
pkgver=0.9.0
pkgrel=2
pkgdesc='Graphical tool for merging and comparing text files'
arch=(any)
url='https://github.com/MightyCreak/diffuse'
license=(GPL)
depends=(python-cairo python-gobject)
makedepends=(git meson ninja)
source=("git+$url#commit=6510966ae9ae278d811f0efc94f4b624bd66effa") # tag: v0.9.0
b2sums=('af34267a35f96141f24a6a551ead77349126408349c5f410f30d3cc73e53a634c670fa6437b867439fc3d599c54e038869be14c1eb4fb18366566a5c5dd6624a')

build() {
  sed -i "s/'appdata'/'metainfo'/g" $pkgname/data/meson.build
  mkdir -p build
  arch-meson build $pkgname -D b_ndebug=true
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
