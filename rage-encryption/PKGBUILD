# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel Peukert <daniel@peukert.cc>

pkgname=rage-encryption
_pkgname=${pkgname%-*}
pkgver=0.10.0
pkgrel=1
pkgdesc='Rust implementation of the age encryption tool'
arch=(loong64 x86_64 i686 arm armv6h armv7h aarch64)
url="https://github.com/str4d/$_pkgname"
license=(Apache-2.0 MIT)
depends=(bzip2
         fuse3
         gcc-libs # libgcc_s.so
         glibc) # libc.so libm.so
makedepends=(cargo)
conflicts=(rage)
replaces=(rust-rage)
provides=("rust-rage=$pkgver")
_archive="$_pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('34c39c28f8032c144a43aea96e58159fe69526f5ff91cb813083530adcaa6ea4')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
	cd "$_archive"
	export CFLAGS+=" -ffat-lto-objects"
	cargo build --frozen --release --all-features
}

check() {
	cd "$_archive"
	cargo test --frozen --all-features
}

package() {
	cd "$_archive"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE-MIT
	pushd target/release
	for bin in "$_pkgname"{,-keygen,-mount}; do
		install -Dm0755 -t "$pkgdir/usr/bin/" "$bin"
		pushd completions
		install -Dm0644 "$bin.bash" "$pkgdir/usr/share/bash-completion/completions/$bin"
		install -Dm0644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" "$bin.fish"
		install -Dm0644 -t "$pkgdir/usr/share/zsh/site-functions/" "_$bin"
		install -Dm0644 -t "$pkgdir/usr/share/elvish/lib/" "$bin.elv"
		popd
		pushd manpages
		find . -name "$bin.1.gz" -exec install -Dm0644 "{}" "$pkgdir/usr/share/man/{}" \;
		popd
	done
}
