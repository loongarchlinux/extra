# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='rails-html-sanitizer'
pkgname="ruby-${_gemname}"
pkgver=1.6.0
pkgrel=1
pkgdesc='HTML sanitization for Rails applications'
arch=('any')
url='https://github.com/rails/rails-html-sanitizer'
license=('MIT')
options=(!emptydirs)
depends=('ruby' 'ruby-loofah')
makedepends=('ruby-bundler' 'ruby-minitest' 'ruby-rake')
checkdepends=('ruby-rails-dom-testing')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0f803050d2abb37a2e06841ce8b10ec6b4f8008f1e95a950e7566eeddbf8af1b01214ff5a8b93121fafc017c1f8cbd4fd8e47aaf55896a1e4fe7a502e0f94303')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed -r 's|~>|>=|g' -i "${_gemname}.gemspec" Gemfile
}

build() {
  cd "${_gemname}-${pkgver}"

  rake build
}

check() {
  cd "${_gemname}-${pkgver}"

  rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" "pkg/${_gemname}-${pkgver}.gem"

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"

  install -D -m644 MIT-LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
