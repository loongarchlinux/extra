# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='rackup'
pkgname="ruby-${_gemname}"
pkgver=2.1.0
pkgrel=2
pkgdesc='A general server command for Rack applications'
arch=('any')
url="https://github.com/rack/${_gemname}"
license=('MIT')
depends=(
  ruby
  ruby-rake
  ruby-webrick
)
checkdepends=(
  ruby-bundler
  ruby-erb
  ruby-minitest
  ruby-minitest-global_expectations
  ruby-rack
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('c470af246388243d2c48f244ccdc72aa8888539f5561fb1352c68ae3946713f14853820348b9a96f9507c316e0f619f111cd3cde077f6ee7f9dadc4bb2f2396d')
b2sums=('56de2ad40a1e4dbd0d7efb6583ceea6836eea4fa37b81c0f670dfe91b1c18e9cc4eed21c9057e027d8eb5b04a0cb148b6f5bd540ec3491e2381976902178aa24')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 license* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
