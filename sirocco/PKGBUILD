# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sirocco
pkgver=2.1.0
pkgrel=3
pkgdesc='C++ library that allows to compute piecewise linear approximations of the path followed by the root of a complex polynomial'
arch=(loong64 x86_64)
url='https://iuma.unizar.es/es/investigacion/software/sirocco'
license=(GPL-3.0-or-later)
depends=(gcc-libs
         glibc
         mpfr)
makedepends=(git)
source=(git+https://github.com/miguelmarco/SIROCCO2#tag=$pkgver)
sha256sums=('c56e200effdf8e7e2b413685aa41e1b6e2424fe13cad3c1ad049cd0d3d431a8d')

prepare() {
  cd SIROCCO2
  autoreconf -vi
}

build() {
  cd SIROCCO2
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # fix overlinking
  make
}

package() {
  cd SIROCCO2
  make DESTDIR="$pkgdir" install
}
