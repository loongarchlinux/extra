# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Ebubekir KARUL <ebubekirkarul@yandex.com>
# Contributor: s1gma,shirokuro

pkgbase=scapy
pkgname=('scapy' 'python-scapy')
pkgver=2.5.0
pkgrel=5
pkgdesc='Powerful interactive packet manipulation program written in Python'
url='http://www.secdev.org/projects/scapy/'
arch=('any')
license=('GPL2')
makedepends=(
  tcpdump
  python-build
  python-installer
  python-setuptools
  python-wheel
  python-sphinx
)
checkdepends=('python-cryptography' 'python-pyx' 'python-matplotlib'
              'graphviz' 'sox')
source=(https://github.com/secdev/scapy/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('5669fdf6a11b6ef7400716690016ffb9dcb6d984d560a63b87c7fad082ff6c3c')
sha512sums=('368f12246047f4b12411e3bfd9b772150d27848b3b1434cd9cc55ca2349320d14f2641d5c219c33231e9f86ced7c491cd52c362a76e2d524f407e8210c57b7bf')

build() {
  cd ${pkgbase}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  # Run only main tests, without network access
  cd ${pkgbase}-${pkgver}/test
  ./run_tests -K netaccess -F
}

package_scapy() {
  depends=('python' 'python-scapy')
  pkgdesc+=' (tools)'
  cd ${pkgbase}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -d "${pkgdir}/usr/share/doc"
  ln -sf /usr/share/doc/python-scapy "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}/usr/lib"
}

package_python-scapy() {
  pkgdesc+=' (library)'
  depends=('tcpdump' 'python')
  # TODO: clenup optdeps
  optdepends=('python-cryptography: WEP, IPsec and SSL/TLS support'
              'python-pyx: psdump() and pdfdump() functions'
              'python-matplotlib: plotting support'
              'graphviz: conversations() method support'
              'sox: for VOIP support'
              'python-prompt_toolkit: expore function')
  cd ${pkgbase}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 doc/scapy/*.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}"{/usr/bin,/usr/share/man}
  rm -r "${pkgdir}"/usr/lib/python*/site-packages/test/
}

# vim: ts=2 sw=2 et:
