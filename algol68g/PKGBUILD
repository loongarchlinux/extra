# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Karolina Lindqvist <karolina.lindqvist@kramnet.se>

pkgname=(algol68g algol68g-doc)
pkgver=3.5.5
pkgrel=1
pkgdesc='Algol 68 compiler and interpreter'
arch=(loong64 x86_64)
_host='https://jmvdveer.home.xs4all.nl'
url="$_host/en.algol-68-genie.html"
license=(GPL-3.0-or-later)
depends=(gsl mpfr plotutils)
optdepends=('postgresql-libs: for postgresql support')
source=("$_host/algol68g-$pkgver.tar.gz"
        "$_host/learning-algol-68-genie.pdf")
sha256sums=('d20657f0882242daf1dc258826a64ae62c35d20170a514dbcc850a3df2cae717'
            '1df5121ca1129f66b1a675a4869c801606fd1a54c6ece08bf758699e32abfb1b')
b2sums=('1ac99c1723e45ff69a72592c0209716409c35ce208e79682ed77a972b08cab49b78b735f1b585640c9a8b5d59853cafb601d8a049bc33bfa42f6a37c93848285'
        '4846266c7ed286694d84a5a02c7bffe2db85d02c1fb35dc0d8672b3032478ebeb5984ffb9f20159dac9283cd54a25055782ad123809a85168f0a9725969a1fd6')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package_algol68g() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" \
    docdir=/usr/share/doc/$pkgname install
}

package_algol68g-doc() {
  install -Dm644 learning-algol-68-genie.pdf \
    "$pkgdir/usr/share/doc/algol68g/learning_algol.pdf"
}
