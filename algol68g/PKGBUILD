# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Karolina Lindqvist <karolina.lindqvist@kramnet.se>

pkgname=(algol68g algol68g-doc)
pkgver=3.5.0
pkgrel=1
pkgdesc='Algol 68 compiler and interpreter'
arch=(loong64 x86_64)
url='https://jmvdveer.home.xs4all.nl/algol.html'
license=(GPL)
depends=(gsl mpfr plotutils)
optdepends=('postgresql-libs: for postgresql support')
source=("https://jmvdveer.home.xs4all.nl/algol68g-$pkgver.tar.gz"
        'https://ftp.tw.freebsd.org/distfiles/a68g-doc.pdf')
sha256sums=('a5ee7221bea6da80a529e65d13ad56ff0c3cf334e709d6f4e16002d40d5af418'
            '064b6761207d937aa704d245fc871a072f5a33ea23a346cb136ec8b2078ea321')
b2sums=('b46f96672cd9ab18cd74347a3fc71cfb686f10dc6cecd04bfa3dff19f57cc510bc08f61a1562097a0178a3ce562c7348cb3eaed97f0847e69de8dd202a9818ac'
        'f8ca710d9a4f611873de41785e70359bd566645268ebb848803d3cd2e9a9f873f8ac423e3e08699998083444b7f6d98df32fdb7a4c6b36692462ee50ddb1e410')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package_algol68g() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" \
    docdir=/usr/share/doc/$pkgname install
}

package_algol68g-doc() {
  install -Dm644 a68g-doc.pdf \
    "$pkgdir/usr/share/doc/algol68g/learning_algol.pdf"
}
