# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Karolina Lindqvist <karolina.lindqvist@kramnet.se>

pkgname=(algol68g algol68g-doc)
pkgver=3.5.4
pkgrel=1
pkgdesc='Algol 68 compiler and interpreter'
arch=(loong64 x86_64)
_host='https://jmvdveer.home.xs4all.nl'
url="$_host/en.algol-68-genie.html"
license=(GPL-3.0-or-later)
depends=(gsl mpfr plotutils)
optdepends=('postgresql-libs: for postgresql support')
source=("$_host/algol68g-$pkgver.tar.gz"
        "$_host/learning-algol-68-genie.pdf")
sha256sums=('0c04a4f92c36e0d1f2e9155c482f232dadced7a845ecffcea3166ce82f311ec6'
            '1df5121ca1129f66b1a675a4869c801606fd1a54c6ece08bf758699e32abfb1b')
b2sums=('b8e2bcc53cf3cc7254da37f1d5655ddc4f79564c51608fea374da25bc828c34056284f2a48e4ada92202f866c38326c1323aa9803bbbb0be22d3a682767c24fa'
        '4846266c7ed286694d84a5a02c7bffe2db85d02c1fb35dc0d8672b3032478ebeb5984ffb9f20159dac9283cd54a25055782ad123809a85168f0a9725969a1fd6')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package_algol68g() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" \
    docdir=/usr/share/doc/$pkgname install
}

package_algol68g-doc() {
  install -Dm644 learning-algol-68-genie.pdf \
    "$pkgdir/usr/share/doc/algol68g/learning_algol.pdf"
}
