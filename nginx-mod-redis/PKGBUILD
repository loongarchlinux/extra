# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-redis
pkgver=0.3.9
pkgrel=15

_modname="${pkgname#nginx-mod-}"

pkgdesc='Nginx module for redis caching'
arch=('loong64' 'x86_64')
makedepends=('nginx' 'nginx-src')
url="https://www.nginx.com/resources/wiki/modules/redis/"
license=('BSD-2-Clause')

source=(
	https://people.freebsd.org/~osa/ngx_http_redis-$pkgver.tar.gz
	nginx-1.23.patch
)
validpgpkeys=(B0F4253373F8F6F510D42178520A9993A1C052F8) # Maxim Dounin <mdounin@mdounin.ru>
sha256sums=('21f87540f0a44b23ffa5df16fb3d788bc90803b255ef14f9c26e3847a6f26f46'
            'd2720b51c5292037d5780e05d47568b93125f10ca205275ca99b300390499637')

prepare() {
	mkdir -p build
	cd build
	ln -sf /usr/src/nginx/auto
	ln -sf /usr/src/nginx/src

	cd "$srcdir/ngx_http_redis-$pkgver"
	patch -p1 -i ../nginx-1.23.patch
}

build() {
	cd build
	/usr/src/nginx/configure \
		--with-ld-opt="$LDFLAGS" \
		--with-compat \
		--add-dynamic-module=../ngx_http_redis-$pkgver
	make modules
}

package() {
	local _nginx_version=$(nginx -v 2>&1)
	_nginx_version=${_nginx_version/* nginx\/}
	depends+=("nginx=${_nginx_version}")

	install -Dm644 "$srcdir"/ngx_http_redis-$pkgver/LICENSE \
	               "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	cd build/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done
}
