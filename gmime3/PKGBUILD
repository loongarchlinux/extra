# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gmime3
pkgver=3.2.15
pkgrel=1
pkgdesc="A C/C++ MIME creation and parser library with support for S/MIME, PGP, and Unix mbox spools"
url="https://github.com/jstedfast/gmime"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  glib2
  glibc
  gpgme
  libidn2
  zlib
)
makedepends=(
  docbook-utils
  git
  glib2-devel
  gobject-introspection
  gtk-doc
  vala
)
provides=(libgmime-3.0.so)
source=("git+https://github.com/jstedfast/gmime#tag=$pkgver")
b2sums=('e14eea1fde01aef5d26ee3c4e775996555793ed1141ad3f40c83eee91192330f00ccf522f1523102a224494178ca9878f64a97592dd0a144b12446820fad6eb9')

prepare() {
  cd gmime
  sed -i '/AM_INIT_AUTOMAKE/s/]/ foreign]/' configure.ac
  autoreconf -fvi
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --enable-crypto
    --enable-gtk-doc
    --disable-static
    --disable-largefile
  )

  cd gmime
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd gmime
  make check
}

package() {
  cd gmime
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et
