# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gmime3
pkgver=3.2.14
pkgrel=1
pkgdesc="A C/C++ MIME creation and parser library with support for S/MIME, PGP, and Unix mbox spools"
url="https://github.com/jstedfast/gmime"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  glib2
  gpgme
  libidn2
  zlib
)
makedepends=(
  docbook-utils
  git
  gobject-introspection
  gtk-doc
  vala
)
provides=(libgmime-3.0.so)
_commit=f119d47e9392bf4a642cec20b5c361f99b2f4b42  # tags/3.2.14^0
source=("git+https://github.com/jstedfast/gmime#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd gmime
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gmime
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --enable-crypto
    --enable-gtk-doc
    --disable-static
    --disable-largefile
  )

  cd gmime
  ./configure "${configure_options[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd gmime
  make check
}

package() {
  cd gmime
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et
