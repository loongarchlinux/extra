# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fcitx5-bamboo
pkgver=1.0.5
pkgrel=1
pkgdesc="Bamboo (Vietnamese Input Method) engine support for Fcitx"
arch=('loong64' 'x86_64')
url="https://github.com/fcitx/fcitx5-bamboo"
license=('GPL')
depends=('fcitx5')
makedepends=('git' 'extra-cmake-modules' 'go')
source=("git+https://github.com/fcitx/fcitx5-bamboo.git#tag=$pkgver?signed"
        "git+https://github.com/BambooEngine/bamboo-core.git")
sha512sums=('4753547584f0bbfd795e79f94cc2cc0f1f1ef1ce2b5ae124e21cd1045294920a2ca939b7e49ce4ec1c4260ac4b73562e7de7c7d0cba6dfb348afda25e436162d'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

prepare() {
  cd fcitx5-bamboo
  git submodule init
  git config submodule.bamboo/bamboo-core.url "$srcdir"/bamboo-core
  git -c protocol.file.allow=always submodule update
}

build(){
  cd $pkgname
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
