# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=m4ri
pkgver=20200125
pkgrel=3
pkgdesc='Algorithms for linear algebra over F_2'
arch=(loong64 x86_64)
url='https://bitbucket.org/malb/m4ri'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         libpng)
makedepends=(git)
source=(git+https://bitbucket.org/malb/m4ri#tag=release-$pkgver)
sha256sums=('3688051e6ee9e81097decf9969bdb7348a497041f5defde2b8abfdb2017a85a5')

prepare() {
  cd $pkgname
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --enable-openmp \
    --enable-thread-safe
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir"
}
