# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Mario Finelli <mario at finel dot li>
# Contributor: Tim Meusel <tim@bastelfreak.de>

_gemname='domain_name'
pkgname="ruby-${_gemname}"
pkgver=0.6.20240107
pkgrel=2
pkgdesc='Domain Name manipulation library for Ruby'
arch=('any')
url='https://github.com/knu/ruby-domain_name'
license=('BSD-2-Clause' 'BSD-3-Clause' 'MPL-2.0')
makedepends=('ruby-rdoc' 'ruby-bundler')
checkdepends=('ruby-rake' 'ruby-minitest' 'ruby-test-unit')
depends=('ruby' 'ruby-unf')
options=(!emptydirs)
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('a189782f3f4ff4bf6ea9d4c7bdd9e7282c7e5ccd31a4ce78b4e4c6652cffeeca71c34220d6cb05b84dac3b57b7739df203347b7ea96d45ef41db62fb5d8dcf47')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # allow latest dependencies
  sed --in-place 's|~>|>=|g' "${_gemname}.gemspec"

  # we built with a tar ball, so `git` won't wwork
  sed --in-place 's|git ls-files|find|' "${_gemname}.gemspec"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rake test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
