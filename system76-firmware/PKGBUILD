# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>
# Contributor: Corey Hinshaw <corey@electrickite.org>

pkgname=system76-firmware
pkgver=1.0.62
pkgrel=1
pkgdesc='System76 Firmware Tool and Daemon'
arch=(loong64 x86_64)
url=https://github.com/pop-os/system76-firmware
license=(GPL3)
depends=(
  ca-certificates
  dbus
  dfu-programmer
  gcc-libs
  glibc
  efibootmgr
  openssl
  xz
)
makedepends=(
  git
  rust
)
provides=(system76-firmware-daemon)
conflicts=(system76-firmware-daemon)
_tag=8264bfda284d035f7c70659fac9afa0272136409
source=(git+https://github.com/pop-os/system76-firmware.git#tag=${_tag})
b2sums=('e1431b81e28b1449c32ad2b6a9bff1777de0b93fe955e1c73e3d933c43373f22f6a48aba254f8af2e998ab42676bf9c8f645702051fe29f8871113e173e35939')

pkgver() {
  cd system76-firmware
  git describe --tags
}

build() {
  cd system76-firmware
  make
}

package() {
  cd system76-firmware
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/etc/systemd "${pkgdir}"/usr/lib/
}

# vim: ts=2 sw=2 et:
