# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pipectl
pkgver=0.5.0
pkgrel=1
pkgdesc="A simple named pipe management utility"
url="https://github.com/Ferdi265/pipectl"
arch=(loong64 x86_64)
license=(GPL-3.0-or-later)
depends=(glibc)
makedepends=(
  cmake
  ninja
  scdoc
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('73b364332b696e7e64741c4afd9357f03aced30f131fc6d846333908f7b584c99105c825389ec016a0d24a03436c945b85998d0998b0a7dc3bdff1b736db9c2f')
b2sums=('eb4e094f09373104396a008c020afdd468a41a05b68436ca2b08757a4bad7b3b4dd30d7199f46e113017846bf3913d2da309bbe1532143bf674aa6cf5b44f9d7')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D INSTALL_DOCUMENTATION=ON
    -G Ninja
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
