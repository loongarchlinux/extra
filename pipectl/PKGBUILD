# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pipectl
pkgver=0.4.2
pkgrel=1
pkgdesc="A simple named pipe management utility"
url="https://github.com/Ferdi265/pipectl"
arch=(loong64 x86_64)
license=(GPL-3.0-or-later)
depends=(glibc)
makedepends=(
  cmake
  ninja
  scdoc
)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('81e4d17e5a5a71b033a78a628a00c758918f428220cc31a1280736729649279278748901d6131a66a3c725d8ee0d9a2a9daaf8df1b9ba236e1cd58067ff89413')
b2sums=('54f25637a910724e08b2553510ee6c946de9b0ae8a0482becd5113aedb99f447863345bfb7b5676b0098a103f957999fc174d530211fb8db8f24ae8fb7798f72')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D INSTALL_DOCUMENTATION=ON
    -G Ninja
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
