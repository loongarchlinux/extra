# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=ailment
pkgname=python-${_pyname}
pkgver=9.2.101
pkgrel=1
pkgdesc='The angr intermediate language'
url='https://github.com/angr/ailment'
license=('custom:BSD2')
arch=('any')
depends=('python' 'python-claripy' 'python-pyvex' 'python-pypcode')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest' 'python-archinfo')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('1ce7045cc918bb7a6e0cab33204533249b5b4c784c8ad3f841ef3981ef1511a8aa3daa1b55a4746c3a9fe668ba2bf53ed4b4296ca51b2f54ba8f14bddd64edcf')
b2sums=('78bcad8099a9fcc83cf9b584bdfba906c68304572bdc6fb79403e3ac3a4d3587a536d089e5d7a16146fa63f43f056517b5b85b5f38799069de76233041d66b9b')

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  # TODO: needed for angr (hard dep)
  # PYTHONPATH=build/lib pytest
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
