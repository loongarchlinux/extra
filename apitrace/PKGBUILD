# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Luca Bennati <lucak3 AT gmail DOT com>
# Contributor: Glaucous <glakke1 at gmail dot com>

pkgname=apitrace
pkgver=11.1
pkgrel=3
pkgdesc="Graphics API Tracing"
arch=('loong64' 'x86_64')
url="https://github.com/apitrace/apitrace"
license=('MIT')
makedepends=('cmake' 'git' 'mesa' 'qt5-base')
depends=('python' 'libgl' 'libprocps')
optdepends=('qt5-base: GUI support')
source=("$pkgname-$pkgver::git+https://github.com/apitrace/apitrace.git#tag=${pkgver}"
        "git+https://github.com/apitrace/gltrim-tests.git"
        "git+https://github.com/google/brotli.git"
        "git+https://github.com/google/googletest.git"
        "git+https://github.com/ianlancetaylor/libbacktrace.git"
        "git+https://github.com/apitrace/libpng.git"
        "git+https://github.com/google/snappy.git"
        "git+https://github.com/madler/zlib.git"
        "git+https://github.com/microsoft/DirectXMath.git")
sha256sums=(SKIP
            SKIP
            SKIP
            SKIP
            SKIP
            SKIP
            SKIP
            SKIP
            SKIP)

prepare() {
  cd apitrace-${pkgver}

  git submodule init
  git config submodule.frametrim/tests.url "$srcdir/gltrim-tests"
  git config submodule.thirdparty/brotli.url "$srcdir/brotli"
  git config submodule.thirdparty/gtest.url "$srcdir/googletest"
  git config submodule.thirdparty/libbacktrace.url "$srcdir/libbacktrace"
  git config submodule.thirdparty/libpng.url "$srcdir/libpng"
  git config submodule.thirdparty/snappy.url "$srcdir/snappy"
  git config submodule.thirdparty/zlib.url "$srcdir/zlib"
  git config submodule.thirdparty/directxmath.url "$srcdir/DirectXMath"
  git -c protocol.file.allow=always submodule update
}

build() {
  cd apitrace-${pkgver}

  cmake . -Bbuild -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_GUI=TRUE 
  make -C build
}

package() {
  cd apitrace-${pkgver}

  make -C build DESTDIR="${pkgdir}/" install

  install -m755 -d "${pkgdir}/usr/share/licenses/apitrace"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/apitrace/"
}
