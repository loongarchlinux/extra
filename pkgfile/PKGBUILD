# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=pkgfile
pkgver=21
pkgrel=2
pkgdesc="a pacman .files metadata explorer"
arch=('loong64' 'x86_64')
url="https://github.com/falconindy/pkgfile"
license=('MIT')
depends=('libarchive' 'curl' 'pcre' 'pacman')
makedepends=('git' 'meson' 'clang' 'systemd')
source=("git+https://github.com/falconindy/pkgfile?signed#tag=v$pkgver"
    pkgfile-use-loong64.patch)
validpgpkeys=('487EACC08557AD082088DABA1EB2638FF56C0C53')  # Dave Reisner
install=pkgfile.install
md5sums=('SKIP'
         'b300e43eaa26d4b46ee37ae21a92d262')

prepare() {
  cd "$pkgname"
  patch -p1 -i $srcdir/pkgfile-use-loong64.patch
}

build() {
  cd "$pkgname"

  arch-meson build
  ninja -v -C build
}

package() {
  cd "$pkgname"

  DESTDIR=$pkgdir ninja -C build install
}

# vim: ft=sh syn=sh
