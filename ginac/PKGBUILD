# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=ginac
pkgver=1.8.7
pkgrel=1
pkgdesc="C++ library for symbolic calculations"
arch=('loong64' 'x86_64')
url='https://www.ginac.de/'
license=('GPL')
depends=('cln')
makedepends=('fig2dev' 'doxygen' 'texlive-bin' 'python')
source=(https://www.ginac.de/$pkgname-$pkgver.tar.bz2)
sha256sums=('71ff4f2d8a00e6f07ce8fee69b76dcc1ebbb727be6760b587c1fbb5ccf7b61ea')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --libexecdir=/usr/lib/ginac --disable-static
  make
}

check() {
  cd "${srcdir}"/$pkgname-$pkgver

  make check
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
