# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Contributor: acxz <akashpatel at yahoo dot com>
# Contributor: JP-Ellis <josh@jpellis.me>

pkgname=miopen-hip
pkgver=6.0.2
pkgrel=1
pkgdesc="AMD's Machine Intelligence Library (HIP backend)"
arch=('loong64' 'x86_64')
url="https://rocm.docs.amd.com/projects/MIOpen/en/latest/index.html"
license=('MIT')
depends=('rocm-core' 'glibc' 'gcc-libs' 'rocblas' 'rocm-clang-ocl' 'hip'
         'comgr' 'bzip2' 'composable-kernel' 'roctracer' 'sqlite')
makedepends=('cmake' 'ninja' 'rocm-cmake' 'boost' 'lbzip2'
             'nlohmann-json' 'eigen' 'half' 'functional-plus' 'frugally-deep')
provides=('miopen')
conflicts=('miopen')
_git='https://github.com/ROCm/MIOpen'
_mlir='https://github.com/ROCm/rocMLIR'
source=("$pkgname-$pkgver.tar.gz::$_git/archive/rocm-$pkgver.tar.gz"
        "$pkgname-rocmlir-$pkgver.tar.gz::$_mlir/archive/rocm-$pkgver.tar.gz")
sha256sums=('e6f671bd6af59f7470f42cda2ff9e77441d8f6c2105772bbf855d31da1085ffa'
            '6ed039e8045169bb64c10fb063c2e1753b8d52d6d56c60e001c929082be1f20b')
options=(!lto)
_dirname="$(basename "$_git")-$(basename "${source[0]}" .tar.gz)"
_mlirname="$(basename "$_mlir")-$(basename "${source[1]}" .tar.gz)"

prepare() {
    # Disable tests as they require an AMD GPU at build time
    sed -i '/add_subdirectory(test)/d' "$_dirname/CMakeLists.txt"
}

build() {
  export CC=/opt/rocm/llvm/bin/clang
  export CXX=/opt/rocm/llvm/bin/clang++
  # -fcf-protection is not supported by HIP, see
  # https://rocm.docs.amd.com/en/latest/reference/rocmcc.html#support-status-of-other-clang-options
  # Set ROCm test chipset to Vega in order to pass the configuration step.
  local llvm_args=(
    -Wno-dev
    -G Ninja
    -B build-mlir
    -S "$_mlirname"
    -D CMAKE_CXX_FLAGS="${CXXFLAGS} -fcf-protection=none"
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=Release
    -D ROCM_TEST_CHIPSET=gfx900
    -D BUILD_FAT_LIBROCKCOMPILER=ON
  )
  cmake "${llvm_args[@]}"
  cmake --build build-mlir
  DESTDIR="$srcdir/deps" cmake --install build-mlir

  # define NDEBUG to silence internal logging (mimics cmake release build)
  # https://rocmsoftwareplatform.github.io/MIOpen/doc/html/DebugAndLogging.html
  local cmake_args=(
     -Wno-dev
    -G Ninja
    -B build
    -S "$_dirname"
    -D CMAKE_CXX_FLAGS="${CXXFLAGS} -fcf-protection=none -DNDEBUG"
    -D CMAKE_INSTALL_PREFIX=/opt/rocm
    -D CMAKE_BUILD_TYPE=None
    -D MIOPEN_BACKEND=HIP
    -D HALF_INCLUDE_DIR=/usr/include/half
    -D CMAKE_PREFIX_PATH="$srcdir/deps/usr/lib/cmake"
  )
  cmake "${cmake_args[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$_dirname/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
