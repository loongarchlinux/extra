# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-notifications
pkgver=8.0.0
pkgrel=1
pkgdesc='Gtk Notifications Server'
arch=(loong64 x86_64)
url=https://github.com/elementary/notifications
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libcanberra.so
  libcanberra-gtk3.so
  libgranite.so
  libhandy-1.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=42062d5be700b93ecf2e55cb23c48a660614a189
source=(pantheon-notifications::git+https://github.com/elementary/notifications.git#tag=${_tag})
b2sums=('6116ba24fd9a50971e5bccc7f13556eb732898c679335ee412bacd781503023599becc7fa065321a9f3c7e08c9baaf76799050350ed52307da2ff535462c4766')

pkgver() {
  cd pantheon-notifications
  git describe --tags
}

build() {
  arch-meson pantheon-notifications build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
