# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-reader
pkgver=5.10.29
pkgrel=8
pkgdesc='A simple PDF reader, supporting bookmarks, highlights and annotations'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-reader"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkcore' 'dtkgui' 'dtkwidget' 'qt5-base' 'qt5-webengine'
         'gcc-libs' 'lcms2' 'openjpeg2' 'freetype2' 'djvulibre' 'libchardet')
makedepends=('cmake' 'gtest' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-reader/archive/$pkgver/$pkgname-$pkgver.tar.gz"
         openjpeg-2.5.patch
         fix-build.patch)
sha512sums=('fd3c4fabd4195de0fce3c16a2efd3c44f2a43074b1ac97738cccada552c456779b61b503f1c2ca4c0fb7be7de4f7366aafe1a70523214ab4254f061e59997a30'
            '4ff8c063cc464a830cee8a5474329127c75f4298819f3384cb01d26cbcff38663eb89ff5ca69e1e800e28e509901c3671898c4dbda4b8426551b56c074af639b'
            '30801a0db2113195a59bd0a065d72f08f7929994c14b10805ebfa9f8f18a9bb86671c447aeba4fbb0d2199db4152cfe695e1541936e85a9e4bf765f8a14916b3')

prepare() {
  cd deepin-reader-$pkgver
  patch -p1 -i ../openjpeg-2.5.patch # Fix build with openjpeg 2.5
  patch -p1 -i ../fix-build.patch
}

build() {
  cd deepin-reader-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-reader-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
