# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: Felix Golatofski <contact@xdfr.de>
# Contributor: Bumsik Kim <k.bumsik@gmail.com>

pkgname=nushell
pkgver=0.92.2
pkgrel=1
pkgdesc='A new type of shell'
arch=('loong64' 'x86_64')
url='https://www.nushell.sh'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'libcrypto.so'
  'libssl.so'
  'zlib'
)
makedepends=('cargo' 'git')
install=nushell.install
source=("git+https://github.com/nushell/nushell.git#tag=$pkgver")
sha256sums=('60e8a6338f7603dc880649fbf7ec8f6835b7943371d0d2c210e89b4fc1374dff')

prepare() {
  cd "$pkgname"

  # Back off from bogus MSRV, the bug everybody is scared of "just in case" is Windows only
  sed -i -e '/^rust-version/s/1.77.2/1.77.1/' Cargo.toml 

  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"

  CFLAGS+=" -ffat-lto-objects"

  cargo build --release --frozen --workspace --features=extra,dataframe
}

check() {
  cd "$pkgname"

  cargo test --frozen --workspace --features=extra,dataframe
}

package() {
  cd "$pkgname"

  find target/release \
    -maxdepth 1 \
    -executable \
    -type f \
    -name "nu*" \
    -exec install -vDm755 -t "$pkgdir/usr/bin" "{}" +

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
