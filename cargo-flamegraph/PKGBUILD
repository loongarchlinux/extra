# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-flamegraph
pkgver=0.6.5
pkgrel=3
pkgdesc='Easy flamegraphs for Rust projects and everything else'
url="https://github.com/flamegraph-rs/flamegraph"
depends=(
  'cargo'
  'gcc-libs'
  'glibc'
  'perf'
)
arch=('loong64' 'x86_64')
license=('MIT' 'Apache-2.0')
source=(https://github.com/flamegraph-rs/flamegraph/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('d4c26f2fbf3c3ec869a8d992a40a6f16a89ded018078771f1d14f8cef4a76e7a')
b2sums=('5dd7edbd109f484cc8e927c4b98fef11f26e0ed586d4cdfe3aa34ebaa762775fa3ba247c983fac03cc9a560a4acb5e10210eb0343b83a2e5f5e75d8003bfe161')

build() {
  cd "flamegraph-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "flamegraph-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "flamegraph-${pkgver}"
  install -Dm755 -t "${pkgdir}/usr/bin" \
    target/release/cargo-flamegraph \
    target/release/flamegraph
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
