# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kreed <kreed@kreed.org>

pkgname=doge
pkgver=3.7.0
pkgrel=2
pkgdesc="doge meme motd script"
url="https://github.com/thiderman/doge"
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Olivia5k/doge/archive/$pkgver.tar.gz")
sha256sums=('043364225e3a504e7d55ad2d3d03a73ab55a81ae3dc9344313ff6e34ead72ff8')

build() {
  cd doge-$pkgver
  python -m build -nw
}

package() {
  cd doge-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
