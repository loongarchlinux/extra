# Maintainer: David Runge <dvzrv@archlinux.org>

_name=hosc
pkgname=haskell-hosc
pkgver=0.20
pkgrel=35
pkgdesc="Haskell Open Sound Control"
arch=(loong64 x86_64)
url="https://github.com/rd--/hosc"
license=(GPL-3.0-only)
depends=(
  bash
  ghc-libs
  glibc
  gmp
  haskell-blaze-builder
  haskell-data-binary-ieee754
  haskell-network
)
makedepends=(ghc)
source=(https://hackage.haskell.org/packages/archive/$_name/$pkgver/$_name-$pkgver.tar.gz)
sha512sums=('f04ea2cbb8a946c6aef246c4785518dd32f134c6d96643487402864d96a468244917091d36e8bf220ca64c5aab019ee859291354c3d8e50860d4169e9a6d0081')

build() {
  local setup_configure_options=(
    -O
    --datasubdir=$pkgname
    --disable-library-vanilla
    --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname
    --dynlibdir=/usr/lib
    --enable-executable-dynamic
    --enable-shared
    --enable-tests
   
    --libsubdir=\$compiler/site-local/\$pkgid
    --prefix=/usr
  )
  cd $_name-$pkgver
  runhaskell Setup configure "${setup_configure_options[@]}"
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_name-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_name-$pkgver
  runhaskell Setup copy --destdir="$pkgdir"
  install -vDm 744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -vDm 744 unregister.sh "$pkgdir/usr/share/haskell/unregister/$pkgname.sh"
}
