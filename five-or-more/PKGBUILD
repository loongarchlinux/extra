# # Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=five-or-more
pkgver=3.32.3
pkgrel=2
pkgdesc="Remove colored balls from the board by forming lines"
url="https://wiki.gnome.org/Apps/Five%20or%20more"
arch=(loong64 x86_64)
license=(GPL)
depends=(gtk3 librsvg libgnome-games-support)
makedepends=(gobject-introspection yelp-tools appstream-glib git meson vala)
groups=(gnome-extra)
_commit=3cf5c431460ff812bc4af59e41772aa7dbe4e68f  # tags/3.32.3^0
source=("git+https://gitlab.gnome.org/GNOME/five-or-more.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
