# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=socat
pkgver=1.8.0.0
pkgrel=1
pkgdesc='Multipurpose relay'
url='http://www.dest-unreach.org/socat/'
license=('GPL-2.0-only')
arch=('loong64' 'x86_64')
depends=(
  'glibc'
  'openssl' 'libssl.so'
  'readline' 'libreadline.so'
)
checkdepends=(
  'bash'
  'net-tools'
  'procps-ng'
)
source=("http://www.dest-unreach.org/socat/download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('6010f4f311e5ebe0e63c77f78613d264253680006ac8979f52b0711a9a231e82')
b2sums=('816f404283425900e0d703c9458cc1c1cf1edbcc343e0f3d9039ec39ff588e74796831e73c383daf807d59f105b81f743fe063e3c8376a87f7526282dc0ff58c')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \

  make
}

check() {
  cd "${pkgname}-${pkgver}"
  # TODO: one of the tests always fails, but lets keep
  # it for manual sanity check
  make test||true
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
