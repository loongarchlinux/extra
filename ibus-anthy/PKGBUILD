# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-anthy
pkgver=1.5.15
pkgrel=1
pkgdesc='Japanese input method Anthy IMEngine for IBus Framework'
arch=('loong64' 'x86_64')
license=('LGPL')
url='https://github.com/ibus/ibus-anthy'
depends=('ibus' 'anthy' 'python' 'gtk-update-icon-cache' 'python-gobject')
makedepends=('intltool' 'gobject-introspection')
source=("https://github.com/ibus/ibus-anthy/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0acebb1b397f98d156ba9d6465bdb1025fb23a6b05b72c9b471827b45e22af55468b7674469f348712682446641c98d68f3d2aa339d2a1997a2df8c2bac45a7c')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --libexec=/usr/lib/ibus
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
