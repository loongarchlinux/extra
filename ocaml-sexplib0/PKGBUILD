# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Peukert <dan.peukert@gmail.com>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

_projectname='sexplib0'
pkgname="ocaml-$_projectname"
pkgver=0.17.0
pkgrel=1
pkgdesc='Library containing the definition of S-expressions and some base converters'
arch=('loong64' 'x86_64')
url="https://github.com/janestreet/$_projectname"
license=('MIT')
depends=('ocaml')
makedepends=('dune')
options=('!strip' '!debug')
source=("$pkgname-$pkgver-$pkgrel.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('5b0910b5dab8ec63633be5dbf92a3e4863d415d803cad9dddf99dba43ce7498b')

_sourcedirectory="$_projectname-$pkgver"

build() {
	cd "$srcdir/$_sourcedirectory/"
	dune build --release --verbose
}

package() {
	cd "$srcdir/$_sourcedirectory/"
	DESTDIR="$pkgdir" dune install --prefix '/usr' --libdir '/usr/lib/ocaml' --release --verbose

	install -dm755 "$pkgdir/usr/share/doc/$pkgname"
	mv "$pkgdir/usr/doc/$_projectname/"* "$pkgdir/usr/share/doc/$pkgname/"
	rm -r "$pkgdir/usr/doc/"

	install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
	ln -sf "/usr/share/doc/$pkgname/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
