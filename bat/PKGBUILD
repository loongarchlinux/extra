# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=bat
pkgver=0.24.0
pkgrel=2
pkgdesc='Cat clone with syntax highlighting and git integration'
arch=(loong64 x86_64)
url='https://github.com/sharkdp/bat'
license=('Apache-2.0 OR MIT')
depends=(gcc-libs glibc libgit2 oniguruma zlib)
makedepends=(clang cmake git rust)
source=("git+$url#tag=v$pkgver")
b2sums=('ef560226cb89deb70bf563fa9ac8106c1832cb7c25b7577f25dfeb9d6cfd0e8f001bb7b3042e050f32cf05c226dd818aab7c25ea203d310a8b2f53d198f1c6f6')

prepare() {
  cargo fetch --locked --manifest-path $pkgname/Cargo.toml
}

build() {
  export CFLAGS+=' -ffat-lto-objects -w'
  cargo build --locked --manifest-path $pkgname/Cargo.toml --release
}

check() {
  cargo test --locked --manifest-path $pkgname/Cargo.toml
}

package() {
  install -Dm755 $pkgname/target/release/$pkgname "$pkgdir/usr/bin/$pkgname"

  # Package licenses
  install -Dm644 $pkgname/LICENSE-APACHE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm644 $pkgname/LICENSE-MIT \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"

  cd $pkgname/target/release/build

  # Find and package the man page (because cargo --out-dir is too new)
  find . -name bat.1 -type f -exec install -Dm644 {} \
    "$pkgdir/usr/share/man/man1/bat.1" \;

  # Find and package the bash completion file
  find . -name bat.bash -type f -exec install -Dm644 {} \
    "$pkgdir/usr/share/bash-completion/completions/bat" \;

  # Find and package the zsh completion file (not in zsh-completions yet)
  find . -name bat.zsh -type f -exec install -Dm644 {} \
    "$pkgdir/usr/share/zsh/site-functions/_bat" \;

  # Find and package the fish completion file
  find . -name bat.fish -type f -exec install -Dm644 {} \
    "$pkgdir/usr/share/fish/vendor_completions.d/bat.fish" \;
}
