# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-usage
pkgver=46.0
pkgrel=1
pkgdesc="View information about use of system resources"
url="https://wiki.gnome.org/Apps/Usage"
arch=(loong64 x86_64)
license=(GPL-3.0-or-later)
depends=(
  glib2
  gtk4
  libadwaita
  libgee
  libgtop
  tracker3
  xdg-user-dirs
)
makedepends=(
  accountsservice
  gamemode
  git
  meson
  vala
)
optdepends=(
  'accountsservice: show user tags for processes'
  'gamemode: show processes requesting game mode'
)
_commit=9ee8603fb8d6a532d0abdff1aba16a00d38d2aba  # tags/46.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-usage.git#commit=$_commit")
b2sums=('999752b4b4fa091431ce00ed38ebb398dfa09e377dc96259d890a7ca5548d988fa3b159b97001232239e451ce6143057d5f58df69c2361b979034fcc9e66090b')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/+1//;s/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
