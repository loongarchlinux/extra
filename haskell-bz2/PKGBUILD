# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=bz2
pkgname=haskell-bz2
pkgver=1.0.1.1
pkgrel=2
pkgdesc="Bindings to libbz2"
url="https://hub.darcs.net/vmchale/bz2"
license=('BSD')
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'bzip2')
makedepends=('ghc' 'uusi' 'c2hs' 'haskell-tasty' 'haskell-tasty-golden' 'haskell-tasty-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('ba4222f1ab2a718ecc3f0d84111561f37436a904d4d4ec6f750403c24cb5e55855dc8092eb06efdc831ab4bde8ecadd7a3b9a5af3d1409a8fc97588f8413555d')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
    uusi -d bzip2-clib

    # Use system libbz2
    sed -i -e '/hs-source-dirs/i \    extra-libraries: bz2' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
