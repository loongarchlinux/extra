# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-sideload
pkgver=6.2.2
pkgrel=1
pkgdesc='Sideload Flatpaks on elementary OS'
arch=(loong64 x86_64)
url=https://github.com/elementary/sideload
license=(GPL3)
depends=(
  flatpak
  gtk4
  libgranite-7.so
  libxml2
)
makedepends=(
  git
  intltool
  meson
  vala
)
groups=(pantheon)
_tag=761b7851ff7bc075d84a86e095ee33cd6d036c35
source=(pantheon-sideload::git+https://github.com/elementary/sideload.git#tag=${_tag})
sha256sums=('76da4293750055cb8b68063db7746ef41630d6d6741e6d91c8a081b05ae0a509')

pkgver() {
  cd pantheon-sideload
  git describe --tags
}

build() {
  arch-meson pantheon-sideload build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
