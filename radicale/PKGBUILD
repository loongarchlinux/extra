# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Moritz Lipp <mlq@pwmt.org>
# Contributor: fordprefect <fordprefect@dukun.de>
# Contributor: Thor77 <thor77@thor77.org>

pkgname=radicale
_name=Radicale
pkgver=3.1.9
pkgrel=2
pkgdesc="Simple calendar (CalDAV) and contact (CardDAV) server"
arch=(any)
url="https://radicale.org/"
_url="https://github.com/Kozea/radicale"
license=(GPL-3.0-or-later)
depends=(
  python
  python-bcrypt
  python-dateutil
  python-defusedxml
  python-passlib
  python-vobject
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest7
  python-typeguard
  python-waitress
)
backup=(
  'etc/radicale/config'
  'etc/radicale/rights'
  'etc/radicale/users'
)
install=$pkgname.install
source=(
  $pkgname-$pkgver.tar.gz::$_url/archive/v$pkgver.tar.gz
  $pkgname-sysusers.conf
  $pkgname-tmpfiles.conf
  $pkgname.service
)
sha512sums=('f4d0b26149c331b4bc02f01bb637cea1d29e1d563a4133c4293ec13990f1553988181582c2e726ce33b563403a14e9ec863403f86e66b13fd8cfd62cf74439e4'
            '56dffb66e018cfbf158dc5d8fe638b3cb31229945f659aae5623f219bcd1d68ddc375f1633fa8e857a9b2f50c9e05a06efce165370137d6e116a4f187466637f'
            '9d0dd88e4a34e9f97abda1785698e4b2a5e8202063deeb91b84e13c05e00b07e45b8d4d9eca09b9241b1138bbbfdc999dba0135c18f5bc0c08d65b0cd83b367b'
            '9c7be18439b508ce34f12e77ce191877c04cd1654b474e685008afec52defbd4f5b4972557c92759926b9ab9c3c5146f658697c64fcb612963e4c96f5c0226bd')
b2sums=('6d74eabd7e09b1804a6a3c556a9f629fb72c4685fdf5d96d0d53ffaf5db712968b054678be866002ab4ceafaf1d3d8ad3f52aae7a9a4ecabbc129149c96ab9b6'
        'b3af60e144ef857e42ec672e806e9600265ab7d2ea4a75011de9ab56918a008437afdacb301df210b54424fb7ff1e9a332831c67b2e58fd6bc0a0aa1eebe8909'
        '41916d62f5e3f1060bd21db0722abe837754a4cb915af218c904dafac4b06794f8fde2e34486fb7392777b4738502f3df4c1390b835050045337585b064e23bb'
        '1e6ffca8fd1d326db390f87d4d0db6622bff081481aacf3f4e81fcbc46098f21f50540ab9c66f39c2ec8937fd7f92a1ef0a9d6fd729b0a676099c2c6e5b3d74a')

prepare() {
  cd $_name-$pkgver
  touch "users"
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    --deselect radicale/tests/test_server.py::TestBaseServerRequests::test_root
    --deselect radicale/tests/test_server.py::TestBaseServerRequests::test_ssl
    --deselect radicale/tests/test_server.py::TestBaseServerRequests::test_command_line_interface
    --deselect radicale/tests/test_server.py::TestBaseServerRequests::test_command_line_interface_with_bool_options
    --deselect radicale/tests/test_server.py::TestBaseServerRequests::test_wsgi_server
    --deselect radicale/tests/test_web.py::TestBaseWebRequests::test_internal
    --deselect radicale/tests/test_web.py::TestBaseWebRequests::test_none
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  # config
  install -vDm 644 {config,rights,users} -t "$pkgdir/etc/$pkgname/"
  # wsgi
  install -vDm 644 $pkgname.wsgi -t "$pkgdir/usr/share/$pkgname/"
  # systemd service
  install -vDm 644 ../$pkgname.service -t "$pkgdir/usr/lib/systemd/system/"
  # sysusers.d
  install -vDm 644 ../$pkgname-sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  # tmpfiles.d
  install -vDm 644 ../$pkgname-tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  # docs
  install -vDm 644 {CHANGELOG,DOCUMENTATION,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
