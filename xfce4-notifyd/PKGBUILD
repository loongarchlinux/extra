# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-notifyd
pkgver=0.9.4
pkgrel=1
pkgdesc="Notification daemon for the Xfce desktop"
arch=('loong64' 'x86_64')
url="https://docs.xfce.org/apps/notifyd/start"
license=('GPL2')
groups=('xfce4-goodies')
depends=('libxfce4ui' 'libxfce4util' 'libnotify' 'xfconf' 'sqlite' 'libcanberra'
         'hicolor-icon-theme')
makedepends=('intltool' 'python' 'xfce4-panel' 'xfce4-dev-tools')
provides=('notification-daemon')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('ae6c128c055c44bd07202f73ae69ad833c5e4754f3530696965136e4d9ea7818')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
