# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Raphael Isemann <teemperor at gmail dot com>

pkgname=cern-vdt
_pkgname=vdt
pkgver=0.4.4
pkgrel=2
pkgdesc='A vectorised math library from CERN'
arch=('loong64' 'x86_64')
url='https://github.com/dpiparo/vdt'
license=('LGPL-3.0-or-later')
depends=('glibc')
makedepends=('cmake' 'python')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dpiparo/${_pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('8b1664b45ec82042152f89d171dd962aea9bb35ac53c8eebb35df1cb9c34e498')

prepare() {
  # remove scripts incompatible with Python 3.12: https://github.com/dpiparo/vdt/issues/18
  rm -v $_pkgname-$pkgver/src/vdt.py
  rm -v $_pkgname-$pkgver/progs/{{vdtNumpytest,vdtNumpyTime,testExp}.py,{vdtTiming,vdtTest}.ipynb}
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $_pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
