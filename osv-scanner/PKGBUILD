# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=osv-scanner
pkgver=1.7.3
pkgrel=1
pkgdesc="Vulnerability scanner written in Go which uses the data provided by https://osv.dev"
url='https://github.com/google/osv-scanner'
arch=('loong64' 'x86_64')
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
source=(https://github.com/google/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('15480c07c7e69b2d54f9c3975e0ce8ae17b85f7f0a291e50d8fc1a0ff9cb2b17')
b2sums=('0c06c329cba2dc332fa8d042f25df0d565e815e0bb2d6a731ec29706516f2923e984663e2e751d35f556626052228455ac7c1e11489bccffc3d4fe9625ca7e8c')

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  go build \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags '-compressdwarf=false -linkmode=external' \
    ./cmd/osv-scanner
}

check() {
  cd ${pkgname}-${pkgver}
  ./osv-scanner --help
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    osv-scanner
}

# vim: ts=2 sw=2 et:
