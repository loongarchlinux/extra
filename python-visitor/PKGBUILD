# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-visitor
pkgver=0.1.3
pkgrel=11
pkgdesc="A tiny pythonic visitor implementation"
url="https://github.com/mbr/visitor"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mbr/visitor/archive/$pkgver.tar.gz")
sha512sums=('414c77df8efcc22df5ee8073e02b8c5c28563954ef9b9a869c4ea3f7f1268a97929c1fe29142ba41e3a5acafea52599f6bf073aacd654643d7d8380cd4f41fbc')

build() {
  cd visitor-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd visitor-$pkgver
  python -m pytest -v
}

package() {
  cd visitor-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
