# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgbase=orbiton
pkgname=(orbiton orbiton-gtk3 orbiton-nano)
pkgver=2.65.12
pkgrel=1
pkgdesc='Fast and configuration-free text editor and IDE limited by VT100'
arch=(loong64 x86_64)
url='https://roboticoverlords.org/orbiton/'
license=(BSD-3-Clause)
makedepends=(git go vte3)
options=(!lto)
source=("git+https://github.com/xyproto/orbiton#commit=ebe984d9d8bda1aa806199de3d4f79197ee72b13") # tag: v2.65.12
b2sums=('57ed2beb07db50dcc7e1d95a6a814aef29d84959270455dfcf7e4fc56b8ddd9c29c26d4b4d6ea12f7639fe8f7aabafe13e56aa550506ee1adf6b495620c7ea15')

prepare() {
  # Stop the install target from rebuilding executables (will be fixed in the next release)
  sed -i 's/install: o/install:/' $pkgbase/Makefile
}

build() {
  # Adjust the LDFLAGS to work together with go build
  export LDFLAGS="$(echo $LDFLAGS | sed 's/ -Wl//g;s/,--sort-common//')"

  make -C $pkgbase o gui
}

package_orbiton() {
  # TODO: consider moving one or more of these from AUR:
  # 'cxx: for compiling C and C++'
  # 'elm-bin: for compiling Elm'
  # 'guessica: for updating PKGBUILD files'
  # 'google-java-format: for formatting Java'
  # 'lua-format: for formatting Lua'
  # 'hare: for compiling Hare'
  # 'ktlint: for formatting Kotlin'
  # 'odin: for compiling Odin'
  # 'scala: for compiling Scala'
  # 'vlang: for compiling and formatting V'
  optdepends=('asciidoctor: for writing man pages'
              'agda: for compiling Agda'
              'astyle: for formatting C#'
              'clang: for formatting C++ code with clang-format'
              'clojure: for compiling Clojure'
              'crystal: for compiling Crystal'
              'dart: for compiling and formatting Dart'
              'fpc: for compiling Object Pascal'
              'gdb: for debugging C'
              'gcc-d: for compiling D'
              'ghc: for compiling Haskell'
              'godot: for using GDScript'
              'haskell-brittany: for formatting Haskell'
              'jad: for decompiling .class files on the fly'
              'java-environment: for compiling Java'
              'just: for editing justfiles'
              'kotlin: for compiling Kotlin'
              'lua: for compiling Lua'
              'mlton: for compiling Standard ML'
              'mono: for compiling C#'
              'nasm: for compiling Assembly'
              'ocaml: for compiling and formatting OCaml'
              'perl-tidy: for formatting Perl'
              'pandoc: for exporting Markdown to PDF'
              'prettier: for formatting JavaScript, TypeScript and CSS'
              'python-black: for formatting Python'
              'rustup: for compiling and formatting Rust'
              'scdoc: for writing man pages'
              'shfmt: for formattig Shell scripts'
              'tidy: for formatting HTML'
              'yasm: for compiling Assembly'
              'zig: for compiling and formatting Zig')
  DESTDIR="$pkgdir" make -C $pkgbase install symlinks easteregg license
}

package_orbiton-gtk3() {
  pkgdesc='Fast and configuration-free text editor and IDE limited by VT100 (GTK3/VTE3)'
  depends=(orbiton vte3)
  DESTDIR="$pkgdir" PROJECT=$pkgname make -C $pkgbase gui-install gui-symlinks license
}

package_orbiton-nano() {
  pkgdesc='Fast and configuration-free text editor and IDE limited by VT100 (Nano/Pico Mode)'
  depends=(orbiton)
  conflicts=(nano)
  provides=(nano)
  install -d "$pkgdir/usr/bin"
  DESTDIR="$pkgdir" PROJECT=$pkgname make -C $pkgbase nano-symlink license
}
