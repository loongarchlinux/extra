# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=aardvark-dns
_commit=e925a2818ba9c55a50d195caf21897c375e162f7  # refs/tags/v1.10.0
pkgver=1.10.0
pkgrel=2
pkgdesc="Authoritative dns server for A/AAAA container records"
arch=(loong64 x86_64)
url="https://github.com/containers/aardvark-dns"
license=(Apache-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cargo
  git
  libgit2
)
source=(git+$url#tag=$_commit
        https://github.com/kyrias/aardvark-dns/commit/b7c40f89712979ee5c2a47d2455a653f8700c07e.patch)
sha256sums=('c2c58e1be2016b7dd9bc14f5201e4bdcc5d6fbd170b3fb600c5c50f94ea051b6'
            '32e7837158946424adf356a100e8c768357472831604b79ff89f5a51172b4e5e')
# NOTE: pinning commit until upstream clarifies commitment to chain of trust:
# https://github.com/containers/aardvark-dns/issues/83
# validpgpkeys=('74FE091D25519980B2D84447160386BECB6F0643')  # Brent Baude <bbaude@redhat.com>

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/v//g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i "$srcdir"/b7c40f89712979ee5c2a47d2455a653f8700c07e.patch
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  cd $pkgname
  cargo build --frozen --release --all-features
}

check() {
  export RUSTUP_TOOLCHAIN=stable

  cd $pkgname
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/lib/podman/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
