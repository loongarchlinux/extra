# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=neatvnc
pkgver=0.8.1
pkgrel=2
pkgdesc='Fast and neat VNC server library'
arch=(loong64 x86_64)
url=https://github.com/any1/neatvnc
license=(custom:ISC)
depends=(
  glibc
  gnutls
  libaml.so
  libavcodec.so
  libdrm
  libpixman-1.so
  libturbojpeg.so
  mesa
  zlib
)
makedepends=(
  git
  meson
  ninja
)
provides=(libneatvnc.so)
_tag=07081567ab21a2b099ceb41ae8cab872a31cbb9a
source=(git+https://github.com/any1/neatvnc.git#tag=${_tag})
b2sums=('e5739d474e1d1610eed391625776a02c2bd1de93bde13bb88962bb8dec4e38aa8ff3db70065d0d29966dedbab10a2959fb83f631ee216650d2e199414a67c5c7')

pkgver() {
  cd neatvnc
  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson neatvnc build \
    -Djpeg=enabled \
    -Dtls=enabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 neatvnc/COPYING -t "${pkgdir}"/usr/share/licenses/neatvnc
}

# vim: ts=2 sw=2 et:
