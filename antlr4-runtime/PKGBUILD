# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=antlr4-runtime
pkgver=4.13.1
pkgrel=1
pkgdesc='Antlr C++ runtime'
url='https://www.antlr.org/'
arch=('loong64' 'x86_64')
depends=('gcc-libs')
makedepends=('git' 'cmake' 'python')
license=('BSD')
source=("https://www.antlr.org/download/antlr4-cpp-runtime-${pkgver}-source.zip")
sha256sums=('d350e09917a633b738c68e1d6dc7d7710e91f4d6543e154a78bb964cfd8eb4de')
# FS#72726
options=(staticlibs)

build() {
  export CFLAGS+=' -ffat-lto-objects'
  export CXXFLAGS+=' -ffat-lto-objects'

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_STANDARD=17 \
    -DANTLR4_INSTALL=ON \
    -DINSTALL_GTEST=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}/" install
  install -Dm644 "${srcdir}"/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}"/LICENSE
}
