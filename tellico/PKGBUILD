# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=tellico
pkgver=3.5.2
pkgrel=2
pkgdesc="A collection manager for KDE"
arch=('loong64' 'x86_64')
url="https://tellico-project.org/"
license=('GPL')
depends=('exempi' 'hicolor-icon-theme' 'kio5' 'libksane' 'libkcddb5'
         'libcdio' 'kfilemetadata5' 'knewstuff5' 'kxmlgui5' 'kitemmodels5'
         'perl-text-bibtex' 'python' 'solid5' 'yaz' 'taglib' 'poppler-qt5' 'qt5-base' 'qt5-charts' 'qt5-webengine')
makedepends=('cmake' 'extra-cmake-modules' 'kdoctools5')
changelog=$pkgname.changelog
source=(https://tellico-project.org/files/$pkgname-$pkgver.tar.xz)
sha256sums=('02c2b8e002d7ba23dafc8a2e7e26a4fc4bfb1395c414543502e56e29ec3e5353')

build() {
  cmake -B build -S $pkgname-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
