# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>

pkgname=castget
pkgver=2.0.1
pkgrel=2
pkgdesc="Command-line based RSS enclosure downloader primarily intended for downloading of podcasts"
arch=('loong64' 'x86_64')
url="https://johndal.com/castget/"
license=('GPL-2.0-or-later' 'LGPL-2.1-or-later')
depends=('glib2' 'libxml2' 'curl' 'id3lib')
changelog=$pkgname.changelog
source=(
  "https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.bz2"
  "https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.bz2.sig"
  "fix-build.patch"
)
sha256sums=(
  '438b5f7ec7e31a45ed3756630fe447f42015acda53ec09202f48628726b5e875'
  'SKIP'
  '48cce3303601ee28e06bdf6b55b7c5023405cacd7c913f8bc6616b3277395b80'
)
validpgpkeys=('561BCBC3DF8A53503A62416EA7DE43E1D881FF60') # Marius Larsen JÃ¸hndal <mariuslj@ifi.uio.no>

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

  # Naively replace deprecated curl option CURLOPT_PROGRESSFUNCTION with
  # CURLOPT_XFERINFOFUNCTION and add a missing include.
  patch -Np1 -i "$srcdir/fix-build.patch"
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}"/$pkgname-$pkgver

  make check
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
