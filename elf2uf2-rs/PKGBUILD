# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=elf2uf2-rs
pkgver=2.0.0
pkgrel=1
pkgdesc="Convert ELF files to UF2 for USB Flashing Bootloaders"
url='https://github.com/JoNil/elf2uf2-rs'
arch=('loong64' 'x86_64')
license=('0BSD')
depends=(
  'gcc-libs'
  'glibc'
  'libudev.so'
)
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::https://crates.io/api/v1/crates/${pkgname}/${pkgver}/download")
sha256sums=('a493507feb4c7f745e1dad027e08eb64c34549f455b8d7c753533f96978b4dab')
b2sums=('ed934d556b39da01ac2763887d88fab44179249f0ccfe7416797f076456ac7ea09c5365ee68b0a52386a29ab568e7d37d24fbc60e4bd6448c4005b8aba5affb3')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/elf2uf2-rs

  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
