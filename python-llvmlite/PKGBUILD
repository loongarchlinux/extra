# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_name=llvmlite
pkgbase="python-${_name}"
pkgname=("${pkgbase}")
pkgver=0.42.0
pkgrel=2
pkgdesc='A Lightweight LLVM Python Binding for Writing JIT Compilers'
arch=('loong64' 'x86_64')
depends=('gcc-libs' 'glibc' 'llvm14-libs' 'python')
makedepends=('cmake' 'git' 'llvm14' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest')
url="https://github.com/numba/${_name}"
license=('BSD')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('f92b09243c0cc3f457da8b983f67bd8e1295d0f5b3746c7a1861d7a99403854a')
b2sums=('13063410eb7716d5a3b2b8b7c4951b1a0161dab1a8c86d953e8938ea9977ae9354463387dbc3416f82cea3f3ab7b79f9943260c461eb4092ea66c61d8d0638ce')

build() {
    cd "${_name}-$pkgver"
    export PATH="/usr/lib/llvm14/bin:$PATH"
    python -m build --wheel --no-isolation
}

check() {
    cd "${_name}-$pkgver"
    pytest -vv $_name/tests
}

package() {
    cd "${_name}-$pkgver"

    python -m installer --destdir="$pkgdir" dist/*.whl

    install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
