# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_name=llvmlite
pkgbase="python-${_name}"
pkgname=("${pkgbase}")
pkgver=0.43.0
pkgrel=1
pkgdesc='A Lightweight LLVM Python Binding for Writing JIT Compilers'
arch=('loong64' 'x86_64')
depends=('gcc-libs' 'glibc' 'llvm14-libs' 'python')
makedepends=('cmake' 'git' 'llvm14' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest')
url="https://github.com/numba/${_name}"
license=('BSD')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('ae2b5b5c3ef67354824fb75517c8db5fbe93bc02cd9671f3c62271626bc041d5')
b2sums=('3cba1575e87d021f6d281cad184063bd76dd6a45da1cb55e64e77ffb0571b81d77c21ce919e3673b50469addc870a83a817ec264941b82f4b79983b40d070f44')

build() {
    cd "${_name}-$pkgver"
    export PATH="/usr/lib/llvm14/bin:$PATH"
    python -m build --wheel --no-isolation
}

check() {
    cd "${_name}-$pkgver"
    pytest -vv $_name/tests
}

package() {
    cd "${_name}-$pkgver"

    python -m installer --destdir="$pkgdir" dist/*.whl

    install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
