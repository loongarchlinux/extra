# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: PyroDevil <p dot devil at gmail dot com>
# Contributor: Anibal Pacheco <apacheco.uy@gmail.com>

pkgname=scrapy
pkgver=2.11.1
pkgrel=1
pkgdesc="A fast high-level scraping and web crawling framework."
arch=('any')
license=('BSD')
url="https://scrapy.org"
depends=(
  'python'
  'python-cryptography'
  'python-cssselect'
  'python-itemadapter'
  'python-itemloaders'
  'python-lxml'
  'python-packaging'
  'python-parsel'
  'python-protego'
  'python-pydispatcher'
  'python-pyopenssl'
  'python-queuelib'
  'python-service-identity'
  'python-setuptools'
  'python-tldextract'
  'python-twisted'
  'python-w3lib'
  'python-zope-interface'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-wheel'
)
checkdepends=(
  'bpython'
  'ipython'
  'mitmproxy'
  'python-attrs'
  'python-botocore'
  'python-brotli'
  'python-h2'
  'python-markupsafe'
  'python-pyftpdlib'
  'python-pytest'
  'python-sybil'
  'python-testfixtures'
  'python-uvloop'
  'python-zstandard'
)
optdepends=(
  'bpython: for ncurses support in cmdline'
  'ipython: for enhanced support of the interactive scraping shell'
  'python-botocore: for various utils'
  'python-brotli: for HTTP compression using brotli'
  'python-h2: for HTTP2 support'
  'python-hpack: for HTTP2 streaming support'
  'python-zstandard: for HTTP compression using zstandard'
)
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/scrapy/scrapy/archive/$pkgver.tar.gz"
)
sha512sums=('a191b95a190ff758b9b392f3b0461470fb968110dae83dda9c413ca1dfc73327f6236568f30e47d653ff67783a544037ae9321cb54792e449558a15fc8f64a72')
b2sums=('5bd647ea3cd4024b73378445ba26a5776afa6d481b281095e7e7bc4dd720036eb3f0acb48e2933bdc41a6aa73b255626f7447687a8cd7607694509bb4f948579')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    # integration tests are blocking and unnecessary
    --ignore tests/test_proxy_connect.py
    # TODO: raise upstream
    --deselect tests/test_crawl.py::CrawlTestCase::test_start_requests_laziness
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}" tests
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -D -m644 README.rst -t "$pkgdir"/usr/share/doc/$pkgname/
  install -D -m644 docs/intro/install.rst -t "$pkgdir"/usr/share/doc/$pkgname/
}
