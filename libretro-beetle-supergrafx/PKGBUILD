# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-beetle-supergrafx
pkgver=1014
pkgrel=1
pkgdesc='NEC SuperGrafx core'
arch=(loong64 x86_64)
url=https://github.com/libretro/beetle-supergrafx-libretro
license=(GPL2)
groups=(libretro)
depends=(
  gcc-libs
  glibc
  libretro-core-info
  zlib
)
makedepends=(git)
_commit=1943f676c70a9ceeee51f05a7422093c525e83ca
source=(libretro-beetle-supergrafx::git+https://github.com/libretro/beetle-supergrafx-libretro.git#commit=${_commit})
sha256sums=('e1e15c28a7a5208ad97357768a1ee9b71be05a48ab1699e88b550bc7a50cd757')

pkgver() {
  cd libretro-beetle-supergrafx

  git rev-list --count HEAD
}

build() {
  make \
    SYSTEM_LIBCHDR=0 \
    SYSTEM_ZLIB=1 \
    -C libretro-beetle-supergrafx
}

package() {
  install -Dm 644 libretro-beetle-supergrafx/mednafen_supergrafx_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
