# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>
pkgname=kcov
pkgver=42
pkgrel=1
pkgdesc="Code coverage tester for compiled programs, Python scripts and shell scripts"
arch=('loong64' 'x86_64')
url="https://simonkagstrom.github.io/kcov"
license=('GPL-2.0-or-later')
depends=('elfutils' 'curl' 'zstd' 'zlib' 'binutils' 'libsframe.so')
makedepends=('cmake' 'python' 'ninja')
source=("https://github.com/SimonKagstrom/kcov/archive/v${pkgver}.tar.gz")
sha512sums=('d28535cf5565c9b4205a2c1f28c703b45488e619d13582f75a97219a27b39339fd5ff3803fdf61e0520c39788226d1e20dafc10282465cc130aa21467d1f6c20')

build() {
  cd $pkgname-$pkgver

  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="$pkgdir/" ninja -C build install
}

# vim:set ts=2 sw=2 et:
