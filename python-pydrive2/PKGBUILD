# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-pydrive2
_name=PyDrive2
pkgver=1.19.0
pkgrel=2
pkgdesc='Google Drive API Python wrapper library (maintained fork of PyDrive)'
arch=('any')
url='https://github.com/iterative/PyDrive2'
license=('Apache-2.0')
depends=(
  'python'
  'python-google-api-python-client'
  'python-httplib2'  # explicitly required: https://github.com/iterative/PyDrive2/issues/336
  'python-oauth2client'
  'python-pyopenssl'  # probably not needed: https://github.com/iterative/PyDrive2/issues/335
  'python-pyyaml'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-setuptools-scm'
  'python-wheel'
)
source=("$_name-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz")
b2sums=('0c32baaa3a1ed8805c3a082c4a8a31d50084d8b4a3b76e35f055f65e34535dbcee6e1dbd14eb4fb56ca76616dd59805d0bf3a31073a8707f15ee517e344aff1b')

build() {
  cd "$_name-$pkgver"

  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

package() {
  cd "$_name-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
