# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=openFPGALoader
pkgname=openfpgaloader
pkgver=0.12.1
pkgrel=1
pkgdesc='Universal utility for programming FPGA'
arch=('loong64' 'x86_64')
url='https://github.com/trabucayre/openFPGALoader'
license=('Apache2')
depends=('libftdi' 'libftdi-compat' 'libhidapi-hidraw.so' 'libudev.so')
makedepends=('cmake' 'libusb' 'systemd')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('bc9a9e0d6f62dc15a78bc84b9b0daa12715d487cd1dfd27b511b820c77756ac2f8dd298d601b5f2d38dc3f96a1a6bce69117defc41cf9572d315e082aaaf78e9')

prepare() {
  sed -i 's|MODE="664", GROUP="plugdev"|MODE="666"|g' $_pkgname-$pkgver/99-openfpgaloader.rules
}

build() {
  mkdir -p $_pkgname-$pkgver/build
  cd $_pkgname-$pkgver/build

  export CFLAGS+=" ${CPPFLAGS}"
  export CXXFLAGS+=" ${CPPFLAGS}"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DENABLE_UDEV=True \
    -DENABLE_CMSISDAP=True

  make
}

package() {
  cd $_pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install

  install -Dm 644 ../99-openfpgaloader.rules "$pkgdir"/usr/lib/udev/rules.d/99-openfpgaloader.rules
}

