# Maintainer: Ivy Foster <iff@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: alive4ever <alive4ever at live.com>
# Contributor: Earnest

pkgname=opendoas
pkgver=6.8.2
pkgrel=3
pkgdesc='Run commands as super user or another user'
url='https://github.com/Duncaen/OpenDoas'
license=(custom:ISC)
arch=(loong64 x86_64)
makedepends=(git)
depends=(pam)
provides=(doas)
replaces=(doas)
source=(${pkgname}::"git+https://github.com/Duncaen/OpenDoas.git?signed#tag=v${pkgver}")
backup=(etc/pam.d/doas)
sha256sums=('43b4c2de1aaa31aac1d322b98883334b864c606783c4dfb3ddbfa0d88af9332b')
validpgpkeys=(C4B6FBA98211F7EFF35DEB73335C1D17EC3D6E35) # Duncan Overbruck <mail@duncano.de>

build() {
  cd "$pkgname"
  ./configure --prefix=/usr --with-timestamp
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
