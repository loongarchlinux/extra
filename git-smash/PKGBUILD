# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=git-smash
pkgver=0.1.1
pkgrel=1
pkgdesc='Smash staged changes into previous commits to support your Git workflow'
url='https://github.com/anthraxx/git-smash'
arch=(loong64 x86_64)
license=(MIT)
depends=(
  glibc
  gcc-libs
  git
)
makedepends=(
  cargo scdoc
)
optdepends=(
  'skim: selection menu alternative'
  'fzf: selection menu alternative'
)
source=(${url}/releases/download/v${pkgver}/${pkgname}-v${pkgver}.tar.gz{,.sig})
options=('!makeflags')
sha512sums=('ddd2aa95ff54e334912fd6b9b9d6dedaabb4ba49d8810a0b01069f2ad3704a389780193bf798f7b92efd7b38c2edeba608f37c38e751c59d1559f73d83a76e81'
            'SKIP')
b2sums=('117ed488b2211a83ec52587b78862e4da80ea44274947798113af9a1fcaf2596a797defb0545b407a6399ac6925c3907c5d538123b6e0ac9bb590066ef53903a'
        'SKIP')
validpgpkeys=(
  'E240B57E2C4630BA768E2F26FC1B547C8D8172C8'  # Levente Polyak
)

build() {
  cd ${pkgname}-${pkgver}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
