# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-keyboard
pkgver=3.2.1.r166.2d87a593
pkgrel=1
pkgdesc='Switchboard Keyboard Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-keyboard
license=(GPL-2.0-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gtk4
  libibus
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
  libxml2
)
makedepends=(
  git
  meson
  vala
)
_commit=2d87a5932b0ae174809deef3778d780246e62d2a
source=(git+https://github.com/elementary/switchboard-plug-keyboard.git#commit=${_commit})
b2sums=(SKIP)

pkgver() {
  cd switchboard-plug-keyboard
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-keyboard build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
