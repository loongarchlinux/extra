# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Shane Stone <shanewstone at gmail>

pkgname=python-pdftotext
_name=${pkgname#python-}
pkgver=2.2.2
pkgrel=6
pkgdesc='Simple PDF text extraction'
arch=('loong64' 'x86_64')
url=https://github.com/jalan/pdftotext
license=('MIT')
depends=('poppler' 'python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('f7e6735e37b9b0395ed6fd50b7138903a7d1120049d6bc0c7f1511acbd0c9150c82aaf704e37b92d85504ec4c532c1829de1d3a4d774b94846b7c69f4fa32f13')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m unittest discover
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"$_name"-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
