# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=adljack
pkgver=1.3.1
pkgrel=2
pkgdesc="A standalone synthesizer for ADLMIDI and OPNMIDI on the text console"
arch=(loong64 x86_64)
url="https://github.com/jpcima/adljack"
license=(BSL-1.0)
groups=(pro-audio)
depends=(
  bash
  gcc-libs
  glibc
)
makedepends=(
  cmake
  jack
  liblo
  ncurses
  rtaudio
  rtmidi
)
source=(
  $url/releases/download/v$pkgver/$pkgname-$pkgver-src.tar.gz
)
sha512sums=('0b1bad761f75057fb4b9734b633a2a6062ab02b898a8f997d2c3571a689bbcda0ff32b3ee1c1912319db91a7db7ed0204a9aaf34e98148df5bf05460e5f31281')
b2sums=('d8267ca609ae5a064537f8d3b62d851ff81cd4099526e62b23bb6df42aaa8c5d51ae8e8aaa41e72f0ce62a03c93f906600520dc23c3749cb80ec5b62d782cac9')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D USE_SYSTEM_RTAUDIO=ON
    -D USE_SYSTEM_RTMIDI=ON
    -S $pkgname-$pkgver-src
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    jack libjack.so
    liblo liblo.so
    ncurses libncursesw.so
    rtaudio librtaudio.so
    rtmidi librtmidi.so
  )

  DESTDIR="$pkgdir" cmake --install build
}
