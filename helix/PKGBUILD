# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: Wojciech Kępka (wojciech@wkepka.dev)

pkgname=helix
pkgver=24.07
pkgrel=2
pkgdesc="A post-modern modal text editor"
arch=('loong64' 'x86_64')
url="https://helix-editor.com"
license=('MPL-2.0')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme')
makedepends=('cargo' 'git')
optdepends=(
  'ansible-language-server: for Ansible language support'
  'bash-language-server: for Bash language support'
  'clang: for C/C++ language support'
  'dart: for Dart language support'
  'elvish: for elvish language support'
  'gopls: for Go language support'
  'haskell-language-server: for Haskell language support'
  'julia: for Julia language support'
  'lua-language-server: for Lua language support'
  'marksman: for Marksman language support'
  'python-lsp-server: for Python language support'
  'r: for R and rmarkdown language support'
  'racket: for racket language support'
  'rust-analyzer: for Rust language support'
  'taplo: for TOML language support'
  'texlab: for LaTeX language support'
  'typescript-language-server: for jsx, tsx, typescript language support'
  'typst-lsp: for Typst language support'
  'vue-language-server: for Vue language support'
  'vscode-css-languageserver: for CSS and SCSS support'
  'vscode-html-languageserver: for HTML language support'
  'vscode-json-languageserver: for JSON language support'
  'yaml-language-server: for YAML language support'
  'zls: for Zig language support'
)
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/helix-editor/helix/archive/$pkgver.tar.gz")
b2sums=('d95f373ccf89d0c97c610131d39bb286878ea01ee265b7d2c2c18986decae11086dceb227b3b67099bfc6d1744f728297ca59c4ae8b15c438fffdc935134de25')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  # NOTE: we are renaming hx to helix so there is no conflict with hex (providing hx)
  sed -i "s|hx|helix|g" contrib/completion/hx.*
  sed -i 's|hx|helix|g' contrib/Helix.desktop
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/hx" "$pkgdir/usr/lib/$pkgname/hx"
  install -vdm 755 "$pkgdir/usr/bin"
  ln -sv /usr/lib/$pkgname/hx "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"

  local runtime_dir="$pkgdir/usr/lib/$pkgname/runtime"
  mkdir -p "$runtime_dir/grammars"
  cp -r "runtime/queries" "$runtime_dir"
  cp -r "runtime/themes" "$runtime_dir"
  find "runtime/grammars" -type f -name '*.so' -exec \
    install -Dm 755 {} -t "$runtime_dir/grammars" \;
  install -Dm 644 runtime/tutor -t "$runtime_dir"

  install -Dm 644 "contrib/completion/hx.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm 644 "contrib/completion/hx.fish" "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -Dm 644 "contrib/completion/hx.zsh" "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -Dm 644 "contrib/Helix.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm 644 "contrib/$pkgname.png" -t "$pkgdir/usr/share/icons/hicolor/256x256/apps"
}

# vim: ts=2 sw=2 et:
