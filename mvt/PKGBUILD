# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquim Monteiro <joaquim.monteiro@protonmail.com>

pkgname=mvt
pkgver=2.5.4
pkgrel=1
pkgdesc="Mobile Verification Toolkit, a forensics tool looking for signs of device compromise"
arch=(any)
url="https://github.com/mvt-project/mvt"
license=(custom:MVT)
depends=(
  'libusb'
  'python'
  'python-adb-shell'
  'python-appdirs'
  'python-click'
  'python-cryptography'
  'python-iosbackup'
  'python-libusb1'
  'python-packaging'
  'python-pyahocorasick'
  'python-requests'
  'python-rich'
  'python-simplejson'
  'python-tld'
  'python-tqdm'
  'python-yaml'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("mvt-${pkgver}.tar.gz::https://github.com/mvt-project/mvt/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('193fcdd8071c949a8f0d9da92b0a96f356b197889e0533b5421e87641231ff7e95d6b8c05324ec73ca8839a0eef9baf911f7d62e2e5c87fcd6f7657965d39129')
b2sums=('0242b2ff08bd9652ca7248c244c6f999f6ddf0802488b9a8d96d94c03cf471b6d8cb6ee5ebd31fdad72cbc345f68e599244bacd695326c2ec895ea12b2e89750')

build() {
  cd "mvt-${pkgver}"
  python -m build -wn
}

check() {
  cd "mvt-${pkgver}"
  dev/mvt-android --help
  dev/mvt-ios --help
}

package() {
  cd "mvt-${pkgver}"
  python -m installer -d "${pkgdir}" dist/*.whl
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  local _py3_ver=$(python --version | cut -d " " -f2)
  rm -rf "$pkgdir/usr/lib/python${_py3_ver%.*}/site-packages/tests"
}

# vim: ts=2 sw=2 et:
