# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Tavian Barnes <tavianator@tavianator.com>

pkgname=bfs
pkgver=3.3.1
pkgrel=1
pkgdesc='A breadth-first version of find'
arch=('loong64' 'x86_64')
url='https://tavianator.com/projects/bfs.html'
license=('0BSD')
depends=(
  'glibc'
  'acl'
  'libcap'
  'liburing'
  'oniguruma'
)
makedepends=('git')
source=("$pkgname::git+https://github.com/tavianator/bfs#tag=$pkgver")
sha512sums=('acdf3ff2184f079a4d2357d4316b704354f45ad0cef7fa97532cf8b93a2ed91dc71d07262786757f64f542990c6192db1394ea319473482937edf8ecebfa6ee6')
b2sums=('ff6f313866a70c7cf9af52caaee0f8191223f9a2651f490bee0f439511f3252a8145b0ea3e54cbbfbd286df484164efb65ee5487e392aa22611b10e210df2c4d')

build() {
  cd "$pkgname"

  ./configure RELEASE=y
  make CFLAGS="$CFLAGS"
}

check() {
  cd "$pkgname"

  make -k check CFLAGS="$CFLAGS"
}

package() {
  cd "$pkgname"

  make install DESTDIR="$pkgdir"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
