# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=ruby-dbus
pkgver=0.23.1
pkgrel=2
pkgdesc='Ruby module for interaction with D-Bus'
arch=('any')
url='https://github.com/mvidner/ruby-dbus'
license=('LGPL-2.1-only')
depends=('ruby')
makedepends=('git')
options=('!emptydirs')
source=("$pkgname::git+$url#tag=v$pkgver")
sha512sums=('fd143487c725513e13d5a08f739b50b5ffe634a66e7ee14878002277f6232e3b588ce6e3c54229fe75c91078295271450eeeae5be3813e1c6c2f51e95685f15c')
b2sums=('5c5b821ae6292562265ff0b0651ebf9ab2fa3e258cc79b4fd6bc4a5e605ba744e091f5e98efccd862721af984e890ebaddcd8a761040875b551880365ac9961b')

build() {
  cd "$pkgname"

  gem build "$pkgname.gemspec"
}

package() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$pkgname-$pkgver.gem"

  # delete cache
  rm -rf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$pkgname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname"
}
