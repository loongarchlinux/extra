# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=comtool
pkgver=2.2.1
pkgrel=2
pkgdesc="A cross platform serial debug tools written by python"
arch=('any')
url="https://github.com/Neutree/COMTool"
license=('LGPL')
depends=('python-pyqt5' 'python-pyserial' 'python-requests' 'python-babel' 'python-qtawesome')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Neutree/COMTool/archive/v$pkgver.tar.gz")
sha512sums=('d08a8ea4048a4e0f682b3fba915e6f04707074b57d07587ded5b7664cede71bb51bfe8d40345bfe16c596d6caf83fc9720bfdfaf97f84bbaa8b61e8c6f964b0f')

prepare() {
  cd COMTool-$pkgver
  sed -i "s/'pyqt5>=5',//" setup.py
}

build() {
  cd COMTool-$pkgver
  python setup.py build
}

package() {
  cd COMTool-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"

  rm "$pkgdir"/usr/{LICENSE,README.MD}
}

# vim:set ts=2 sw=2 et:
