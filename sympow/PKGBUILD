# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sympow
pkgver=2.023.7
pkgrel=1
pkgdesc="A package to compute special values of symmetric power elliptic curve L-functions"
arch=(loong64 x86_64)
url="http://www.sagemath.org"
license=(LicenseRef-Sympow)
depends=(glibc
         pari
         sh)
makedepends=(git
             help2man)
source=(git+https://gitlab.com/rezozer/forks/sympow#tag=v$pkgver)
sha256sums=('7a6c1fb752baffbc0093331426b09cfd4d575541b8ef66f965479ea6852ac891')

prepare() {
  cd $pkgname
  sed -e 's|VERBOSE_DEFAULT 1|VERBOSE_DEFAULT 0|' -i sympow.h
  sed -e 's|libexec|lib|g' -i Configure -i sympow.h
}

build() {
  cd $pkgname

  PREFIX=/usr ./Configure
  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
