# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=add-determinism
pkgver=0.3.5
pkgrel=1
pkgdesc="Build postprocessor to reset metadata fields for build reproducibility"
url='https://github.com/keszybz/add-determinism'
arch=('loong64' 'x86_64')
license=('GPL-3.0-only')
depends=(
  'gcc-libs'
  'glibc'
  'zlib' 'libz.so'
)
makedepends=('cargo')
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/keszybz/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz
        Cargo.lock)
sha256sums=('07589cc9214cc3b515ab4b9ef68e2c24da30e41bc2b4bdb0348fcd96987b7253'
            '2cdf47dda5fa99159d887224ee0c2e1afb72a6cdbdbd0101b04bee8aca17e08a')
b2sums=('7d48e75452e8a486496ba3a175271c20dc26aeffeaec56f4ba17859e7c84c0855f07d004702831b75f9458bd51f3a903dff582b464b9d1f259e133999072e66a'
        '65796975e41c8ac2feed5572a1ea9c7f85368c3592efd88a3badb5375b29a5ccc6e6b3d0c55bdfd30c2fb9a8368225569df9fdc6098abb6a87471896b5067d15')

prepare() {
  cd "${pkgname}-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

updlockfiles() {
  cd "${pkgname}-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --frozen -- --skip test_handlers::test_pyc
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/${pkgname}
}

# vim: ts=2 sw=2 et:
