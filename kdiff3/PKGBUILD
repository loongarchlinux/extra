# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=kdiff3
pkgver=1.11.0
pkgrel=1
pkgdesc='A file comparator/merge tool'
arch=(loong64 x86_64)
url='https://kde.org/applications/development/kdiff3/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         kconfigwidgets
         kcrash
         ki18n
         kio
         kwidgetsaddons
         kxmlgui
         qt6-5compat
         qt6-base)
makedepends=(boost
             extra-cmake-modules
             kdoctools)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('3bf37954ca19a365f37b558b574c8fbd366719c087d7b821788d3efad0c44851'
            'SKIP')
validpgpkeys=(EE56D710885D36FB1E4D21F3F442B36D614B06BC) # Michael Reeves <reeves.87@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_WITH_QT6=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
