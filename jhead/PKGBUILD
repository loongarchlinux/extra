# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: François Charette <francois ατ archlinux δοτ org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=jhead
pkgver=3.08
pkgrel=1
pkgdesc='EXIF JPEG info parser and thumbnail remover'
url="http://www.sentex.net/~mwandel/jhead/"
arch=('loong64' 'x86_64')
license=('custom')
depends=('glibc')
optdepends=('libjpeg: to up-right images according to rotation tag')
source=(https://github.com/Matthias-Wandel/jhead/archive/$pkgver/$pkgname-$pkgver.tar.gz
        LICENSE)
sha256sums=('999a81b489c7b2a7264118f194359ecf4c1b714996a2790ff6d5d2f3940f1e9f'
            '0f32e3f3e903e508f6ec94ecd4926012259c699564ce615e076259fc7bda520a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -Dm755 jhead "${pkgdir}/usr/bin/jhead"
  install -Dm644 jhead.1 "${pkgdir}/usr/share/man/man1/jhead.1"
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
