# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=58.1
pkgrel=2
pkgdesc="A sound synthesizer and musical instrument."
arch=(loong64 x86_64)
url="https://dinisnoise.org/"
license=(GPL-2.0-only)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
  hicolor-icon-theme
  libglvnd
  sdl
  tcl
)
makedepends=(
  boost
  glu
  rtaudio
  rtmidi
)
source=(
  https://archive.org/download/dinisnoise_source_code/$pkgname-$pkgver.tar.gz
  $pkgname-58.1-devendor_rtaudio_rtmidi.patch
)
sha512sums=('ee2d63309347167af3b4e41a0ad3ce1faad727a4e079cd9e63d402b680144aed81dbee9d38bc517acbb30a1c9af18b726841d0c94d96cbfb2b1581da98af21c1'
            '70ab82005f97bee92554db8e5effadce361cb8bd3e0c0dbdf379f4a8632c9718b0552bf9c84168aeaabe4a5cb655dedd0ac3988c01e1cf8b09cf911e83abb96e')
b2sums=('2ccb48fed8aa16edb53cf6643b63a842c28cd700621d2273a94913ab7a6ae8b50da72c3509a8b9b6189fcd4beaa5afbfa3a92430acb18c4cc9a7cfb057209c29'
        '8dab458e1524d71e91760eac2cd98d6cd3f7cfd44c2d6694220215cb6fce5b51c4d0533d4934637d7d602db81ca85ba5aedf9fe1f5375e3f8af0e6444d84888e')

prepare() {
  # use system rtaudio/rtmidi, instead of vendored versions
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-58.1-devendor_rtaudio_rtmidi.patch

  cd $pkgname-$pkgver
  rm -fv src/{RtAudio,RtMidi}.*
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  export CXXFLAGS+=" -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export CFLAGS+=" -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export LIBS+=" $(pkg-config --libs rtaudio rtmidi)"
  ./configure --prefix='/usr'
  make
}

package() {
  depends+=(
    rtaudio librtaudio.so
    rtmidi librtmidi.so
  )

  make install DESTDIR="$pkgdir" -C $pkgname-$pkgver
  # docs
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
