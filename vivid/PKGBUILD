# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=vivid
pkgver=0.10.1
pkgrel=1
pkgdesc='LS_COLORS manager with multiple themes'
arch=(loong64 x86_64)
url=https://github.com/sharkdp/vivid
license=(
  Apache-2.0
  MIT
)
depends=(gcc-libs)
makedepends=(
  git
  rust
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('f65e1d057b0d7872696a22874d75b5328b453591c16fb8c0bcbdb2ae2f8ff931e40953d7e706290996753988a65e138e9a9c31f92678361698d94abeba5288b4')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
