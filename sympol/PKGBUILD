# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sympol
pkgver=0.1.9
pkgrel=16
pkgdesc="A C++ tool to work with symmetric polyhedra"
arch=(loong64 x86_64)
url="http://www.math.uni-rostock.de/~rehn/software/sympol.html"
license=(GPL-2.0-or-later)
depends=(bliss
         boost-libs
         cddlib
         gcc-libs
         glibc
         gmp
         lrs)
makedepends=(cmake
             eigen
             git
             permlib)
source=(git+https://github.com/tremlin/SymPol#tag=v$pkgver
        cddlib-0.94k.patch
        sympol-fix-headers.patch
        sympol-lrs-071.patch
        sympol-lrs-073.patch)
sha256sums=('5c3c24a0d3dbab13b7f314d953b6a99a0cd25c60dd77db3980b88292fe3ee8ae'
            '23f85255ad1acbaf9c63134c6d331e0f3b8d06230a05e3f63e57399863579649'
            'f3ed90e6b9af5dae0728c52ce4bb9107f3040481cac6c08116dbf8c19bfe3971'
            '0aa37a4b87021ed04e02fcdae525dc305a8b0490c98bfb82bba2d9af04ef1d35'
            '695c35962541badc25c5517f55db28c8eee0e46f2df66ea70c5f08892c8006a2')

prepare() {
  cd SymPol
  sed -e '/(external/d' -i CMakeLists.txt # Don't use bundled cddlib and lrslib
  rm -r external/{lrslib*,cddlib*}
  patch -p1 -i ../cddlib-0.94k.patch # Fix build with cddlib 0.94k
  patch -p1 -i ../sympol-fix-headers.patch # Keep headers directory structure
  patch -p0 -i ../sympol-lrs-071.patch # Fix build with lrs 071 (Fedora)
  patch -p1 -i ../sympol-lrs-073.patch # Fix build with lrs 073
  git cherry-pick -n d142539f # Fix build with bliss 0.77
}

build() {
  cmake -B build -S SymPol \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -I/usr/include/lrslib -DMA -DGMP -DBLISS_USE_GMP"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
