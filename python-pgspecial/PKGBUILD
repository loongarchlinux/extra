# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: fzerorubigd <fzero@rubi.gd>

pkgname=python-pgspecial
pkgver=2.1.2
pkgrel=1
pkgdesc='Meta-commands handler for PostgreSQL'
arch=('any')
url='https://github.com/dbcli/pgspecial'
license=('BSD-3-Clause')
depends=(
  'python'
  'python-click'
  'python-sqlparse'
  'python-psycopg'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest' 'python-configobj')
source=("$pkgname::git+$url.git#tag=v$pkgver")
sha512sums=('cbbe8db882e84ecfb3a438fc334106780d9b4aa851e39c602bd24061b462aa6d28a171fa541aed6f590634b207e2712179c4441631786740756562e9ab1c6382')
b2sums=('9652b21337f0981cfb6ccca883124a38cbc8d8168d55847a884c63737c0a337174ac10c2b18aae53bc1d53f4830dba61c3a8536e39072cce46c4703455d9fed0')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  PYTHONPATH="$(pwd):$PYTHONPATH" pytest --ignore tests/test_specials.py
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.rst

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" License.txt
}
