# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor:	Batuhan Baserdem <lastname dot firstname at gmail>

_name="hjson"
pkgname="python-hjson"
pkgver=3.1.0
pkgrel=2
pkgdesc="JSON for Humans, allows comments and is less error prone."
arch=(any)
url="https://github.com/laktak/hjson-py"
license=('MIT')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
depends=('python')
source=("$pkgname-$pkgver.tar.gz::https://pypi.io/packages/source/h/hjson/hjson-$pkgver.tar.gz")
sha256sums=('55af475a27cf83a7969c808399d7bccdec8fb836a07ddbd574587593b9cdcf75')

build() {
  cd "${_name}-${pkgver}"
  python -m build --wheel --no-isolation
}

# Tests not published by pypi package
# check(){
#   cd "${_name}-${pkgver}"
#   PYTHONPATH="$PYTHONPATH:${PWD}" python hjson/tests/__init__.py
# }

package() {
  cd "${_name}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
