# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Chris Mosher <cmosher01@gmail.com>

pkgname=xa
pkgver=2.4.1
pkgrel=1
pkgdesc='Cross-assembler for the 6502 microprocessor'
arch=(loong64 x86_64)
url='https://www.floodgap.com/retrotech/xa'
license=(GPL-2.0-only)
makedepends=(setconf)
source=("$url/dists/$pkgname-$pkgver.tar.gz")
b2sums=('86092d2a215d141f76bb8a31861f20e5124f2ec7125a5649b9b28a86e727d5f201826581c38aea8693d16692d8be13103a917c1152980d6a4764555751af7dac')

prepare() {
  cd $pkgname-$pkgver
  setconf Makefile CFLAGS="$CFLAGS -fPIC -Wl,-z,relro,-z,now"
  sed -i 's/all: killxa/all:/' Makefile
}

build() {
  make -C $pkgname-$pkgver
}

check() {
  make -C $pkgname-$pkgver test
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir/usr" install
}
