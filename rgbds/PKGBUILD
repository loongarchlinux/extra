# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Vaporeon <vaporeon@vaporeon.io>

pkgname=rgbds
pkgver=0.8.0
pkgrel=1
pkgdesc='Rednex GameBoy Development System'
arch=(loong64 x86_64)
url=https://github.com/gbdev/rgbds
license=(MIT)
depends=(
  glibc
  libpng
)
makedepends=(
  cmake
  git
  ninja
)
_tag=556c8a968aebfa5ed20589b514690a5e26e7c0d0
source=(git+https://github.com/gbdev/rgbds.git#tag=${_tag})
b2sums=('7dc74703baac355f95a77d12b63d886ed5501575ded4c72f85392a1faca2340ec77c98c3ca9f81bdb4852d76c4c81ffd9dd60beeda72d8bb82814c3a08822aad')

pkgver() {
  cd rgbds

  git describe --tags | sed 's/^v//'
}

build() {
  cmake -S rgbds -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "${pkgdir}"/build
  install -Dm 644 rgbds/LICENSE -t "${pkgdir}"/usr/share/licenses/rgbds/
  find rgbds/contrib/zsh_compl -type f -exec install -Dm 644 {} -t "${pkgdir}"/usr/share/zsh/site-functions/ \;
}

# vim: ts=2 sw=2 et:
