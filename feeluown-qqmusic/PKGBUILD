# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-qqmusic
pkgver=1.0.5
pkgrel=1
pkgdesc="feeluown qqmusic plugin"
arch=('any')
url="https://github.com/feeluown/feeluown-qqmusic"
license=('GPL3')
depends=('feeluown' 'python-marshmallow' 'python-requests')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest')
groups=('feeluown-full')
source=("git+https://github.com/feeluown/feeluown-qqmusic.git#tag=v$pkgver")
sha512sums=('53ca8c736edc32a08ea3d7e9506e3c1313c9fdf3550f4e536e1b391e1303fcfe5b4163e8555021138c68bea95b16c187f26d30aa7e793d68790727408006fab7')

build() {
  cd feeluown-qqmusic
  python setup.py build
}

check() {
  cd feeluown-qqmusic
  pytest
}

package() {
  cd feeluown-qqmusic
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
