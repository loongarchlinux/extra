# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com>

pkgname=sniffit
pkgver=0.6
pkgrel=1
pkgdesc="Very good packet sniffer for unix with ncurses interactive mode"
arch=('loong64' 'x86_64')
url="https://packages.ubuntu.com/source/sniffit"
#seems the website are out, ubuntu have all sources
#url=http://reptile.rug.ac.be/~coder/sniffit/sniffit.html"
license=('BSD')
depends=('ncurses' 'libpcap')
makedepends=('libtool')
source=("http://archive.ubuntu.com/ubuntu/pool/universe/s/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz")
sha512sums=('1566d2832cb06ca8f2f5bf6a7a202404cca4f13fc40d7ca23122047974b13e2b2903834a7920406783fae3c1b092a44c0f747c85472f3874ff04d58bc59338f7')

build() {
  cd "${srcdir}/sniffit-sniffit-${pkgver}"
  test -x ./configure || ./autogen.sh
  ./configure \
    --prefix=/usr \
    --no-recursion \
    --sbindir=/usr/bin \
    CFLAGS="-Wno-error -Wno-int-conversion -Wno-incompatible-pointer-types -Wno-unused-result $CFLAGS"
  make
}

package() {
  cd "${srcdir}/sniffit-sniffit-${pkgver}"

  make install DESTDIR="$pkgdir"

  install -dm0755 -g root -o root "$pkgdir"/usr/share/licenses/${pkgname}
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}

  install -dm0755 -g root -o root "$pkgdir"/usr/share/doc
  cp -r docs "$pkgdir"/usr/share/doc/sniffit
  cp -r examples "$pkgdir"/usr/share/doc/sniffit/examples
}
