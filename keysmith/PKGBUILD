# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=keysmith
pkgver=23.04.3
pkgrel=1
pkgdesc='OTP client for Plasma Mobile and Desktop'
url='https://apps.kde.org/keysmith/'
arch=(loong64 x86_64)
license=(GPL LGPL)
groups=(kde-applications kde-utilities)
depends=(kirigami2 ki18n kdbusaddons kwindowsystem libsodium qt5-svg)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('924eb9a378bae98a7581746dfd3c20e38b8d6e0e0a926e4f613ed2a64c28d7e5'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
