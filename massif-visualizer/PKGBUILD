# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Niels Martign√®ne <niels.martignene@gmail.com>
# Contributor: Matz Radloff <matzradloff@googlemail.com>

pkgname=massif-visualizer
pkgver=0.8.0
pkgrel=1
pkgdesc='Visualizer for Valgrind Massif data files'
arch=(loong64 x86_64)
url='https://apps.kde.org/massif-visualizer/'
license=(GPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kdiagram
         ki18n
         kio
         kparts
         kwidgetsaddons
         kxmlgui
         qt6-5compat
         qt6-base
         qt6-svg)
makedepends=(extra-cmake-modules
             kgraphviewer)
optdepends=('kgraphviewer: call graph tab')
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('5fad3f0e0d9fbb6bc8cfb744cb4e2c99f231d57ee0dd66dd594d36c2cc588a80'
            'SKIP')
validpgpkeys=(2D1D5B0588357787DE9EE225EC94D18F7F05997E  # Jonathan Riddell <jr@jriddell.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
