# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=gcovr
pkgver=7.2
pkgrel=2
pkgdesc='provides a utility for managing the use of the GNU gcov utility'
arch=(any)
url="https://gcovr.com/"
license=(BSD-3-Clause)
depends=('python' 'python-jinja' 'python-lxml' 'gcc' 'python-colorlog')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("https://github.com/gcovr/gcovr/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('17d3014b2dcfdfd155a8c38ef808ad6da11502cbb156e58172af259631e9b4ad')

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname}-${pkgver}
  # requires pyutillib, ply
  #pytest gcovr
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
