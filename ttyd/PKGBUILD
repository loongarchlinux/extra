# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=ttyd
pkgver=1.7.7
pkgrel=1
pkgdesc='Share your terminal over the web'
arch=('loong64' 'x86_64')
url='https://tsl0922.github.io/ttyd/'
license=('MIT')
depends=(
  'zlib'
  'libwebsockets'
  'json-c'
  'libuv'
  'openssl'
)
makedepends=('git' 'cmake')
source=("$pkgname::git+https://github.com/tsl0922/ttyd#tag=$pkgver")
sha512sums=('1ee41bacecd86eea8ab454a664bbb1c0fe755c0740dfaba1ca832de7e7e1938de85c89374dcc715b1ea4760a15b2a766d5349815b0033e057b2d3823f4fc657f')
b2sums=('ac65ac091cad53d7b3b62213d4e6d80724516543fd150407c612600f1642bbd900192ee962706cf1c33e079e8710d27505ac370c6dd1ae4b921ea1b2a9ee4c63')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -D CMAKE_BUILD_TYPE='None' \
    -D CMAKE_INSTALL_PREFIX='/usr' \
    -W no-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE"
}
