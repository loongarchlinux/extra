# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=balsa
pkgver=2.6.4
pkgrel=2
pkgdesc="An e-mail client for GNOME"
arch=('loong64' 'x86_64')
url='https://pawsa.fedorapeople.org/balsa/'
license=('GPL')
depends=('gmime3' 'webkit2gtk' 'libical' 'libnotify'
         'gpgme' 'gtksourceview4' 'gspell')
makedepends=('yelp-tools' 'intltool')
changelog=$pkgname.changelog
source=(https://pawsa.fedorapeople.org/balsa/$pkgname-$pkgver.tar.xz)
sha256sums=('befa5984511db33d41f2b1ecbfc99e22a15d45d08efe5d737b5174a1a6ac8fc1')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --with-gss \
      --with-ldap \
      --with-gtksourceview \
      --with-spell-checker=gspell \
      --with-rubrica \
      --with-sqlite \
      --with-gnome \
      --with-html-widget=webkit2
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make GTK_UPDATE_ICON_CACHE=/bin/true DESTDIR="${pkgdir}" install
}
