# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gpaste
pkgver=45.1
pkgrel=1
pkgdesc="Clipboard management system"
url="https://www.imagination-land.org/tags/GPaste.html"
license=(BSD-2-Clause)
arch=(loong64 x86_64)
depends=(
  cairo
  dconf
  gcc-libs
  gcr-4
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  gtk4
  libadwaita
  libx11
  libxi
)
makedepends=(
  git
  gjs
  gnome-control-center
  gobject-introspection
  meson
  pango
  systemd
  vala
)
optdepends=("wgetpaste: Upload clipboard contents")
_commit=a493116a124997d072f591745e00ebcc4e092ce1  # master
source=("git+https://github.com/keruspe/gpaste#commit=$_commit")
b2sums=('e29065afdf50d95c282978647753b0a8e03b9460258df126524014b9dbe49b3251b9ac0d18b93321c872a1d590370ecc5d6603c48673817823f93cd8c57c1551')

pkgver() {
  cd gpaste
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gpaste
  git tag -f v45.1 a493116a124997d072f591745e00ebcc4e092ce1 # Fixup tag
}

build() {
  arch-meson gpaste build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 gpaste/COPYING
}

# vim:set sw=2 sts=-1 et:
