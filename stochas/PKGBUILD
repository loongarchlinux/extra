# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Stochas
pkgbase=stochas
pkgname=(stochas stochas-{clap,standalone,vst3})
pkgver=1.3.10
_commit=b62d684a24f179cc969f086d53f7c7656cc36ca4  # refs/tags/v1.3.10
pkgrel=1
pkgdesc="A probabilistic polyrhythmic sequencer"
arch=(loong64 x86_64)
url="https://surge-synth-team.org/stochas"
license=(GPL-3.0-only)
depends=(
  gcc-libs
  glibc
  ttf-liberation
)
makedepends=(
  alsa-lib
  cmake
  flac
  freetype2
  gendesk
  git
  jack
  juce
  libjpeg-turbo
  libogg
  libpng
  libvorbis
  libx11
  libxcursor
  libxinerama
  libxrandr
  zlib
)
source=(
  git+https://github.com/surge-synthesizer/$pkgname.git#commit=$_commit
  git+https://github.com/free-audio/clap-juce-extensions.git
  git+https://github.com/free-audio/clap.git
  git+https://github.com/free-audio/clap-helpers.git
  $pkgname-1.3.10-devendor_juce.patch
)
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'db06c3662932cd2d64ea991979021220164e9c45d677bd61f6d40632fd774c6021e7a554c19f94c827370af2bc4dbcfad31dde8dea6733e8027a87bf92c9ccb5')
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        '3acfa843d49a47b8f0fd05a56eba26a37c28a7e3317e8d18aba5984f20a1bf93b6e7b49a2d8967ba6430b8a6f0b25510067e790a61005fe4e0722c6dd4ac64d7')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g'
}

prepare() {
  (
    cd $pkgname
    git submodule init
    git submodule deinit lib/JUCE  # devendor juce
    git config submodule.lib/clap-juce-extensions.url "$srcdir/clap-juce-extensions"
    git -c protocol.file.allow=always submodule update

    cd lib/clap-juce-extensions
    git submodule init
    git config submodule.clap-libs/clap.url "$srcdir/clap"
    git config submodule.clap-libs/clap-helpers.url "$srcdir/clap-helpers"
    git -c protocol.file.allow=always submodule update
  )

  patch -Np1 -d $pkgname -i ../$pkgname-1.3.10-devendor_juce.patch

  gendesk -n \
          --pkgname org.surge-synth-team.$_name \
          --pkgdesc "$pkgdesc" \
          --exec "$_name" \
          --name "$_name" \
          --icon org.surge-synth-team.$_name \
          --genericname "Audio Sequencer" \
          --categories "AudioVideo;Audio"
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname
    -W no-dev
  )

  export CFLAGS+=" -DNDEBUG"
  export CXXFLAGS+=" -DNDEBUG"
  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package_stochas() {
  depends=(
    stochas-{clap,standalone,vst3}
  )
}

package_stochas-clap() {
  pkgdesc+=" - CLAP plugin"
  groups=(
    clap-plugins
    pro-audio
  )
  depends+=(
    clap-host
    freetype2 libfreetype.so
    libjpeg-turbo libjpeg.so
    libpng libpng16.so
    zlib libz.so
  )

  install -vDm 755 build/${pkgbase}_artefacts/None/CLAP/$_name.clap -t "$pkgdir/usr/lib/clap/"
}

package_stochas-standalone() {
  pkgdesc+=" - standalone"
  groups=(
    pro-audio
  )
  depends+=(
    alsa-lib libasound.so
    freetype2 libfreetype.so
    hicolor-icon-theme
    libjpeg-turbo libjpeg.so
    libpng libpng16.so
    zlib libz.so
  )

  install -vDm 755 build/${pkgbase}_artefacts/None/Standalone/$_name -t "$pkgdir/usr/bin/"
  install -vDm 644 ./*.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 $pkgbase/image/app_logo_512.png "$pkgdir/usr/share/icons/hicolor/512x512/apps/org.surge-synth-team.$_name.png"
}

package_stochas-vst3() {
  pkgdesc+=" - VST3 plugin"
  groups=(
    pro-audio
    vst3-plugins
  )
  depends+=(
    freetype2 libfreetype.so
    gcc-libs
    glibc
    libjpeg-turbo libjpeg.so
    libpng libpng16.so
    vst3-host
    zlib libz.so
  )

  install -vDm 755 build/${pkgbase}_artefacts/None/VST3/$_name.vst3/Contents/Resources/*.json -t "$pkgdir/usr/lib/vst3/$_name.vst3/Contents/Resources/"
  install -vDm 755 build/${pkgbase}_artefacts/None/VST3/$_name.vst3/Contents/$CARCH-linux/$_name.so -t "$pkgdir/usr/lib/vst3/$_name.vst3/Contents/$CARCH-linux/"
}
