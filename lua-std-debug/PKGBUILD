# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kilian Guillaume "cafehaine" <kilian dot guillaume at gmail dot com>

_rockname=std-_debug
pkgbase=lua-std-debug
pkgname=("$pkgbase" "lua51-${pkgbase#lua-}" "lua52-${pkgbase#lua-}" "lua53-${pkgbase#lua-}")
pkgver=1.0.1
_rockrel=1
pkgrel=3
pkgdesc='Lua-stdlib projectâ€™s debug hints library'
arch=(any)
url="https://github.com/lua-stdlib/${_rockname#std-}"
license=(MIT)
makedepends=(lua
             lua51
             lua52
             lua53
             luarocks)
# checkdepends=(specl)
_archive="${_rockname#std-}-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('efc560540f08dcf880ac48c6ab527bd741bbe3d6d66202a3c8be3ff5c9e1e907')


_package() {
	cd "$_archive"
	depends=(${pkgname%%-*})
	luarocks --lua-version="$1" --tree="$pkgdir/usr/" \
		make --deps-mode=none --no-manifest "${_rockname/-/.}-$pkgver-$_rockrel.rockspec"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.md
}

package_lua-std-debug() {
	provides=(lua-std-_debug)
	replaces=(lua-std-_debug)
	_package 5.4
}

package_lua51-std-debug() {
	provides=(lua51-std-_debug)
	replaces=(lua51-std-_debug)
	_package 5.1
}

package_lua52-std-debug() {
	provides=(lua52-std-_debug)
	replaces=(lua52-std-_debug)
	_package 5.2
}

package_lua53-std-debug() {
	provides=(lua53-std-_debug)
	replaces=(lua53-std-_debug)
	_package 5.3
}
