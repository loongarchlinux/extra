# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: João Figueiredo <jf.mundox@gmail.com> (11/07/2020)
# Contributor: freggel.doe (Fixed i18n problem with make test,  13/07/2020)
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jakob Matthes <jakob.matthes@gmail.com>
# Contributor: Bram Schoenmakers <me@bramschoenmakers.nl>
# Contributor: henning mueller <henning@orgizm.net>
# Contributor: rakoo (AUR)
# Contributor: SanskritFritz (gmail)

pkgname=bup
pkgver=0.33.3
pkgrel=2
pkgdesc='Efficient backup system based on the git packfile format'
arch=(loong64 x86_64)
url='https://bup.github.io/'
license=(GPL)
depends=(acl
         git
         glibc
         par2cmdline
         python
         python-fuse
         python-pyxattr
         readline
         sh)
optdepends=('python-tornado: for bup web')
makedepends=(pandoc)
source=(https://github.com/bup/bup/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('0aa6e98352c939180e82bbb0a647afd8d1b3d5eda6771b65e694099f6b956ac5')

build() {
  cd $pkgname-$pkgver
  ./configure
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr install
}
