# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Geoffrey Frogeye <geoffrey+aur@frogeye.fr>

pkgname=check-sieve
pkgver=0.9
pkgrel=2
pkgdesc='Syntax checker for Sieve scripts'
arch=('loong64' 'x86_64')
url='https://github.com/dburkart/check-sieve'
license=('MIT')
depends=('gcc-libs')
makedepends=('git')
checkdepends=('python')
source=("$pkgname::git+$url#tag=check-sieve-$pkgver")
sha512sums=('bd9c2ffb8c21ffa701709036b223d03ef7264f89d659cd84305bc01ba5ab0e353912b89f2f1f0f0d3e1216a0c693a1d67bdaf669b9db5fac4a9fb1e21fbfd07b')
b2sums=('de14c6e90712501f11e1994b1253a0ca9051e0141965ac01641d59c36a2ae85b868884ccc9b8d6a77ad6576c0a2222beb094bdf67e53fdced09a81c64e243495')

prepare() {
  cd "$pkgname"

  # ensure LDFLAGS are passed to compiler
  sed \
    -i Makefile \
    -e '/\t$(CXX).*/s/$/ $(LDFLAGS)/'
}


build() {
  cd "$pkgname"

  export CFLAGS+=" -ffat-lto-objects"

  make
}

# https://github.com/dburkart/check-sieve/issues/66
#check() {
#  cd "$pkgname"
#
#  make test
#}

package() {
  cd "$pkgname"

  make INSTALL_PREFIX="$pkgdir/usr" install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # fix incorrect permissions on library
  chmod 644 "$pkgdir/usr/lib/libchecksieve.a"
}

