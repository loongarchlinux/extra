# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-debug
pkgver=0.4.3
pkgrel=8
pkgdesc="A small debugging helper for Flask"
url="https://github.com/mbr/flask-debug"
license=('MIT')
arch=('any')
depends=('python-flask' 'python-inflection')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mbr/flask-debug/archive/$pkgver.tar.gz")
sha512sums=('7bc1cf9f88f28e2ca5a24c1454dcdb4e2a450eff3c00329f299c3840032e0803079f565652974f4c3a0e5aac443365ce47a3d2c2fee8e21e2e0c7150367e8816')

build() {
  cd Flask-Debug-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd Flask-Debug-$pkgver
  PYTHONPATH="$PWD" pytest
}

package() {
  cd Flask-Debug-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
