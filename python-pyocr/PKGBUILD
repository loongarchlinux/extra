# Maintainer: Balló György <ballogyor+arch at gmail dot com>

_pkgname=pyocr
pkgname=python-pyocr
pkgver=0.8.5
pkgrel=2
pkgdesc='Python wrapper for Tesseract and Cuneiform'
arch=('any')
url='https://gitlab.gnome.org/World/OpenPaperwork/pyocr'
license=('GPL3')
depends=('python-pillow' 'python-six')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
optdepends=('cuneiform: OCR backend'
            'tesseract: OCR backend')
_commit=ca191ba776743fe10a1c3dd99eaa82626ee4edc1
source=("git+https://gitlab.gnome.org/World/OpenPaperwork/pyocr.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd $_pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $_pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
