# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=clap
pkgver=1.2.1
pkgrel=1
pkgdesc="CLever Audio Plugin API"
arch=(any)
url="https://github.com/free-audio/clap"
license=(MIT)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('0cff5634e73a9254157f8e809090c6918d8fcd320fac398d4aee8aeeb6df468a0653aa1b27f9437d9f9948e7f8daec5db0673c80b4827e7d8cc7f30f2fc63c04')
b2sums=('16d1587026ea4573b8ca37e91dbea447786847938cc04e8bd5e248a0fcb1e38289bb13b691d550db13e5366019946076fe29c325ed3cd82f9063c7dd921cc1df')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
