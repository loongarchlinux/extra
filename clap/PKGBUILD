# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=clap
pkgver=1.2.0
pkgrel=1
pkgdesc="CLever Audio Plugin API"
arch=(any)
url="https://github.com/free-audio/clap"
license=(MIT)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('9113673a911d023ea29d9cb246fbd7e82b4baccf4df5eb63ffe2605cc65543f82cb3a66982803e26f9ecfc8c159508470608b1d3b47009965094fbcdede49cf5')
b2sums=('ac48ee2cf0dfb62afa674257da3e4ea32c7fd88827d4a8d90b2f41ab89efac5302efe0090e802bca8c35048a73197dfd7cc82036ea7ddebad34d61756d68b802')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
