# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

_pkg=GnuPG-Interface
pkgname=perl-${_pkg,,}
pkgver=1.04
pkgrel=1
pkgdesc="Object-oriented methods for interacting with GnuPG"
arch=('any')
url="https://metacpan.org/release/${_pkg}"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('gnupg' 'perl-math-bigint' 'perl-moo' 'perl-moox-handlesvia' 'perl-moox-late')
makedepends=('perl-module-install')
source=("https://cpan.metacpan.org/authors/id/B/BP/BPS/${_pkg}-${pkgver}.tar.gz")
sha512sums=('27394f08210aa1581c91640aa18006f1946098cfbe13870706cbc910de39dfa80985d265e4541cdebee70149d59c89361297f76aab0417bf904cf47ba10d1afd')

build() {
    cd ${_pkg}-${pkgver}
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd ${_pkg}-${pkgver}
    # 19 tests failing, likely because we are in a chroot
    make test
}

package() {
    cd ${_pkg}-${pkgver}
    make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
}
