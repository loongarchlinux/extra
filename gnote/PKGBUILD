# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: uastasi <uastasi@archlinux.us>

pkgname=gnote
pkgver=46.0
pkgrel=1
pkgdesc="A note taking application"
url="https://wiki.gnome.org/Apps/Gnote"
arch=(loong64 x86_64)
license=(GPL-3.0-or-later)
depends=(
  cairomm-1.16
  dconf
  gcc-libs
  glib2
  glibc
  glibmm-2.68
  gtk4
  gtkmm-4.0
  hicolor-icon-theme
  libadwaita
  libsecret
  libsigc++-3.0
  libxml2
  libxslt
  pango
  pangomm-2.48
  util-linux-libs
)
makedepends=(
  git
  meson
  yelp-tools
)
_commit=cd83266e36c841a961d6b6182b0708fc3876576d  # tags/46.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnote.git#commit=$_commit")
b2sums=('67b564df2ab3a33074f22367b4f334f354ec168bf44a683c3307eef071cea1ef74971b8e432f3b8ed3f0b977b14a072884b3500fb5a488c76b05e7d2a0ec06fc')

pkgver() {
  cd gnote
  git describe --tags | sed -r 's/\.([a-z])/\1/;s/([a-z])\./\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gnote
}

build() {
  arch-meson gnote build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
