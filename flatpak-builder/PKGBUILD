# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=flatpak-builder
pkgver=1.4.4
pkgrel=1
pkgdesc="Tool to build flatpaks from source"
url="https://flatpak.org"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  appstream
  binutils
  breezy
  cpio
  curl
  debugedit
  elfutils
  flatpak
  fuse3
  gcc-libs
  git
  glib2
  glibc
  json-glib
  libelf
  libxml2
  libyaml
  ostree
  patch
  rpmextract
  tar
  unzip
)
makedepends=(
  docbook-xsl
  git
  gobject-introspection
  meson
  xmlto
)
checkdepends=(valgrind)
replaces=('flatpak<0.9.10')
source=(
  "git+https://github.com/flatpak/flatpak-builder#tag=$pkgver"
  "git+https://gitlab.gnome.org/GNOME/libglnx.git"
  fusermount3.diff
)
b2sums=('04a6e52e225ebebeff17e3c7e712f09a926b341bea6051fb819c9d0bfdaecc0e789d0c7770c7ffd562acec8a22cb29f406c28af248f8d11d16b46a2690126b3b'
        'SKIP'
        'ef1a73c8e2876a8c0c1401300b4367cfc3e656d2144fb66fbf22a57c178588a42ac842f5d7ccec9fba15084dc8bb6a544b2b77c1877c4c924dbe4ab6cd1962c4')

prepare() {
  cd $pkgname

  # https://bugs.archlinux.org/task/75649
  git apply -3 ../fusermount3.diff

  git submodule set-url subprojects/libglnx "$srcdir/libglnx"
  git -c protocol.file.allow=always -c protocol.allow=never \
    submodule update --init -- subprojects/libglnx
}

build() {
  local meson_options=(
    -D fuse=3
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
