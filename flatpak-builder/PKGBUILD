# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=flatpak-builder
pkgver=1.4.2
pkgrel=1
pkgdesc="Tool to build flatpaks from source"
url="https://flatpak.org"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  appstream
  binutils
  breezy
  cpio
  debugedit
  elfutils
  flatpak
  fuse3
  git
  patch
  rpmextract
  tar
  unzip
)
makedepends=(
  docbook-xsl
  git
  gobject-introspection
  meson
  xmlto
)
checkdepends=(valgrind)
replaces=('flatpak<0.9.10')
_commit=0b9070037a573a6061d7893312611957561ca841  # tags/1.4.2
source=(
  "git+https://github.com/flatpak/flatpak-builder#commit=$_commit"
  "git+https://gitlab.gnome.org/GNOME/libglnx.git"
  fusermount3.diff
)
b2sums=('SKIP'
        'SKIP'
        'ef1a73c8e2876a8c0c1401300b4367cfc3e656d2144fb66fbf22a57c178588a42ac842f5d7ccec9fba15084dc8bb6a544b2b77c1877c4c924dbe4ab6cd1962c4')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix build (update libglnx)
  git cherry-pick -n ae86bde81fbb0316f08121793c0824c18b1cfb9a

  # https://bugs.archlinux.org/task/75649
  git apply -3 ../fusermount3.diff

  git submodule set-url subprojects/libglnx "$srcdir/libglnx"
  git -c protocol.file.allow=always submodule update --init -- subprojects/libglnx
}

build() {
  local meson_options=(
    -D fuse=3
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
