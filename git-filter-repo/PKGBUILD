# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

pkgname=git-filter-repo
pkgver=2.38.0
pkgrel=3
pkgdesc='Quickly rewrite git repository history (filter-branch replacement)'
arch=(any)
url="https://github.com/newren/$pkgname"
license=(MIT)
depends=(git
         python)
makedepends=(python-{build,installer,wheel}
             python-setuptools-scm)
checkdepends=(rsync)
_archive="$pkgname-$pkgver"
source=("$url/releases/download/v$pkgver/$_archive.tar.xz"
        https://src.fedoraproject.org/rpms/git-filter-repo/raw/rawhide/f/git-filter-repo-support-git-2_43.diff
        https://src.fedoraproject.org/rpms/git-filter-repo/raw/rawhide/f/git-filter-repo-fix-t9390-t41.diff)
sha256sums=('db954f4cae9e47c6be3bd3161bc80540d44f5379cb9cf9df498f4e019f0a41a9'
            '6d7be296681dc32c88d3e8aade1380e48cb62941d0261fe507109e78fb53ea4b'
            '82399400e7c5c7b2dcad9aa685786a8d73de6bdea1f52189cc26b2f9ce896a92')

prepare() {
	cd "$_archive"
	# if it is defined both in setup.cfg and setup.py, installation will explode...
	sed -e '/scripts/d' -i release/setup.cfg
	patch -p1 -i "$srcdir/git-filter-repo-support-git-2_43.diff"
	patch -p1 -i "$srcdir/git-filter-repo-fix-t9390-t41.diff"
	# Neuter test returning different results with new Git 2.44
	sed -e '721a exit 0' -i t/t9390-filter-repo.sh
}

build() {
	cd "$_archive/release"
	export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
	python -m build -wn
}

check() {
	cd "$_archive"
	./t/run_tests
}

package() {
	cd "$_archive/release"
	python -m installer -d "$pkgdir" dist/*.whl
	pushd ..
	install -Dm644 -t "$pkgdir/usr/share/man/man1/" Documentation/man1/$pkgname.1
	install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" COPYING COPYING.mit
}
