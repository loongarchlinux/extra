# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=html5-parser
pkgname=python-html5-parser
pkgver=0.4.12
pkgrel=2
pkgdesc="Fast C based HTML 5 parsing for python"
arch=('loong64' 'x86_64')
url="https://github.com/kovidgoyal/${_pkgname}"
license=('Apache-2.0')
depends=('python-chardet' 'python-lxml')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel' 'git')
checkdepends=('python-beautifulsoup4')
optdepends=('python-beautifulsoup4: to use the soup treebuilder')
source=("git+${url}#tag=v${pkgver}?signed")
sha256sums=('57c3a8bc8454cd7ddb7f579f291545edc59076c1c6011501814be83e7ca9bf0c')
b2sums=('a2685a57d95be31bdca22ac3a8dc1c41d454da10940f4c0e018cb8e3eeea8ce69d31edcb53b17f547f3923a7328dc0728bc3839fdbf4e29e585a7a2312b6b7b1')
validpgpkeys=('3CE1780F78DD88DF45194FD706BC317B515ACE7C') # Kovid Goyal (New longer key) <kovid@kovidgoyal.net>

build() {
    cd "${srcdir}"/${_pkgname}

    python -m build --wheel --no-isolation
}

check() {
    cd "${srcdir}"/${_pkgname}
    local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')

    # For pytest
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$python_version" python run_tests.py
}

package() {
    cd "${srcdir}"/${_pkgname}

    python -m installer --destdir="$pkgdir" dist/*.whl
}
