# Maintainer: Torsten Ke√üler <tpkessler@archlinux.org>
# Contributor: Butui Hu <hot123tea123@gmail.com>

pkgname=python-onnx
pkgver=1.16.0
pkgrel=3
epoch=1
pkgdesc='Open standard for machine learning interoperability'
arch=('loong64' 'x86_64')
url='https://onnx.ai'
license=('MIT')
depends=(
  python
  protobuf
  python-protobuf
  python-numpy
)
makedepends=(
  cmake
  git
  pybind11
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=("${pkgname}::git+https://github.com/onnx/onnx.git#tag=v${pkgver}")
sha512sums=('9d0d6ca4d0808e12ef1c10a38c2232ee6f0f4c996186c66d16819f72c379d0d708a3d29724b6d1c46bb38a9d1eec7ba05022cb85a2dc34f0f40d959fc6b6ea48')


prepare() {
  cd "${pkgname}"
  # Remove cmake and protobuf dependencies since we don't want to pull the
  # corresponding packages from PyPI
  sed -i 's/requires = .*/requires = ["setuptools"]/' pyproject.toml
}

build() {
  cd "${pkgname}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${pkgname}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
# vim:set ts=2 sw=2 et:
