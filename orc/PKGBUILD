# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=orc
pkgver=0.4.38
pkgrel=1
pkgdesc="Optimized Inner Loop Runtime Compiler"
url="https://gstreamer.freedesktop.org/modules/orc.html"
arch=(loong64 x86_64)
license=(BSD-3-Clause)
depends=(
  glibc
)
makedepends=(
  git
  gtk-doc
  meson
  valgrind
)
provides=(liborc{,-test}-${pkgver%.*}.so)
_commit=f071d3a14f28c7c5acff5ff0b5a67ecd3f785ef3  # tags/0.4.38^0
source=("git+https://gitlab.freedesktop.org/gstreamer/orc.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd orc
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd orc
}

build() {
  arch-meson orc build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 orc/COPYING
}

# vim:set sw=2 sts=-1 et:
