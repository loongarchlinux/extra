# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=orc
pkgver=0.4.39
pkgrel=1
pkgdesc="Optimized Inner Loop Runtime Compiler"
url="https://gstreamer.freedesktop.org/modules/orc.html"
arch=(loong64 x86_64)
license=(BSD-3-Clause)
depends=(
  glibc
)
makedepends=(
  git
  gtk-doc
  meson
  valgrind
)
provides=(liborc{,-test}-${pkgver%.*}.so)
source=("git+https://gitlab.freedesktop.org/gstreamer/orc.git?signed#tag=$pkgver")
b2sums=('cd93b7a29c0240d299752338f7e87569cfb4f5b7e9dfca40005d1e4c6509236ac2a499050db01d94004b7061faed5b78dd8b4f3d358ffe90fbf65964d38e5c4d')
validpgpkeys=(
  D637032E45B8C6585B9456565D2EEE6F6F349D7C # Tim-Philipp MÃ¼ller <tim@centricular.com>
)

prepare() {
  cd orc
}

build() {
  arch-meson orc build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 orc/COPYING
}

# vim:set sw=2 sts=-1 et:
