# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=2.0.7
pkgrel=1
pkgdesc='A distributed control protocol designed to operate astronomical instrumentation'
url='https://www.indilib.org/index.php?title=Main_Page'
license=(GPL2)
arch=(loong64 x86_64)
depends=(cblas
         cfitsio
         curl
         fftw
         gcc-libs
         glibc
         gsl
         libev
         libjpeg
         libnova
         libogg
         libtheora
         libusb
         libxisf
         rtl-sdr
         zlib)
makedepends=(cmake
             qt5-base)
source=(https://github.com/indilib/indi/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('a729d1857f28014530094b1de1062221af924dc10311093e3f5b89cb8c5f73f5')

build() {
  cmake -B build -S indi-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINDI_BUILD_QT5_CLIENT=ON \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DCMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects -Wp,-U_GLIBCXX_ASSERTIONS"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
