# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

pkgname=python-xcffib
pkgver=1.5.0
pkgrel=1
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=('any')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
depends=('python-cffi' 'libxcb')
makedepends=('git' 'python-setuptools' 'haskell-xcffib')
checkdepends=('python-pytest' 'xorg-server-xvfb' 'xorg-xeyes')
source=("git+https://github.com/tych0/xcffib.git#tag=v$pkgver")
sha512sums=('9173252f126610b39a683f8d79a78cbe160980b686bfa3b0d0538e0c07048db273a4fa9e206b3d01df202511b54d34b9050dc68e1e333f52d1c6a9651d7af1ba')

prepare() {
  cd xcffib
  # Remove redundant six import
  git cherry-pick -n 96eb6d4a6fa0b1404bf4ba072d0bb8f9e58af306
}

build() {
  cd xcffib
  make xcffib GEN=xcffibgen CABAL=true
  python setup.py build
}

check() {
  cd xcffib
  xvfb-run pytest
}

package() {
  cd xcffib
  python setup.py install --root="$pkgdir/" --optimize=1
}
