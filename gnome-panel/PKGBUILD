# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-panel
pkgver=3.52.0
pkgrel=4
pkgdesc='Panel of GNOME Flashback'
arch=('loong64' 'x86_64')
url='https://wiki.gnome.org/Projects/GnomePanel'
license=('GPL-2.0-or-later AND LGPL-2.1-or-later')
depends=('at-spi2-core' 'cairo' 'dconf' 'evolution-data-server' 'gdk-pixbuf2' 'geocode-glib-2'
         'glib2' 'glibc' 'gnome-desktop' 'gnome-menus' 'gsettings-desktop-schemas' 'gtk3'
         'hicolor-icon-theme' 'libgdm' 'libgweather-4' 'libical' 'libwnck3' 'libx11' 'libxrandr'
         'nautilus' 'pango' 'polkit' 'systemd-libs')
makedepends=('autoconf-archive' 'git' 'glib2-devel' 'yelp-tools')
optdepends=('alacarte: Main menu editor'
            'gnome-applets: Extra applets for the panel')
source=("git+https://gitlab.gnome.org/GNOME/$pkgname.git?signed#tag=$pkgver"
        'gnome-panel-60.patch'
        'gnome-panel-64.patch'
        'gnome-panel-65.patch')
b2sums=('c6dfba78c22870287b35e1ef24fa3b5a19fc5e1c9ce1050c0560bef22ca594c7d76d8b7a98de5df1d9cef931455e0cd5192f940cae331a6ca377cfdb705b5733'
        'aefa0a36a5a9bf7ba08c53c5b8f651df24cb2e3b39c5860aebf2be545d8c733e97465824ae1d2c91cf132d0adf484dd9cbf0d36a867b1a1e570b6b3a3ab08c74'
        'ccf3f767d6072785a7c128e28d4801f17c4a76769a3cc9c47f2a583e4e62e38d3c6c6625bb21722d019396b929ca3d596a272c0c1e16db50d76cb3e85149b59a'
        '3dc41fe465dade3d63e19ae4fc4045b06d572ea2d3123996b294bcf0eaf31553b4eeaacbcd36265a9692ed5936994802e5a0ab910ab36bb40e4b348d5064605a')
validpgpkeys=('7B44FD78E49334EC10B3B288A3D013EC303E1894') # Alberts Muktupāvels <alberts.muktupavels@gmail.com>

prepare() {
  cd $pkgname

  # https://gitlab.gnome.org/GNOME/gnome-panel/-/merge_requests/72
  git cherry-pick -n 569e08f292f8c29252046b9a9f932e24db592ab2

  # https://gitlab.gnome.org/GNOME/gnome-panel/-/merge_requests/73
  git cherry-pick -n 1f625434826f3536aea03fa8dcc933db8e48850c

  # https://gitlab.gnome.org/GNOME/gnome-panel/-/merge_requests/60
  git apply -3 ../gnome-panel-60.patch

  # https://gitlab.gnome.org/GNOME/gnome-panel/-/merge_requests/64
  git apply -3 ../gnome-panel-64.patch

  # https://gitlab.gnome.org/GNOME/gnome-panel/-/merge_requests/65
  git apply -3 ../gnome-panel-65.patch

  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-debug --enable-compile-warnings
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
