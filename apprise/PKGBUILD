# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: AlphaJack <alphajack at tuta dot io>
# Contributor: Pieter Goetschalckx <3.14.e.ter <at> gmail <dot> com>

pkgname=apprise
pkgver=1.8.1
pkgrel=1
pkgdesc='Push Notifications that work with just about every platform'
arch=(any)
url=https://github.com/caronc/apprise
license=(BSD-2-Clause)
depends=(
  python-certifi
  python-click
  python-markdown
  python-requests-oauthlib
  python-yaml
)
makedepends=(
  git
  python-babel
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  dbus-python
  python-paho-mqtt
  python-pytest-mock
)
optdepends=(
  'dbus-python: for Linux DBus Notifications'
  'python-gobject: for Linux DBus Notifications'
  'python-paho-mqtt: for MQTT support'
)
source=("git+$url.git#tag=v$pkgver")
b2sums=('c71d7649c998edd7ad6991a429f3540049489899c6454490a1690b714d1622ecc72eec184b1db6cbb901311dce39a0f4fcf86ca941ac43657bb85a4af140cbbd')

build(){
  cd $pkgname
  python -m build --wheel --no-isolation
}

check(){
  cd $pkgname
  PYTHONPATH="$pwd/$pkgname:$PYTHONPATH" pytest
}

package(){
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
