# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: schuay <jakob.gruber@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=qgit
pkgver=2.10
pkgrel=3
pkgdesc='A GIT GUI viewer built on Qt/C++'
url="https://github.com/tibirna/$pkgname"
arch=(loong64 x86_64)
license=(GPL)
makedepends=(cmake)
depends=(git
         hicolor-icon-theme
         qt5-base
         qt5-svg)
optdepends=(source-highlight)
source=("git+$url.git#tag=$pkgname-$pkgver")
sha256sums=('7fecc8288ca516d01a87b50c5411dcc126638cae53b2694421fe347c88dc4f7d')

prepare() {
	cd "$pkgname"
	mkdir -p build
}

build() {
	cd "$pkgname/build"
	cmake -DCMAKE_INSTALL_PREFIX="/usr" ..
	make
}

package() {
	cd "$pkgname/build"
	make DESTDIR="$pkgdir" install
}
