# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-camera
pkgver=8.0.0
pkgrel=1
pkgdesc='The Pantheon Camera Application'
arch=(loong64 x86_64)
url=https://github.com/elementary/camera
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gstreamer
  gst-plugins-base-libs
  gst-plugins-good
  gtk4
  hicolor-icon-theme
  libcanberra.so
  libgranite-7.so
)
makedepends=(
  appstream
  git
  intltool
  meson
  vala
)
_tag=8b6dcd23eaaac10ef6fdfad2731bdfb44ba03c6f
source=(pantheon-camera::git+https://github.com/elementary/camera.git#tag=${_tag})
b2sums=('3e92b540e55a14720083465807fe9039d2764a629293ceb2cbfd15559295f77441c40dacab00356fd7286fb73d2479b3b0dff3cda84a49dd434a1fc531270890')

pkgver() {
  cd pantheon-camera
  git describe --tags
}

build() {
  arch-meson pantheon-camera build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
