# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=pacman-contrib
pkgver=1.10.6
pkgrel=1
pkgdesc='Contributed scripts and tools for pacman systems'
arch=('loong64' 'x86_64')
url=https://gitlab.archlinux.org/pacman/pacman-contrib
license=('GPL-2.0-or-later')
depends=('pacman')
makedepends=('asciidoc' 'git')
optdepends=(
  'diffutils: for pacdiff'
  'fakeroot: for checkupdates'
  'findutils: for pacdiff --find'
  'mlocate: for pacdiff --locate'
  'perl: for pacsearch'
  'sudo: privilege elevation for several scripts'
  'vim: default merge program for pacdiff'
)
source=("git+$url.git#tag=v$pkgver")
source+=(pkgbuild-vim-la64.patch)
b2sums=('bc84555fb64552b21b3146c91defccecfaa879931720fb4f38eebd37199319409ae4011db5be9b4523a516ea1a2cfa4a07930a00c629e8e905e8d66126f185d0'
        '766b44a73e5106507ead70d5a563c2a91f42895abebfa7070ab5fdbd5d7de3b13858006ad870bb128732448c3fc50b02edafb47a977ad92097f01e6c279d6243')
validpgpkeys=('04DC3FB1445FECA813C27EFAEA4F7B321A906AD9') # Daniel M. Capella <polyzen@archlinux.org>
#             '5134EF9EAF65F95B6BB1608E50FB9B273A9D0BB5')  # Johannes Löthberg <johannes@kyriasis.com>

prepare() {
  cd $pkgname
  patch -p1 -i $srcdir/pkgbuild-vim-la64.patch
  ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
