# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mark Wagie <mark.wagie@proton.me>

pkgname=cosmic-settings-daemon
pkgver=1.0.0.alpha.1
pkgrel=1
pkgdesc='Cosmic settings daemon'
arch=(loong64 x86_64)
url=https://github.com/pop-os/cosmic-settings-daemon
license=(GPL-3.0-only)
groups=(cosmic)
depends=(
  acpid
  adw-gtk-theme
  gcc-libs
  geoclue
  glibc
  libinput
  systemd-libs
)
makedepends=(
  cargo
  git
  lld
)
_tag=362c77f9faaeb7f1b9e4aa79a7d5588001f04874
source=(git+https://github.com/pop-os/cosmic-settings-daemon.git#tag=${_tag})
b2sums=('57e2a6f63b2f455300776f8127ef866f54be79e09cb2199e4c2c5d3d77d9171b54051b2279e693210327e51e80c2890f93ddad9ae4d30da11c947b74d7f4379d')

prepare() {
  cd cosmic-settings-daemon
  cargo fetch --locked
  sed 's/libexec/lib/g' -i Makefile src/main.rs
  sed 's/sudo/wheel/g' -i data/polkit-1/rules.d/cosmic-settings-daemon.rules
}

pkgver() {
  cd cosmic-settings-daemon
  git describe --tags | sed 's/^epoch-//; s/-/./g'
}

build() {
  cd cosmic-settings-daemon
  RUSTFLAGS+=" -C link-arg=-fuse-ld=lld"
  make ARGS+=" --frozen --release"
}

package() {
  cd cosmic-settings-daemon
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
