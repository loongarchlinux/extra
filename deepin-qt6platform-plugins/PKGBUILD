# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt6platform-plugins
pkgver=6.0.12
pkgrel=1
pkgdesc='Qt 6 platform plugins for DDE'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/qt6platform-plugins"
license=('LGPL3')
depends=('qt6-base' 'libx11' 'libxi' 'libxcb' 'xcb-util-wm' 'glibc' 'gcc-libs')
makedepends=('git' 'cmake' 'ninja' 'qt6-tools' 'libglvnd' 'gtest' 'qt6-xcb-private-headers'
             'extra-cmake-modules' 'dtkcommon' 'cairo')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/qt6platform-plugins.git#tag=$pkgver"
        qt-6.7.patch)
sha512sums=('0643b85b1b7b614a64794ae18907208fe9dbe79c032a3432f57558b93b450360e9695f462fad75d94a64e7c233bcf11d0694aa1066d1e4b01f9d1bc08c7a4eed'
            '410bb56570e14abdb961bcc30505ee7a960a978af948bbdfa7130c228fb42d4c827f9bff10887305c62f96066750927bc2dcbceb511516340e893c73cdb4e45e')

prepare() {
  cd qt6platform-plugins
  patch -Np1 -i ../qt-6.7.patch
}

build() {
  cd qt6platform-plugins
  cmake . -GNinja \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_XCB_PRIVATE_HEADERS=/usr/include/qt6xcb-private \
    -DDTK_VERSION=$pkgver
  ninja
}

package() {
  cd qt6platform-plugins
  DESTDIR="$pkgdir" ninja install
}
