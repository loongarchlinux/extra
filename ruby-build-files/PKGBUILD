# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-build-files
pkgver=1.9.0
pkgrel=1
pkgdesc='Abstractions for handling and mapping paths'
arch=(any)
url='https://github.com/ioquatix/build-files'
license=(MIT)
depends=(ruby)
checkdepends=(ruby-covered ruby-sus ruby-bake-modernize ruby-bake-test ruby-bake-test-external)
options=(!emptydirs)
source=(https://github.com/ioquatix/build-files/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('3fd12bf39d9993f8f205d78d8f024099f52012f554de2f80bc2b251d2b323fa0')

prepare() {
  cd build-files-$pkgver
  sed -e 's|~>|>=|' -e '/signing_key/d' -i build-files.gemspec
  sed -i '/bake-gem/d;/bake-github-pages/d;/utopia-project/d' gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd build-files-$pkgver
  gem build build-files.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    build-files-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd build-files-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd build-files-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
