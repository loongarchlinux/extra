# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=recursion-schemes
pkgname=haskell-recursion-schemes
pkgver=5.2.2.5
pkgrel=10
pkgdesc="Representing common recursion patterns as higher-order functions"
url="https://github.com/ekmett/recursion-schemes"
license=('BSD')
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-comonad' 'haskell-data-fix' 'haskell-free' 'haskell-th-abstraction')
makedepends=('ghc' 'uusi' 'haskell-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('74ae862329d7a909e2afd6fe1a996b58b012bd1211fbb17f3eda6ec2f5367f27f6c3c10270dce033ec02911939d366279a2eb1f6eb884cc005e3f48b654c608d')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
