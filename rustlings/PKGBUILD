# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=rustlings
pkgver=6.2.0
pkgrel=1
pkgdesc="Small exercises to get you used to reading and writing Rust code"
arch=('loong64' 'x86_64')
url="https://github.com/rust-lang/rustlings"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
install="rustlings.install"
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('f6c6b9d7bcfbace1fecc37c143afc1ad0109fb7084741a211823c1cb9f65fc9e')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
