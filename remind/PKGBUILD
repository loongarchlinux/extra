# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Sebastian Sontberg <ssontberg@h3c.de>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=remind
pkgver=4.3.7
_pkgver=$(echo "$pkgver" | awk -F. '{ printf "%02d.%02d.%02d", $1, $2, $3 }')
pkgrel=1
pkgdesc='A sophisticated calendar and alarm program.'
arch=('loong64' 'x86_64')
url='https://dianne.skoll.ca/projects/remind/'
license=('GPL2')
optdepends=('tk: for tkremind')
source=("https://dianne.skoll.ca/projects/$pkgname/download/$pkgname-$_pkgver.tar.gz"{,.sig})
options=('!emptydirs')
sha256sums=('b3f8c4b0d5d406280d1cf4c28b77b8576a4650d84adace7f9feb14d68c3b514d'
            'SKIP')
validpgpkeys=('738E4D954052902C147D07B2685A5A5E511D30E2') # Dianne Skoll

build() {
  cd "${pkgname}-${_pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
}
