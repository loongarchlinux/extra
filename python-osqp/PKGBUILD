# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=osqp
pkgname=python-$_pyname
pkgver=0.6.5
pkgrel=2
pkgdesc='Python wrapper for OSQP: the Operator Splitting QP Solver'
url='https://osqp.org/'
license=(Apache-2.0)
arch=(loong64 x86_64)
depends=(python
         python-qdldl
         python-scipy)
makedepends=(cmake
             python-build
             python-installer
             python-setuptools
             python-wheel)
checkdepends=(intel-oneapi-mkl
              python-pytest)
source=(https://pypi.org/packages/source/${_pyname:0:1}/$_pyname/$_pyname-$pkgver.tar.gz)
sha256sums=('b2810aee7be2373add8b6c0be5ad99b810288774abca421751cb032d6a5aedef')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd $_pyname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
