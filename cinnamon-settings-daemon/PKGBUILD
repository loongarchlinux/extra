# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=6.2.0
pkgrel=2
pkgdesc="The Cinnamon Settings daemon"
arch=(loong64 x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(cinnamon-desktop colord dbus-glib libcanberra-pulse libcups
         libgnomekbd libgudev libnotify librsvg libwacom nss polkit
         pulse-native-provider upower)
optdepends=('cinnamon-translations: i18n')
makedepends=(meson samurai xf86-input-wacom glib2-devel)
options=(!emptydirs)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('c2102ace141448694a4d22331f1b039ccdca41988165f0e1af4446c527be89f8ae2f87e0555a0cf4fa5bbb31e5c866f0c9c7a52174a1664183695695b5eed7be')
b2sums=('24b9591f5e6bbacd92f564bfb4f1960c14bca78194dd00746ff4b31f2a6b99d09a701ca939aa39b932141c7c13d998b1a061185bd7b751815dbd8b43d1fceec2')

build() {
  arch-meson --libexecdir=lib/${pkgname} ${pkgname}-${pkgver} build
  samu -C build
}

package() {
  DESTDIR="${pkgdir}" samu -C build install
}
