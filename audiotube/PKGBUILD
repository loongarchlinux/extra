# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=audiotube
pkgver=24.05.2
pkgrel=1
pkgdesc='Client for YouTube Music'
url='https://apps.kde.org/audiotube/'
arch=(loong64 x86_64)
license=(GPL-2.0-or-later
         LGPL-2.0-or-later)
depends=(futuresql
         gcc-libs
         glibc
         kcoreaddons
         kcrash
         ki18n
         kirigami
         kirigami-addons
         kwindowsystem
         purpose
         python
         python-ytmusicapi
         qt6-base
         qt6-declarative
         qt6-imageformats
         qt6-multimedia
         qt6-multimedia-gstreamer
         qt6-svg
         yt-dlp)
makedepends=(extra-cmake-modules
             pybind11
             qcoro-qt6)
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('5a2bb1e9726f41957a628f08a364f04122c1d81b7bb8d28fe790ee4f1f3d3c44'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
