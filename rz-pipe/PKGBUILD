# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=rz-pipe
pkgname=(python-rzpipe)
pkgver=0.6.0
pkgrel=1
pkgdesc='Access rizin via pipe from any programming language'
url='https://github.com/rizinorg/rz-pipe'
arch=('any')
license=('MIT')
makedepends=('python' 'python-setuptools' 'rizin')
source=(${url}/archive/python-v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('37fb091fa305ecdb0417bc500189fb52799ac7b5118739b8f9b094f84122d2f82c0ad879c08a02696f3ae17a40cb09b84d6ca574d8a4d8f6467916a48fdc0096')
b2sums=('97487f0d02571086bea5125a8cfcd16a195b7abc372f32825f67a3d5a173af6851c8634b56ba7acbd5d897d0c9f695bb4274afd6884621e7a662a8f4b29aa4db')

build() {
  cd rz-pipe-python-v${pkgver}/python
  python setup.py build
}

package_python-rzpipe() {
  depends=('python' 'rizin')
  cd rz-pipe-python-v${pkgver}/python
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
