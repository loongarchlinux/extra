# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-cangjie
pkgver=0.0.0.20240325
_commit=0ac8452eeb4abbcd8dd1f9e7314012310743285f
pkgrel=1
pkgdesc="Cangjie input for rime"
arch=('loong64' 'x86_64')
url="https://github.com/rime/rime-cangjie"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-luna-pinyin')
makedepends=('git' 'librime' 'rime-prelude' 'rime-essay')
source=("git+https://github.com/rime/rime-cangjie.git#commit=$_commit")
sha512sums=('6b88013c581d66ee156fd23c5f9f25c77636dd6dc8c517eb5cfe447b0d2340042bb90c3620656909238e0d9a7e30432abecaab2c2efba95f5460aaabd5334498')

prepare() {
  cd $pkgname
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
