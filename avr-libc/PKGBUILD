# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

# Build order: avr-binutils -> avr-gcc -> avr-libc

pkgname=avr-libc
pkgver=2.2.0
pkgrel=1
pkgdesc='The C runtime library for the AVR family of microcontrollers'
arch=(any)
url='https://github.com/avrdudes/avr-libc'
license=(BSD)
depends=(avr-gcc)
makedepends=(python)
options=(!strip) # it corrupts avr libraries
_tag=avr-libc-${pkgver//./_}-release
source=(https://github.com/avrdudes/avr-libc/archive/refs/tags/$_tag.tar.gz)
b2sums=('c4358095c2d43493172e7deff4d5aa942bf1d5c7dcfcc09814f3926d0202fc6ea4f35c7a7a5f67675718c5abbf652b86ccaa48323d3b6aa1763922a05d57ace2')
validpgpkeys=('56628323218C669FF578705C7E9EADC3030D34EB') # Joerg Wunsch

build() {
  cd avr-libc-$_tag
  ./bootstrap
  ./configure --build=$(./config.guess) --host=avr --prefix=/usr
  make
}

package() {
  cd avr-libc-$_tag
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
