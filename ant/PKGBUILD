# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgbase=ant
pkgname=('ant' 'ant-doc')
pkgver=1.10.14
pkgrel=2
pkgdesc='Java based build tool'
url='https://ant.apache.org/'
arch=('any')
license=('Apache-2.0')
makedepends=('bash' 'coreutils' 'which' 'java-environment' 'junit' 'java-hamcrest')
source=(https://www.apache.org/dist/ant/source/apache-${pkgbase}-${pkgver}-src.tar.bz2{,.asc}
        ant.conf)
sha256sums=('c451b8abca000892ef82f9e23ab26b02369973f5ca76fdf2b8d8fa258debffef'
            'SKIP'
            '23bbef577b56d48adb1985dbd9795e5533146646f1e8bb879dd061a4014ffcf2')
# https://downloads.apache.org/ant/KEYS
validpgpkeys=(
  'CE8075A251547BEE249BC151A2115AE15F6B8B72' # Stefan Bodewig <bodewig@apache.org>
  '8DA70C00DF7AF1B0D2F9DC74DDBCC1270A29D081' # jaikiran@apache <jaikiran@apache.org>
  '0A123C1ED3F13A6A0140E166C71FB765CD9DE313' # Jaikiran Pai (CODE SIGNING KEY) <jaikiran@apache.org>
)

_replace_lib() {
  # explicitly call rm to ensure we replace instead of add
  rm "$2"
  ln -s "$1" "$2"
}

prepare() {
  cd apache-${pkgbase}-${pkgver}
  sed -i 's|/usr/bin/python|/usr/bin/python2|' src/script/runant.py
}

build() {
  cd apache-${pkgbase}-${pkgver}
  export JAVA_HOME=/usr/lib/jvm/default
  ./bootstrap.sh
  bootstrap/bin/ant -Ddest=optional -f fetch.xml
  _replace_lib /usr/share/java/junit.jar lib/optional/junit-4.13.2.jar
  _replace_lib /usr/share/java/hamcrest-core.jar lib/optional/hamcrest-core-1.3.jar
  _replace_lib /usr/share/java/hamcrest-library.jar lib/optional/hamcrest-library-1.3.jar
  bootstrap/bin/ant dist
}

package_ant() {
  depends=('java-environment' 'bash' 'coreutils' 'which')
  optdepends=('junit: junit tasks'
              'java-hamcrest: junit tasks')
  replaces=('apache-ant')
  provides=('apache-ant')
  conflicts=('apache-ant')
  backup=('etc/ant.conf')

  cd apache-${pkgbase}-${pkgver}/apache-${pkgbase}-${pkgver}
  local _ant_home=/usr/share/ant

  install -d "${pkgdir}${_ant_home}"
  cp -Rp etc "${pkgdir}${_ant_home}"

  find bin -type f -a ! -name \*.bat -a ! -name \*.cmd \
    -exec install -Dm 755 {} -t "${pkgdir}${_ant_home}/bin" \;
  install -d "${pkgdir}/usr/bin"
  ln -s /usr/share/ant/bin/ant "${pkgdir}/usr/bin/ant"

  install -Dm 644 "${srcdir}/ant.conf" -t "${pkgdir}/etc"
  install -Dm 644 lib/*.jar -t "${pkgdir}/usr/share/java/ant"
  ln -s /usr/share/java/ant "${pkgdir}${_ant_home}/lib"

  install -Dm 644 ../{LICENSE,NOTICE} -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_ant-doc() {
  pkgdesc='Apache Ant build tool documentation'
  replaces=('apache-ant-doc')
  provices=('apache-ant-doc')
  conflicts=('apache-ant-doc')

  install -d "${pkgdir}/usr/share/doc/ant"
  cp -r apache-${pkgbase}-${pkgver}/apache-${pkgbase}-${pkgver}/manual/* \
    "${pkgdir}/usr/share/doc/ant"
}

# vim: ts=2 sw=2 et:
