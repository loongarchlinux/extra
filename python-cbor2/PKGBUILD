# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_name=cbor2
pkgname=python-cbor2
pkgver=5.6.3
pkgrel=3
pkgdesc="Pure Python CBOR (de)serializer with extensive tag support"
arch=(loong64 x86_64)
url="https://github.com/agronholm/cbor2"
license=(MIT)
depends=(glibc
         python)
makedepends=(python-{build,installer,wheel}
             python-setuptools-scm
             python-toml)
checkdepends=(python-hypothesis
              python-pytest)
source=($url/archive/refs/tags/$pkgver/$_name-$pkgver.tar.gz)
sha512sums=('3636bcbf9156c78d9ed2b5cc95d6a1df991e9d9d1eb1d9e8f956d3edbe1cbc5cdb8f727785d7045119ff393e9dc139e170e7d415e32f8c3ddb6835669122b9c8')
b2sums=('9c21cd5eec1210f75e508df7d912890b3ec660d2adc6a1868d8317c8cfd4d0103960649c299ee97cceae447b2f38d11200b895616758723c901807e88ca0d8b8')

build() {
  cd $_name-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build -wn
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v -c /dev/null tests
}

package() {
  cd $_name-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
