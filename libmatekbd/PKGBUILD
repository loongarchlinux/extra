# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=libmatekbd
pkgver=1.28.0
pkgrel=1
pkgdesc="MATE keyboard library"
url="https://mate-desktop.org"
arch=('loong64' 'x86_64')
license=('LGPL')
depends=('gtk3' 'gettext' 'libxklavier') 
makedepends=('python')
conflicts=('libmatekbd-gtk3')
replaces=('libmatekbd-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('5d2e58483c2b23d33503d24c88f8b90a28cc0189d7e4001b3e273a604f6fe80e')

build() {
    	cd "$pkgname-$pkgver"
    	./configure \
        	--prefix=/usr 

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    	make
}

package_libmatekbd() {
	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
