# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-grequests
_libname=grequests
pkgver=0.7.0
pkgrel=2
pkgdesc="Allows you to use Requests with Gevent to make asyncronous HTTP Requests easily."
url="https://pypi.python.org/pypi/grequests/"
license=('custom: ISC')
arch=('any')
depends=(python-requests python-gevent)
makedepends=(python-setuptools python-wheel python-build python-installer)
source=(https://files.pythonhosted.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz
        $_libname-$pkgver-LICENSE::https://github.com/kennethreitz/grequests/raw/v$pkgver/LICENSE)
sha256sums=('5c33f14268df5b8fa1107d8537815be6febbad6ec560524d6a404b7778cf6ba6'
            '4063882a8913e4b37457d58fe6600bd975ba053c8db2e8c6b218e7c9c8575ca8')

build() {
    cd "$srcdir"/$_libname-$pkgver
    python -m build --wheel --no-isolation
}

package_python-grequests() {
    cd "$srcdir"/$_libname-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -m0644 -D "$srcdir"/$_libname-$pkgver-LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
