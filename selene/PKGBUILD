# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=selene
pkgver=0.27.1
pkgrel=1
pkgdesc='Blazing-fast modern Lua linter written in Rust'
arch=('loong64' 'x86_64')
url=https://github.com/Kampfkarren/selene
license=('MPL-2.0')
depends=('gcc-libs' 'glibc' 'openssl')
makedepends=('cargo' 'git')
conflicts=("$pkgname-linter")
replaces=("$pkgname-linter")
options=('!lto')
source=("git+$url.git#tag=$pkgver")
b2sums=('b6773d6766145355c080d4807e31854b3b56a0d47ee43f68edf205842a39b5c00edacfe93f5ccf07aa27b471614975cbdea631e3f361e5e6ef5ed37ab040ef00')

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  cargo build --release --locked --offline
}

check() {
  cd $pkgname
  cargo test --locked --offline
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
}

# vim:set ts=2 sw=2 et:
