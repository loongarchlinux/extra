# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-calculator
pkgver=8.0.0
pkgrel=1
pkgdesc='The Pantheon Calculator'
arch=(loong64 x86_64)
url=https://github.com/elementary/calculator
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libgranite-7.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=1b9ed3b626d03cd14c06d53daac7070c6a69ad59
source=(pantheon-calculator::git+https://github.com/elementary/calculator.git#tag=${_tag})
sha256sums=('e06e072cccee8d6f05f681991871b49a67769ad39ee736ff82dd4e47d18e0a8a')

pkgver() {
  cd pantheon-calculator
  git describe --tags
}

build() {
  arch-meson pantheon-calculator build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
