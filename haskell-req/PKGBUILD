# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=req
pkgname=haskell-req
pkgver=3.13.1
pkgrel=8
pkgdesc="HTTP client library"
url="https://github.com/mrkkrp/req"
license=('BSD')
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-authenticate-oauth' 'haskell-blaze-builder'
         'haskell-case-insensitive' 'haskell-crypton-connection' 'haskell-http-api-data'
         'haskell-http-client' 'haskell-http-client-tls' 'haskell-http-types' 'haskell-modern-uri'
         'haskell-monad-control' 'haskell-retry' 'haskell-transformers-base' 'haskell-unliftio-core')
makedepends=('git' 'ghc' 'haskell-quickcheck' 'haskell-hspec' 'haskell-hspec-core')
source=("git+https://github.com/mrkkrp/req#tag=$pkgver")
sha512sums=('f08c2026afde72503e1e0de090e1e8c9cab940f12036cc8599b17dae0be72862c99aa0da38607378d4de3a144d02537b62c6e5aa835609f8a0f41a01cbbf5c3e')

prepare() {
  cd $_hkgname
  # Fix tests
  git cherry-pick -n f7c3b88e1989df250b62bb1a8595c422043bea45
  git cherry-pick -n 0f7692ad76a795ffa7920482bec1a90de97a1891
  # httpbin.org is very unstable, httpbun.org seems to be a typo
  # sed -i 's|httpbun.org|httpbin.jsteelkw.dev|' httpbun-tests/Network/HTTP/ReqSpec.hs
}

build() {
  cd $_hkgname
    
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
