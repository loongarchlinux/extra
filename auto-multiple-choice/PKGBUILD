# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Baptiste Jonglez
# Contributor: Benoit LANDRIEU
# Contributor: Thomas LEGRAND
# Contributor: Alexis Bienvenue
# Contributor: fabmen

pkgname=auto-multiple-choice
pkgver=1.6.0
pkgrel=3
pkgdesc="AMC is a piece of software that can help you creating and managing multiple choice questionnaires (MCQ), with automated marking. Answers sheets are formatted with LaTeX."
arch=('loong64' 'x86_64')
url='https://www.auto-multiple-choice.net/'
license=('GPL2')
depends=('perl' 'perl-gtk3' 'perl-glib-object-introspection' 'perl-locale-gettext'
         'perl-xml-writer' 'perl-xml-simple' 'perl-dbd-sqlite' 'perl-file-mimeinfo'
         'perl-text-csv' 'perl-dbi' 'perl-archive-zip' 'perl-clone' 'perl-locale-codes'
         'ttf-linux-libertine'
         'texlive-latex'
         'opencv' 'poppler' 'poppler-glib')
optdepends=('perl-email-sender: allow email sending (1 of 3)'
            'perl-module-pluggable: allow email sending (2 of 3)'
            'perl-email-mime: allow email sending (3 of 3)')
makedepends=('netpbm' 'xorgproto' 'libxslt')
source=("https://download.auto-multiple-choice.net/auto-multiple-choice_${pkgver}_dist.tar.gz"{,.sig})
sha256sums=('23d5f0d4137c6524218b91754776b11c1284ead9da08d93c939b6db3a2e83236'
            'SKIP')
validpgpkeys=('1F0FAB7CC69ACAD8374DB119D3F94059A398971E') # Alexis Bienven√ºe <palnch@passoire.fr>

prepare() {
  cd "$srcdir"/$pkgbase-$pkgver
  # PERLDIR hack to conform to https://wiki.archlinux.org/index.php/Perl_package_guidelines#Package_file_placement
  # We can't edit Makefile.conf because it triggers a rebuild of precompiled doc targets.
  # So just overwrite PERLDIR after Makefile.Conf is included in the main Makefile.
  sed -i -e '/^include Makefile-all.conf/aPERLDIR="/usr/share/perl5/vendor_perl"' Makefile
}

build() {
  cd "$srcdir"/$pkgbase-$pkgver
  make all_precomp \
    GCC_OPENCV="$(pkg-config --cflags opencv4)" \
    GCC_OPENCV_LIBS="$(pkg-config --libs opencv4)"
}

package() {
  cd "$srcdir"/$pkgbase-$pkgver
  make DESTDIR="$pkgdir/" install
}
