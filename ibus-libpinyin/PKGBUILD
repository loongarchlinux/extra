# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor:  Yangtse Su<yangtsesu@gmail.com>
# Contributor:  Jekyll Wu<adaptee [at] gmail [dot] com>
# Contributor: riverscn<riverscn at gmail.com>
# Contributor: rainy<rainylau at gmail.com>
# Contributor: Lee.MaRS<leemars at gmail.com>

pkgname=ibus-libpinyin
pkgver=1.15.4
_commit=a4135b9cc686f2c269d535dba2602ebd112aca75
pkgrel=2
pkgdesc="Intelligent Pinyin engine based on libpinyin for IBus"
arch=('loong64' 'x86_64')
license=('LGPL')
url="https://github.com/libpinyin/ibus-libpinyin"
depends=('ibus' 'libpinyin' 'libsoup3' 'lua' 'opencc' 'python-pyxdg')
makedepends=('boost' 'git' 'gnome-common' 'intltool')
source=("git+https://github.com/libpinyin/ibus-libpinyin.git#commit=$_commit"
ibus-libpinyin-gcc14.patch)
sha512sums=('0765cd25e37f70039f362a7b9e93013eb34956359f8c9cbac8c4090669092344e18eb8ea4f0ce57bc3916a19d2aaa07f6b51a399cf9590af5acfcc8809488e17'
            'c72bc9f4e1c67ea9af3f531c7fb2e90b1979df8080712a41a3f05c7958ecd3289a9912d9a1cccb501b8e8c812442fcc82547e54b442f51ab266537f0bf50e969')

prepare() {
  cd "$pkgname"
  patch -p1 -i "$srcdir/ibus-libpinyin-gcc14.patch"
}

build() {
  cd $pkgname
  ./autogen.sh --prefix=/usr --libexecdir=/usr/lib/$pkgname --enable-boost --enable-opencc --enable-cloud-input-mode
  make
}

package() {
  cd $pkgname
  make NO_INDEX=true DESTDIR="$pkgdir" install
}
