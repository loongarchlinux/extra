# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-locale
pkgver=2.5.9.r177.9e8cd84
pkgrel=1
pkgdesc='Switchboard Locale Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-locale
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  accountsservice
  dconf
  glib2
  glibc
  gnome-desktop-4
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libibus
  libswitchboard-3.so
  polkit
)
makedepends=(
  cmake
  git
  meson
  vala
)
_commit=9e8cd84653d4de78117fc25a6aaefbf763714dc1
source=(git+https://github.com/elementary/switchboard-plug-locale.git#commit=${_commit})
b2sums=('7c56394722f90b0246ffe56abab488fedd3006bf567ff40df0574c83c66f0e69bde3bced0972570a7a96e758d85dc87f710174be4476fb3c7d0f034a5c279713')

pkgver() {
  cd switchboard-plug-locale
  git describe --tags | sed 's/-/.r/; s/-g/./; s/2.5.8/2.5.9/'
}

build() {
  arch-meson switchboard-plug-locale build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
