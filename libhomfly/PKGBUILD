# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libhomfly
pkgver=1.02r6
pkgrel=3
pkgdesc='Library to compute the homfly polynomial of a link'
arch=(loong64 x86_64)
url='https://github.com/miguelmarco/libhomfly'
license=(LicenseRef-libhomfly)
depends=(gc
         glibc)
makedepends=(git)
source=(git+https://github.com/miguelmarco/libhomfly#tag=$pkgver)
sha256sums=('b601ec59b4e832f1306967fdf906e10a7895d1726aa208829c3137c57bebef39')

prepare() {
  cd $pkgname
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
