# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=js80p
pkgver=3.1.1
pkgrel=1
pkgdesc='A MIDI driven, performance oriented, versatile synthesizer'
arch=('loong64' 'x86_64')
url='https://attilammagyar.github.io/js80p/'
license=('GPL-3.0-only')
groups=('pro-audio' 'vst3-plugins')
depends=(
  'glibc'
  'gcc-libs'
  'libxcb'
  'cairo'
)
makedepends=(
  'git'
  'alsa-lib'
  'libx11'
  'zenity'
  'kdialog'
  'cppcheck'
)
install=js80p.install
source=("$pkgname::git+https://github.com/attilammagyar/js80p#tag=v$pkgver")
sha512sums=('3fb858d212040bea951ff4cf7cf32037a6b3b3cf745421f8dd7987b92e990b3a89a01aad97b622f656e8ed0dd9abb40840099e9cf0b0f81e6e776fed4d95d3be')
b2sums=('2478479737e81c2acaee3c446186813186b9ab45d404e5fe810f1ebe74eb2d8ad8d4035e09c0b4a2414275189bcd0d3fd7435500f656e27a50faf18ccb78a220')

build() {
  cd "$pkgname"

  # supports AVX, but that's not in x86_64_v1
  make \
    SYS_LIB_PATH=/usr/lib \
    TARGET_PLATFORM="$CARCH-gpp" \
    INSTRUCTION_SET=sse2 \
    VERSION_STR=$(git describe --tags) \
    VERSION_INT=$(git describe --tags | sed "s/[^0-9]//g") \
    vst3
}

package() {
  cd "$pkgname"

  # vst3
  install -vDm644 \
    "dist/js80p-dev-linux-$CARCH-sse2-vst3_single_file/js80p.vst3" \
    "$pkgdir/usr/lib/vst3/$pkgname.vst3/Contents/$CARCH-linux/$pkgname.so"

  # presets
  install -vDm644 -t "$pkgdir/usr/share/$pkgname/presets" presets/*.js80p

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
