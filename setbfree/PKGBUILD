# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Johannes Maibaum <jmaibaum at gmail dot com>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

_name=setBfree
pkgbase=setbfree
pkgname=(setbfree setbfree-common setbfree-lv2 setbfree-standalone)
pkgver=0.8.13
pkgrel=1
pkgdesc="DSP tonewheel organ"
arch=(loong64 x86_64)
url="https://setbfree.org"
license=(GPL-2.0-or-later)
makedepends=(
  cairo
  ftgl
  gendesk
  glib2
  jack
  libglvnd
  liblo
  libx11
  lv2
  pango
  ttf-dejavu
)
checkdepends=(lv2lint)
source=(
  https://github.com/pantherb/$_name/archive/v$pkgver/$_name-$pkgver.tar.gz
)
sha512sums=('d891d721e2c9d13cf2f876d290a3aa4c3c0e52565973b59a68227a9b886d445ae04e6cb451f5b049cd521b76475a63d70dbf231a54ee7f13cc2d330faf7b7be6')
b2sums=('611395ecebed1a30375e8788fcf3a9367e7d0f72b73481e7b22983c5fbacc40d0a6be33f4c62cf9a95d298db28f8852bc7030cfe6d21b3e28396b6550755c899')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  gendesk -n \
          --pkgname "${pkgbase}" \
          --name "${_name}" \
          --exec "${_name}UI" \
          --icon "${_name}" \
          --genericname "${pkgdesc}" \
          --pkgdesc "${pkgdesc}" \
          --categories "AudioVideo;Audio"
  gendesk -n \
          --pkgname x42-whirl \
          --name x42-whirl \
          --genericname "Leslie speaker emulation" \
          --pkgdesc "Leslie speaker emulation as JACK client" \
          --categories "AudioVideo;Audio"
}

build() {
  export FONTFILE=/usr/share/fonts/TTF/DejaVuSans-Bold.ttf
  make PREFIX=/usr -C $_name-$pkgver
}

check() {
  lv2lint -Mpack -I $_name-$pkgver/b_synth "http://gareus.org/oss/lv2/b_synth"
  lv2lint -Mpack -I $_name-$pkgver/b_whirl "http://gareus.org/oss/lv2/b_whirl#simple"
  lv2lint -Mpack -I $_name-$pkgver/b_whirl "http://gareus.org/oss/lv2/b_whirl#extended"
}

package_setbfree() {
  depends=(
    "setbfree-lv2=$pkgver"
    "setbfree-standalone=$pkgver"
  )

  make PREFIX=/usr DESTDIR="$pkgdir" install -C $_name-$pkgver
  install -vDm 644 $_name-$pkgver/{AUTHORS,ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"

  (
    cd "$pkgdir"
    _pick $pkgbase-common usr/share

    _pick $pkgbase-lv2 usr/lib/lv2

    _pick $pkgbase-standalone usr/bin
  )
}

package_setbfree-common() {
  pkgdesc+=" common files"
  mv -v $pkgname/* "$pkgdir"
}

package_setbfree-lv2() {
  pkgdesc+=" LV2 plugin"
  depends=(
    cairo
    ftgl
    glibc
    glib2 libgobject-2.0.so
    libglvnd
    libx11
    lv2-host
    pango libpango-1.0.so libpangocairo-1.0.so
    setbfree-common
  )
  groups=(
    lv2-plugins
    pro-audio
  )

  mv -v $pkgname/* "$pkgdir"
}

package_setbfree-standalone() {
  pkgdesc+=" JACK standalone"
  depends=(
    cairo
    ftgl
    glib2 libgobject-2.0.so
    glibc
    jack
    libglvnd
    liblo
    libx11
    pango libpango-1.0.so libpangocairo-1.0.so
    setbfree-common
  )
  groups=(
    pro-audio
  )

  mv -v $pkgname/* "$pkgdir"

  # xdg desktop files
  install -vDm 644 *.desktop -t "$pkgdir/usr/share/applications"
  # icons
  install -vDm 644 $_name-$pkgver/doc/{$_name,x42-whirl}.png -t "$pkgdir/usr/share/pixmaps/"
  # man
  install -vDm 644 $_name-$pkgver/doc/*.1 -t "$pkgdir/usr/share/man/man1/"
}
