# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Maintainer: Mantas MikulÄ—nas <grawity@gmail.com>
pkgname=mokutil
pkgver=0.7.0
pkgrel=1
pkgdesc="Shim.efi Machine Owner Key management tool"
arch=(i686 loong64 x86_64)
url="https://github.com/lcp/mokutil"
license=('GPL-3.0-or-later')
depends=(efivar openssl keyutils libxcrypt)
makedepends=(git)
_commit=c361087100fbb6955f32a9f364dee21b24724fb4
source=("git+https://github.com/lcp/mokutil#commit=$_commit")
sha256sums=('SKIP')
validpgpkeys=()

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/-/.r/; s/-/./'
}

prepare() {
  cd "$pkgname"
  autoreconf -f -i
}

build() {
  cd "$pkgname"
  ./configure \
    --prefix=/usr           \
    --sbindir=/usr/bin      \
    --sysconfdir=/etc       ;
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
}

# vim: ts=2:sw=2:et
