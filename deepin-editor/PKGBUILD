# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-editor
pkgver=6.0.17
pkgrel=2
pkgdesc='Simple editor for Deepin'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-editor"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkcore' 'dtkgui' 'dtkwidget' 'deepin-qt-dbus-factory'
         'gcc-libs' 'icu' 'qt5-base' 'qt5-svg'
         'kcodecs5' 'syntax-highlighting5' 'libchardet' 'uchardet')
makedepends=('git' 'cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-editor/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('374fb29654d83e243a8236904f2a61e11309df8e76e7a9b5fc7dfb79fc0dead3c9e173cc3313583ca82ea48d7ddf742494e3f70ed3eda046c7c498930007fe6d')

build() {
  cd deepin-editor-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DVERSION=$pkgver .
  ninja
}

check() {
  cd deepin-editor-$pkgver
  # Tests not available unless in debug mode
  #ninja test || echo "Tests failed"
}

package() {
  cd deepin-editor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
