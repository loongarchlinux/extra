# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-sys-virt
pkgver=10.2.0
pkgrel=1
pkgdesc="Represent and manage a libvirt hypervisor connection"
arch=('loong64' 'x86_64')
license=('PerlArtistic')
url="https://metacpan.org/release/Sys-Virt"
depends=('libvirt' 'perl')
makedepends=('perl-cpan-changes' 'perl-module-build' 'perl-xml-xpath' 'perl-test-pod'
             'perl-test-pod-coverage')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/D/DA/DANBERR/Sys-Virt-v$pkgver.tar.gz")
sha512sums=('c362e37511aff879b08a67b6337cee6d20c197fea2bb7f6ce023eafa642f53029744d704250533772fd48448ff39ea49027b83563cd9821dcd22d3621b2372ef')

build() {
  cd Sys-Virt-v$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Sys-Virt-v$pkgver
  make test
}

package() {
  cd Sys-Virt-v$pkgver
  make DESTDIR="$pkgdir" install
}
