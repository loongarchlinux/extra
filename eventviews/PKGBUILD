# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=eventviews
pkgver=24.02.2
pkgrel=1
pkgdesc='Library for creating events'
arch=(loong64 x86_64)
url='https://kontact.kde.org'
license=(LGPL-2.0-or-later)
depends=(akonadi-calendar
         calendarsupport
         gcc-libs
         glibc
         kcalendarcore
         kcalutils
         kcodecs
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kdiagram
         kguiaddons
         kholidays
         ki18n
         kiconthemes
         kitemmodels
         kwidgetsaddons
         libakonadi
         libkdepim
         qt6-base)
makedepends=(doxygen
             extra-cmake-modules
             qt6-doc
             qt6-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('52ac97dcd270e2e0c1389b71f97457492330d4c9f5e46a4d8b765b695e8f55ed'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
