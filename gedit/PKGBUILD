# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=47.0
pkgrel=1
pkgdesc="GNOME Text Editor"
url="https://gedit-technology.github.io/apps/gedit/"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  bash
  cairo
  dconf
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gobject-introspection-runtime
  gsettings-desktop-schemas
  gspell
  gtk3
  hicolor-icon-theme
  libgedit-amtk
  libgedit-gfls
  libgedit-gtksourceview
  libgedit-tepl
  libgirepository
  libpeas
  pango
  python
  python-gobject
)
makedepends=(
  appstream-glib
  desktop-file-utils
  git
  gobject-introspection
  gtk-doc
  meson
  vala
  yelp-tools
)
optdepends=('gedit-plugins: Additional features')
conflicts=('gedit-code-assistance<=3.16.0+4+gd19b879-1')
groups=(gnome-extra)
source=(
  "git+https://gitlab.gnome.org/GNOME/gedit.git#tag=${pkgver/[a-z]/.&}"
  "git+https://gitlab.gnome.org/GNOME/libgd.git"
)
b2sums=('8a38d15cac226063dcd0e80b78a3f861ec94f16eb5a68ec7c81894656c6e859c036b4528ecc021cdf13b7bcea5c4aa1c79d25231aceea55f1a1e9cedf9dab387'
        'SKIP')

prepare() {
  cd gedit

  git submodule init
  git submodule set-url subprojects/libgd "$srcdir/libgd"
  git -c protocol.file.allow=always submodule update
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D require_all_tests=true
  )

  arch-meson gedit build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
