# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Douglas Soares de Andrade
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=varnish
pkgver=7.5.0
pkgrel=1
pkgdesc="High-performance HTTP accelerator"
arch=('loong64' 'x86_64')
url="https://www.varnish-cache.org/"
license=('BSD')
depends=('gcc' 'libnsl' 'pcre2')
makedepends=('python-docutils' 'python-sphinx' 'git')
optdepends=('python: needed for vmod development')
backup=('etc/varnish/default.vcl')
install=$pkgname.install
source=("https://varnish-cache.org/_downloads/varnish-$pkgver.tgz"
        "git+https://github.com/varnishcache/pkg-varnish-cache.git#commit=a5df068")
sha512sums=('ca77abcb21299942b2bfd433e5f964d2e974cdae92d6a8889217fcd81933a3b7bc1e70ba87d7e842a4f90b59d7948242155380054d67ad49aab8dcea343055a2'
            'ef6433ba3eed89fb35b6d70cf5acb26ffed35c50127837bd78486e616c8b15fc3f6506888cab50c9a5372ab9f2d05e9e0a0dfac93b54dc605f918b527b0238b3')

build() {
  cd "varnish-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var/lib \
    --sbindir=/usr/bin

  make
}

check() {
  cd "varnish-$pkgver"

  rm bin/varnishtest/tests/m00000.vtc
  make check
}

package() {
  cd "varnish-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/pkg-varnish-cache/systemd/varnish.service" "$pkgdir/usr/lib/systemd/system/varnish.service"
  install -Dm644 "$srcdir/pkg-varnish-cache/systemd/varnishncsa.service" "$pkgdir/usr/lib/systemd/system/varnishncsa.service"
  install -Dm755 "$srcdir/pkg-varnish-cache/systemd/varnishreload" "$pkgdir/usr/bin/varnishreload"
  install -Dm755 "$srcdir/pkg-varnish-cache/systemd/varnish.logrotate" "$pkgdir/etc/logrotate.d/varnish"
  install -Dm755 "$srcdir/pkg-varnish-cache/systemd/varnish.sysusers" "$pkgdir/usr/lib/sysusers.d/varnish.conf"

  # config
  install -Dm644 "etc/example.vcl" "$pkgdir/etc/varnish/default.vcl"

  # license
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
