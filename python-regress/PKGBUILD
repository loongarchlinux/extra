# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-regress
_name=regress
pkgver=2024.8.1
pkgrel=1
pkgdesc="Python bindings to the Rust regress crate which provides ECMA regular expressions"
arch=(loong64 x86_64)
url="https://github.com/crate-py/regress"
license=(MIT)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-maturin
  python-wheel
)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('acd0362866ad6a38a5d4f1ff7b45f6d97681b41302de0da058d0eb0f3f9db42ea425dbd380aedbbfb1fbec706be5baaae0cc451703b4d54780ae80e51c75e51a')
b2sums=('c6c95467dd17659c158bfd971b6d06acd57d5cfd56e6d14ab9e16b2bbdfe9ab33e10fefa81be023d7d8bd2a67b982bfdd1c380008f41fe92b0cbeb1e0aa8790f')

prepare() {
  cd $_name-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  local python_build_options=(
    --wheel
    --no-isolation
    -C="--target=$(rustc -vV | sed -n 's/host: //p')"
    -C="--locked"
    -C="--release"
    -C="--all-features"
    -C="--strip"
  )

  cd $_name-$pkgver
  python -m build "${python_build_options[@]}"
}

check() {
  local pytest_options=(
    -vv
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
