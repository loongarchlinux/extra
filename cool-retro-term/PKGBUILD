# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Doug Newgard <scimmia at archlinux dot info>
# Contributor: Andrey Mivrenik <gim at fastmail dot fm>
# Contributor: Glen Oakley <goakley123@gmail.com>

pkgname=cool-retro-term
pkgver=1.2.0
pkgrel=3
pkgdesc='A good looking terminal emulator which mimics the old cathode display'
arch=('loong64' 'x86_64')
url='https://github.com/Swordfish90/cool-retro-term'
license=('GPL3')
depends=('qmltermwidget' 'qt5-quickcontrols' 'qt5-quickcontrols2' 'qt5-graphicaleffects' 'hicolor-icon-theme')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Swordfish90/cool-retro-term/archive/$pkgver.tar.gz"
        0001-nvidia-fix.patch)
sha256sums=('68bd3137439941e6253a97c7dcd27b553ce4d76c8c38e7f8db63db4d6cc7000b'
            '2dacc4c110283facbeff7f0ce69b5edb2feba41c9d4761e9f5703e17df45f74b')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Don't build vendored qmltermwidget as we package it separately.
  sed -i '/qmltermwidget/d' cool-retro-term.pro

  patch -Np1 -i "$srcdir"/0001-nvidia-fix.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  qmake
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make INSTALL_ROOT="$pkgdir" install
}
