# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Eric Bailey <nerflad@gmail.com>
# Contributor: Bruno Pagani <bruno.n.pagani@gmail.com>

pkgname=toilet
_commit=3eb9d58037afb0a1baec6dca82caa045fb2217c0
pkgver=0.3.r155.${_commit:0:7}
pkgrel=3
pkgdesc='Free replacement for the FIGlet utility'
arch=(loong64 x86_64)
url=https://github.com/cacalabs/toilet
license=(WTFPL)
depends=(libcaca)
makedepends=(git)
source=("git+$url.git#commit=$_commit")
b2sums=('166f5145416ca3cd1b646be5f42c9bf14dd1353a81b3a2b2e3462dddab834bf0369d2329216246360b71570f0292a58ed3ec40e593bf222d125cd88634c98dd5')

build() {
  cd $pkgname
  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname COPYING
}
