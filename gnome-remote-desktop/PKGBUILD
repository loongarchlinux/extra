# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-remote-desktop
pkgver=46.1
pkgrel=1
pkgdesc="GNOME Remote Desktop server"
url="https://wiki.gnome.org/Projects/Mutter/RemoteDesktop"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  freerdp
  fuse3
  gcc-libs
  glib2
  glibc
  libdrm
  libei
  libepoxy
  libfdk-aac
  libnotify
  libpipewire
  libsecret
  libvncserver
  libxkbcommon
  opus
  pipewire
  polkit
  systemd
  systemd-libs
  tpm2-tss
)
makedepends=(
  asciidoc
  ffnvcodec-headers
  git
  meson
  python-packaging
)
checkdepends=(
  dbus-broker
  libegl
  mutter
  python-dbus
  python-gobject
  wireplumber
)
groups=(gnome)
source=("git+https://gitlab.gnome.org/GNOME/gnome-remote-desktop.git?signed#tag=${pkgver/[a-z]/.&}")
b2sums=('7204bc6b9c04308f406284345b734bddbd0a170cbe3ed16fc9034b4aa76750ae63f2b853a8b7e92f37c77309a551c1f416122aa184584b4d46b7bc222804326c')
validpgpkeys=(
  8307C0A224BABDA1BABD0EB9A6EEEC9E0136164A # Jonas Ã…dahl <jadahl@gmail.com>
)

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D vnc=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs -t 3
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
