# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Firegem <mrfiregem [at] protonmail [dot] ch>

pkgname=taplo-cli
_name=${pkgname%-cli}
pkgver=0.9.1
pkgrel=1
pkgdesc='TOML toolkit written in Rust'
arch=('loong64' 'x86_64')
url=https://taplo.tamasfe.dev/cli/introduction.html
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo' 'git')
provides=("$_name")
options=('!lto')
source=("git+https://github.com/tamasfe/$_name.git#tag=release-$_name-cli-$pkgver")
b2sums=('SKIP')

prepare() {
  cd "$_name"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$_name"
  cargo build --release --locked --offline --all-features
}

package() {
  cd "$_name"
  install -Dt "$pkgdir"/usr/bin target/release/"$_name"
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.md
}
