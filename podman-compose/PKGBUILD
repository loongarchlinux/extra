# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=podman-compose
pkgver=1.2.0
pkgrel=1
pkgdesc='A script to run docker-compose.yml using podman'
arch=(any)
url='https://github.com/containers/podman-compose'
license=(GPL-2.0-only)
depends=(
  podman
  python
  python-dotenv
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-parameterized
)
optdepends=(
  'aardvark-dns: resolve hostnames of linked containers'
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('2b45bcb331e334d362a0906563377e1514aca95f50c575e4b53039e58394b39af1ceeb606d90c652ef6d8cc5e8764a27edf11fc9782a89b186812a474b583579')
b2sums=('ab1f458cef6db99c5727a6676cd1a15ccc58108a14763b9bf8e1093bc674e1adbee21c81744a0cb7798be6134e7b566a4ed9222be71bda0d8f7e4568b3d2fcb7')

build() {
  cd $pkgname-$pkgver

  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    # ignore tests where podman is called (as we can not run the tests due to missing subuid/subgid entries)
    --ignore tests/test_podman_compose.py
    --ignore tests/test_podman_compose_additional_contexts.py
    --ignore tests/test_podman_compose_build_secrets.py
    --ignore tests/test_podman_compose_build_ulimits.py
    --ignore tests/test_podman_compose_config.py
    --ignore tests/test_podman_compose_in_pod.py
    --ignore tests/test_podman_compose_include.py
    --ignore tests/test_podman_compose_networks.py
    --ignore tests/test_podman_compose_tests.py
    --ignore tests/test_podman_compose_up_down.py
    --ignore tests/test_utils.py
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $pkgname-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl
  find examples -type f -exec install -vDm 644 {} "$pkgdir/usr/share/doc/$pkgname/"{} \;
  install -vDm 644 {README.md,docs/*} -t "$pkgdir/usr/share/doc/$pkgname/"
}

# vim:set ts=2 sw=2 et:
