# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=podman-compose
pkgver=1.1.0
pkgrel=2
pkgdesc='A script to run docker-compose.yml using podman'
arch=(any)
url='https://github.com/containers/podman-compose'
license=(GPL-2.0-only)
depends=(
  podman
  python
  python-dotenv
  python-yaml
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-pytest
  python-parameterized
)
optdepends=(
  'aardvark-dns: resolve hostnames of linked containers'
)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('27d732f7e92cff7cc6149982b7b55998467118b2db6a24f588919c747f6e97369d8a24a0198e19179de3baf504220f81d7a1246b12f7c0b1959191cf877e8f14')
b2sums=('faeee541920ed0383a139a6fef8a98a5e47efdd30e3535fb4d56b51dfe140c3cdd2f8a424e4957db6e5dfa6e1a80fc48f6aac621c8161aeda007015ee29ff78b')

build() {
  cd $pkgname-$pkgver

  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    # ignore tests where podman is called (as we can not run the tests due to missing subuid/subgid entries)
    --ignore tests/test_podman_compose.py
    --ignore tests/test_podman_compose_build_secrets.py
    --ignore tests/test_podman_compose_build_ulimits.py
    --ignore tests/test_podman_compose_config.py
    --ignore tests/test_podman_compose_include.py
    --ignore tests/test_podman_compose_networks.py
    --ignore tests/test_podman_compose_tests.py
    --ignore tests/test_podman_compose_up_down.py
    --ignore tests/test_utils.py
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}"
}

package() {
  cd $pkgname-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl
  find examples -type f -exec install -vDm 644 {} "$pkgdir/usr/share/doc/$pkgname/"{} \;
  install -vDm 644 {README.md,docs/*} -t "$pkgdir/usr/share/doc/$pkgname/"
}

# vim:set ts=2 sw=2 et:
