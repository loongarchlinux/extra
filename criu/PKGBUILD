# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: Pavel Borzenkov <pavel@voidptr.ru>
# Contributor: aksr <aksr at t-com dot me>

pkgname=criu
pkgver=3.19
pkgrel=1
pkgdesc='Utilities to checkpoint and restore processes in userspace'
arch=('loong64' 'x86_64')
url='https://criu.org'
license=(
  'GPL-2.0-only'
  'LGPL-2.1-only'
)
depends=(
  'gcc-libs'
  'glibc'
  'gnutls'
  'libbpf'
  'libbsd'
  'libdrm'
  'libnet'
  'libnl'
  'nftables'
  'protobuf-c'
  'python'
  'python-protobuf'
)
makedepends=(
  'asciidoc'
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
  'xmlto'
)
# Can't run tests (only unit tests) due to privilege escalation required.
# https://github.com/checkpoint-restore/criu/issues/434
# checkdepends=(
#   'libaio'
#   'python-yaml'
# )
provides=(
  'libcompel.so'
  'libcriu.so'
)
options=('!lto')
source=("git+https://github.com/checkpoint-restore/criu#tag=v$pkgver")
sha512sums=('f38581b5709e4057f35f36ece46be4b122509e466b6daff4a413efd9b23582d7b35daee43bfbd292524683f6c57cd51994caaabf3f53c1e6dacf29c070276cd3')
b2sums=('da93a2358346490c364cda5e26ea5618ad68bf0984481d2e58d6d08fb42370cc4c38a2c354f1eaf099764a95df59df54b4ee9ffea8c1be5c25d27d8d2d27c250')

build() {
  cd "$pkgname"

  # shellcheck disable=SC2001
  export CFLAGS=$(echo "$CFLAGS" | sed 's/-Wp,-D_FORTIFY_SOURCE=[0-9]//g')
  make criu crit amdgpu_plugin docs
  python -m build --wheel --no-isolation crit
  python -m build --wheel --no-isolation --skip-dependency-check lib
}

check() {
  cd "$pkgname"

  make unittest
  # make test
}

package() {
  cd "$pkgname"

  make \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    SBINDIR=/usr/bin \
    LIBDIR=/usr/lib \
    LIBEXECDIR=/usr/lib \
    install
  python -m installer --destdir="$pkgdir" crit/dist/*.whl
  python -m installer --destdir="$pkgdir" lib/dist/*.whl
  # Remove empty directory to silence namcap warning.
  rm -r "$pkgdir/usr/include/compel/common/asm"
}
