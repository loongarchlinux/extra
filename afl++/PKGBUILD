# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor GI Jack <GI_Jack@hackermail.com>

pkgname=afl++
pkgver=4.20c
pkgrel=1
pkgdesc='instrumentation-driven fuzzer for binary format'
url='https://aflplus.plus/'
arch=(loong64 x86_64)
license=(Apache-2.0)
depends=(
  bash
  clang
  compiler-rt
  gcc-libs
  glibc
  lld
  llvm
  python
)
makedepends=(
  cpio
  git
  grep
  pax-utils
  python-jinja
  python-msgpack
  rust
)
checkdepends=(
  cmocka
)
provides=(
  afl
)
conflicts=(
  afl
  aflplusplus
)
replaces=(
  afl
  aflplusplus
)
source=("https://github.com/AFLplusplus/AFLplusplus/archive/refs/tags/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('855ddefbe9c88911146c1b7cb50dc5423b7623a7a59343f34f31bf038a865a24')
b2sums=('d9353399eab42cf5366592dccba3129da8f5da777691d2999dd1c38d5a1b4410106fdfc48cb6dee2f63b67ebab2cc3419806ff135e622b01404b4c475f23e43d')

build() {
  cd "AFLplusplus-${pkgver}"
  export LLVM_LTO=1
  make PREFIX=/usr
}

check() {
  cd "AFLplusplus-${pkgver}"
  # Unset our CFLAGS/CXXFLAGS for the tests since these may
  # interact in unexpected ways with afl-cc instrumentation.
  CFLAGS= CXXFLAGS= make test
}

package() {
  cd "AFLplusplus-${pkgver}"
  make install PREFIX=/usr DESTDIR="${pkgdir}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
