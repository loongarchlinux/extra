# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Gabriele Musco <gabmus@disroot.org>

pkgname=gfeeds
pkgver=2.2.0
pkgrel=3
pkgdesc='RSS/Atom feed reader for GNOME'
arch=(any)
url='https://gfeeds.gabmus.org/'
license=(GPL-3.0-or-later)
depends=(
  dconf
  glib2
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
  python
  python-beautifulsoup4
  python-dateutil
  python-gobject
  python-humanize
  python-lxml
  python-magic
  python-pillow
  python-pygments
  python-pytz
  python-readability-lxml
  python-requests
  syndication-domination
  webkitgtk-6.0
)
makedepends=(
  blueprint-compiler
  git
  meson
)
checkdepends=(python-pytest)
source=("git+https://gitlab.gnome.org/World/$pkgname.git#tag=$pkgver")
b2sums=(5d6cda735a54fec34fb5d1eb5d88a6fe38e63385afca0466467e1654db90b88130e46aed78f94a28e9342cb206570d30f17ad12da3f5393f37fbdcd88b148457)

prepare() {
  cd $pkgname

  # Fix tests
  git cherry-pick -n 2e897978137b7bd278149beda572986acc92129c

  # Fix build with blueprint-compiler 0.8
  git cherry-pick -n cfe860f44f685be302e2ad9f30b55bab08e078ce
  git cherry-pick -n d099fda0c62e338080061683a154f711cc487b30
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
