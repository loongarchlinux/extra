# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jack_mixer
pkgver=19
pkgrel=1
pkgdesc="Jack desktop mixer application"
arch=(loong64 x86_64)
url="https://github.com/jack-mixer/jack_mixer"
license=(GPL-2.0-only)
groups=(pro-audio)
depends=(
  glibc
  gobject-introspection-runtime
  hicolor-icon-theme
  python
  python-cairo
  python-gobject
  python-platformdirs
)
makedepends=(
  cython
  glib2
  jack2
  meson
  python-docutils
)
optdepends=('new-session-manager: for session management')
source=($pkgname-$pkgver.tar.gz::$url/archive/release-${pkgver//./-}.tar.gz)
sha512sums=('3acc49b12e12e5e565c5277b023f82b975a835b828cd7a2d53a7f184f1d0cf106841748eb4fdbf2c0c595baca98eb6c022819f79aac2ebcb5e673f89431d9075')
b2sums=('7d26f7e1e95e22a7023b5b2451a0d99b56ed6365ecee9002551bb844d7d7893c7300d5bca988b1f6541e9d6fdfb87360867960e82a25ca3b361d808c71a5c791')

build() {
  arch-meson $pkgname-release-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  depends+=(
    glib2 libglib-2.0.so
    jack libjack.so
  )

  meson install -C build --destdir="$pkgdir"
  install -vDm 644 $pkgname-release-$pkgver/*.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
