# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgname=python-greenlet
pkgver=2.0.2
pkgrel=4
pkgdesc="Lightweight in-process concurrent programming"
license=("MIT")
arch=('loong64' 'x86_64')
url="https://pypi.org/project/greenlet/"
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-objgraph' 'python-psutil')
source=("https://files.pythonhosted.org/packages/source/g/greenlet/greenlet-${pkgver}.tar.gz"
        python-greenlet-la64.patch)
sha512sums=('03d0b2764c52524c97930a7bd856a432360a96ddbe3d1f2352ec49750994e6811fd28378f83b96cac91520234d0a6e9e56232ff688c71ff57f6340d3d182635b'
            'a427f6121802409a0d5d83a44eeafa50e19e5b1a02041220b2ee98f90d262e4acc0affc8d7f9ae5bbec05a738293dbf5c6d37adca0291ab753c0f41f7a7d5c2a')

prepare() {
	cd greenlet-$pkgver
    patch -p1 -i $srcdir/python-greenlet-la64.patch
}

build() {
    cd greenlet-$pkgver
    python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
    cd greenlet-$pkgver
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-311" python -m unittest discover -v greenlet.tests
}

package() {
    cd greenlet-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -Dm644 LICENSE.PSF -t "$pkgdir"/usr/share/licenses/$pkgname/
}
