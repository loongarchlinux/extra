# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-dbd-sqlite
pkgver=1.74
pkgrel=2
pkgdesc="Self-contained RDBMS in a DBI driver"
arch=('loong64' 'x86_64')
url="https://search.cpan.org/dist/DBD-SQLite"
license=('GPL' 'PerlArtistic')
depends=('perl-dbi' 'sqlite')
source=("https://www.cpan.org/authors/id/I/IS/ISHIGAKI/DBD-SQLite-$pkgver.tar.gz"
        $pkgname-sqlite-3.37.patch::https://github.com/DBD-SQLite/DBD-SQLite/commit/ba4f472e7372.patch
        libsqlite.patch)
sha512sums=('46bb26ffac07ca6d25d44101bd59fe7ebb397d159154fe10528e4ca4ee270e62159a40be0325099237ce28fb6ce11b5dc1da5396c3e9a3e0e03062a65c16cbab'
            'ed468728a6a1d1e7228ccb8df9fd14ad0c22ae7b5e1b9e7bb32352e4e34794684bcd570da322a94f34e8e967d6155520c4f4652c0ec2f4d0fc0aee6f89314ec7'
            'cf492402f9127c40130c052a6b3f78e86abc0342385130be386aa12e23b1166f20deac50eee1dcc1e43598ba5785d0a218f2cb0a6bb7f5dd7f7c0fc3220bd2bb')

prepare() {
  cd DBD-SQLite-$pkgver

  # Ensure that we can never ever build this. If the perl module links against
  # the system lib this won't matter, if it does not, it will fail during
  # make test.
  rm sqlite3.c

  # https://github.com/DBD-SQLite/DBD-SQLite/issues/92
  patch -Np1 -i ../$pkgname-sqlite-3.37.patch

  # Makefile asks us to patch this to allow to link with system sqlite...
  patch -i "$srcdir/libsqlite.patch"
}

build() {
  cd DBD-SQLite-$pkgver

  # USE_LOCAL_SQLITE=0 disables use of the bundled sqlite
  perl Makefile.PL INSTALLDIRS=vendor USE_LOCAL_SQLITE=0
  make
}

check() {
  cd DBD-SQLite-$pkgver
  make test
}

package() {
  cd DBD-SQLite-$pkgver
  make install DESTDIR="$pkgdir"
}
