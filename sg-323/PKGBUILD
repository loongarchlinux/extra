# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=sg-323
pkgname=(
  'sg-323-clap'
  'sg-323-vst3'
)
pkgver=0.6.3
pkgrel=2
pkgdesc='Emulation of the Ursa Major Stargate 323 digital reverb'
arch=('loong64' 'x86_64')
url='https://github.com/greyboxaudio/SG-323'
license=('GPL-3.0-only')
groups=('pro-audio')
_common_depends=(
  'glibc'
  'gcc-libs'
  'alsa-lib'
  'freetype2'
)
makedepends=(
  "${_common_depends[@]}"
  'git'
  'cmake'
  'curl'
  'gendesk'
  'jack'
  'libpng'
  'libx11'
  'libxrandr'
  'libxinerama'
  'libxcursor'
  'webkit2gtk'
)
source=(
  "$pkgbase::git+$url#tag=$pkgver"
  'github.com-juce-framework-JUCE::git+https://github.com/juce-framework/JUCE'
  'github.com-free-audio-clap-juce-extensions::git+https://github.com/free-audio/clap-juce-extensions'
  'github.com-free-audio-clap::git+https://github.com/free-audio/clap'
  'github.com-free-audio-clap-helpers::git+https://github.com/free-audio/clap-helpers'
  'skip-install.patch'
)
sha512sums=('441269b76239a2e57760f33efd505a8aec42ff60eaa3f56641b9ef874340b324c79ec64fe9af38cb050ea9dfc2821b5bee2027e9f3dee00d9798ca4e3f1011d2'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '3a8fc1cf39943d2f925c669c74a4fc387f13d1874b7cc4176f287c007374231408d279878c956101da2b20927ffc2628c853706494b9b7684caadf040f8bc7a9')
b2sums=('5dfa9a468037865a613aee7c48acfe3c244356e87bf2a09637c5283a954ceb6effc0ba80f802fa5e25ddf70838ae696aa27ad599d155e63972118a6079501e76'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        '8a5880c9954d381538e4a8993697df921bfe2b63f97fb7187557331a347326617efcb3b56d21a125ecd7aae8a2ce8236f50e0679e1ccd9fe0766c84e17119c32')

prepare() {
  cd "$pkgbase"

  # prepare git submodule(s)
  git submodule init
  git config submodule.libs/JUCE.url "$srcdir/github.com-juce-framework-JUCE"
  git config submodule.libs/clap-juce-extensions.url "$srcdir/github.com-free-audio-clap-juce-extensions"
  git -c protocol.file.allow=always submodule update --init

  pushd libs/clap-juce-extensions
  git submodule init
  git config submodule.clap-libs/clap.url "$srcdir/github.com-free-audio-clap"
  git config submodule.clap-libs/clap-helpers.url "$srcdir/github.com-free-audio-clap-helpers"
  git -c protocol.file.allow=always submodule update --init
  popd

  # prevent JUCE from installing to $HOME after successful build
  patch -p1 -i "$srcdir/skip-install.patch"
}

build() {
  # https://archlinux.org/todo/lto-fat-objects/
  CFLAGS+=" -ffat-lto-objects"
  CXXFLAGS+=" -ffat-lto-objects"

  cmake \
    -B build \
    -S "$pkgbase"

  cmake --build build
}

package_sg-323-clap() {
  pkgdesc+=' - CLAP plugin'
  groups+=('clap-plugins')
  depends=(
    "${_common_depends[@]}"
    'clap-host'
  )

  install -vDm644 -t "$pkgdir/usr/lib/clap" build/SG323_artefacts/CLAP/SG-323.clap
}

package_sg-323-vst3() {
  pkgdesc+=' - VST3 plugin'
  groups+=('vst3-plugins')
  depends=(
    "${_common_depends[@]}"
    'vst3-host'
  )

  install -vd "$pkgdir/usr/lib/vst3"
  cp -vr build/SG323_artefacts/VST3/SG-323.vst3 "$pkgdir/usr/lib/vst3"
}
