# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-weather
pkgver=46.0
pkgrel=1
pkgdesc="Access current weather conditions and forecasts"
url="https://wiki.gnome.org/Apps/Weather"
arch=(any)
license=(GPL-2.0-or-later)
depends=(
  geoclue
  gjs
  gtk4
  libadwaita
  libgweather-4
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
)
groups=(gnome)
_commit=0f746b99629fa73b9a5233ca58bc35d93ca16f58  # tags/46.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-weather.git#commit=$_commit")
b2sums=('0b22ea5d6961dabece025114e73aa1cc4ba5164ebd5df0793e8b576f9781e090e27037cab722082bc7e159bf4f7e2fb99bbbfb03c5550392394567a41e101c8f')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
