# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=python-database-knotinfo
_pyname=database_knotinfo
pkgver=2024.5.1
pkgrel=1
arch=(any)
pkgdesc='Snapshot of the complete content of the KnotInfo and LinkInfo databases for Python'
url='https://github.com/soehms/database_knotinfo'
depends=(python)
makedepends=(git
             python-build
             python-installer
             python-setuptools-scm
             python-wheel)
license=(GPL-3.0-only)
source=(git+https://github.com/soehms/database_knotinfo#tag=$pkgver)
sha256sums=('415671f8f687da829d9eb0ab5bfd2c8428e97fdc101fe4970ff59eb32912460a')

build() {
  cd $_pyname
  python -m build --wheel --no-isolation
}

package() {
  cd $_pyname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
