# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=clientsession
pkgname=haskell-clientsession
pkgver=0.9.2.0
pkgrel=14
pkgdesc="Securely store session data in a client-side cookie."
url="https://github.com/yesodweb/clientsession/tree/master"
license=("MIT")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-base64-bytestring' 'haskell-cereal' 'haskell-crypto-api'
         'haskell-cryptonite' 'haskell-crypto-random' 'haskell-entropy' 'haskell-setenv'
         'haskell-skein' 'haskell-tagged')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-hspec')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('dc83f7d2398e6bc1bf8ba398a5ad2d930d66b4f436f78a4a54b5ebe12cb951f1ad361470f51770dea507083b7f4331ee8919422bbba967b85d7190cc6111597b')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-test
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
