# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-sharing
pkgver=2.1.6.r359.2d6b2f1
pkgrel=1
pkgdesc='Switchboard Sharing Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-sharing
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  glib2
  glibc
  gnome-user-share
  gtk4
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_commit=2d6b2f1d6e7e6e58bf64693730129c9ca3bc77f2
source=(git+https://github.com/elementary/switchboard-plug-sharing.git#commit=${_commit})
b2sums=('c45e27f81909db9b8ea9ec1a93f8c08696817389b299f903a75293bb8fecbc3be0721f94a454ed76e13db906240bef4c520faacdf796ba57cc35d66c1a1aa6c7')

pkgver() {
  cd switchboard-plug-sharing
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-sharing build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
