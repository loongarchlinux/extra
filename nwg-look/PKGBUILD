# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Piotr Miller <nwg.piotr@gmail.com>

pkgname=nwg-look
pkgver=0.2.7
pkgrel=1
pkgdesc="GTK3 settings editor adapted to work on wlroots-based compositors"
url="https://github.com/nwg-piotr/nwg-look"
arch=('loong64' 'x86_64')
license=('MIT')
depends=(
  'glibc'
  'gtk3'
  'libatk-1.0.so'
  'libcairo-gobject.so'
  'libcairo.so'
  'libfontconfig.so'
  'libfreetype.so'
  'libgdk-3.so'
  'libgdk_pixbuf-2.0.so'
  'libgio-2.0.so'
  'libglib-2.0.so'
  'libgmodule-2.0.so'
  'libgobject-2.0.so'
  'libgtk-3.so'
  'libharfbuzz.so'
  'libpango-1.0.so'
  'libpangocairo-1.0.so'
  'libz.so'
  'xcur2png'
)
makedepends=(
  'go'
)
source=(https://github.com/nwg-piotr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('20a7773b1bae7ae013efa359e73dcefc89bfb516d45bba4375f59c8a9216c9c3')
b2sums=('da1eedf00c5eafe5e5e4b8890c1a98366a20d50fa2da4ab07965fac54c9d92ea4d871ce75e83c3d1d5e35a075a585f8f22cbad7aa42b7504e63e842034af85b4')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o bin/nwg-look \
    .
}

package() {
  make DESTDIR="${pkgdir}" install -C "${pkgname}-${pkgver}"
}

# vim: ts=2 sw=2 et:
