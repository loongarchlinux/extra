# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=editorconfig-checker
pkgver=3.0.3
pkgrel=1
pkgdesc='A tool to verify that your files are in harmony with your .editorconfig'
arch=('loong64' 'x86_64')
url="https://github.com/editorconfig-checker/$pkgname"
license=(MIT)
depends=(glibc)
makedepends=(go)
provides=(ec)
_archive="$pkgname-$pkgver"
source=("$url/archive/refs/tags/v$pkgver/$_archive.tar.gz")
sha256sums=('b3d927bbbf3a89bc75d91ce00dcb3c836906e6522097c836285adb26850ca3eb')

build() {
	cd "$_archive"
	export CGO_CPPFLAGS="$CPPFLAGS"
	export CGO_CFLAGS="$CFLAGS"
	export CGO_CXXFLAGS="$CXXFLAGS"
	export CGO_LDFLAGS="$LDFLAGS"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build -o "$pkgname" ./cmd/...
}

check() {
	cd "$_archive"
	go test ./...
}

package() {
	cd "$_archive"
	install -Dm755 -t "$pkgdir/usr/bin/" "$pkgname"
	ln -sf "$pkgname" "$pkgdir/usr/bin/ec"
	install -Dm644 -t  "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim: ts=2 sw=2 et:
