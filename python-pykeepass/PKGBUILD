# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-pykeepass
pkgver=4.0.7.post1
pkgrel=1
pkgdesc="Python library to interact with keepass databases"
arch=('any')
url="https://github.com/libkeepass/pykeepass"
license=('GPL-3.0-only')
depends=(
  python
  python-argon2_cffi
  python-construct
  python-lxml
  python-pycryptodomex
  python-setuptools
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
)
checkdepends=(python-pyotp)
source=("git+https://github.com/libkeepass/pykeepass.git#tag=v${pkgver/.post/-post}")
b2sums=('fa3052930f41f7a9017c6194ce9acf09f22601a21fc9fcc2252d361c0293c8bcc1ed04af95f0ed82822c42009d22de7d35075107c00a4481bb906447333cc6ce')

prepare() {
  cd pykeepass
}

build() {
  cd pykeepass
  python -m build --wheel --no-isolation
}

check() {
  cd pykeepass
  python -m unittest tests.tests
}

package() {
  cd pykeepass
  python3 -m installer --destdir="$pkgdir" dist/*.whl
}
