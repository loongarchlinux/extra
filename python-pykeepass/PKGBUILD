# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-pykeepass
pkgver=4.1.0.post1
pkgrel=1
pkgdesc="Python library to interact with keepass databases"
arch=('any')
url="https://github.com/libkeepass/pykeepass"
license=('GPL-3.0-only')
depends=(
  python
  python-argon2_cffi
  python-construct
  python-lxml
  python-pycryptodomex
  python-setuptools
)
makedepends=(
  git
  python-build
  python-installer
  python-wheel
)
checkdepends=(python-pyotp)
source=("git+https://github.com/libkeepass/pykeepass.git#tag=v${pkgver}")
b2sums=('4468ef1a62f32874acae93ade51e868dc83bcb29f75a605be13b5baacffe3a32daaf41f1676a5dc2fdbfaa355b6ab8b2c99be97fe09ac518b2adcb22fe4f59f8')

prepare() {
  cd pykeepass
}

build() {
  cd pykeepass
  python -m build --wheel --no-isolation
}

check() {
  cd pykeepass
  python -m unittest tests.tests
}

package() {
  cd pykeepass
  python3 -m installer --destdir="$pkgdir" dist/*.whl
}
