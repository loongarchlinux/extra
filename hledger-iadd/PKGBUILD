# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_hkgname=hledger-iadd
pkgname=hledger-iadd
pkgver=1.3.18
pkgrel=12
pkgdesc="A terminal UI as drop-in replacement for hledger add"
url="https://github.com/hpdeifel/hledger-iadd#readme"
license=("BSD")
arch=('loong64' 'x86_64')
depends=('ghc-libs' 'haskell-brick' 'haskell-free' 'haskell-hledger-lib' 'haskell-megaparsec'
         'haskell-microlens' 'haskell-microlens-mtl' 'haskell-microlens-th'
         'haskell-optparse-applicative' 'haskell-text-zipper' 'haskell-unordered-containers'
         'haskell-vector' 'haskell-vty' 'haskell-xdg-basedir')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-hspec' 'haskell-hspec-discover')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        $pkgname-ghc-9.0.patch::https://github.com/hpdeifel/hledger-iadd/pull/74.patch)
sha512sums=('33404b9cc1399d995db0269e5aa3bb27b9057dfb7cfcdc91b8ce1ba0b8f78c35f5439c0a298b3b89c1c747605b81d93d79fbb99d5ead0aca00c866fc4b74c49e'
            'cd9c20b9470cdb4d63ef6d79dea2d19b561ea458bedd9d651d02b2fce2a80cb9d9e5bbba3d96989e29881a6ba5f8501c8f28e3fe4f989878dad4c1a5fa4376c6')

prepare() {
  cd $_hkgname-$pkgver
  patch -Np1 -i ../$pkgname-ghc-9.0.patch
  uusi -u megaparsec $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
