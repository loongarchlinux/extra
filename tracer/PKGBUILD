# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=tracer
pkgver=0.7.11
pkgrel=1
pkgdesc="Tracer finds outdated running applications in your system"
arch=('any')
url="http://tracer-package.com/"
license=('GPL')
depends=(python python-distro python-psutil python-six dbus-python pyalpm)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=("$pkgname-$pkgver.tar.gz::https://github.com/FrostyX/tracer/archive/refs/tags/$pkgname-$pkgver-1.tar.gz")
sha512sums=('01fed4041d10cd1bbbcff722eec264e8e0378cbb35cbb3e87233a8698833beff832a78cb4437dc5530df03fd55acb77c3e4c4fea3f71fdfb7bfcdd94fcbe581e')

prepare() {
    cd "$pkgname-$pkgname-$pkgver-1"
    cp LICENSE LICENCE.md
}

check() {
    cd "$pkgname-$pkgname-$pkgver-1"
    PYTHONPATH=. pytest tests
}

build() {
    cd "$pkgname-$pkgname-$pkgver-1"

    python setup.py build
}

package() {
    cd "$pkgname-$pkgname-$pkgver-1"

    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
    install -Dm644 LICENCE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
