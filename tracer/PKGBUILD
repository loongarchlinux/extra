# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=tracer
pkgver=1.1
pkgrel=1
pkgdesc="Tracer finds outdated running applications in your system"
arch=('any')
url="http://tracer-package.com/"
license=('GPL')
depends=(python python-distro python-psutil python-six python-dbus pyalpm)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=("$pkgname-$pkgver.tar.gz::https://github.com/FrostyX/tracer/archive/refs/tags/$pkgname-$pkgver-1.tar.gz")
sha512sums=('b994c9197d9371cba801f413c47a982cc54f03b4ed9764529a097b03ad8aee13ce635af1fc388fc4a9db02b2026a7e6c8a02e6f143a15ed347f1ce5e8a7eda30')

prepare() {
    cd "$pkgname-$pkgname-$pkgver-1"
    cp LICENSE LICENCE.md
}

check() {
    cd "$pkgname-$pkgname-$pkgver-1"
    PYTHONPATH=. pytest tests
}

build() {
    cd "$pkgname-$pkgname-$pkgver-1"

    python setup.py build
}

package() {
    cd "$pkgname-$pkgname-$pkgver-1"

    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
    install -Dm644 LICENCE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
