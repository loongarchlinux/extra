# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=vis
pkgver=0.9
pkgrel=2
pkgdesc='modern, legacy free, simple yet efficient vim-like editor'
arch=('loong64' 'x86_64')
url='https://github.com/martanne/vis#vis-a-vim-like-text-editor'
depends=('acl' 'libacl.so' 'bash' 'ncurses' 'libncursesw.so' 'libtermkey'
         'libtermkey.so' 'lua' 'tre' 'file')
optdepends=('wl-clipboard: wayland clipboard support'
            'xclip: X11 clipboard support'
            'xsel: X11 clipboard support'
            'lua-lpeg: syntax highlighting')
makedepends=('git')
checkdepends=('vim' 'lua-lpeg')
license=('custom:ISC')
backup=('etc/vis/visrc.lua')
validpgpkeys=('54A47F12462D26A7D078A932C83A52CD1962562C') # Marc Andr√© Tanner <mat@brain-dump.org>
# TODO: can we have signed tag back?
source=("git+https://github.com/martanne/vis.git#tag=v${pkgver}"
        'git+https://github.com/martanne/vis-test.git')
sha256sums=('ef6ca7af3d62a4617aca253a9b22f953fa50cde964debe831a982ed010f26de7'
            'SKIP')

_backports=(
  # lua: themes: add `STYLE_CODE`
  'a9a4f5cabb766e3dadd504df4fd9583192c22315'

  # lua: filetype: properly search for lexer path
  'b6ae2fb782ac2c47dda3b786c3e6399d2972efc6'
)

_reverts=(
)

prepare() {
  cd vis/

  git config --file=.gitmodules submodule.test.url ../vis-test/
  git update-index --assume-unchanged .gitmodules
  git submodule init
  git -c protocol.file.allow=always submodule update
  
  local _c
  for _c in "${_backports[@]}"; do
    git log --oneline -1 "${_c}"
    git cherry-pick -n "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    git log --oneline -1 "${_c}"
    git revert -n "${_c}"
  done
}

build() {
  cd vis/

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd vis/

  make -C test/
}

package() {
  cd vis/

  make DESTDIR="${pkgdir}" install

  install -D -m0644 lua/visrc.lua "${pkgdir}"/etc/vis/visrc.lua

  install -D -m0644 'LICENSE' "${pkgdir}/usr/share/licenses/vis/LICENSE"
}

