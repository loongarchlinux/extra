# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=konqueror
pkgver=24.02.2
pkgrel=1
pkgdesc='KDE File Manager & Web Browser'
arch=(loong64 x86_64)
url='https://apps.kde.org/konqueror/'
license=(LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         karchive
         kbookmarks
         kcmutils
         kcodecs
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcoreaddons
         kcrash
         kdbusaddons
         kguiaddons
         ki18n
         kiconthemes
         kio
         kitemviews
         kjobwidgets
         kparts
         kservice
         ktextwidgets
         kwallet
         kwidgetsaddons
         kwindowsystem
         kxmlgui
         qt6-base
         qt6-webengine
         solid
         sonnet
         zlib)
makedepends=(extra-cmake-modules
             kdesu
             kdoctools)
optdepends=('kdesu: shell command plugin')
groups=(kde-applications
        kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('064f1f066dfdd4f74ff1c9e47575c8ba47de47dd6d5f5dd1856500ef4d9bc632'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DQT_MAJOR_VERSION=6 \
    -DUSE_SYSTEM_DICTIONARIES=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
