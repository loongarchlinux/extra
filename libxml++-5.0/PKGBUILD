# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libxml++-5.0
pkgname=(
  libxml++-5.0
  libxml++-5.0-docs
)
pkgver=5.4.0
pkgrel=1
pkgdesc="C++ bindings to libxml2"
url="https://libxmlplusplus.github.io/libxmlplusplus/"
arch=(loong64 x86_64)
license=(LGPL-2.1-or-later)
depends=(
  gcc-libs
  glibc
  libxml2
)
makedepends=(
  git
  meson
  mm-common
)
options=(!emptydirs)
source=("git+https://github.com/libxmlplusplus/libxmlplusplus#tag=$pkgver")
b2sums=('8899e4f548a007eef86eff8509a0ede5a02a9e53ef23b348269043f181f0a2352f602163901c36f5b62f65c55f948259139a50de99a7517f83ed1f066dd7d97a')

prepare() {
  cd libxmlplusplus
}

build() {
  local meson_options=(
    -D maintainer-mode=true
  )

  arch-meson libxmlplusplus build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libxml++-5.0() {
  provides=(libxml++-5.0.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_libxml++-5.0-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
