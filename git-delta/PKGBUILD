# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kr1ss $(echo \<kr1ss+x-yandex+com\>|sed s/\+/./g\;s/\-/@/)
# Contributor: ccorn

pkgname=git-delta
_pkgname=delta
pkgver=0.17.0
pkgrel=3
pkgdesc='Syntax-highlighting pager for git and diff output'
arch=(loong64 x86_64)
url='https://dandavison.github.io/delta/'
_url='https://github.com/dandavison/delta'
license=(MIT)
depends=(glibc # libc.so libm.so
         gcc-libs # libgcc_s.so
         libgit2 libgit2.so
         oniguruma # libonig.so
         zlib)
makedepends=(cargo
             mdbook)
checkdepends=(git)
source=("$_url/archive/$pkgver/$_pkgname-$pkgver.tar.gz")
sha512sums=('7b3d4aa9bad2f3dc920d9855163e81813ea5467cd92939d4781ed3d896d10bb39110265d5294967c79f8451e8a6d3d4def7e42efb4064f3d5fbc5d99e5c245a6')
b2sums=('45af5d1391a3623c947ed74bde530fa4511dd16826dea808a3846b11a5da9a737d9768cbc08684010794cf766291100aaaf4cd942907932766ac73a64d9b63eb')

prepare() {
  cd "$_pkgname-$pkgver"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname-$pkgver"

  # https://archlinux.org/todo/lto-fat-objects/
  CFLAGS+=' -ffat-lto-objects'

  export RUSTONIG_DYNAMIC_LIBONIG=1

  # binary
  cargo build --frozen --release --all-features

  # html manual
  cd manual
  mdbook build --dest-dir html
}

check() {
  cd "$_pkgname-$pkgver"
  # some tests hang for 60+ seconds, find out why
  # took my machine ~ 470 seconds
  cargo test --frozen --all-features
}

package() {
  cd "$_pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/delta

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # shell auto-completions
  install -vDm644 etc/completion/completion.bash \
    "$pkgdir/usr/share/bash-completion/completions/$_pkgname"
  install -vDm644 etc/completion/completion.zsh \
    "$pkgdir/usr/share/zsh/site-functions/_$_pkgname"
  install -vDm644 etc/completion/completion.fish \
    "$pkgdir/usr/share/fish/vendor_completions.d/$_pkgname.fish"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # html manual
  cp -r manual/html "$pkgdir/usr/share/doc/$pkgname"
}
