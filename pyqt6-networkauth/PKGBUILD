# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-networkauth
pkgname=(python-pyqt6-networkauth)
pkgver=6.7.0
pkgrel=2
pkgdesc='Python bindings for QtNetworkAuth'
arch=(loong64 x86_64)
url='https://www.riverbankcomputing.com/software/pyqtnetworkauth/intro'
license=(GPL3)
groups=(pyqt6)
depends=(gcc-libs
         glibc
         python
         python-pyqt6
         qt6-base
         qt6-networkauth)
makedepends=(pyqt-builder
             sip)
source=(https://pypi.python.org/packages/source/P/PyQt6-NetworkAuth/PyQt6_NetworkAuth-$pkgver.tar.gz)
sha256sums=('974983525437633a0f016c0ffa0dc311847089f253dfe6840b0ec8ce21dc8685')

build() {
  cd PyQt6_NetworkAuth-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake6
  cd build
  make
}

package_python-pyqt6-networkauth(){
  cd PyQt6_NetworkAuth-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
