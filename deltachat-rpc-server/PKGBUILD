# Maintainer: David Runge <dvzrv@archlinux.org>

# WARNING: this package needs to be kept in sync with the requirements of deltachat-desktop
_upstream=deltachat-core-rust
pkgname=deltachat-rpc-server
pkgver=1.142.1
pkgrel=1
pkgdesc="A JSON-RPC 2.0 interface to DeltaChat over standard I/O"
arch=(loong64 x86_64)
url="https://github.com/deltachat/deltachat-core-rust/tree/main/deltachat-rpc-server"
_url="https://github.com/deltachat/deltachat-core-rust"
license=(MPL-2.0)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  rust
)
# NOTE: the ring dependency can not be built with LTO
options=(!lto)
source=($_upstream-$pkgver.tar.gz::$_url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('c746c7eacc7c12d99b80ba644979ecee42e1477d5f42b980636a7d43117ec7665eb274e64604765b976f744ce69a1e03168e4e5656bc8abd8801d3eeae1b84e8')
b2sums=('cec2ce18851c00a70ebf7ed00cace11ffc59d0a0ca4628fc02d8f55edfa71ef5c0da1d4fab9eae8c014305b4e880f3251a2c1e885c0ca746191c497c0d938e45')

prepare() {
  export RUSTUP_TOOLCHAIN=stable
  cd $_upstream-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"

}

build() {
  cd $_upstream-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features --package $pkgname
}

check() {
  cd $_upstream-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo test --frozen --all-features --package $pkgname
}

package() {
  cd $_upstream-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
