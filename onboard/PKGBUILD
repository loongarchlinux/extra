# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=onboard
pkgver=1.4.1
pkgrel=9
pkgdesc="On-screen keyboard useful on tablet PCs or for mobility impaired users"
arch=('loong64' 'x86_64')
url="https://launchpad.net/onboard"
license=('GPL')
depends=('gtk3' 'hunspell' 'iso-codes' 'libcanberra' 'libxkbfile' 'python-cairo' 'python-dbus' 'python-gobject' 'systemd')
makedepends=('python-distutils-extra')
optdepends=('mousetweaks: hover click with mouse')
source=(https://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz{,.asc}
        python-3.12.patch)
validpgpkeys=('BCFE9B5A71A0B0F860521CF963087078B67DCFED') # Francesco Fumanti
sha256sums=('01cae1ac5b1ef1ab985bd2d2d79ded6fc99ee04b1535cc1bb191e43a231a3865'
            'SKIP'
            '236561159732d40f231e53ca4efed5953f65d58d27e510cfd9cd853c180041fc')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../python-3.12.patch # Fix build with Python 3.12
}

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}
