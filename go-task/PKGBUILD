# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=go-task
pkgver=3.38.0
pkgrel=1
pkgdesc='A task runner written in Go'
arch=('loong64' 'x86_64')
url='https://taskfile.dev'
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
install=go-task.install
source=(
  "$pkgname::git+https://github.com/go-task/task#tag=v$pkgver"
  'rename-completions.patch'
)
sha512sums=('9e3ddabbe0608c60aa1538a63978098687f87e0d36171fbcc853f9c3d62704f874d848ca61e053082e44b14cdc544752d9aba53cea713b9924eb767ba4e71d18'
            '2b64a294b3b6b91c3d3e44c5aa776bd84b8ca32c88195e696be1c784ca5f83394eb7520b0a13b517b99dbdcd9833d2936707c8efdb79223b642dbdc3ffc7ac8c')
b2sums=('89933286e1cf1879fd3a831000a7610909f82486a0d4d9498d32fe6e63b5ed5fcbac7aa2aec5aca96e4cb9dd49e0a8ba52c939698c922a45b42685e22992badc'
        'b68ac73a03c94946169fd92f41b746b15fdb1f86c3fbebf8ab45b28272147d189646d407decadc482f3e89aa64b2f4a0ae14aedec4f5f5eb640f4b934f3b85bd')

prepare() {
  cd "$pkgname"

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download

  # rename completions from `task` to `go-task`
  patch -p1 -i "$srcdir/rename-completions.patch"
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/go-task/task/v3/internal/version.version=${pkgver}" \
    -o go-task \
    ./cmd/task
}

check() {
  cd "$pkgname"

  go test -v ./...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" go-task

  # shell completions
  install -vDm644 completion/bash/task.bash "$pkgdir/usr/share/bash-completion/completions/go-task"
  install -vDm644 completion/fish/task.fish "$pkgdir/usr/share/fish/vendor_completions.d/go-task.fish"
  install -vDm644 completion/zsh/_task "$pkgdir/usr/share/zsh/site-functions/_go-task"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
