# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-color-manager
pkgver=3.36.0+r62+g82000f1d
pkgrel=1
pkgdesc="GNOME Color Profile Tools"
url="https://gitlab.gnome.org/GNOME/gnome-color-manager"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  colord
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  lcms2
  libcolord
  pango
)
makedepends=(
  appstream-glib
  docbook-sgml
  docbook-utils
  git
  meson
  perl-sgmls
  yelp-tools
)
checkdepends=(xorg-server-xvfb)
groups=(gnome)
_commit=82000f1dd8c032f049ccd698324bce10b1ed13c2  # master
source=("git+$url.git#commit=$_commit")
b2sums=('816ab7fb3742cb6b9106f81a18e1867659a224d5bb25aacac7a9d671a61c130f520518e71568fa6c557675235fe3528870e2d61d6aa280c82fc103cd721b810d')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_COLOR_MANAGER_//;s/_/./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
