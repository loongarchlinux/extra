# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyRSS2Gen
pkgname=python-pyrss2gen
pkgver=1.1
pkgrel=16
pkgdesc="Generate RSS2 using a Python data structure"
arch=(any)
url="http://www.dalkescientific.com/Python/PyRSS2Gen.html"
license=(BSD-3-Clause)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=(http://www.dalkescientific.com/Python/$_name-$pkgver.tar.gz)
sha512sums=('2240686914c87984626eb739676927fcd53488158bdebf589e07e8a778e6d24305964e032581c61836f0fabc1264e42d05c88eac8f4c5f6536c31c775434ce02')
b2sums=('1f388e8616c79d8b5436f69191d479c529e1348308d69782a833bec714da6289fc459ea2b554a1e660027a25d0581c441a0e75a623add3c42c8c33c5cc8518e1')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README -t "$pkgdir/usr/share/doc/$pkgname/"
}
