# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Michael DÃ¼ll <mail@akurei.me> PGP-Key: AAAEE882
# Contributor: TDY <tdy@gmx.com>
# Contributor: Sergio Rubio <rubiojr.ml@gmail.com>
# Contributor: Hannes Rist <cowider@gmail.com>

pkgname=unp
pkgver=2.0
pkgrel=1
pkgdesc='A script for unpacking a wide variety of archive formats'
arch=(any)
url="https://tracker.debian.org/pkg/unp"
license=(GPL-2.0-only)
depends=(perl)
epoch=1
optdepends=(
  'unarj: arj support'
  'unrar: rar support'
  'unzip: zip support'
  'bzip2: bzip2 support'
  'p7zip: p7zip support'
  'unace: ace support'
  'cpio: cpio support'
  'sharutils: sharutils'
  'cabextract: cab support'
  'rpmextract: rpm support'
)
source=("https://ftp.debian.org/debian/pool/main/u/unp/unp_${pkgver}.tar.xz")
sha512sums=('bdb2059aa3f1872efd49eb6c8e2fa3a09c05d9e1c26abdcfc4e728699c25a132ea610525461e94183263086756e9b60daeb5cecf69533578d01ab1a37b0af744')
b2sums=('d807cdae6cccd846b3d138b7060b17f37f6f6962e2dd75400e86f0fd8f7f4721afbf29509dfce16beb5dac66996df45cc146d72312f859999f33d435f66890d5')

build() {
  cd unp-$pkgver/po
  make
}

package() {
  cd unp-$pkgver
  install -Dm755 unp "$pkgdir"/usr/bin/unp
  install -Dm755 ucat "$pkgdir"/usr/bin/ucat
  install -Dm644 debian/unp.1 "$pkgdir"/usr/share/man/man1/unp.1
  install -Dm755 debian/unp.bash-completion "$pkgdir"/usr/share/bash-completion/completions/unp

  cd po
  make DESTDIR="$pkgdir" install
}
