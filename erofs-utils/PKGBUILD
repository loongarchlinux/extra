# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=erofs-utils
pkgver=1.8.1
pkgrel=1
pkgdesc="Userspace utilities for linux-erofs file system"
arch=(loong64 x86_64)
url="https://git.kernel.org/pub/scm/linux/kernel/git/xiang/erofs-utils.git"
license=(
  Apache-2.0
  GPL-2.0-or-later
)
depends=(
  glibc
  lz4
  zlib
)
makedepends=(
  fuse2
  util-linux
  xz
  zstd
)
optdepends=(
  'fuse2: for erofsfuse'
)
source=($url/snapshot/$pkgname-$pkgver.tar.gz)
sha512sums=('b7fe2441e5295680bc683e978ad97ee359785fd842d77fa86ef6bf869b5da87ad613f48782b5f59684995b6cbc5c274938c9ea9067baa537f09c029eabb4cca3')
b2sums=('0ecf7ad0d42f3941751511f3ab42e7e25b85dc64849867a221272cf4596f6bfff3cb1c22877a485de79b4445f68eea8e77cb3f8c742a23ee5ad7ce0232b85091')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --enable-fuse
    --enable-lzma
  )

  cd $pkgname-$pkgver
  ./configure "${configure_options[@]}"
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    util-linux libuuid.so
    xz liblzma.so
    zstd libzstd.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
