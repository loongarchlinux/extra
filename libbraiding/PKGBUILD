# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libbraiding
pkgver=1.2
pkgrel=2
pkgdesc='Library to compute several properties of braids, including centralizer and conjugacy check'
arch=(loong64 x86_64)
url='https://github.com/miguelmarco/libbraiding'
license=(GPL-3.0-or-later)
depends=(gcc-libs
         glibc)
makedepends=(git)
source=(git+https://github.com/miguelmarco/libbraiding#tag=$pkgver)
sha256sums=('e73a032c9d74b97ab4903f908f225ee665dcd1ba1964751010a406b9f9bd3448')

prepare() {
  cd $pkgname
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
