# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=waylock
pkgver=1.2.0
pkgrel=1
pkgdesc="A simple screenlocker for wayland compositors"
arch=(loong64 x86_64)
url="https://codeberg.org/ifreund/waylock"
license=(ISC)
depends=(
  glibc
)
makedepends=(
  git
  libxkbcommon
  pam
  scdoc
  wayland
  wayland-protocols
  zig
)
source=(
  "git+$url?signed#tag=v$pkgver"
)
sha512sums=('7dc8a68c8e35787e3de7f993999b087a4f40f8fb0f02a1ee483bee2c0898aa70082e4687b07ff13bfaa7ca4d74d3bac2c9f0dc168343f9c58bdfcede787e6dd4')
b2sums=('72aff74ba56e9a19816b7cde45b6689e609c9e22228a206ced4a5feb93126ff93dfc55ab5cdad8731ffae7574d0d1e320f3085a4e267fbdf3d1f08893a4f008a')
validpgpkeys=('5FBDF84DD2278DB2B8AD8A5286DED400DDFD7A11') # Isaac Freund <mail@isaacfreund.com>

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g'
}

prepare() {
  # https://codeberg.org/ifreund/waylock/src/branch/master/build.zig.zon
  zig fetch --global-cache-dir zig-cache https://codeberg.org/ifreund/zig-wayland/archive/v0.2.0.tar.gz
  zig fetch --global-cache-dir zig-cache https://codeberg.org/ifreund/zig-xkbcommon/archive/v0.2.0.tar.gz
}

build() {
  local zig_options=(
    -Dcpu=baseline
    -Dpie
    -Dtarget=native-linux.5.15-gnu
    --prefix /usr
    --search-prefix /usr
    --system ../zig-cache/p
    --verbose
  )

  # this uses malloc_usable_size, which is incompatible with fortification level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  cd $pkgname
  DESTDIR=build zig build "${zig_options[@]}"
}

package() {
  depends+=(
    libxkbcommon libxkbcommon.so
    pam libpam.so
    wayland libwayland-client.so
  )

  cd $pkgname

  mv -v build/* "$pkgdir"

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
