# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=waylock
pkgver=1.0.0
pkgrel=1
pkgdesc="A simple screenlocker for wayland compositors"
arch=(loong64 x86_64)
url="https://github.com/ifreund/waylock"
license=(ISC)
depends=(
  glibc
)
makedepends=(
  git
  libxkbcommon
  pam
  scdoc
  wayland
  wayland-protocols
  zig
)
source=(
  "git+$url?signed#tag=v$pkgver"
  git+https://codeberg.org/ifreund/zig-wayland.git
  git+https://codeberg.org/ifreund/zig-xkbcommon.git
)
sha512sums=('0c9a3ba2431b087fcb5a5747abf59bb8097dc4f599c10f351707a60f829075ef36cda45f9204c75e0151530b9266923ad782546e2ae8f871fc29c8e17b3e2810'
            'SKIP'
            'SKIP')
b2sums=('a9f292e53762a2e2e3ab93d2b6727b9496de087dfe45a160ec4f66311964df84fee81a4e39a84eb1f0917dd9e9137987132b6ed1b0e3cd71826475a85cb6cefd'
        'SKIP'
        'SKIP')
validpgpkeys=('5FBDF84DD2278DB2B8AD8A5286DED400DDFD7A11') # Isaac Freund <mail@isaacfreund.com>

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g'
}

prepare() {
  cd $pkgname
  git submodule init
  git config submodule.deps/zig-wayland.url "../zig-wayland"
  git config submodule.deps/zig-xkbcommon.url "../zig-xkbcommon"
  git -c protocol.file.allow=always submodule update
}

build() {
  local zig_options=(
    -Dcpu=baseline
    -Dpie
    -Dtarget=native-linux.5.15-gnu
    --prefix /usr
    --search-prefix /usr
    --verbose
  )

  # this uses malloc_usable_size, which is incompatible with fortification level 3
  export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
  export CXXFLAGS="${CXXFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  cd $pkgname
  DESTDIR=build zig build "${zig_options[@]}"
}

package() {
  depends+=(
    libxkbcommon libxkbcommon.so
    pam libpam.so
    wayland libwayland-client.so
  )

  cd $pkgname

  mv -v build/* "$pkgdir"

  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
