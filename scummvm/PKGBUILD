# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: juergen <juergen@archlinux.org>
# Contributor: Manolis Tzanidakis, Petter Gundersen

pkgname=scummvm
pkgver=2.8.1
pkgrel=1
pkgdesc="A 'virtual machine' for several classic graphical point-and-click adventure games."
arch=('loong64' 'x86_64')
license=('GPL')
url="http://www.scummvm.org/"
depends=('libpng' 'libtheora' 'sdl2' 'sdl2_net' 'fluidsynth' 'flac' 'faad2' 'libvorbis' 'libmad' 'freetype2'
         'libgl' 'glu' 'libjpeg-turbo' 'libmpeg2' 'curl' 'a52dec' 'giflib' 'glew' 'libspeechd' 'gtk3' 'sndio')
makedepends=('mesa')
source=("https://downloads.scummvm.org/frs/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('aef6c7b83f087cf0742dcd1f896f5706e2408e54c4c902e75f03e69f4c5475b1d33a2149246646afb96c02aa2e85ba93995dff732b3aa84338b7bc96c62b802c')
b2sums=('716503fbbd736e522918203400ccf777bc24a8e43c8c030c0e86d9c4f3c3da3f0c580269781fe030901e06854d43e2ed2f6ec9bc4e269c4d4dde8d3e08f494f2')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --enable-release \
    --enable-all-engines \
    --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
