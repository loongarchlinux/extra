# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Damir Perisa <damir@archlinux.org>

pkgname=potrace
pkgver=1.16
pkgrel=3
pkgdesc='Utility for tracing a bitmap (input: PBM,PGM,PPM,BMP; output: EPS,PS,PDF,SVG,DXF,PGM,Gimppath,XFig)'
url='http://potrace.sourceforge.net/'
arch=(loong64 x86_64)
license=(GPL-2.0-only)
depends=(glibc # libc.so libm.so
         zlib libz.so)
_archive="$pkgname-$pkgver"
source=("http://potrace.sourceforge.net/download/$pkgver/$_archive.tar.gz")
sha256sums=('be8248a17dedd6ccbaab2fcc45835bb0502d062e40fbded3bc56028ce5eb7acc')

build() {
	cd "$_archive"
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--with-libpotrace
	make
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
}
