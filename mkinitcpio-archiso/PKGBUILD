# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mkinitcpio-archiso
pkgver=70
pkgrel=1
pkgdesc="Initcpio scripts used by archiso"
arch=(any)
url="https://gitlab.archlinux.org/mkinitcpio/mkinitcpio-archiso"
license=(GPL-3.0-or-later)
depends=(
  bash
  device-mapper
  mkinitcpio
)
makedepends=(git)
checkdepends=(
  shellcheck
  shfmt
)
optdepends=(
  'curl: for PXE over HTTP'
  'gnupg: for PGP signature verification of rootfs over PXE'
  'mkinitcpio-nfs-utils: for PXE over NFS'
  'nbd: for PXE over NBD'
  'openssl: for CMS signature verification of rootfs over PXE'
  'pv: for status display during copy to RAM'
)
source=(git+https://gitlab.archlinux.org/mkinitcpio/$pkgname.git#tag=v$pkgver?signed)
sha512sums=('1737dc00986ba4552533a064f4d12c25c32736d22f3b7598b76eaf4955063685290db6e1f2d3d1c3769a97898180a6291f8414cb3362a8a1b94515ece438fe40')
b2sums=('6fca81694242c620bf848afc4919f30205f96a62397167cff63086dc341ca0935edeab989e70996384799de8cdcfc6eb4e66759e42c8a010c96db2cbdf68d0df')
validpgpkeys=(
  '991F6E3F0765CF6295888586139B09DA5BF0D338'  # David Runge <dvzrv@archlinux.org>
  'BB8E6F1B81CF0BB301D74D1CBF425A01E68B38EF'  # nl6720 <nl6720@archlinux.org>
)

check() {
  make -k check -C $pkgname
}

package() {
  make DESTDIR="$pkgdir/" PREFIX=/usr install -C $pkgname
}
