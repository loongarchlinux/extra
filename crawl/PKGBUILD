# Maintainer: Ivy Foster <iff@archlinux.org>
# Contributor: Jakob Gruber <jakob.gruber@gmail.com>

pkgbase=crawl
pkgname=(crawl-data crawl-ncurses crawl-tiles)
pkgdesc="Dungeon Crawl Stone Soup: open-source, single-player, role-playing \
         roguelike game of exploration and treasure-hunting"
url='https://crawl.develz.org/'
license=(GPL-2.0-or-later)

pkgver=0.31.0
pkgrel=2
arch=(loong64 x86_64)

# line-by-line: both versions, graphical, ncurses, makedepends
makedepends=(
	gcc-libs glibc hicolor-icon-theme lua51 sqlite zlib
	freetype2 glu libglvnd sdl2 sdl2_image sdl2_mixer ttf-dejavu
	ncurses
	bison flex imagemagick mesa perl pngcrush python-yaml
)

source=("https://github.com/crawl/crawl/releases/download/$pkgver/stone_soup-$pkgver-nodeps.tar.xz")
# Checksum provided by packager
b2sums=('5b46b31f275ce6dec718428cb90ee492b45ca65a4122573b93337c8b5429a4292d318c0873290a541ee0b98882fa97d971b5e07c607671a22c3202a4a4ef16ea')

prepare() {
	mv stone_soup-$pkgver crawl
	cp -a crawl crawl-tiles
}

_make() {
	make -C "$1/source" \
		EXTERNAL_FLAGS="$CXXFLAGS" \
		EXTERNAL_LDFLAGS="$LDFLAGS" \
		INSTALL_UGRP=root:root \
		MCHMOD=755 \
		NO_TRY_GOLD=1 \
		NO_TRY_LLD=1 \
		SOUND=1 \
		STRIP=/usr/bin/true \
		bin_prefix=bin \
		prefix=/usr \
		"${@:2}"
}

build() {
	_make crawl       all
	_make crawl-tiles all TILES=y GAME=crawl-tiles
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_crawl-data() {
	pkgdesc="Data files for $pkgdesc"
	depends=(crawl-game)
	optdepends=(
		'crawl-ncurses: ncurses user interface'
		'crawl-tiles: graphical user interface'
	)
	provides=(stone-soup)
	conflicts=(stone-soup)
	replaces=(stone-soup)
	# Apparently, dbscripts [doesn't support][1] split packages with different architecture
	# [1]: https://wiki.archlinux.org/title/DeveloperWiki:HOWTO_Be_A_Packager#Adding_a_new_Package
#	arch=(any)

	_make crawl       DESTDIR="$pkgdir" install
	_make crawl-tiles DESTDIR="$pkgdir" install TILES=y GAME=crawl-tiles

	_pick ncurses "$pkgdir/usr/bin/crawl"
	_pick tiles   "$pkgdir/usr/bin/crawl-tiles" "$pkgdir/usr/share/crawl/dat/tiles"

	install -Dm644 crawl/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_crawl-ncurses() {
	pkgdesc="$pkgdesc (ncurses version)"
	provides=(crawl-game)
	depends=(
		crawl-data
		gcc-libs glibc hicolor-icon-theme lua51 sqlite zlib
		ncurses
	)

	mv ncurses/* "$pkgdir"
	_make crawl DESTDIR="$pkgdir" install-xdg-data

	install -Dm644 crawl/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_crawl-tiles() {
	pkgdesc="$pkgdesc (graphical version)"
	provides=(crawl-game)
	depends=(
		crawl-data
		gcc-libs glibc hicolor-icon-theme lua51 sqlite zlib
		freetype2 glu libglvnd sdl2 sdl2_image sdl2_mixer ttf-dejavu
	)

	mv tiles/* "$pkgdir"
	_make crawl DESTDIR="$pkgdir" install-xdg-data TILES=y GAME=crawl-tiles

	install -Dm644 crawl/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=0 sts=-1 noet:
