# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=d-spy
pkgver=1.10.0
pkgrel=1
pkgdesc="D-Bus debugger for GNOME"
url="https://gitlab.gnome.org/GNOME/d-spy"
arch=(loong64 x86_64)
license=(
  GPL-3.0-or-later
  LGPL-3.0-or-later
)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
)
makedepends=(
  git
  meson
)
provides=(libdspy-1.so)
groups=(gnome-extra)
_commit=e9b571ab7f486569312eb57d69f47dad5194b0da  # tags/1.10.0^0
source=("git+https://gitlab.gnome.org/GNOME/d-spy.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd d-spy
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson d-spy build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
