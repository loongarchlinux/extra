# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-system-monitor
pkgver=45.0.2
pkgrel=1
pkgdesc="View current processes and monitor system state"
url="https://wiki.gnome.org/Apps/SystemMonitor"
arch=(loong64 x86_64)
license=(GPL)
depends=(
  cairo
  dconf
  gdk-pixbuf2
  glib2
  glibmm
  gtk3
  gtkmm3
  hicolor-icon-theme
  libgtop
  libhandy
  librsvg
  libsigc++
  pango
  polkit
  systemd
)
makedepends=(
  appstream-glib
  git
  meson
  yelp-tools
)
groups=(gnome)
_commit=e74545dbdebd8e591c1679181c5ff3d53c53386e  # tags/45.0.2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-system-monitor.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
