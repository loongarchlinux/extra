# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-gdstk
pkgver=0.9.42
_commit=41f72281ca4ec9f1727cde702b88cc780ccdb27a
pkgrel=3
pkgdesc="Python module for creating GDSII stream files, usually CAD layouts"
url="https://github.com/heitzmann/gdstk"
license=('BSL-1.0')
arch=('loong64' 'x86_64')
depends=('python-numpy')
makedepends=('git' 'python-setuptools' 'python-build' 'python-installer' 'python-wheel' 'cmake')
checkdepends=('python-pytest')
source=("git+https://github.com/heitzmann/gdstk.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cd gdstk
  # Upstream wants to test CI :/
  sed -i 's/oldest-supported-numpy/numpy/' pyproject.toml
}

build() {
  cd gdstk
  python -m build --wheel --no-isolation
}

check() {
  cd gdstk
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$python_version" pytest
}

package() {
  cd gdstk
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
