# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=rsop
pkgver=0.3.7
pkgrel=1
pkgdesc="Stateless OpenPGP (SOP) based on rpgp"
arch=(loong64 x86_64)
url="https://codeberg.org/heiko/rsop"
license=(
  'Apache-2.0 OR MIT'
  CC0-1.0
)
groups=(stateless-openpgp)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  pcsclite
  rust
)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgname/v$pkgver.tar.gz
)
sha512sums=('e5f9542d063c0b0a0a0ee97303e1004ad4a4db0a596e97dfd3075d35be0e990c4aa77fa38bbbd5813907db82781d45b43a1b5872922698b7e7d540b2e51f7b43')
b2sums=('5171e6091c132b4e3e9d65b2d4b47939a98e6793d2735b6d9ad1139f67c684d2751f72cb036d0d0f3d51331c12b43af045cf50f3e5de3f2660c413bec4787248')

prepare() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  depends+=(
    pcsclite libpcsclite.so
  )

  cd $pkgname
  install -vDm 0755 "target/release/$pkgname" -t "$pkgdir/usr/bin/"
  install -vDm 0644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
