# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=flashgbx
pkgver=4.2
pkgrel=1
pkgdesc='Reads and writes Game Boy and Game Boy Advance cartridge data'
arch=(any)
url=https://github.com/lesserkuma/FlashGBX
license=(GPL3)
depends=(
  pyside6
  python-dateutil
  python-pillow
  python-pyserial
  python-requests
  python-setuptools
)
makedepends=(git)
_tag=201d76301d1ba02c2479fd117f6a0582a4ec58f8
source=(git+https://github.com/lesserkuma/FlashGBX.git#tag=${_tag})
sha256sums=('c22773773e747704df308c9f415a37d7b12658c9b32c21feb29ff0a98f252837')

pkgver() {
  cd FlashGBX
  git describe --tags
}

build() {
  cd FlashGBX
  python setup.py build
}

package() {
  cd FlashGBX
  python setup.py install --skip-build --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
