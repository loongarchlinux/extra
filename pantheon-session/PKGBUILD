# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Helder Bertoldo <helder.bertoldo@gmail.com>

pkgname=pantheon-session
pkgver=6.0.0.r14.71b7b44
pkgrel=1
pkgdesc='Session settings for Pantheon'
arch=(any)
url=https://github.com/elementary/session-settings
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  dconf
  gala
  gnome-keyring
  gnome-session
  gnome-settings-daemon
  pantheon-applications-menu
  onboard
  orca
  plank
  wingpanel
  xdg-user-dirs-gtk
)
makedepends=(
  git
  meson
)
optdepends=(pantheon-default-settings)
_commit=71b7b445189419c34ef24bfbb47709f714055136
source=(pantheon-session-settings::git+https://github.com/elementary/session-settings.git#commit=${_commit})
b2sums=('29fd84df3c5affe7fa299074632e1ab8fc16ffce0fcfe8b5eb7065153025a6cfa5d77844cc99950e51769e8771c3d0139f617f0480a2ff30a5cc4d0f5c1715f6')

pkgver() {
  cd pantheon-session-settings
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson pantheon-session-settings build \
    -Dfallback-session=gnome \
    -Dwayland=true
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
