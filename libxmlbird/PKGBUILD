# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Andrew Sun <adsun701 at gmail dot com>
# Contributor: Manuel Hüsers <manuel dot huesers at uni-ol dot de>
# Contributor: fstirlitz <felix dot von dot s at posteo dot de>

pkgname=libxmlbird
pkgver=1.2.14
pkgrel=1
pkgdesc='XML parsing library written in Vala'
arch=('loong64' 'x86_64')
url='https://birdfont.org/xmlbird.php'
license=('LGPL3')
depends=('glib2')
makedepends=('python' 'vala')
source=("https://birdfont.org/xmlbird-releases/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('ab282f242d609527efc162fba103c5ca4822b6f9d65f01cc8727128d204692c5'
            'SKIP')
validpgpkeys=('FB3BEFA59A6FF7F0E0682B68BCD31D4CCCEB9DD4') # Johan Mattsson

build() {
  cd $pkgname-$pkgver
  ./configure --prefix /usr --libdir lib --cflags "$CFLAGS" --ldflags "$LDFLAGS"
  ./build.py
}

package() {
  cd $pkgname-$pkgver
  ./install.py --dest "$pkgdir"
}
