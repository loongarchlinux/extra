# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=perl-tie-cycle
_dist=Tie-Cycle
pkgver=1.228
pkgrel=1
pkgdesc="Cycle through a list of values via a scalar"
arch=('any')
url="https://metacpan.org/release/${_dist}"
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/B/BD/BDFOY/${_dist}-${pkgver}.tar.gz)
md5sums=('2c99b09532d80599f4b041f3ecae7af8')
sha256sums=('875651be1c657d85bcac056a76be22888395412c8e5af3d84d4c37b8fbb5b448')

build() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1
    make test
}

package() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
