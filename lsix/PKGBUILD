# Maintainer: Christian Hesse <eworm@archlinux.org>

pkgname=lsix
pkgver=1.8.2
pkgrel=3
pkgdesc='Like ls, but for images, shows thumbnails in terminal using sixel graphics'
arch=('any')
url='https://github.com/hackerb9/lsix'
license=('GPL-3.0-or-later')
depends=('imagemagick' 'ttf-dejavu')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hackerb9/lsix/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('79bf81bd66747a9fab1692c52dcda004fe500fbae118dc0a6bdbc6d6aefa20c1')

prepare() {
  sed -i '/fontfamily=Dejavu-Sans/s|^#||' "${srcdir}/${pkgname}-${pkgver}"/lsix
}

package () {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -Dm0755 lsix "${pkgdir}/usr/bin/lsix"
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

