# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgname=luajit
# LuaJIT has a "rolling release" where you should follow git HEAD
_commit=b51f842b20d98408cc73f11962e9e2f60bbb60f5
# The patch version is the timestamp of the above git commit, obtain via `git show -s --format=%ct`
_ct=1713773202
pkgver="2.1.${_ct}"
pkgrel=11
pkgdesc='Just-in-time compiler and drop-in replacement for Lua 5.1'
arch=('loong64' 'x86_64')
url='https://luajit.org/'
license=('MIT')
depends=('gcc-libs')
source=("LuaJIT-${_commit}.tar.gz::https://github.com/loongson-community//LuaJIT/archive/${_commit}.tar.gz")
md5sums=('d32e1d86728c99be863f0dae76fbb3e1')
sha256sums=('40cada021f41ee958276628365563fad03b966587ff5e730aa40d36921adb539')
b2sums=('5305a6f4c52facf59f746f97b94244fee9c8ffe4689007142e33289a43f2654c233a109827b84dc12e77c12650cd77b4c9716ba0fef1bb5f8816b1d9fba28c4e')

build() {
  cd "LuaJIT-${_commit}"

  # Avoid early stripping
  make amalg PREFIX=/usr BUILDMODE=dynamic TARGET_STRIP=" @:"
}

check() {
  cd "LuaJIT-${_commit}"

  # Make sure that _ct was updated
  test "${_ct}" == "$(cat .relver)"
}

package() {
  cd "LuaJIT-${_commit}"

  make install DESTDIR="$pkgdir" PREFIX=/usr
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}
