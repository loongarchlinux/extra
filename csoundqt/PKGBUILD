# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ainola
# Contributor: rtfreedman

_name=CsoundQt
pkgname=csoundqt
pkgver=1.1.2
pkgrel=1
epoch=1
pkgdesc="A frontend for Csound"
arch=(loong64 x86_64)
url="https://csoundqt.github.io/"
license=(
  CC-BY-SA-4.0
  GFDL-1.3-only
  GPL-2.0-or-later
  LGPL-2.1-or-later
  LGPL-3.0-or-later
)
groups=(pro-audio)
depends=(
  csound-doc
  gcc-libs
  glibc
  hicolor-icon-theme
  qt5-base
  qt5-declarative
  qt5-webchannel
  qt5-webengine
)
makedepends=(
  csound
  rtmidi
)
optdepends=('python: for examples')
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('89d552d7fafa16cd096128ebf7cb74f295f28c6bb113560a930e6d0200e49da207e43ad3197f6f1944d6211528cd1b374fea5769d3e30c7b9bd0c64ecfc2884e')
b2sums=('9cfa3316d3967e36dfa2c8ec54f34f97fdcdfe5f9f777d3c7258ad79cf6eed3f6b574fcd66fd827bc401ce577eaa1fd4c66e40bc8fa3cc8bf0f8a74a3a5bb812')

build() {
  cd $_name-$pkgver
  qmake-qt5 qcs.pro \
    SHARE_DIR="$pkgdir/usr/share" \
    INSTALL_DIR="$pkgdir/usr" \
    CONFIG+=rtmidi \
    CONFIG+=html_webengine \
    CONFIG+=record_support
  make
}

package() {
  depends+=(
    csound libcsnd6.so libcsound64.so
    rtmidi librtmidi.so
  )

  make DESTDIR="${pkgdir}" install -C $_name-$pkgver
  install -vDm 644 $_name-$pkgver/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
