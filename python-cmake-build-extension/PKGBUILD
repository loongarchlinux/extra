# Maintainer: Maxime Gauduin <alucryd@archinux.org>
# Contributor: xiretza <xiretza+aur@xiretza.xyz>

pkgname=python-cmake-build-extension
pkgver=0.5.1
pkgrel=3
pkgdesc='Setuptools extension to build and package CMake projects'
arch=(any)
url=https://github.com/diegoferigo/cmake-build-extension
license=(MIT)
depends=(
  cmake
  python
  python-gitpython
  python-setuptools
  python-setuptools-scm
  python-wheel
  ninja
)
makedepends=(
  git
  python-build
  python-installer
)
_tag=2c3d822e40fe3fd2d769ad09dba367e6617dcc82
source=(git+https://github.com/diegoferigo/cmake-build-extension.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd cmake-build-extension
  git describe --tags | sed 's/^v//'
}

build() {
  cd cmake-build-extension
  python -m build -wn
}

package() {
  python -m installer --destdir="${pkgdir}" cmake-build-extension/dist/*.whl
  install -Dm 644 cmake-build-extension/LICENSE -t "${pkgdir}"/usr/share/licenses/python-cmake-build-extension/
}

# vim: ts=2 sw=2 et
