# Maintainer: Maxime Gauduin <alucryd@archinux.org>
# Contributor: xiretza <xiretza+aur@xiretza.xyz>

pkgname=python-cmake-build-extension
pkgver=0.6.0
pkgrel=1
pkgdesc='Setuptools extension to build and package CMake projects'
arch=(any)
url=https://github.com/diegoferigo/cmake-build-extension
license=(MIT)
depends=(
  cmake
  python
  python-gitpython
  python-setuptools
  python-setuptools-scm
  python-wheel
  ninja
)
makedepends=(
  git
  python-build
  python-installer
)
_tag=d7450a39009db5f5ccc181942e4ac8b0b0c54787
source=(git+https://github.com/diegoferigo/cmake-build-extension.git#tag=${_tag})
b2sums=('44cf0e9fce72e4f0dba4f6dde4a68dd70000043804229c34b4a54e526b5690d35172334d3bccab2e2f331f43db24253e7cc763c9fb90b9391cea1bd8f3dd9257')

pkgver() {
  cd cmake-build-extension
  git describe --tags | sed 's/^v//'
}

build() {
  cd cmake-build-extension
  python -m build -wn
}

package() {
  python -m installer --destdir="${pkgdir}" cmake-build-extension/dist/*.whl
  install -Dm 644 cmake-build-extension/LICENSE -t "${pkgdir}"/usr/share/licenses/python-cmake-build-extension/
}

# vim: ts=2 sw=2 et
