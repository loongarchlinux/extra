# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-manhole
pkgver=1.8.0
pkgrel=6
arch=('any')
pkgdesc='Debugging manhole for python applications'
license=('BSD-2-Clause')
url='https://github.com/ionelmc/python-manhole'
depends=('python')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-virtualenv' 'python-pytest' 'python-process-tests' 'python-requests' 'uwsgi')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ionelmc/python-manhole/archive/v$pkgver.tar.gz")
sha512sums=('041452d602895f7ceaafc74ac50713acd470120b022a942946de83f90d2d839ee15b333dfc329f88597b13e0b3fee436af526beae95a3ff94e51376fd2cc27e6')

prepare() {
  cd python-manhole-$pkgver
  # Drop imp module, removed from Python 3.12
  sed -i tests/test_manhole.py \
    -e 's|^import imp$|import importlib.util|' \
    -e 's|imp.find_module|importlib.util.find_spec|'
}

build() {
  cd python-manhole-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  # Hack to process .pth files properly

  cd python-manhole-$pkgver
  virtualenv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    PYTHONPATH=src pytest
  ) || echo "Tests failed"
}

package() {
  cd python-manhole-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
