# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-persistent
pkgver=5.2
pkgrel=1
pkgdesc="Translucent persistent objects"
arch=('loong64' 'x86_64')
license=('ZPL-2.1')
url="https://github.com/zopefoundation/persistent/"
depends=('python-cffi' 'python-zope-interface')
makedepends=('git' 'python-setuptools')
checkdepends=('python-zope-testrunner' 'python-manuel')
source=("git+https://github.com/zopefoundation/persistent.git#tag=$pkgver")
sha512sums=('76f6dd20021c28f9ea61271bbcf4358fe278488715db086601600bba63276fb4e8d7f7ef27b86cd1d8d9398c073c97ccd1b47bcc806c077d351551ed65fc2134')

build() {
  cd persistent
  python setup.py build
}

check() {
  cd persistent
  python -m venv "$srcdir"/pyvenv --system-site-packages
  "$srcdir"/pyvenv/bin/python setup.py develop
  "$srcdir"/pyvenv/bin/python -m zope.testrunner --test-path=src
}

package() {
  cd persistent
  python setup.py install --root="$pkgdir" --optimize=1
}
