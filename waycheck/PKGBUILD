# Maintainer: Campbell Jones <arch at serebit dot com>

pkgname=waycheck
pkgver=1.2.1
pkgrel=1
pkgdesc="Simple GUI that displays the protocols implemented by a Wayland compositor"
arch=("loong64" "x86_64")
url="https://gitlab.freedesktop.org/serebit/$pkgname"
license=("Apache-2.0")
depends=("hicolor-icon-theme" "qt6-base>=6.5" "qt6-wayland>=6.5" "wayland")
makedepends=("meson")
checkdepends=("appstream")
source=("$url/-/archive/v$pkgver/${pkgname}-v${pkgver}.tar.gz"
        "$url/uploads/04252cfa1d9a8c1e0fb5fd09cac66714/${pkgname}-v${pkgver}.tar.gz.asc")
b2sums=('0afc3c643d4fc3eea7d6ec7ea6d58c3d99985552950acb2109aebcaff0edc988719a1de47cbfe6b76cfb92f176b170dc379175be3fdb7ea167255c845e6685ea'
        'SKIP')
validpgpkeys=("E87E5B39F04A5D889D8C0147F6D84143496F6680")

build() {
	arch-meson "$pkgname-v$pkgver" build
	meson compile -C build
}

check() {
	meson test -C build
}

package() {
	meson install -C build --destdir "$pkgdir"
}
