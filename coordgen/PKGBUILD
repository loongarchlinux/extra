# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=coordgen
pkgver=3.0.2
pkgrel=2
pkgdesc='Schrodinger-developed 2D Coordinate Generation'
arch=(loong64 x86_64)
url='https://github.com/schrodinger/coordgenlibs'
license=(BSD-3-Clause)
depends=(gcc-libs
         glibc
         maeparser)
makedepends=(boost
             cmake
             git)
source=(git+https://github.com/schrodinger/coordgenlibs#tag=v$pkgver)
sha256sums=('4fae2a90370dfa4ad62792bbbb7a01169d3b6f96a929fe3af53ada0b389e6962')

build() {
  cmake -B build -S coordgenlibs \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOORDGEN_USE_MAEPARSER=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 coordgenlibs/templates.mae -t "$pkgdir"/usr/share/coordgen
  install -Dm644 coordgenlibs/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
