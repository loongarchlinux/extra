# Maintainer: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=packer
pkgver=1.10.3
pkgrel=2
pkgdesc="tool for creating identical machine images for multiple platforms from a single source configuration"
arch=('loong64' 'x86_64')
url="https://github.com/hashicorp/packer"
license=('LicenseRef-BSL-1.1')
makedepends=('git' 'go' 'go-tools')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hashicorp/packer/archive/v${pkgver}.tar.gz")
sha512sums=('5e32f70a67dca22b5fa91a1d29f81218d13a47976f9eee8c316a5bb2fc0432db59b4c742091e66888423e3263f7c30e5316a0c5c685a002c733aa873d55966bb')

prepare() {
	export GOPATH="${srcdir}"
	export PATH="$PATH:$GOPATH/bin"
	mkdir -p "src/github.com/hashicorp"
	mv "${pkgname}-${pkgver}" "src/github.com/hashicorp/${pkgname}"
	sed -e '/VersionPrerelease = ""/ {N;N;N;d;}' -i "${srcdir}/src/github.com/hashicorp/${pkgname}/Makefile"
}

build() {
  cd "src/github.com/hashicorp/${pkgname}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
	go build -o packer-binary
}

# Currently broken
# check() {
#   cd "src/github.com/hashicorp/${pkgname}"
#   go test ./...
# }

package() {
	cd "src/github.com/hashicorp/${pkgname}"
	install -Dm755 packer-binary "${pkgdir}/usr/bin/packer"
	install -Dm644 contrib/zsh-completion/_packer "${pkgdir}"/usr/share/zsh/site-functions/_packer
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set et sw=2 ts=2 tw=79:
