# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=akonadi-search
pkgver=23.08.3
pkgrel=1
pkgdesc='Libraries and daemons to implement searching in Akonadi'
url='https://kontact.kde.org'
arch=(loong64 x86_64)
license=(GPL LGPL FDL)
depends=(xapian-core akonadi-mime kcontacts5 kcalendarcore5 krunner5)
makedepends=(extra-cmake-modules qt5-tools doxygen corrosion)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ccf1d5d11d53e5d032a6bed68310847f1489eb95285531b4a8f6a3321830db18'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(!lto)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
