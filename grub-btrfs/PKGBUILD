# Maintainer: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: gato_lento <vukk.euob at gmail dot com>
# Contributor: James W. Barnett <james at wbarnett dot us>

pkgname=grub-btrfs
pkgver=4.13
pkgrel=2
pkgdesc='Include btrfs snapshots in GRUB boot options'
arch=('any')
url="https://github.com/Antynea/${pkgname}"
license=('GPL3')
depends=('btrfs-progs' 'grub')
optdepends=(
    'snapper: For snapper support'
    'inotify-tools: For grub-btrfsd daemon'
)
backup=('etc/default/grub-btrfs/config')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
b2sums=('e2f11a0a8282e3ec8ff8223e3bad70b5d5c5e81b4d740a1bf3554db412dbe48a8a0f216f4e6c65ae1d11c01a27b8b92bae9b470c60d2389505ce089511536e1a')

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" INITCPIO=true install
}

# vim:set ts=4 sw=4 et:
