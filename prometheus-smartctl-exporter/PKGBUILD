# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>

_name=smartctl_exporter
pkgname=prometheus-smartctl-exporter
pkgver=0.12.0
pkgrel=2
pkgdesc="Prometheus exporter for S.M.A.R.T. metrics using smartctl"
arch=(loong64 x86_64)
url="https://github.com/prometheus-community/smartctl_exporter"
license=(Apache-2.0)
depends=(glibc smartmontools)
makedepends=(go)
backup=("etc/conf.d/$pkgname")
# we cannot use LTO as otherwise we do not get reproducible package with full RELRO
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz"
        "$pkgname.conf"
        "$pkgname.service")
b2sums=('9ab0fa3e4dcee1e21a566cd9677b99f579f60e761d40d9ab1dd75380bbff9c189d9895ee2e914d1b6bdeee0c5b6d185c71ae62f4ce7585b3954192091cc96e25'
        '8f90333769207a931453ef129c1cb91bfd5351d43df64826bc2fafe07b149028b156eb74ac5dfe4e5d990f39605b94e93d1b2598d4eca85ead06b0db5555c50c'
        '124b6b6273e99e7e72d8a3a569ffe46efe07e0fa70fa5f6a3135f3b2ad6f49c9737e2d491a29adac292f95e8524b8f11cb2e4aad617ff0c26b4f50c819d767ac')

prepare() {
  cd "$_name-$pkgver"
  go mod vendor
  mkdir -p build
}

build() {
  cd "$_name-$pkgver"

  # set flags for cgo
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"

  # set GOPATH so makepkg puts source files into the debug package
  export GOPATH="$srcdir"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false -linkmode external" \
    -o build .
}

package() {
  # systemd service
  install -vDm 644 "$pkgname.service" -t "$pkgdir/usr/lib/systemd/system/"
  install -vDm 644 "$pkgname.conf" "$pkgdir/etc/conf.d/$pkgname"

  cd "$_name-$pkgver"

  # binary
  install -vDm 755 "build/$_name" "$pkgdir/usr/bin/$pkgname"

  # license
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
