# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer: Vlad M. <vlad@archlinux.net>

pkgname=toxic
pkgdesc='CLI Tox client'
license=('GPL3')
pkgver=0.15.1
pkgrel=1
depends=('curl'
         'freealut'
         'libnotify'
         'qrencode'
         'libx11'
         'toxcore')
makedepends=('git')
arch=('loong64' 'x86_64')
url='https://github.com/JFreegman/toxic'
source=("git+https://github.com/JFreegman/$pkgname?signed#tag=v$pkgver"
	)
sha512sums=('c91cdc00decce47ee161c89a2819b3508c00e89dec4a44e3d304fbcbdd73f468443b7aa9382de5f43282d34dbb2453879a6308ab7ad2f1f78c01e0e611e4d906')
validpgpkeys=('BABD00573A065BFA90D53D563627F3144076AE63')  # Jfreegman <jfreegman@gmail.com>

build() {
  cd "$pkgname"
  make PREFIX=/usr DISABLE_GAMES=1
}

package() {
  cd "$pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
