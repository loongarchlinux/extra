# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-chewing
pkgver=2.0.0
pkgrel=1
pkgdesc='Chinese Chewing Engine for IBus Framework'
arch=('loong64' 'x86_64')
# "GPLv2+" in https://github.com/chewing/ibus-chewing/blob/1.6.2/CMakeLists.txt#L90
license=('GPL-2.0-or-later')
url='https://github.com/chewing/ibus-chewing'
depends=('ibus' 'libchewing' 'gtk4' 'libadwaita')
makedepends=('cmake' 'minisign')
checkdepends=('dbus' 'xorg-server-xvfb')
source=("https://github.com/chewing/ibus-chewing/releases/download/v$pkgver/$pkgname-$pkgver-Source.tar.xz"{,.minisig})
sha512sums=('23449ac67c06e63442f859e0c0883d9f55c0de9ccf9c97f8dda4aab1066e0be9dc9f3964989c992c95d8fce17714a624d430a422c6d866646f0f0102165b6190'
            'a3898a5a84a83e86213760101e753bdcab7111255f0823d878223c534eb39c9443a84d9b3996d098725ff186e0c306bb08eb1f4a0819d0208b3169dffdedcb3d')
# The key is mentioned on https://github.com/chewing/ibus-chewing
_validminisignkey='RWRzJFnXiLZleAyCIv1talBjyRewelcy9gzYQq9pd3SKSFBPoy57sf5s'

# XXX: move to verify() when pacman 7.0 is out
# https://gitlab.archlinux.org/pacman/pacman/-/merge_requests/37
prepare() {
  minisign -Vm $pkgname-$pkgver-Source.tar.xz -P $_validminisignkey
}

build() {
  cd $pkgname-$pkgver-Source

  cmake -B build -S . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/ibus

  make -C build
}

check() {
  cd $pkgname-$pkgver-Source/build

  # glib-compile-schemas is needed for tests
  # https://github.com/chewing/ibus-chewing/blob/v2.0.0/.github/workflows/ci.yml#L33
  glib-compile-schemas ../src/setup --targetdir "$srcdir/$pkgname-$pkgver-Source/build/bin"
  GSETTINGS_SCHEMA_DIR="$srcdir/$_pkgname/build/bin/" xvfb-run --auto-display dbus-run-session make test
}

package() {
  cd $pkgname-$pkgver-Source/build
  make DESTDIR="$pkgdir" install
}
