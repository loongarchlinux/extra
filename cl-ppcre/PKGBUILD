# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=cl-ppcre
pkgver=2.1.2
pkgrel=1
pkgdesc='Perl-compatible portable regexp library for Common Lisp'
arch=('any')
url='https://edicl.github.io/cl-ppcre/'
license=('BSD-2-Clause')
depends=('common-lisp' 'cl-asdf' 'cl-unicode')
makedepends=('git')
checkdepends=('sbcl' 'cl-flexi-streams' 'perl')
source=(
  "$pkgname::git+https://github.com/edicl/cl-ppcre#tag=v$pkgver"
  'run-tests.lisp'
)
sha512sums=('ba61c40d9d4052247f22241140f003e4dc23127800301514db03b686473b9a45045650d3afd7da7d5bd6d6bb93dd0ccd10c00efa1929298168af6946d344c00f'
            '238f4255854d448485453d7ba47bfeb12a3844f945b638c7a3232ed63c2fa2e10b7f6e08bbc4db6da49bb6cf763d98df933ab8245ac83ee1ab9ee731db3787f2')
b2sums=('991780ec87219c1884eb80f64923065fcb6f3849b9a4b73c89a285d65bbe1bdd6338131816d57f150958741c2c1002801b3bfbecb89862b961c053235cbf5ff5'
        'cd4fc113c2f1b1e3180010a79621d84b44d79dd47f10aaef9615b56b41f94625584a034059164546456b472adcac19ca0c2db98c889c159ac3c323fc5163d469')

check() {
  cd "$pkgname"

  sbcl --script ../run-tests.lisp
}

package() {
  cd "$pkgname"

  # library
  install -vd "$pkgdir/usr/share/common-lisp/source/$pkgname"
  cp -vr test cl-ppcre-unicode ./*.{lisp,asd} "$pkgdir/usr/share/common-lisp/source/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md docs/index.html

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
