# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=jellyfin-server
_pkgname="${pkgname%-server}"
pkgver=10.9.9
pkgrel=1
pkgdesc='Jellyfin server backend'
arch=('loong64' 'x86_64')
url='https://jellyfin.org'
_url='https://github.com/jellyfin/jellyfin'
license=('GPL-2.0-or-later')
_dotnet_runtime=linux-x64
depends=(
  'aspnet-runtime'
  'bash'
  'sqlite'
  'fontconfig'
  'jellyfin-ffmpeg'
)
makedepends=(
  'git'
  'dotnet-sdk'
)
optdepends=('jellyfin-web: to run web-app on the same machine')
backup=(
  "etc/$_pkgname/logging.json"
  "etc/$_pkgname/$_pkgname.env"
)
options=('!debug')
source=(
  "$pkgname::git+$_url#tag=v$pkgver"
  "jellyfin-packaging::git+${_url}-packaging"
  'sysusers.conf'
  'tmpfiles.conf'
  'fix-envfile-path.patch'
)
sha512sums=('88ff25718565fbd83004f39ef949edd957b243ae4dcc50090c263eae697c2d907ead0a00589251a1a7b08a0137abc0350d31b3b19a9463402d20f52fcc07de91'
            'SKIP'
            '9f62481faa5f6a2fad75b0a5bb727c74f46ada991aff938db225dee0232a2c948ba3e22f56b8fdaf960901b37d8cd6c292665dac1c954c6bc4b5e104da45ef6f'
            '3e12ec3d3fcb15975d5f86bc3ce3363ae89b0e9e0b2580c29fc8a612c0220a74a067138b15c48ae27bb3c5777eca33055f10651949678a1ee7bd094293f6abb6'
            '26d4a2c081c5a2563863fb21a427e6b5ff0f551549b7ba77075054e81fbda064991e3443ec3f11f42ffdcc8adef0e20363f00d248d7967d327656280db919672')
b2sums=('c5c3416ac5b7ecce8ba64026f893bc70f8aa9fedb46c3aced184cf17881b2e97c6e9dc3c5805593d0145093f48b83a860aa391c7b4b0be272932aee4df4a48fd'
        'SKIP'
        'f73c72e1d66480c410dbe71be1af91a2bd76206dfed76e64b8264ee3537f6eb477bcba905ce69a0ec8b273e33c2e46439b9b32ce21543a48d98d80434a46a781'
        'bc8001cf28ebf76a125e7ab0d9d5a8fcf35e0def5b907dc5fe90e16cdbb3fdf8b7f098779ced9a44e7a3918ee605058b379d445a224178456a32a6a99cd084b4'
        '4f63c425fc60e80c36c958674f0bbd704feb79cac9e8d05e76e0bc01d6d3e10555395a45f994e240a68433c01f9a14d5a1db1765eccb7409f11da34161c75ffc')

prepare() {
  cd jellyfin-packaging

  # fix envfile path in systemd service
  patch -p1 -i "$srcdir/fix-envfile-path.patch"
}

build(){
  cd "$pkgname"

  # disable dotnet telemetry
  export DOTNET_CLI_TELEMETRY_OPTOUT=1
  export DOTNET_SKIP_FIRST_TIME_EXPERIENCE=1
  export DOTNET_NOLOGO=1

  dotnet \
    publish \
    Jellyfin.Server \
    --configuration Release \
    --output builddir \
    --self-contained false\
    --runtime "$_dotnet_runtime" \
    -p:DebugSymbols=false \
    -p:DebugType=none
}

package() {
  cd "$pkgname"

  # install binaries
  install -vd "$pkgdir/usr/"{lib,bin}
  cp -r builddir "$pkgdir/usr/lib/jellyfin"
  ln -sf /usr/lib/jellyfin/jellyfin "$pkgdir/usr/bin/jellyfin"

  # ensure binaries have correct permissions
  chmod 755 "$pkgdir/usr/lib/$_pkgname/jellyfin"

  # use upstream provided packaging files
  cd "$srcdir/jellyfin-packaging/debian/conf"

  # systemd integration
  install -vDm644 jellyfin.service -t "$pkgdir/usr/lib/systemd/system"
  install -vDm644 "$srcdir/sysusers.conf" "$pkgdir/usr/lib/sysusers.d/$_pkgname.conf"
  install -vDm644 "$srcdir/tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/$_pkgname.conf"
  install -vDm640 jellyfin "$pkgdir/etc/$_pkgname/$_pkgname.env"
  install -vDm640 logging.json -t "$pkgdir/etc/$_pkgname"
}

# vim: ts=2 sw=2 et:
