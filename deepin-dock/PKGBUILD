# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=6.0.35
pkgrel=1
pkgdesc='The dock of Deepin Desktop Environment'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('LGPL3')
# deepin-wloutput-daemon: org.deepin.dde.KWayland1
# deepin-session-shell: org.deepin.dde.LockFront1
depends=('deepin-menu' 'deepin-daemon' 'deepin-launchpad' 'deepin-qt5integration'
         'deepin-network-core' 'deepin-control-center' 'dwayland' 'wayland' 'dtkcore' 'dtkgui' 'dtkwidget'
         'qt5-base' 'qt5-svg' 'qt5-x11extras' 'qt5-wayland' 'glib2' 'gsettings-qt' 'libdbusmenu-qt5'
         'libx11' 'libxext' 'libglvnd' 'libxtst' 'libxcursor' 'libxres' 'libxcb' 'libxkbcommon'
         'xcb-util-wm' 'xcb-util-image' 'gcc-libs' 'dconf' 'deepin-application-manager'
         'deepin-appearance' 'deepin-widgets' 'deepin-wloutput-daemon' 'deepin-session-shell')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools' 'extra-cmake-modules')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-dock.git#tag=$pkgver")
sha512sums=('00a3cf45c8d286f380ffc49fededbade7d4e5f0c8e17db2a5eb8073ecae7bfeb4da97aa6fbd0c6f93bc21ec2d907b7b273f72804055c39dd54f4f336433a366f')

build() {
  cd dde-dock
  cmake . -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc
  ninja
}

check() {
  cd dde-dock
  # ninja test
}

package() {
  cd dde-dock
  DESTDIR="$pkgdir" ninja install
}
