# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=python-fabulous
pkgver=0.4.0
pkgrel=7
pkgdesc='Print images, colors, and stylish text to the terminal with Python'
arch=(any)
url='https://github.com/jart/fabulous'
license=(Apache-2.0)
depends=(
  python
  python-pillow
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=(
  "git+https://github.com/jart/fabulous.git#tag=$pkgver"
  python-fabulous-pillow10.patch
)
b2sums=(
  feca83ce2a52a754a818ed2b09a6098c6606bbaa81b293358640fb444cafa89769aa31595c316ae49b5dc3f63d3e2e54613c60e3f7cad292343effc7a1c08bab
  5a613c01a25c36d263cba2a56308d2febc8f92bfa652554de2b500d368ca1232cce594a9e014a80acee2f93a171c0b909e9d1906a3238fe106f996c533901598
)

prepare() {
  cd fabulous

  # Fix compatibility with Pillow 10
  # https://github.com/jart/fabulous/pull/23
  git apply -3 ../python-fabulous-pillow10.patch
}

build() {
  cd fabulous
  python -m build --wheel --no-isolation
}

package() {
  cd fabulous
  python -m installer --destdir="$pkgdir" dist/*.whl
}
