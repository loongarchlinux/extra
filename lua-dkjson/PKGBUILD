# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgbase=lua-dkjson
pkgname=("lua-dkjson" "lua53-dkjson" "lua52-dkjson" "lua51-dkjson")
pkgver=2.8
_rockrel=1
pkgrel=1
pkgdesc='David Kolfâ€™s Pure Lua JSON module with UTF-8 support and no external dependencies'
arch=('any')
url="http://dkolf.de/src/dkjson-lua.fsl/home"
license=('MIT')
makedepends=('luarocks'
             'lua51' 'lua52' 'lua53' 'lua' # https://github.com/luarocks/luarocks/issues/1275
            )
_archive="${pkgbase#*-}-$pkgver"
_rock="$_archive-$_rockrel.all.rock"
_rockspec="$_archive-$_rockrel.rockspec"
source=("http://dkolf.de/dkjson-lua/$_archive.tar.gz"
        "https://luarocks.org/manifests/dhkolf/$_rockspec"
        "LICENSE")
sha256sums=('24e8cd3beb91c1c861eb7bb3fbc9bd418bbff9ad4aa5d0471816258236a31532'
            '6ab6ac25e5fbc90d9183bd11c9ea6218dbcb762c90cfc5a7e075eb7531080418'
            '695b49734d1244ff236ec8785aa908883deb0f65b2356e36e8739ea06002b03c')

build() {
	cd "$_archive"
	for LUAVER in 5.{1,2,3,4}; do
		luarocks --lua-version "$LUAVER" \
			make --pack-binary-rock --deps-mode none -- "$srcdir/$_rockspec"
		install -Dm0644 -t "lua-$LUAVER/" "$_rock"
	done
}

_package() {
	cd "$_archive"
	depends=("${pkgname%-*}")
	luarocks --lua-version "$1" --tree "$pkgdir/usr/" \
		install --deps-mode none --no-manifest -- "lua-$1/$_rock"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" "$srcdir/LICENSE"
}

package_lua-dkjson() {
	optdepends=('lua-lpeg: speed up decoding')
	_package 5.4
}

package_lua53-dkjson() {
	optdepends=('lua53-lpeg: speed up decoding')
	_package 5.3
}

package_lua52-dkjson() {
	optdepends=('lua52-lpeg: speed up decoding')
	_package 5.2
}

package_lua51-dkjson() {
	optdepends=('lua51-lpeg: speed up decoding')
	_package 5.1
}
