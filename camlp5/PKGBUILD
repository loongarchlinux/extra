# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Matej Grabovsky <matej.grabovsky at gmail>
# Contributor: Kohei Suzuki <eagletmt@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>
# Contributor: George Giorgidze <giorgidze@gmail.com>
# Contributor: Massimiliano Brocchini <brocchini@netseven.it>
# Contributor: Tej Chajed <tchajed at mit dot edu>

pkgname=camlp5
pkgver=8.03.00
_relname=8.03.00
pkgrel=1
arch=('loong64' 'x86_64')
pkgdesc='A preprocessor-pretty-printer of OCaml.'
url='https://camlp5.github.io/'
license=('BSD')
depends=('ocaml' 'camlp-streams' 'ocaml-rresult' 'ocaml-bos' 'ocaml-re')
makedepends=('ocaml-compiler-libs' 'ocaml-findlib')
source=("camlp5-${_relname}.tar.gz::https://github.com/camlp5/camlp5/archive/$_relname.tar.gz")
sha256sums=('0dae6d32184aca6f2cdbe818ee2f26aa58baa87d9e82f820914c63b35aa075de')
options=('!strip' 'staticlibs' '!debug')

build() {
  cd "${srcdir}/camlp5-${_relname}"

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -strict

  make world.opt
}

package() {
  cd "${srcdir}/camlp5-${_relname}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 etc/META "${pkgdir}/usr/lib/ocaml/camlp5"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/camlp5/LICENSE"
}

