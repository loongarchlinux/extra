# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nikola
pkgver=8.3.1
pkgrel=1
pkgdesc="A static site and blog generator"
arch=(any)
url="https://getnikola.com/"
_url="https://github.com/getnikola/nikola"
license=(MIT)
depends=(
  python
  python-babel
  python-blinker
  python-doit
  python-dateutil
  python-docutils
  python-logbook
  python-lxml
  python-mako
  python-markdown
  python-natsort
  python-piexif
  python-pillow
  python-pygments
  python-pyrss2gen
  python-requests
  python-setuptools  # https://github.com/getnikola/nikola/issues/3743
  python-unidecode
)
makedepends=(
  git
  jupyter-notebook
  python-aiohttp
  python-build
  python-ghp-import
  python-husl
  python-html5lib
  python-installer
  python-ipykernel
  python-jinja
  python-micawber
  python-phpserialize
  python-pygal
  python-pyphen
  python-ruamel-yaml
  python-toml
  python-typogrify
  python-watchdog
  python-wheel
  zeromq
)
checkdepends=(
  libwebp
  python-feedparser
  python-freezegun
  python-pytest
)
optdepends=(
  'libwebp: for WEBP image file support'
  'jupyter-notebook: for .ipynb support'
  'python-aiohttp: for nikola auto'
  'python-ghp-import: for upload to Github pages'
  'python-hsluv: for section color mixing'
  'python-html5lib: for HTML5 minify and format support'
  'python-husl: for optional color support'
  'python-ipykernel: for .ipynb support'
  'python-jinja: for jinja2 based themes'
  'python-micawber: for embedding media in posts'
  'python-phpserialize: for Wordpress import'
  'python-pygal: for SVG graph plotting'
  'python-pyphen: for hyphenation support'
  'python-ruamel-yaml: for YAML support'
  'python-toml: for TOML support'
  'python-typogrify: for typographical enhancements'
  'python-watchdog: for nikola auto'
)
source=(
  git+$_url.git?signed#tag=v$pkgver
)
sha512sums=('e41b3d26e2c431b9ed9fa40d6e71f9bd40eb6f8c5c5312133d0f835ddcdac96b0ab3d5b8c3d6dab56bde35717f684b80cdcced7e64c66829de1fe4acf8724eb8')
b2sums=('a0a6f70e0903442a7611553ba410fadc8724158653f023b1da75aa46c7dadbbd4a5fe19337e33d1b20debbbac57c3391f55d0c8355729895a762c5bebf770029')
validpgpkeys=('DF32C99CFF84282CF9D4CF7171023DBD5EAAEA16') # Chris Warrick <kwpolska@gmail.com>

prepare() {
  cd $pkgname
  # fix issue with pytest 8: https://github.com/getnikola/nikola/issues/3753
  git cherry-pick -n 5f1003f91cd59f62622d379efe9be5fb19a1ed3e
}

build() {
  cd $pkgname
  python -m build --wheel --no-isolation

  # shell completions
  python -m $pkgname tabcompletion --shell bash --hardcode-tasks > ${pkgname}_bash
  python -m $pkgname tabcompletion --shell zsh --hardcode-tasks > ${pkgname}_zsh
  # ensure that the shell completion is assigned to the correct executable
  sed -e "s/__main__.py/$pkgname/g" -i "${pkgname}_"{bash,zsh}
}

check() {
  local pytest_options=(
    -vv
    -o addopts=''
  )
  cd $pkgname
  pytest "${pytest_options[@]}"
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {AUTHORS,CHANGES}.txt {CONTRIBUTING,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 ${pkgname}_bash "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -vDm 644 ${pkgname}_zsh "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
