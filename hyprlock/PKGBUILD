# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=hyprlock
pkgver=0.4.1
pkgrel=1
pkgdesc='hyprlandâ€™s GPU-accelerated screen locking utility'
arch=(loong64 x86_64 aarch64)
url="https://github.com/hyprwm/$pkgname"
license=(BSD-3-Clause)
depends=(cairo # libcairo.so
         gcc-libs # libgcc_s.so libstdc++.so
         glib2 libgobject-2.0.so libglib-2.0.so
         glibc # libc.so libm.so
         hyprlang libhyprlang.so
         hyprutils libhyprutils.so
         libdrm # libdrm.so
         libglvnd libOpenGL.so libEGL.so
         libwebp libwebp.so
         libjpeg-turbo libjpeg.so
         libxkbcommon libxkbcommon.so
         mesa # libgbm.so
         pam libpam.so
         pango libpango-1.0.so libpangocairo-1.0.so
         wayland libwayland-client.so libwayland-egl.so
         wayland-protocols)
makedepends=(cmake)
backup=("etc/pam.d/$pkgname")
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('87531a43088cafcadf29115889f37f73ab4a8cb1e4347723dfe8d53fa0aaba60')

build() {
	cd "$_archive"
	cmake -B build \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D CMAKE_BUILD_TYPE=Release
	cmake --build build
}

package() {
	cd "$_archive"
	DESTDIR="$pkgdir" cmake --install build
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
