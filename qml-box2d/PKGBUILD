# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=qml-box2d
pkgver=20220602
_commit=2c20f757c37820a4c01611ee16d6ade86076666d
pkgrel=1
pkgdesc="Box2D QML plugin"
url="https://github.com/qml-box2d/qml-box2d"
arch=('loong64' 'x86_64')
license=('BSD')
depends=('qt5-declarative')
makedepends=('git')
source=("git+https://github.com/qml-box2d/qml-box2d.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git log -1 --date=format:%Y%m%d --pretty=format:%cd
}

build() {
  cd $pkgname
  qmake
  make
}

package() {
  cd $pkgname
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
