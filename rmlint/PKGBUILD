# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Blisfull <narthana.epa@gmail.com>
# Contributor: SahibBommelig <sahib@online.de>

pkgbase=rmlint
pkgname=(rmlint rmlint-shredder)
pkgver=2.10.2
pkgrel=2
url="https://github.com/sahib/rmlint"
license=('GPL3')
arch=('loong64' 'x86_64')
makedepends=('scons' 'python-sphinx' 'gettext' 'sqlite' 'util-linux-libs'
             'binutils' 'json-glib' 'libelf' 'python-gobject' 'python-cairo'
             'dconf' 'gtksourceview3' 'librsvg' 'desktop-file-utils' 'python-requests'
             'python-setuptools')
source=("https://github.com/sahib/rmlint/archive/v$pkgver/$pkgbase-$pkgver.tar.gz"
        "setuptools.patch")
sha256sums=('0a0233ad517e0fb721f21e188a57cdee5279dd5329b38acb56d5d5312ce46388'
            'a9056ff1ae15272ad43af13a030d38be658c860026e8f5774f9e5481e880c902')

prepare() {
    cd "$srcdir"/$pkgbase-$pkgver
    patch -p1 -i ../setuptools.patch
}

build() {
    cd "$srcdir"/$pkgbase-$pkgver
    scons config
    scons -j4 DEBUG=1 --prefix="$pkgdir"/usr --actual-prefix=/usr
}

package_rmlint() {
    pkgdesc="Tool to remove duplicates and other lint, being much faster than fdupes"
    depends=('sqlite' 'util-linux-libs' 'binutils' 'json-glib' 'libelf')

    cd "$srcdir"/$pkgbase-$pkgver
    scons DEBUG=1 --prefix="$pkgdir"/usr install --actual-prefix=/usr

    rm -rf "$pkgdir"/usr/share/{glib-2.0,icons,applications}
    rm -rf "$pkgdir"/usr/lib
}

package_rmlint-shredder() {
    pkgdesc="Graphical user interface for rmlint"
    depends=('rmlint' 'python-gobject' 'python-cairo' 'dconf' 'librsvg'
             'gtksourceview3' 'xdg-utils')

    cd "$srcdir"/$pkgbase-$pkgver
    scons DEBUG=1 --prefix="$pkgdir"/usr install --actual-prefix=/usr

    rm -rf "$pkgdir"/usr/{bin,share/locale,share/man}
    rm "$pkgdir"/usr/share/glib-2.0/schemas/gschemas.compiled
}
