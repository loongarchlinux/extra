# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=colorcet
pkgname=python-${_pkg}
pkgver=3.1.0
pkgrel=2
pkgdesc="Set of useful perceptually uniform colormaps for plotting scientific data"
arch=(any)
url="https://colorcet.holoviz.org/"
license=(CC-BY-4.0)
depends=(python-pyct)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-wheel
)
checkdepends=(python-pytest)
source=(https://github.com/holoviz/colorcet/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('810b53ba732b439c37c15eed122069d38e77b44aa69ca099442121b57d167e38')

build() {
  cd ${_pkg}-${pkgver}
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  cd ${_pkg}-${pkgver}
  pytest --color=yes colorcet
}

package() {
  cd ${_pkg}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
