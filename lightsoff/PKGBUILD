# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lightsoff
pkgver=46.0
pkgrel=1
pkgdesc="Turn off all the lights"
url="https://wiki.gnome.org/Apps/Lightsoff"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  gtk3
  librsvg
)
makedepends=(
  git
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
_commit=b383e33570b957b823036b0d7813d4d9d70c1aa8  # tags/46.0^0
source=("git+https://gitlab.gnome.org/GNOME/lightsoff.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
