# Maintainer: Jakub Klinkovsk√Ω <lahwaacz at archlinux dot org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=arch-wiki-docs
pkgver=20240501
pkgrel=1
pkgdesc='Pages from Arch Wiki optimized for offline browsing'
arch=('any')
url='https://github.com/lahwaacz/arch-wiki-docs'
license=(GFDL-1.3-or-later)
makedepends=(
  'git'
  'python'
  'python-simplemediawiki'
  'python-lxml'
  'python-cssselect'
  'python-requests'
)
options=('!strip' '!debug')
_commit='62059ba9c5ea8589a9277c46fcf069c710f18558'
source=("$pkgname::git+https://github.com/lahwaacz/arch-wiki-docs#commit=$_commit")
b2sums=('dead6eca758071a06c73c34ad81cd28555245d27d5d2fa21c543559bdece739cca3af5962eecc8c59a9d3d0e9b4444b34a135b915097d594c502f3f7c837f26e')

pkgver() {
  date "+%Y%m%d"
}

prepare() {
  cd "$pkgname"

  LANG=en_US.UTF-8 python \
    arch-wiki-docs.py \
    --output-directory "$srcdir/build_wiki" \
    --clean \
    --safe-filenames
}

package() {
  install -vdm755 "$pkgdir/usr/share/doc/arch-wiki/html"
  cp -r build_wiki/* "$pkgdir/usr/share/doc/arch-wiki/html"
}
