# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

_name=show-in-file-manager
pkgname=python-$_name
pkgver=1.1.5
pkgrel=2
pkgdesc="Python module to open the system file manager and select files in it"
arch=('any')
url="https://github.com/damonlynch/showinfilemanager"
license=('MIT')
depends=('python-pyxdg' 'python-packaging')
makedepends=('python-build' 'python-installer' 'python-wheel'
             'python-setuptools' 'python-argparse-manpage')
source=(https://files.pythonhosted.org/packages/source/s/$_name/$_name-$pkgver.tar.gz)
sha256sums=('ed13a180a1d48fd88fdd4c58bfbcb3860268641a38805192c814d41387192d84')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
