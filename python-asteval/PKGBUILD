# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-asteval
pkgver=0.9.32
pkgrel=2
pkgdesc="Minimalistic evaluator of python expression using ast module"
url="https://github.com/lmfit/asteval"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest-cov' 'python-numpy' 'tk')
source=("git+https://github.com/lmfit/asteval.git#tag=$pkgver")
sha512sums=('6a103640417583bf02138a7682bcbe53d4c8cf000e581b78def267630ccd2df5608a9364e423f518c8114f4bfce8eb7c27b57c0c8852ffd788fff8f283ad89af')

build() {
  cd asteval
  python -m build -nw
}

check() {
  cd asteval
  python -m pytest
}

package() {
  cd asteval
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
