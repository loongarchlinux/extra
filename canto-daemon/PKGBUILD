# Maintainer: David Runge <dvzrv@archlinux.org>

_name=canto-next
pkgname=canto-daemon
pkgver=0.9.8
pkgrel=5
pkgdesc="Next-gen console RSS/Atom reader"
arch=(any)
url="https://github.com/themoken/canto-next"
license=(GPL-2.0-only)
depends=(
  python
  python-feedparser
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
optdepends=('python-requests: for inoreader plugin')
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('5394a0fea38ecbe28c02bd6f68e1e10e0523cf087bb0f79f17e53bdf8e9df16213beaa1c0a5f5873b3126022faf8d0fd999dbd01aa727c35b3c1a823501787da')
b2sums=('55b5e9be9980df471e5488b7583be3b06f8f7048ecb3a89a64ecc9aafa806b201272bd16944a25e7a412da914cc83b76d1f19c0813366209c33195442a84586e')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
