# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-release
pkgver=0.25.7
pkgrel=1
pkgdesc='Cargo subcommand "release": everything about releasing a rust crate'
url='https://github.com/crate-ci/cargo-release'
arch=(loong64 x86_64)
license=(
  Apache-2.0
  MIT
)
depends=(
  cargo
  gcc-libs
  glibc
  zlib
)
makedepends=(rust)
options=(!lto)
source=($url/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('2fad0d243833d6badca9adb9619c116f5cf28088855cb784241281eaaf1bd8308f6236e210dd3156cf6f42f0ad46be91e7680e32e7a275afc3ec01c010ed02b9')
b2sums=('f628c20567f2dcf50b26669f0746916b6c2b530247b3f874ded49875be2d5b3c7faf7865f57f67932793ce95decfecdabe05f127a6dbfd5f7d0d95067b543029')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  cargo build --frozen --release --no-default-features
}

package() {
  cd $pkgname-$pkgver
  install -Dm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:
