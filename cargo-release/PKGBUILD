# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-release
pkgver=0.25.10
pkgrel=1
pkgdesc='Cargo subcommand "release": everything about releasing a rust crate'
url='https://github.com/crate-ci/cargo-release'
arch=(loong64 x86_64)
license=(
  Apache-2.0
  MIT
)
depends=(
  cargo
  gcc-libs
  glibc
  zlib
)
makedepends=(rust)
options=(!lto)
source=($url/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('cc795155341bc6cc08c01b206dca7d6419fdfac895178bed4a4d835681baa3f95467923c4f66f055e2ca0ab4a3440766353753e72f2c73009847a96190f0c170')
b2sums=('e68ce77c2e34fc144d912fd199d48fca99cb9e03d12b3055f300e5c644dc771b7f7bab5f3885477c329b63bc443c301b1a0e7dc9d2f287f502a9773369a34b2a')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  cargo build --frozen --release --no-default-features
}

package() {
  cd $pkgname-$pkgver
  install -Dm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:
