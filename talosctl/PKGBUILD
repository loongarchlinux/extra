# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=talosctl
pkgver=1.7.6
pkgrel=1
pkgdesc='CLI for Talos - A modern OS for Kubernetes'
arch=('loong64' 'x86_64')
url='https://github.com/siderolabs/talos'
license=('MPL-2.0')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
source=("$pkgname::git+https://github.com/siderolabs/talos.git#tag=v${pkgver}?signed")
sha512sums=('f2acdc18105abbd31ad024e433c3d94a224e7e7bfba129a520b76ff74a7ba82fb39de5224860c22353071d74ee79474fe94f6bc78dd850ce05e2203499d4361e')
b2sums=('375bce378fd6508d2b5e875ac5cf17ab847bfc73a4108ba26b6831a73a49db762b76fd1ec98269437bb543d8a98095e894ffd4130a276311b25e9b713e273f4d')
validpgpkeys=('DB997306E3102F11C4E8F5527B26396447AB6DFD'  # "Andrey Smirnov <andrey.smirnov@talos-systems.com>"
              'CC51116A94490FA6FB3C18EB2401FCAE863A06CA'  # frezbo (Noel Georgi) <git@frezbo.dev>
              '15D5721F5F5BAF121495363EFE042E3D4085A811') # Andrey Smirnov <andrey.smirnov@siderolabs.com>

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download

  # https://github.com/siderolabs/talos/blob/d8a435f0e4e093570325b737f89ff3b1205de48e/Dockerfile#L252
  # upstream switched from using LDFLAGS to go:embed some time ago,
  # this is how sha/pkgver/etc are added to the binary
  git rev-parse HEAD > pkg/machinery/gendata/data/sha
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}'" \
    -o build/talosctl \
    ./cmd/talosctl

  # generate shell completions
  for shell in bash fish zsh; do
    ./build/talosctl completion "$shell" > "build/$shell.completion"
  done
}

package() {
  cd "$pkgname/build"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" talosctl

  # shell completions
  install -vDm644 bash.completion "$pkgdir/usr/share/bash-completion/completions/talosctl"
  install -vDm644 fish.completion "$pkgdir/usr/share/fish/completions/talosctl.fish"
  install -vDm644 zsh.completion "$pkgdir/usr/share/zsh/site-functions/_talosctl"
}
