# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=nickel
pkgname=(
  'nickel'
  'nickel-docs'
  'nickel-language-server'
)
pkgver=1.6.0
pkgrel=1
pkgdesc='A generic configuration language'
arch=('loong64' 'x86_64')
url='https://nickel-lang.org'
license=('MIT')
depends=('glibc' 'gcc-libs' 'nix')
makedepends=('git' 'rust' 'nlohmann-json' 'boost')
checkdepends=('python')
options=('!lto')
source=("$pkgbase::git+https://github.com/tweag/nickel#tag=$pkgver")
sha512sums=('a45813f03814d3dbd8098652a529d24d929e481b9a09ad9aa71e310206039997d455a5ecf6103a7445c5bc242515c00b1d1c02c553c256cebadd539cb08b62a0')
b2sums=('c8fd2e5439e0fe46ffa6d8c71a0b0551fb4e9335e4cb8508c8b79347e54e89e359681a9c05ba9b991a4b79d927ac3ca731bdf3ce809e36fe0b468f4820d9ba1e')

prepare() {
  cd "$pkgbase"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgbase"

  # binaries
  cargo build --frozen --release --all-features

  # generate shell completions
  pushd target/release
  for shell in bash fish zsh; do
    ./nickel gen-completions "$shell" > "$shell-completion"
  done
  popd
}

check() {
  cd "$pkgbase"

  cargo test --frozen --all-features
}

package_nickel() {
  optdepends=(
    'nickel-docs: documentation'
    'nickel-language-server: language server'
  )
  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/nickel

  # shell completions
  pushd target/release
  install -vDm644 bash-completion "$pkgdir/usr/share/bash-completion/completions/nickel"
  install -vDm644 fish-completion "$pkgdir/usr/share/fish/vendor_completions.d/nickel.fish"
  install -vDm644 zsh-completion "$pkgdir/usr/share/zsh/site-functions/_nickel"
  popd

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

package_nickel-docs() {
  pkgdesc='Documentation & examples for the Nickel Programming Language'
  depends=()
  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" ./*.md
  cp -vr examples doc/manual "$pkgdir/usr/share/doc/$pkgbase"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

package_nickel-language-server() {
  pkgdesc='A language server for the Nickel Programming Language'
  cd "$pkgbase"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/nls

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
