# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Kevin Guan <KevinGuan.gm@gmail.com>

pkgname=iptux
pkgver=0.8.4
pkgrel=2
pkgdesc='LAN messenger and file sender, an IP Messenger client'
arch=('loong64' 'x86_64')
url='https://github.com/iptux-src/iptux/'
license=('GPL2')
depends=('google-glog' 'gtk3' 'jsoncpp' 'libsigc++')
makedepends=('appstream' 'gflags' 'meson')
checkdepends=('xorg-server-xvfb')
source=("https://github.com/iptux-src/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
         cinttypes.patch)
sha256sums=('b96b6f1f36618d7516af71be19d36ce1fe9f65558b3c95d229399ad91a43b7ec'
            '7435708ea725f2a916cda116a88c76b1a72f4136295ee2dc88374a1efc5d70d3')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../cinttypes.patch # Add missing include
}

build() {
# Fix build with glog 0.7
  CXXFLAGS+=" -DGLOG_USE_GLOG_EXPORT -DGLOG_USE_GFLAGS" \
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  xvfb-run meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
