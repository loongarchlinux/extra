# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-schemas
pkgver=6.0.5
pkgrel=3
pkgdesc='GSettings deepin desktop-wide schemas'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-schemas"
license=('GPL3')
depends=('dconf' 'deepin-gtk-theme' 'deepin-icon-theme' 'deepin-sound-theme')
makedepends=('python' 'go')
conflicts=('deepin-artwork-themes')
replaces=('deepin-artwork-themes')
groups=('deepin')
source=("https://github.com/linuxdeepin/deepin-desktop-schemas/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1a643ce876efa715d2f1efae2ee77d783effbdbe7b9a2aedbd90a002fdcdb8bd8df8f8993b86c10e8bce00ff6c927b65fcada3896e04553982d5d1013ca70008')

prepare() {
  cd deepin-desktop-schemas-$pkgver
  # fix default background url
  sed -i "s#/usr/share/backgrounds/default_background.jpg#/usr/share/backgrounds/deepin/desktop.jpg#" \
    overrides/common/com.deepin.wrap.gnome.desktop.override schemas/com.deepin.dde.appearance.gschema.xml
  # fix network checker url
  sed -i "s#'http://detect.uniontech.com', 'http://detectportal.deepin.com'#'http://ping.archlinux.org/nm-check.txt'#" schemas/com.deepin.dde.network-utils.gschema.xml
  grep uniontech schemas/com.deepin.dde.network-utils.gschema.xml && exit 1 || :
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd deepin-desktop-schemas-$pkgver
  make
}

check() {
  cd deepin-desktop-schemas-$pkgver
  make test
}

package() {
  cd deepin-desktop-schemas-$pkgver
  make DESTDIR="$pkgdir" install
}
