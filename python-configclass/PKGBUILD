# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-configclass
_name="${pkgname#python-}"
pkgver=0.2.0
pkgrel=6
pkgdesc="A Python class to hold configuration values"
arch=(any)
url="https://github.com/schettino72/configclass/"
license=(MIT)
depends=(
  python
  python-mergedict
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('7f22679d3470ede4106bdd99f1216bc49631c32452dc681fbd6a6b91735bbc82db1babed3cd94f93a0f9f2915d0866ee18704ffcfe477fd3134b6f18a094d192')
b2sums=('8e273e539a9f3cce70397ef0bcc420ba7d5b2b007de490bc4347412546b6af0bbea4950218e69590817f41c884044bd7d976c6a188f8665f2de0bc173f1e9c8e')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  export PYTHONPATH="build:$PYTHONPATH"
  pytest -v
}

package() {
  cd "$_name-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CHANGES,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
}
