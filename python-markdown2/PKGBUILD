# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-markdown2
pkgver=2.5.0
pkgrel=1
pkgdesc='A fast and complete implementation of Markdown in Python'
arch=('any')
url='https://github.com/trentm/python-markdown2'
license=('MIT')
depends=('python' 'python-pygments')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('d98752e25fbce637b3bc1deab5141d80afee13894a043ebcef7948bd6a47b70f97fc814accef25a6e334616a44311a8dcf8149f5b778a74c1f7243e36b1b2841')
b2sums=('ce91aa2c96b8fe679cdf9917acd7eb35cf8d4ecbaa237888135faba475544dede19504c967cef5f6261dfc035bb5614adc7a44b7171f484bb081a3cc32139937')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname/test"

  python testall.py
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
# vim:set ts=2 sw=2 et:
