# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tuning-library
pkgver=1.1.0
pkgrel=3
pkgdesc="Micro-tuning format parsing and frequency finding as a header-only C+ library"
arch=(any)
url="https://surge-synth-team.org/tuning-library/"
_url="https://github.com/surge-synthesizer/tuning-library"
license=(MIT)
makedepends=(
  catch2
  cmake
)
source=(
  $_url/archive/release_${pkgver}/$pkgname-release_$pkgver.tar.gz
  $pkgname-1.1.0-fix_missing_include.patch
  $pkgname-1.1.0-use_system_catch2.patch
)
sha512sums=('af924f622ffa220e15fcdae54e94fa93ac4fe3a65d8eebecee7a1cdb97458f5686712c08c9262a0d7b6b05521939faf97e3723396b94b125275de68fc29a8e9d'
            '22602ae0b718d0aec7a4577180c9cdce904f5fbfe5347523173cbcaaffac8cd18815e624b86e92152d998b4d8a5fb7b802b4232c16f0afc29afe51a0eb9bac8e'
            'b507e833faeb0baafe890bd61cb15a552bd29699b21746af114e896418c69968f154dc3e5941aa5f11cf5c0f39c703af2d4316ccc628f1f5b06c21c53b42c3e5')
b2sums=('6a201a08eadc2622e357d75d839d2d101dd1026e112bed6c11240f37aec5a12b1547da453ab88aa48b3da6a0df33dccc71c4d295be6a4ea439a5952db9706abf'
        'c53fd874cb7a1333041e9255e6190a9d58466c7f6797b3a002e4e2aaf6daef4c20101c9b52e3b65439ac1d33f5ec4e23507155f0d14f3b37bad89b5518386b31'
        '0f64851ad93abe6b5493289c3ade079fb6f3a5f7e18df8a433449c8d8f91a477706f3b9092a27234b3f73e51d7161f3476853bbd6525da9ca38403878c9448da')

prepare() {
  patch -Np1 -d $pkgname-release_$pkgver -i ../$pkgname-1.1.0-fix_missing_include.patch
  patch -Np1 -d $pkgname-release_$pkgver -i ../$pkgname-1.1.0-use_system_catch2.patch
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $pkgname-release_$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  make VERBOSE=1 -k run-all-tests -C build
}

package() {
  cd $pkgname-release_$pkgver
  install -vDm 644 include/*.h -t "$pkgdir/usr/include/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}
