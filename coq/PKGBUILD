# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: acieroid
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: George Giorgidze <giorgidze@gmail.com>
# Contributor: William J. Bowman <bluephoenix47@gmail.com>

pkgbase=coq
pkgname=("${pkgbase}" "${pkgbase}ide" "${pkgbase}-doc")
pkgver=8.19.1
pkgrel=3
pkgdesc='Formal proof management system'
arch=('loong64' 'x86_64')
url='https://coq.inria.fr/'
license=('GPL')
groups=('coq')
options=('!emptydirs' '!strip' 'staticlibs' '!debug')
depends=('ocaml' 'ocaml-findlib')
makedepends=('ocaml-num' 'ocaml-zarith' 'gtk3' 'gtksourceview3' 'dune' 'git'
             'lablgtk3' 'gendesk' # coqide
             'texlive-bin' 'texlive-latexextra' 'texlive-pictures' # coq-doc
             'texlive-fontsextra' 'texlive-mathscience' 'texlive-binextra'
             'fig2dev' 'imagemagick' 'hevea' 'ghostscript'
             'python' 'python-sphinx' 'python-sphinx_rtd_theme' 'python-pexpect'
             'python-beautifulsoup4' 'python-sphinxcontrib-bibtex' 'antlr4')
source=("coq-${pkgver}.tar.gz::https://github.com/coq/coq/archive/V${pkgver}.tar.gz" "python-antlr4-4.9.3.tar.gz::https://github.com/antlr/antlr4/archive/4.9.3.tar.gz")
sha256sums=('1e535ed924234f18394efce94b12d9247a67e8af29241eb79615804160f21674'
            'efe4057d75ab48145d4683100fec7f77d7f87fa258707330cadd1f8e6f7eecae')




build() {
  # generate a desktop file
  cd "${srcdir}"
  gendesk -f -n --pkgname "${pkgbase}ide" \
    --name "CoqIDE Proof Assistant" \
    --pkgdesc "Graphical interface for the Coq proof assistant" \
    --categories "Development;Science;Math;IDE;GTK"

  # prepare python-antlr 4.9.3 (latest supported version) build
  cd "${srcdir}/antlr4-4.9.3/runtime/Python3"
  python setup.py build
  PYTHON_ANTRL4_LIB=${srcdir}/antlr4-4.9.3/runtime/Python3/build/lib

  # build package
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make dunestrap
  # export PYTHONPATH=/home/juergen/Downloads/antlr4-python3-runtime-4.7.1/src
  PYTHONPATH=$PYTHON_ANTRL4_LIB SPHINXWARNERROR=0 dune build -p coq,coq-core,coq-stdlib,coqide-server,coqide
  PYTHONPATH=$PYTHON_ANTRL4_LIB SPHINXWARNERROR=0 dune build -p coq,coq-core,coq-stdlib,coqide-server,coqide,coq-doc || true # this is expected to fail, see ocaml/dune#1868
}

package_coq() {
  optdepends=('coqide: graphical Coq IDE'
              'coq-doc: offline documentation'
              'coin-or-csdp: for psatz plugin')
  # coq-nox was the old name for coq without coqide
  replaces=('coq-nox')
  conflicts=('coq-nox')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  dune install coq coq-core coq-stdlib coqide-server \
       --prefix=/usr \
       --destdir="$pkgdir" \
       --mandir=/usr/share/man \
       --docdir=/usr/share/doc
}

package_coqide() {
  pkgdesc="GTK-based graphical interface for the Coq proof assistant"
  depends+=("${pkgbase}" 'gtk3' 'gtksourceview3')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  dune install coqide \
       --prefix=/usr \
       --destdir="$pkgdir" \
       --mandir=/usr/share/man \
       --docdir=/usr/share/doc

  # Desktop file generated by gendesk
  install -D -m 644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m 644 ide/coqide/coq.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}



package_coq-doc() {
  pkgdesc="HTML and PDF documentation for the Coq proof assistant"
  depends=()

  cd "${srcdir}/${pkgbase}-${pkgver}"

  # From old Makefile based installation, Workaround, see: doc/dune install target
  mkdir -p ${pkgdir}/usr/share/doc/coq/html/{stdlib,refman}
  install -m 644  ./_build/install/default/doc/coq-doc/html/stdlib/* "${pkgdir}/usr/share/doc/coq/html/stdlib"
  for f in $(cd ./_build/install/default/doc/coq-doc/html/refman; find .  -type f); do
    mkdir -p ${pkgdir}/usr/share/doc/coq/html/refman/$(dirname $f)
    install -m 644 ./_build/install/default/doc/coq-doc/html/refman/$f ${pkgdir}/usr/share/doc/coq/html/refman/$f
  done
}
