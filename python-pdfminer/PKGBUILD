# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pdfminer
pkgname=python-${_pkg}
pkgver=20231228
pkgrel=2
pkgdesc="Python PDF Parser"
arch=(any)
url="https://github.com/pdfminer/pdfminer.six"
license=(MIT)
depends=(python-charset-normalizer python-cryptography)
makedepends=(python-build python-installer python-setuptools python-wheel cython)
checkdepends=(python-pytest)
# Provided tarballs do not include tests
#source=(${url}/releases/download/${pkgver}/${_pkg}.six-${pkgver}.tar.gz)
source=(${url}/archive/${pkgver}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('548db64879d6bef83d20654389a5d0ab4c1df496d7e13cc982c3109054f31080')

prepare() {
  sed -e '/setuptools-git-versioning/d' -e 's|"enabled": True|"enabled": False|' -i ${_pkg}.six-${pkgver}/setup.py
}

build() {
  cd ${_pkg}.six-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pkg}.six-${pkgver}
  PYTHONPATH="${PWD}"/build/lib:"${PWD}" pytest -vv --color=yes
}

package() {
  cd ${_pkg}.six-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  # Fix ownership
  chmod a+r "${pkgdir}"$(python -c "import site; print(site.getsitepackages()[0])")/pdfminer/cmap/*
}
