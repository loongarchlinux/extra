# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: tobias [tobias at archlinux.org]

pkgname=mousepad
pkgver=0.6.2
pkgrel=2
pkgdesc="Simple text editor for Xfce"
arch=('loong64' 'x86_64')
url="https://docs.xfce.org/apps/mousepad/start"
license=('GPL2')
groups=('xfce4-goodies')
depends=('gtksourceview4' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('gspell' 'libxfce4ui' 'polkit')
optdepends=('gspell: spell checking plugin'
            'libxfce4ui: shortcuts editor plugin')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('e7cacb3b8cb1cd689e6341484691069e73032810ca51fc747536fc36eb18d19d')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
