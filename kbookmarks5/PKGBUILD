# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_name=kbookmarks
pkgname=${_name}5
pkgver=5.116.0
pkgrel=1
pkgdesc='Support for bookmarks and the XBEL format'
arch=(loong64 x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kxmlgui5)
makedepends=(extra-cmake-modules qt5-tools qt5-doc doxygen)
conflicts=("$_name<5.111")
replaces=("$_name<5.111")
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$_name-$pkgver.tar.xz{,.sig})
sha256sums=('540a5ae54b7325218798af79f73a97d6ffcbcc683156de33b3a964e934368f05'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $_name-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
