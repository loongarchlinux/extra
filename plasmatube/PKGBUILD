# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasmatube
pkgver=24.02.2
pkgrel=1
pkgdesc='Kirigami YouTube video player based on QtMultimedia and youtube-dl'
url='https://apps.kde.org/plasmatube/'
arch=(loong64 x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(gcc-libs
         glibc
         kconfig
         kcoreaddons
         kdbusaddons
         ki18n
         kirigami
         kirigami-addons
         kitemmodels
         knotifications
         kwindowsystem
         mpvqt
         purpose
         qt6-5compat
         qt6-base
         qt6-declarative
         qtkeychain-qt6
         yt-dlp)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('af8ab3d41d2902b16705c82b8f4ca274cf145520deec747f8bfb46e6b4c339db'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
