# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname=async
pkgname=ruby-${_gemname}
pkgver=2.14.2
pkgrel=2
pkgdesc='Composable asynchronous I/O'
url='https://github.com/socketry/async'
arch=('any')
license=('MIT')
depends=(
  ruby
  ruby-console
  ruby-io-event
  ruby-nio4r
  ruby-timers
)
makedepends=(
  ruby-rdoc
)
options=('!emptydirs')
source=("https://github.com/socketry/async/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e92d2b69648f88b5f24703df9c35eb0da0413b8db312f70f1c81f02de9ebd546d88657c32f6e582148ef29af1dbc36d460029e3aeab12a333a0364272a27d20f')
b2sums=('722c63de889144b4563491f873d618a77ea6fb98ed9132684c62ea6ccfbd040c1ff2b857f42ce66a09c1aca1ef0c648b163d34eccd759f4f4509fe7809ddc776')

prepare() {
  cd ${_gemname}-${pkgver}

  sed --in-place 's|git ls-files|find|' "${_gemname}.gemspec"
  sed --in-place 's|~>|>=|g' "${_gemname}.gemspec"
  sed --in-place '/release\.pem/d' "${_gemname}.gemspec"
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem

  install -Dm 644 readme.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 license.md -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
