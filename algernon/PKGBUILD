# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=algernon
pkgver=1.17.1
pkgrel=1
pkgdesc='Web server with Lua, Markdown, QUIC, Redis and PostgreSQL support'
arch=(loong64 x86_64)
url='https://algernon.roboticoverlords.org/'
license=(BSD-3-Clause)
makedepends=(git go)
optdepends=('mariadb: For using the MariaDB/MySQL database backend'
            'ollama: For using Ollama from Lua or .prompt files'
            'postgresql: For using the PostgreSQL database backend'
            'redis: For using the Redis database backend')
source=("git+https://github.com/xyproto/algernon#commit=131aa2dbd8900dfe022576113572279ea31e9232") # tag: v1.17.1
b2sums=('91fda070741d8c3ca3543015a7ae72376a413f502e479fc2d5af41f0322764ed794b2826478f8593c29628e70fc86bc78a4e51177427ea3ca0b65b58ac039587')

prepare() {
  cd $pkgname
  go build -buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags="-s -w -linkmode=external -extldflags '$LDFLAGS'"
}

package() {
  cd $pkgname
  install -Dm755 algernon "$pkgdir/usr/bin/algernon"
  install -d "$pkgdir/usr/share/doc/$pkgname/samples"
  cp -r samples "$pkgdir/usr/share/doc/$pkgname/samples"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd system
  install -Dm644 logrotate "$pkgdir/etc/logrotate.d/algernon"
  install -Dm644 serverconf.lua "$pkgdir/etc/algernon/serverconf.lua"
  install -Dm644 algernon_dev.service "$pkgdir/usr/share/doc/$pkgname/algernon.service.example"

  cd ../desktop
  install -Dm755 mdview "$pkgdir/usr/bin/mdview"
  install -Dm644 algernon.desktop "$pkgdir/usr/share/applications/algernon.desktop"
  install -Dm644 algernon_md.desktop "$pkgdir/usr/share/applications/algernon_md.desktop"
  install -Dm644 markdown.png "$pkgdir/usr/share/pixmaps/markdown.png"
}
