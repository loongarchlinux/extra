# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=scip
pkgver=9.0.0
pkgrel=1
pkgdesc='Solving Constraint Integer Programs'
arch=(loong64 x86_64)
url='https://www.scipopt.org/'
license=(Apache-2.0)
depends=(gcc-libs
         glibc
         gmp
         mpfr
         readline
         tbb
         zlib)
makedepends=(boost
             cmake
             papilo
             soplex)
source=(https://github.com/scipopt/scip/archive/v${pkgver//./}/$pkgname-$pkgver.tar.gz)
sha256sums=('ee221bd13a6b24738f2e74321e2efdebd6d7c603574ca6f6cb9d4472ead2c22f')

build() {
  cmake -B build -S $pkgname-${pkgver//./} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DAUTOBUILD=ON \
    -DSYM=bliss
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
