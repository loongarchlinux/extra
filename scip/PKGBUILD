# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=scip
pkgver=9.1.0
pkgrel=1
pkgdesc='Solving Constraint Integer Programs'
arch=(loong64 x86_64)
url='https://www.scipopt.org/'
license=(Apache-2.0)
depends=(gcc-libs
         glibc
         gmp
         mpfr
         readline
         tbb
         zlib)
makedepends=(boost
             cmake
             git
             papilo
             soplex)
source=(git+https://github.com/scipopt/scip#tag=v${pkgver//./})
sha256sums=('9c4e065a53b27bcd0c047281f5776afe7ddcfe3921e447452dc0e2312f3b3697')

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DAUTOBUILD=ON \
    -DSYM=bliss
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
