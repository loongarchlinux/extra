# Maintainer: David Runge <dvzrv@archlinux.org>

_name=jaraco.logging
pkgname=python-jaraco.logging
pkgver=3.3.0
pkgrel=2
pkgdesc="Quickly solicit log level info from command-line parameters"
arch=(any)
url="https://github.com/jaraco/jaraco.logging"
license=(MIT)
depends=(
  python
  python-tempora
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-setuptools-scm
  python-toml
  python-wheel
)
conflicts=(python-jaraco)
replaces=(python-jaraco)
source=($_name-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha256sums=('72466b94d3a82be8c3b45ed7a536560ff9de2725c21e501e21f84a3d5b07f4e2')
b2sums=('df49d73e6d1475f470d4b536d593123548bbd85c56a9d5cfcd72c0deeb61a4c9c4732b2a673aefb0bc93fc59b2c3be26177e51f6ac6a5a100318652a2e4bf64e')

build() {
  cd $_name-$pkgver
  SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  # NOTE: there are actually no tests to run
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 *.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
