# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screen-recorder
pkgver=5.12.15
pkgrel=4
pkgdesc='Deepin Screen Recorder'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-screen-recorder"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'dtkwidget'
         'qt5-base' 'qt5-x11extras' 'qt5-multimedia' 'deepin-turbo' 'ffmpeg4.4' 'libxtst' 'opencv'
         'dconf' 'libxext' 'libx11' 'libxfixes' 'portaudio' 'deepin-image-editor' 'gcc-libs')
makedepends=('deepin-dock')
optdepends=('deepin-shortcut-viewer: for shortcut display')
provides=('deepin-screenshot')
conflicts=('deepin-screenshot')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-screen-recorder/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-unqualified-id.patch::https://github.com/linuxdeepin/deepin-screen-recorder/pull/416.patch)
sha512sums=('9e9281918b27b34e27f9f7c2ef7b490eda272adc63370fb2908fd98735983467195cc24a466ca3b9a04e7e6a1b622eb905158da513947b633b434882fe797fa2'
            '5b26c7127cde0ae041ca53ab87edb3ea341d883f7d204f03e697175245c5ad564f9f0e30448fb291cff34fa26d03e1ee2dec4d368527f0bb217b086d7f1dde78')

prepare() {
  cd deepin-screen-recorder-$pkgver
  patch -Np1 -i ../$pkgname-unqualified-id.patch

  # https://github.com/linuxdeepin/developer-center/issues/3035
  sed -i "s|/etc/os-version|/etc/uos-version|" src/src.pro

  # OpenCV 4 compatibility
  sed -i '/#include<opencv2/i #include <opencv2/imgproc/types_c.h>' src/utils/pixmergethread.h

  # OpenCV missing in pkg-config targets
  sed -i 's/dframeworkdbus/dframeworkdbus opencv4/' src/src.pro
}

build(){
  cd deepin-screen-recorder-$pkgver
  export PKG_CONFIG_PATH='/usr/lib/ffmpeg4.4/pkgconfig'
  qmake-qt5 PREFIX=/usr
  CFLAGS+=" -Wno-implicit-function-declaration -I/usr/include/ffmpeg4.4/" \
  make
}

package() {
  cd deepin-screen-recorder-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
