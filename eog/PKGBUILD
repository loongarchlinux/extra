# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=eog
pkgname=(
  eog
  eog-docs
)
pkgver=45.4
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
url="https://wiki.gnome.org/Apps/EyeOfGnome"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  cairo
  dconf
  exempi
  gcc-libs
  gdk-pixbuf2
  glib2
  glibc
  gnome-desktop
  gtk3
  hicolor-icon-theme
  lcms2
  libexif
  libgirepository
  libhandy
  libjpeg-turbo
  libpeas
  librsvg
  libx11
  webp-pixbuf-loader
  zlib
)
makedepends=(
  gi-docgen
  git
  glib2-devel
  gobject-introspection
  meson
  systemd
  yelp-tools
)
source=("git+https://gitlab.gnome.org/GNOME/eog.git#tag=${pkgver/[a-z]/.&}")
b2sums=('c7875b5faa571941924353e734587b1e7cdd19867b7d57d5f8b3f2a4103e3aa5b62dc1d8c1abee3cdeea63524aee6dc346d697cc4059baf3a3fd39321584c80f')

build() {
  local meson_options=(
    -D gtk_doc=true
    -D libportal=false
  )

  arch-meson eog build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_eog() {
  optdepends=('eog-plugins: Additional features')
  groups=(gnome-extra)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/gtk-doc
}

package_eog-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
