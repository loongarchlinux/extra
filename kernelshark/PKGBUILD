# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=kernelshark
pkgver=2.3.1
pkgrel=1
pkgdesc="GUI frontend for trace-cmd based Linux kernel Ftrace captures"
arch=(loong64 x86_64)
url="https://kernelshark.org/"
license=(
  GPL-2.0-only
  LGPL-2.0-only
)
depends=(
  freeglut
  gcc-libs
  glibc
  glu
  qt6-base
  qt6-scxml
  ttf-fira-sans
)
makedepends=(
  boost
  cmake
  json-c
  libglvnd
  libtracefs
  libtraceevent
  trace-cmd
)
optdepends=(
  'bash: for kshark-su-record'
)
provides=(
  libkshark.so
  libkshark-plot.so
  libkshark-gui.so
)
source=($pkgname-$pkgver.tar.gz::https://git.kernel.org/pub/scm/utils/trace-cmd/kernel-shark.git/snapshot/kernel-shark-$pkgname-v$pkgver.tar.gz)
sha512sums=('ba5e7ebc713d296bef211174869445ccb3dffb8a96d0634776bd28c183487225e7c8082c8be5957795973833d6ac67851fb46f75cf68aae66eb3e5e06c081f5f')
b2sums=('818448c3d2e01412e12fc3406b13ffd4d6ab7d442aef7867048913996edbc7f64b2c2c9e88810d6c0bd9e8a6ee9726369ab097d2164ec9966660f098e123c702')

prepare() {
  cd kernel-shark-$pkgname-v$pkgver

  # replace FreeSans with ttf-fira-sans
  sed -e 's/FreeSans/FiraSans-Regular/g' \
      -i CMakeLists.txt
}

build() {
  local cmake_options=(
    -B build
    -D _INSTALL_PREFIX=/usr
    -D _LIBDIR=/usr/lib
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -S kernel-shark-$pkgname-v$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  depends+=(
    json-c libjson-c.so
    libglvnd libGL.so
    libtraceevent libtraceevent.so
    libtracefs libtracefs.so
    trace-cmd libtracecmd.so
  )

  DESTDIR="$pkgdir" cmake --install build
}
