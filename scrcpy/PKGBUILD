# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Christian Heusel <christian@heusel.eu>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=2.6.1
pkgrel=1
pkgdesc='Display and control your Android device'
arch=(loong64 x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache-2.0)
depends=(android-tools ffmpeg sdl2 glibc libusb)
makedepends=(git meson)
source=("git+$url#tag=v${pkgver}"
        "$pkgname-server-$pkgver.apk::$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('c06b15342fd14e28ef32c83b675bf3d6daef3600515b97d9e123df9ec7869f45d50a437eca729442ceb931e409b20ad60bf5d1ef454ef48497c3bf0df3d46404'
        '41afffcaa0e6373ec1f69c89a112316b6153a4bff3da70f6c5925030d635bff83920392f5e658dc53757883ff222a40e6584a608d8fcbc4bc9c799d625b7b4f1')

build() {
  mkdir -p build
  arch-meson build $pkgname \
    --buildtype release \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server=../$pkgname-server-$pkgver.apk
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
