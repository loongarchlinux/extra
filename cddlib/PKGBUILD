# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=cddlib
pkgver=0.94m
pkgrel=2
epoch=1
pkgdesc='C library implementing Double Description Method for convex polyhedra'
arch=(loong64 x86_64)
url='https://www.inf.ethz.ch/personal/fukudak/cdd_home/'
license=(GPL-2.0-only)
depends=(glibc
         gmp)
makedepends=(git
             texlive-latex)
source=(git+https://github.com/cddlib/cddlib#tag=$pkgver)
sha256sums=('3c5ab4c72799f929bcffd98a442b89431fe144aec83931e44e78613278693e2f')

prepare() {
  cd $pkgname
  autoreconf -vi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
