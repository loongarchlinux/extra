# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=crane
_pkgname=go-containerregistry
pkgver=0.20.2
pkgrel=1
pkgdesc="Interacting with remote container images and registries."
arch=('loong64' 'x86_64')
url="https://github.com/google/go-containerregistry"
license=('Apache-2.0')
depends=('glibc')
makedepends=('go')
# we can not use LTO as otherwise we get no reproducible package with full RELRO
options=(!lto)
source=(https://github.com/google/${_pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('064e9c47e3dac49acddccb4afe908b9d835725d371ee9a471fd445d134fbbf1d')
b2sums=('6b24af5395f1ee6c812ca7b2f90e6820cbfc81b9fd3ddd2511487e6128977f9cf18ec977e592653dbe02ad342c3f467277430dba9175453730a6595565969433')

prepare() {
  cd ${_pkgname}-${pkgver}
  go mod download
  cd cmd/krane
  go mod download
}

build() {
  cd ${_pkgname}-${pkgver}

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_REQUIRED="1"

  local go_options=(
    -buildmode=pie
    -mod=readonly
    -modcacherw
    -ldflags '-compressdwarf=false -linkmode=external'
  )

  go build "${go_options[@]}" -o build/ ./cmd/{crane,gcrane}
  cd cmd/krane
  go build "${go_options[@]}" -o ../../build/ .
}

check() {
  cd ${_pkgname}-${pkgver}
  ./build/crane --help
  ./build/gcrane --help
  ./build/krane --help
}

package() {
  cd ${_pkgname}-${pkgver}
  install -Dm755 -t "${pkgdir}/usr/bin" \
    build/{crane,gcrane,krane}

  # setup completions
  install -dm 755 "${pkgdir}/usr/share/bash-completion/completions" \
                  "${pkgdir}/usr/share/zsh/site-functions" \
                 "${pkgdir}/usr/share/fish/vendor_completions.d"
  for bin in crane gcrane krane; do
    "${pkgdir}/usr/bin/${bin}" completion bash > "${pkgdir}/usr/share/bash-completion/completions/${bin}"
    "${pkgdir}/usr/bin/${bin}" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${bin}"
    "${pkgdir}/usr/bin/${bin}" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${bin}.fish"
  done

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
