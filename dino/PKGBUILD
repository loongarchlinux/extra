# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=dino
pkgver=0.4.3
pkgrel=5
pkgdesc="Modern XMPP (Jabber) chat client written in Vala"
arch=(loong64 x86_64)
url="https://dino.im"
license=('GPL-3.0-only')
depends=(glib2 glib-networking gtk4 libadwaita gpgme libgee libgcrypt libsignal-protocol-c
         libsoup3 sqlite qrencode gspell libnice gstreamer gst-plugin-gtk
         gst-plugins-base gst-plugins-good libsrtp webrtc-audio-processing gdk-pixbuf2)
makedepends=(git cmake vala ninja)
source=(https://github.com/dino/dino/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc}
        fix_build_with_new_vala.patch)
sha256sums=('a615fc14d27402316d45036d55471d5ccb68194390e0654efb192f8de57b4b06'
            'SKIP'
            '5f2de305e36efa4b17bac09f947d09e6c2259ccaa152077e17d613e36da9e16b')
validpgpkeys=(A32134DD19952FB12EC43E4501FABCC83FEA225E)

prepare() {
	cd "${pkgname}-${pkgver}"
	# Temporary patch to fix build with new vala
	patch -Np1 < "${srcdir}/fix_build_with_new_vala.patch"
}

build() {
	CFLAGS+=' -Wno-error=implicit-function-declaration' # 'implicit-function-declaration' is now an error in GCC 14, making the build fails
	cmake -G Ninja -B build -S ${pkgname}-${pkgver} \
	  -DBUILD_TESTS='ON' \
	  -DCMAKE_BUILD_TYPE='None' \
	  -DCMAKE_INSTALL_PREFIX='/usr' \
	  -Wno-dev
	ninja -C build
}

check() {
	build/xmpp-vala-test
	build/signal-protocol-vala-test
}

package() {
	DESTDIR="${pkgdir}" ninja -C build install
}
