# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=arduino-cli
pkgver=1.0.3
pkgrel=1
arch=('loong64' 'x86_64')
pkgdesc='Arduino command line interface'
url='https://github.com/arduino/arduino-cli'
license=('GPL3')
groups=('arduino')
depends=('glibc')
makedepends=('go' 'git')
source=("git+$url.git#tag=v$pkgver")
sha512sums=('cae89ec19f778a7939d9f9958a63eb3e06f476db358ece91dee8fb07a49dd6ee6d673e000bc67b934a8f61c9d9a01701eec70c53a3ccdc1261d211934a92f5ff')

build(){
  cd $pkgname

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -mod=readonly -modcacherw'

  go build \
        -ldflags "-linkmode=external -X github.com/arduino/arduino-cli/version.versionString=$pkgver-arch -X github.com/arduino/arduino-cli/version.commit=$(git rev-parse HEAD)" \
        -v .
}

package() {
  install -Dm 755 $pkgname/$pkgname "$pkgdir"/usr/bin/$pkgname
}
