# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=gfold
pkgver=4.4.1
pkgrel=3
pkgdesc="A CLI tool to help keep track of Git repositories"
arch=('loong64' 'x86_64')
url="https://github.com/nickgerace/gfold"
license=('Apache-2.0')
depends=('gcc-libs' 'glibc' 'libgit2')
makedepends=('cargo' 'git')
source=("git+$url.git#tag=$pkgver")
sha256sums=('e5f2af274e94af48dc72d4b4a2f9f8042c75d53ecb5696ac99838cf405e8573c')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname"
  CFLAGS+=" -ffat-lto-objects"
  cargo build --release --frozen
}

check() {
  cd "$pkgname"
  cargo test --frozen
}

package() {
  cd "$pkgname"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
