# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=file-roller
pkgver=44.2
pkgrel=1
pkgdesc="Create and modify archives"
url="https://wiki.gnome.org/Apps/FileRoller"
arch=(loong64 x86_64)
license=(GPL-2.0-or-later)
depends=(
  dconf
  file
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  json-glib
  libadwaita
  libarchive
  libnautilus-extension
  libportal
  libportal-gtk4
  pango
  unzip
  zip
)
makedepends=(
  appstream-glib
  git
  meson
  yelp-tools
)
optdepends=(
  'lrzip: lrzip archive support'
  'p7zip: 7z, arj, exe and encrypted zip files support'
  'squashfs-tools: squashfs image support'
  'unace: ACE archive support'
  'unrar: better RAR archive support'
)
groups=(gnome-extra)
source=("git+https://gitlab.gnome.org/GNOME/file-roller.git#tag=$pkgver")
b2sums=('552c23f12194fba6c39a6dc7d0906005b48f1325cda8d067270366f4f4ff8b7b68c2bbf7e5f81f152f29697badffd1ad9dede4a72948dd6ef1c00d87406e9472')

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
