# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Hannes Rist <hrist@phreeknet.org>

pkgname=mg
pkgver=20230501
pkgrel=3
pkgdesc="Micro GNU/emacs"
arch=('loong64' 'x86_64')
url="https://github.com/hboetes/mg"
makedepends=('git')
depends=('ncurses' 'libbsd')
license=('custom')
source=("git+https://github.com/hboetes/mg.git#tag=${pkgver}")
sha256sums=('83b589f160953e1acc9af0f338fb8189bd331ac0cf40ef49b4c48a7170d5ca43')

build() {
  cd mg
  make prefix=/usr mandir=/usr/share/man
}

package() {
  cd mg

  make install INSTALL=install prefix=/usr mandir=/usr/share/man DESTDIR="$pkgdir" BINDIR=/usr/bin

  install -Dm644 README -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 tutorial -t "$pkgdir/usr/share/doc/$pkgname"
}
