# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-about
pkgver=6.2.0.r282.94871c21
pkgrel=1
pkgdesc='Switchboard About Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-about
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  appstream
  fwupd
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libgtop
  libgudev
  libpackagekit-glib
  libswitchboard-3.so
  polkit
  switcheroo-control
  udisks2
)
makedepends=(
  git
  meson
  vala
)
_commit=94871c2132021adc7e2887a69e48ce7fa632498c
source=(git+https://github.com/elementary/switchboard-plug-about.git#commit=${_commit})
b2sums=(SKIP)

pkgver() {
  cd switchboard-plug-about
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-about build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
