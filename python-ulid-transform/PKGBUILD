# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Lukasz Pozarlik <lpozarlik@gmail.com>

pkgname=python-ulid-transform
pkgver=0.13.1
pkgrel=1
pkgdesc='Create and transform ULIDs'
arch=(loong64 x86_64)
url=https://github.com/bdraco/ulid-transform
license=(MIT)
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  cython
  git
  python-build
  python-installer
  python-poetry-core
  python-setuptools
  python-wheel
)
_tag=a5291fbb725b477c24722126d31d30e1bda3262d
source=(git+https://github.com/bdraco/ulid-transform.git#tag=${_tag})
b2sums=('49baa1a66de1701a85dc06e22534772f82acc8cdb8da94b2e609bdd88451045f80660a067333634d1c01fc96d95b17e84820f25e1512796cd359e71333442fdf')

pkgver() {
  cd ulid-transform
  git describe --tags | sed 's/^v//'
}

build() {
  cd ulid-transform
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" ulid-transform/dist/*.whl
  install -Dm 644 ulid-transform/LICENSE -t "${pkgdir}"/usr/share/licenses/python-ulid-transform/
}

# vim: ts=2 sw=2 et:
