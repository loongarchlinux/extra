# Maintainer: Caleb Maclennan <caleb@alerque.com>

_pyname=crate
pkgname=python-$_pyname
pkgver=0.35.2
pkgrel=2
pkgdesc='A Python client library for CrateDB'
arch=(any)
url="https://github.com/crate/crate-python"
license=(Apache)
depends=(python
         python-urllib3)
makedepends=(python-{build,installer,wheel}
             python-setuptools)
optdepends=(python-sqlalchemy)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('e211800adb0aef586f727f0bf608080fbcd1fae9ad4f0bec901c521c1a4bc8c1')

build() {
	cd "$_archive"
	python -m build -wn
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" {CONTRIBUTING,DEVELOP,README}.rst
}
