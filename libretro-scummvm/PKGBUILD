# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Oliver Jaksch <arch-aur@com-in.de>

pkgname=libretro-scummvm
pkgver=153492
pkgrel=1
pkgdesc='ScummVM core'
arch=(loong64 x86_64)
url=https://github.com/libretro/scummvm
license=(GPL)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=4f5f776b8d25cfc4636ff02e01b8fa7fe4259950
source=(libretro-scummvm::git+https://github.com/libretro/scummvm.git#commit=${_commit})
sha256sums=('ecba138453efbbcfbe3512b41bc14b0a42c99821703e109032224e6ddec25b4c')

pkgver() {
  cd libretro-scummvm

  git rev-list --count HEAD
}

build() {
  make -C libretro-scummvm/backends/platform/libretro
}

package() {
  install -Dm 644 libretro-scummvm/backends/platform/libretro/scummvm_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
