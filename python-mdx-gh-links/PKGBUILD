# Maintainer: Maxime Gauduin <alucryd@archlinux>org>
# Contributor: D. Can Celasun <can[at]dcc[dot]im>

pkgname=python-mdx-gh-links
pkgver=0.3
pkgrel=1
pkgdesc='Python-Markdown GitHub Links Extension'
arch=(any)
url=https://github.com/Python-Markdown/github-links
license=(MIT)
depends=(
  python
  python-markdown
)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=8175031e051d4580b74a4c5db6e3186cfd96902d
source=(git+https://github.com/Python-Markdown/github-links.git#tag=${_tag})
b2sums=('bc257250deee2477c8e3e18e162c23595a8ef96f577c110863441c551199c0d9327aa4bb4facf84a3900506ff0898da0cce91b0157e7da744c099e820f2ba68c')

build() {
  cd github-links
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" github-links/dist/*.whl
  install -Dm 644 github-links/LICENSE -t "${pkgdir}"/usr/share/licenses/python-mdx-gh-links/
}

# vim: ts=2 sw=2 et:
