# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=mythes-ro
pkgver=3.3
pkgrel=8
pkgdesc="Romanian thesaurus"
arch=('any')
url="https://sourceforge.net/projects/rospell/"
license=('GPL-2.0-or-later')
optdepends=('libmythes: offers thesaurus library functions')
source=(https://downloads.sourceforge.net/rospell/th_ro_RO.${pkgver}.zip)
sha256sums=('097f0a58be60859292fb0c8109669d579abd19a500b9d93bda3c16b9aa446be0')

package() {
  install -dm755 "${pkgdir}"/usr/share/mythes

  install -m644 th_ro_RO.dat "${pkgdir}/usr/share/mythes/th_ro_RO_v2.dat"
  install -m644 th_ro_RO.idx "${pkgdir}/usr/share/mythes/th_ro_RO_v2.idx"

  # the symlinks
  install -dm755 "${pkgdir}"/usr/share/myspell/dicts
  pushd "${pkgdir}/usr/share/myspell/dicts"
  for _file in "${pkgdir}"/usr/share/mythes/*; do
    ln -sv /usr/share/mythes/$(basename ${_file}) .
  done
  popd
}

