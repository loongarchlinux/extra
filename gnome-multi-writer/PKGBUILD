# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-multi-writer
pkgver=3.35.90
pkgrel=3
pkgdesc='Write an ISO file to multiple USB devices at once'
arch=(loong64 x86_64)
url='https://wiki.gnome.org/Apps/MultiWriter'
license=(GPL-2.0-or-later)
groups=(gnome-extra)
depends=(
  dconf
  gcc-libs
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libcanberra
  libgudev
  libgusb
  polkit
  udisks2
)
makedepends=(
  appstream-glib
  docbook-sgml
  docbook-utils
  git
  meson
  perl-sgmls
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gnome-multi-writer.git?signed#tag=GNOME_MULTI_WRITER_${pkgver//./_}"
  gnome-multi-writer-window-icon.patch
)
b2sums=(
  13663ec912e88e038626d2deac7538d15f80ebf57015e97a1db39a70fd8f371a5773256320be7a82ce6b6a7bd5b925c57e26c076b985f7d014b370358678b9fa
  da803f330ca427b23d3023f31b9ead72b5925e6796684acb8bb995c6a702bc7cc05fd852575bf784038bfb2b57111b93a6b3595f29ca6e67f86bf56506c94787
)
validpgpkeys=(163EB50119225DB3DF8F49EA17ACBA8DFA970E17) # Richard Hughes <richard@hughsie.com>

prepare() {
  cd $pkgname

  # https://gitlab.gnome.org/GNOME/gnome-multi-writer/-/merge_requests/8
  git apply -3 ../gnome-multi-writer-window-icon.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
