# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: John Gerritse <tyrannis dot hawk at gmail dot com>

pkgname=python-pyspellchecker
_name=${pkgname#python-}
pkgver=0.8.1
pkgrel=2
pkgdesc='Pure python spell checker based on work by Peter Norvig'
arch=('any')
url=https://github.com/barrust/pyspellchecker
license=('MIT')
depends=('python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
checkdepends=('python-pytest')
source=("git+$url.git#tag=v$pkgver")
b2sums=('642569d20d8262dc37b5342cde65a0eac883fd7be75b3e9b582e7851f35788da1a7f848c434422931a9d65bfeb1256925d38af631a1bc41a9e2d6a2fa1425b97')

build() {
  cd "$_name"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$_name"
  pytest
}

package() {
  cd "$_name"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/"$_name"-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
