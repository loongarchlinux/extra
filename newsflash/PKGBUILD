# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Felix Bühler <account at buehler dot rocks>

pkgname=newsflash
_pkgname=news_flash_gtk
pkgver=2.3.1
pkgrel=1
pkgdesc='Desktop application designed to complement an already existing web-based RSS reader account'
arch=('loong64' 'x86_64')
url='https://gitlab.com/news-flash/news_flash_gtk'
license=('GPL3')
depends=('libadwaita' 'webkitgtk-6.0')
makedepends=('appstream-glib' 'meson' 'rust')
source=("https://gitlab.com/news-flash/$_pkgname/-/archive/v.$pkgver/$_pkgname-v.$pkgver.tar.gz")
sha256sums=('af7365602726def736d525d105498ed85f6c4d6a3fa51bdffdcfc6941cf99a7d')
options=(!lto)

build() {
  export FEEDLY_CLIENT_ID="boutroue"
  export FEEDLY_CLIENT_SECRET="FE012EGICU4ZOBDRBEOVAJA1JZYH"
  export PASSWORD_CRYPT_KEY="ypsSXCLhJngks9qGUAqShd8JjRaZ824wT3e"
  export MERCURY_PARSER_USER="newsflash"
  export MERCURY_PARSER_KEY="R4qcKEHpr9RTq6YuRAPkm9kMhjp4GuxiWG44VDk3Na4ZsN7F"
  export RUSTC_BOOTSTRAP=1

  arch-meson $_pkgname-v.$pkgver build
  meson compile -C build

  # "failed to stat '/root/.gitconfig'" from cargo in fakeroot
  meson install -C build --destdir "$srcdir/fakeinstall"
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  cp -a fakeinstall/* "$pkgdir"
  ln -s com.gitlab.newsflash "$pkgdir/usr/bin/$pkgname"
}
