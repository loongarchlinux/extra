# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe@archlinux.org>
# Contributor: Felix Bühler <account at buehler dot rocks>

pkgname=newsflash
pkgver=3.3.4
pkgrel=1
pkgdesc='Keep up with your feeds'
arch=('loong64' 'x86_64')
url='https://gitlab.com/news-flash/news_flash_gtk'
license=('GPL-3.0-only')
depends=(
  cairo
  gcc-libs
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita
  libclapper
  libclapper-gtk
  libsoup3
  libxml2
  openssl
  pango
  sqlite
  webkitgtk-6.0
)
makedepends=(
  appstream-glib
  blueprint-compiler
  git
  glib2-devel
  meson
  rust
)
source=("git+https://gitlab.com/news-flash/news_flash_gtk.git#tag=v.${pkgver}")
b2sums=('9bccd80fa27eedb8b70f470207dbb733233f75c2e1a98a333e37e1fec24c003dad98ba6af232ab245389d22da5f3f7c593e5b45acaaff3438f431ba9f90548b6')

build() {
  # Used to encrypt user passwords.
  # DO NOT CHANGE
  export PASSWORD_CRYPT_KEY="ypsSXCLhJngks9qGUAqShd8JjRaZ824wT3e"

  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  arch-meson news_flash_gtk build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir" --no-rebuild
  ln -s io.gitlab.news_flash.NewsFlash "$pkgdir/usr/bin/$pkgname"
}
