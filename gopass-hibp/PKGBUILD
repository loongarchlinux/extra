# Maintainer: christian rebischke <chris.rebischke@archlinux.org>
# Maintainer: Christian Heusel <gromit@archlinux.org>

pkgname=gopass-hibp
pkgver=1.15.13
pkgrel=1
pkgdesc="haveibeenpwnd.com integration for gopass"
arch=('loong64' 'x86_64')
url="https://github.com/gopasspw/gopass-hibp"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gopasspw/gopass-hibp/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('eb2eff7ffd16c166ee9ebfaa52273893b4f349fddba900ea966d0b39bf5f6f22b37cb76de8e0a327002138f1013d227bb066afcb9ff785b9afded97ebba17030')
options=('!lto')

prepare(){
  cd ${pkgname}-${pkgver}
  sed -i 's|-gcflags="-trimpath=$(GOPATH)" -asmflags="-trimpath=$(GOPATH)"||' Makefile
  sed -i 's/install-completion//g' Makefile
}

build(){
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
