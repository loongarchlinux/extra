# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=bugstalker
_pkgname=BugStalker
pkgver=0.2.2
pkgrel=1
pkgdesc="Rust debugger for Linux x86-64"
arch=('loong64' 'x86_64')
url="https://github.com/godzie44/BugStalker"
license=('MIT')
depends=('gcc-libs' 'libunwind')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('c3c4456da2a8facde15205c84b1ec9f749787715a330714e9b77db2445e0568c6e9194d069ead0a8a6add6758d09e59fb7172b0cfdcd79a4942ec842c845f42e')
options=('!lto')

prepare() {
  mv "$_pkgname-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --lib
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/bs" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
