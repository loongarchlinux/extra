# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Tomislav Ivek <tomislav.ivek@gmail.com>

pkgname=python-fasteners
_name="${pkgname#python-}"
pkgver=0.19
pkgrel=3
pkgdesc="A python package that provides useful locks."
arch=('any')
url="https://github.com/harlowja/fasteners"
license=('Apache-2.0')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=(
  # 'python-diskcache'  # TODO: package
  'python-eventlet'
  'python-more-itertools'
  'python-pytest'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/harlowja/fasteners/archive/$pkgver.tar.gz")
sha512sums=('47db216ff5bad5c669ca2d9be0ee09feba4a3d55ed0eac8d3623bd3d112b0b87fb9019f8785d035ec32ad9fa9e4ae7d1c92a576c97eec29acc82711d7e71a465')

build() {
  cd $_name-$pkgver
  python3 -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  # ignored test requires python-diskcache
  pytest -vv --ignore tests/test_reader_writer_lock.py
}

package() {
  cd $_name-$pkgver
  python3 -m installer --destdir="$pkgdir" dist/*.whl
}
