# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=gom
pkgname=(
  gom
  gom-docs
)
pkgver=0.5.3
pkgrel=1
pkgdesc='GObject to SQLite object mapper library'
arch=(loong64 x86_64)
url='https://wiki.gnome.org/Projects/Gom'
license=(LGPL-2.1-or-later)
depends=(
  glib2
  glibc
  sqlite
)
makedepends=(
  gdk-pixbuf2
  gi-docgen
  git
  gobject-introspection
  meson
  python-gobject
)
source=("git+https://gitlab.gnome.org/GNOME/$pkgbase.git#tag=$pkgver")
b2sums=('80e47b45b8f94c04c6215bb3525ce9431cc0e76b44ac407ead03c0f0d1ef2c747e529833730a5db86c4b67742373d0e6b3d96bf34ac3c77d8794e4bd65d6e660')

build() {
  arch-meson $pkgbase build \
    -D enable-gtk-doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_gom() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_gom-docs() {
  pkgdesc+=" - documentation"
  depends=()

  mv doc/* "$pkgdir"
}
