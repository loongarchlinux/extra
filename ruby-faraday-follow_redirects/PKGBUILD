# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='faraday-follow_redirects'
_archivename='faraday-follow-redirects'
pkgname="ruby-${_gemname}"
pkgver=0.3.0
pkgrel=1
pkgdesc='Faraday 2.x compatible extraction of FaradayMiddleware::FollowRedirects'
arch=('any')
url="https://github.com/tisba/${_archivename}"
license=('MIT')
depends=(
  ruby
  ruby-faraday
)
checkdepends=(
  ruby-bundler
  ruby-rake
  ruby-rspec
  ruby-webmock
)
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('357b9618f203749f806b7882b39dfd1b90a403c9a76734f4d3ae59ef2a70430254e616be66fb5a0ec74e770e2ee9df6e0f805b00f70a6f99de12ff70fa4d2bda')
b2sums=('463cfb9e797f21355fea5318f0a647936194b671828e55c6ad6dff1c3e853b15ed0facac28e2f59c0cccf8db5d9f11f3bd66c1c877d9a9538d5deea832c4d3d2')

prepare() {
  cd "${_archivename}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_archivename}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build --verbose "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install${_gemdir}/cache/" \
    "tmp_install${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_archivename}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  GEM_HOME="tmp_install${_gemdir}" rake spec
}

package() {
  cd "${_archivename}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose -D --mode=0644 LICENSE* --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
