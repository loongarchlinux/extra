# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Harry Jeffery <harry|@|exec64|.|co|.|uk>
# Contributor: Alex Jordan <alexander3223098@gmail.com>

pkgname=zerotier-one
pkgver=1.12.1
pkgrel=1
pkgdesc='Creates virtual Ethernet networks of almost unlimited size.'
arch=('loong64' 'x86_64')
url='https://www.zerotier.com/'
license=('custom:BSL')
depends=('gcc-libs' 'libnatpmp' 'miniupnpc')
makedepends=('rust')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zerotier/ZeroTierOne/archive/refs/tags/$pkgver.tar.gz"
        'sysusers.conf')
sha512sums=('eaab3c669e4bf2578c5224c54e49b0447213767f5ebfd948818bd7c86d8ae87943170cf6164c085e939d890ede1a65dafb4e3c3aa0443cb9af889432d0bbe01c'
            '1fdcc2e2a888ac7a93985bb6c16ac818aa6b7a56ccdeedf3cbbd22158251f6119f841e41e79b5522f9d1c6b8a315525d178cc42197c5eef2c70498b7954541f0')

prepare() {
  cd ZeroTierOne-$pkgver

  sed \
    -e 's/sbin/bin/' \
    -e 's/LDFLAGS=/LDFLAGS?=/' \
    -e 's/RUSTFLAGS=/RUSTFLAGS?=/' \
    -e 's/cargo build $(RUSTFLAGS)/cargo build --release/' \
    -i make-linux.mk debian/zerotier-one.service
}

build() {
  cd ZeroTierOne-$pkgver

  make V=1
}

check() {
  cd ZeroTierOne-$pkgver

  make selftest
  ./zerotier-selftest
}

package() {
  cd ZeroTierOne-$pkgver

  make DESTDIR="$pkgdir" install
  install -D -m0644 debian/zerotier-one.service "$pkgdir"/usr/lib/systemd/system/zerotier-one.service

  install -D -m0644 "$srcdir"/sysusers.conf "$pkgdir"/usr/lib/sysusers.d/"$pkgname".conf
}
