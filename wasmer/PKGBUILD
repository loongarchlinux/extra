# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Julius Michaelis <gitter@liftm.de>

pkgname=wasmer
pkgver=4.3.5
pkgrel=1
pkgdesc="Universal Binaries Powered by WebAssembly"
arch=('loong64' 'x86_64')
url="https://github.com/wasmerio/wasmer"
license=('MIT')
depends=('gcc-libs' 'zlib' 'ncurses' 'libffi' 'libxkbcommon')
makedepends=('rustup' 'cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wasmerio/wasmer/archive/refs/tags/v${pkgver}.tar.gz"
        reproducible-builds.patch)
sha512sums=('b8d6ddd12b641595f3893104b4a38afd70f5f123e26724e9606d7be4afe7085cd6942b3b7c58ba7b660d5ff6838d6261b3d6681048a0b290b1a1b509a212c9de'
            'dbefb95fb2cb4449251451e931f6c3ee2e128809b34f096393b5dbf9d260a55185e5542aafce2714d8db9baded086ec9af97b63d87340d8a22d3010a1f756a17')
options=('staticlibs' '!lto')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/reproducible-builds.patch"
}

build() {
  cd "$pkgname-$pkgver"
  # Activate LLVM once
  # https://github.com/TheDan64/inkwell/issues/406 lands.
  make ENABLE_CRANELIFT=1 ENABLE_LLVM=0 WASMER_INSTALL_PREFIX=/usr
}

check() {
  cd "$pkgname-$pkgver"
  make test-examples
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir"/usr install
}

# vim:set ts=2 sw=2 et:
