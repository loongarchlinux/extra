# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Yunxiang Li <shironeko@waifu.club>

pkgname=poke
pkgver=4.2
pkgrel=1
pkgdesc="Interactive, extensible editor for binary data."
arch=(loong64 x86_64)
url="http://www.jemarch.net/poke.html"
license=('GPL3')
depends=('gc' 'readline' 'gettext')
makedepends=('help2man')
checkdepends=('dejagnu')
options=('!lto')
source=("https://ftp.gnu.org/gnu/poke/poke-${pkgver}.tar.gz"{,.sig})
validpgpkeys=('BDFA5717FC1DD35C2C3832A23EF90523B304AF08'
              'F951CD18180E20B7DBD9359D54583674549E7E3A')
sha256sums=('8aaf36e61e367a53140ea40e2559e9ec512e779c42bee34e7ac24b34ba119bde'
            'SKIP')

build() {
    cd "${pkgname}-${pkgver}"
    # TODO: This vendors two dependencies I think.
    ./configure --prefix=/usr
    make
}

check() {
    cd "${pkgname}-${pkgver}"
    make -k check
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}/" install
}
