# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: schuay <jakob.gruber@gmail.com>

pkgname=puzzles
pkgver=20240103
pkgrel=1
_commit=7a93ae5
pkgdesc="Simon Tatham's Portable Puzzle Collection"
arch=(loong64 x86_64)
url='https://www.chiark.greenend.org.uk/~sgtatham/puzzles/'
license=(MIT)
depends=(cairo
         gdk-pixbuf2
         glib2
         glibc
         gtk3
         pango)
makedepends=(cmake
             halibut
             imagemagick)
source=(https://www.chiark.greenend.org.uk/~sgtatham/puzzles/puzzles-$pkgver.$_commit.tar.gz)
sha256sums=('d694ebb9217e2a5d70a93148698607aef6c3f69fa4fb53c66b93e95788ef8049')

build() {
  cmake -B build -S puzzles-$pkgver.$_commit \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DNAME_PREFIX=puzzles-
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 puzzles-$pkgver.$_commit/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
