# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Dino Morelli < dino@ui3.info >

pkgname=glulxe-term
pkgver=0.6.1
pkgrel=1
epoch=1
pkgdesc='The Glulx VM reference interpreter'
arch=('loong64' 'x86_64')
url='http://www.eblong.com/zarf/glulx/'
license=('MIT')
depends=('libglkterm')
source=(${pkgname}-$pkgver}.tar.gz::"https://github.com/erkyrath/glulxe/archive/refs/tags/${pkgname%-term}-${pkgver}.tar.gz"
        "${pkgname}-flags.patch")
sha512sums=('5f126171ac9891eb611f5102ee47ee223136e054df35d371fcab8e5c2c02d6aa8ed3d928465b29f3c9aab15ccf9af295b9da7155873025ea14bfcc9cfd7349e7'
            '2a65cd455fcbf90520223ea5820ebbf41172a3d456d56534358e34858c37db954eed4400ee3b8a146a37b9b2c8c314e00f3754c314b737e86ca621130a0617c6')
b2sums=('f862b6cc1960420ec646a32db92f55d06978ce08f8c8c550630fda39a4da69f7f747a5667846ee12cb8d8a377b7d8e221cbfe30672f33a9db1c2f44691b0b339'
        '527553355e99ffdde5db8ded82a57d1824dd89732029799c41678423894a7bd2361b5781c29063cfcf32ce3e2b13c10d86c56d697ab16f87ca04946edefd9094')

prepare() {
	cd glulxe-glulxe-${pkgver}
	patch -N -i ../${pkgname}-flags.patch
}

build() {
	cd glulxe-glulxe-${pkgver}
	make GLKINCLUDEDIR=/usr/include GLKLIBDIR=/usr/lib GLKMAKEFILE=Make.glkterm
}

package() {
	cd glulxe-glulxe-${pkgver}
	install -vDm755 glulxe "$pkgdir/usr/bin/glulxe-term"
	install -vDm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
