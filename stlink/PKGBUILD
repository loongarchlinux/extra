# Maintainer:
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=stlink
pkgver=1.8.0
pkgrel=1
pkgdesc='Open source STM32 MCU programming toolset'
arch=(loong64 x86_64)
url='https://github.com/texane/stlink'
license=(BSD-3-Clause)
depends=(
  glib2
  glibc
  gtk3
  hicolor-icon-theme
  libusb
)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('49245a4b0aeb926dd71ec4260b707d900318fe68e3a36e2e5543587fefb4886a9ff0ff42e0e12a8e727a5d4e9c7b4ce13989ca4d963842dbf960065e5fa0c968')
b2sums=('93977a0f1700b70b7fd8e17ef6ab2d636323665e4f98e5753ced1fce9de79b0cc3a47a5db973c39ce1ec1d1ecb7dbd2bfb91b4e41ee895d65fa4907136eba9dd')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D STLINK_MODPROBED_DIR=/usr/lib/modprobe.d
    -D STLINK_UDEV_RULES_DIR=/usr/lib/udev/rules.d
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
