# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-parental-controls
pkgver=6.0.1.r100.4910078
pkgrel=1
pkgdesc='Switchboard Parental Controls Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-parental-controls
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  accountsservice
  flatpak
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libgee
  libgranite-7.so
  libmalcontent-0.so
  libswitchboard-3.so
  polkit
)
makedepends=(
  git
  meson
  vala
)
_commit=49100787dcfd354f6a249c9f8307182d3e659226
source=(git+https://github.com/elementary/switchboard-plug-parental-controls.git#commit=${_commit})
b2sums=('d807d00e544ad5a23832225afc86d9c0613a59bde64692209f64254ec08534b864c241759d4f4685d15b108d6e1075e9b09698593f8a50885e6f2f7bb775bea6')

pkgver() {
  cd switchboard-plug-parental-controls
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

prepare() {
  cd switchboard-plug-parental-controls
  sed "s/dbus_dep.get_pkgconfig_variable('sysconfdir')/dbus_dep.get_pkgconfig_variable('datadir')/" -i data/meson.build
}

build() {
  arch-meson switchboard-plug-parental-controls build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
