# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Henrik Ronellenfitsch <searinox@web.de>
# Contributor: Alessio Sergi <sergi.alessio {at} gmail.com>
# Contributor: Dario 'Dax' Vilardi <dax [at] deelab [dot] org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=amule
epoch=1
pkgver=2.3.3
pkgrel=9
pkgdesc='An eMule-like client for ed2k p2p network'
arch=(loong64 x86_64)
url='http://www.amule.org'
license=(GPL-2.0-or-later)
depends=(crypto++
         geoip
         libupnp
         wxwidgets-gtk3)
makedepends=(boost
             cmake
             gd
             git)
provides=(amule-daemon)
source=(git+https://github.com/amule-project/amule#tag=$pkgver
        amuled.systemd
        amuleweb.systemd
        amule.sysusers
        amule.tmpfiles)
sha256sums=('9e28bde45b432bb966a2972141f8a445c9cdfe8a808d9874cfa4d67f277ce153'
            '20ac6b60c5f3bf49c0b080dfc02409da3c9d01b154344188008c6a75ca69681e'
            'f4f43b1154ddccc9036a4291a58c6715f097b171fec62ea7aead0c9d9fa654f2'
            'c4ca658ab4105b3b90e0bb3efcc8121eca1c4d873787db1ed4f637925c16d502'
            'e9d1b7019c7075b0f8616c6507a767b87de8f899936680e9ff5829d8cbba224d')

prepare() {
  cd $pkgname
  git cherry-pick -n 9bcd201 # Fix translations install
  git cherry-pick -n 8085232
  git cherry-pick -n f186b3a
  git cherry-pick -n d22e057 # Fix build
  git cherry-pick -n 9e1d9eb
  git cherry-pick -n a667c61 # Install icons
  rm -fr .git
}

build() {
  cmake -B build -S $pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_ALC=ON \
    -DBUILD_ALCC=ON \
    -DBUILD_AMULECMD=ON \
    -DBUILD_CAS=ON \
    -DBUILD_DAEMON=ON \
    -DBUILD_REMOTEGUI=ON \
    -DBUILD_WEBSERVER=ON \
    -DBUILD_WXCAS=ON \
    -DBUILD_FILEVIEW=ON \
    -DENABLE_IP2COUNTRY=ON \
    -DENABLE_NLS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$srcdir"/amuled.systemd "$pkgdir"/usr/lib/systemd/system/amuled.service
  install -Dm644 "$srcdir"/amuleweb.systemd "$pkgdir"/usr/lib/systemd/system/amuleweb.service
  install -Dm644 "$srcdir"/amule.sysusers "$pkgdir"/usr/lib/sysusers.d/amule.conf
  install -Dm644 "$srcdir"/amule.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/amule.conf
}
