# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Benoit Brummer < trougnouf at gmail dot com >
# Contributor: Carl George < arch at cgtx dot us >

pkgname=gnome-shell-extension-dash-to-panel
_pkgname=dash-to-panel
pkgver=62
pkgrel=1
pkgdesc='Extension for GNOME Shell to combine the dash and main panel'
arch=('any')
url='https://github.com/home-sweet-gnome/dash-to-panel'
license=('GPL-2.0-or-later')
depends=('gnome-shell')
source=("https://github.com/home-sweet-gnome/$_pkgname/archive/v$pkgver/$_pkgname-$pkgver.tar.gz"
        'gnome-shell-extension-dash-to-panel-system-install-paths.patch'
        'gnome-shell-extension-dash-to-panel-fix-crash-on-close.patch'
        'gnome-shell-extension-dash-to-panel-menu-button.patch')
sha256sums=('b3b23fbcfd37fa6b9bae84820aaa364b1fa2c8acf599d5e3038e242eaac7a74b'
            'b9c247fc8ab7f1ee18ce2c98f8312f86da0340622d4c5daadd3a4579f4213fc5'
            'd772d9aa2995602e5b11fa9624586d7a1c485fde9b21f046f2cf52e5fb099735'
            '3c06765d11e1df3e8b73b96042c1034e7ea153378076293cee8efe2ae7996e43')

prepare() {
  cd $_pkgname-$pkgver
  # https://github.com/home-sweet-gnome/dash-to-panel/pull/1948
  patch -Np1 -i ../gnome-shell-extension-dash-to-panel-system-install-paths.patch

  # https://github.com/home-sweet-gnome/dash-to-panel/pull/1969
  patch -Np1 -i ../gnome-shell-extension-dash-to-panel-fix-crash-on-close.patch

  # https://github.com/home-sweet-gnome/dash-to-panel/commit/12675269a860728adea9c962ba67d613376dc397
  patch -Np1 -i ../gnome-shell-extension-dash-to-panel-menu-button.patch
}

build() {
  cd $_pkgname-$pkgver
  make
}

package() {
  cd $_pkgname-$pkgver
  make VERSION="$pkgver" DESTDIR="$pkgdir" install
}
