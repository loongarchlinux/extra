# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cysignals
pkgname=python-cysignals
pkgver=1.11.4
pkgrel=2
pkgdesc='Interrupt and signal handling for Cython'
arch=(loong64 x86_64)
url='https://www.sagemath.org'
license=(LGPL)
depends=(glibc
         pari
         python)
makedepends=(cython
             python-build
             python-installer
             python-setuptools
             python-wheel)
source=(https://pypi.python.org/packages/source/c/cysignals/cysignals-$pkgver.tar.gz)
sha256sums=('0f1e321e55a07f901c86a36a1e4497f6ff9dfe700681d0130a38c36e4eb238c3')

build() {
  cd $pkgbase-$pkgver
  python -m build --wheel --no-isolation
}

package_python-cysignals() {
  cd $pkgbase-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
