# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=5.25.18
pkgrel=3
pkgdesc='DDE window manager forked from KWin'
arch=('loong64' 'x86_64')
url="https://github.com/linuxdeepin/deepin-kwin"
license=('GPL3')
depends=(qt5-declarative qt5-x11extras kconfig5 kconfigwidgets5 kcoreaddons5 kdbusaddons5 kdeclarative5
         kglobalaccel5 ki18n5 kidletime5 knotifications5 kcrash5 kpackage5 plasma-framework5 libepoxy
         kwidgetsaddons5 kwindowsystem5 kcompletion5 kcmutils5 knewstuff5 kservice5 ktextwidgets5 kxmlgui5
         breeze dwayland xcb-util-cursor xorg-xwayland libpipewire libkscreen5 kdecoration5 kscreenlocker5 qt5-wayland
         lcms2 libqaccessibilityclient-qt5 qt5-quickcontrols python fontconfig freetype2 libcap
         systemd-libs kauth5 bash kactivities5 libxkbcommon libxss libxi libinput xcb-util-wm wayland
         perl libxcb xcb-util-keysyms libx11 libxtst libdrm gcc-libs mesa qt5-base deepin-qt5platform-plugins)
makedepends=(extra-cmake-modules kdoctools5 ninja qt5-tools wayland-protocols
             deepin-wayland-protocols)
source=("https://github.com/linuxdeepin/deepin-kwin/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ae1bdc1656b9f96a686af28b9d282678e6c39d54d01059783206e97d95971e100b0587ed7b86a9242bbe063919e42811df40e85bed6ff1fec0b8fd84cbc0ef1c')

prepare() {
  cd deepin-kwin-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/effects/multitaskview/multitaskview.cpp
  sed -i 's|org.kde.screensaver.xml|org.kde.screensaver5.xml|' src/CMakeLists.txt
}

build() {
  cd deepin-kwin-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DKScreenLocker_ROOT=/usr/lib/cmake/plasma5 \
    -DKDecoration2_ROOT=/usr/lib/cmake/plasma5 \
    -DKWIN_BUILD_RUNNERS=OFF
  ninja
}

package() {
  cd deepin-kwin-$pkgver
  DESTDIR="$pkgdir" ninja install
}
