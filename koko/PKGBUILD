# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=koko
pkgver=24.05.2
pkgrel=1
pkgdesc='Image gallery application'
url='https://apps.kde.org/koko/'
arch=(loong64 x86_64)
license=(GPL-2.0-or-later LGPL-2.0-or-later)
depends=(exiv2
         gcc-libs
         glibc
         kconfig
         kconfigwidgets
         kcoreaddons
         kdbusaddons
         kdeclarative
         kfilemetadata
         kguiaddons
         ki18n
         kio
         kirigami
         kirigami-addons
         knotifications
         kquickimageeditor
         kwindowsystem
         libxcb
         purpose
         qt6-base
         qt6-declarative
         qt6-multimedia
         qt6-positioning
         qt6-svg)
makedepends=(extra-cmake-modules)
groups=(kde-applications
        kde-graphics)
# update with ./download-data
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://sources.archlinux.org/other/packages/koko/cities1000-2023-11-14.zip
        https://sources.archlinux.org/other/packages/koko/admin1CodesASCII-2023-11-14.txt
        https://sources.archlinux.org/other/packages/koko/admin2Codes-2023-11-14.txt)
sha256sums=('c2ca5ff0671c98244a279b10e083dded5a50d0804e79c8e9120a040dd2085a16'
            'SKIP'
            '68e71c0790b37ac1e102c49d5ed4bb28a8ef3106d92fd03764538db623167259'
            '14e12e9d94edac5c97bbc02869c051b2188b2ec2b4c7af18597a7b36167f211e'
            '25a34f94da79abe10272bacd0ed870a13a17b179bc08a7d975c5afd8e1b7b0dd')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  cp -L cities1000-2023-11-14.zip admin1CodesASCII-2023-11-14.txt $pkgname-$pkgver/src
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
