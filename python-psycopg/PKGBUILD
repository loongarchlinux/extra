# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-psycopg
pkgver=3.2.1
pkgrel=1
pkgdesc='PostgreSQL database adapter for Python'
arch=('any')
url='https://www.psycopg.org/psycopg3/'
license=('LGPL-3.0-only')
depends=('python' 'postgresql-libs')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
checkdepends=('python-pytest' 'python-pytest-asyncio')
source=("$pkgname::git+https://github.com/psycopg/psycopg.git#tag=$pkgver")
sha512sums=('94a3e35e30fb21b0c7be604b6038a2a2ea3673c65ac212ea5ea13f56c524e808af3904ec4b5aaca9a947eb18fa59c3d1959b696212d472cf84c2b2e74d9ffa29')
b2sums=('a2e2b45a84f38f5a340cfe6fdabb26e7db150752e163b3bddbf3f4d2dcfda0a552e2983e50848aa55c416bc9c58579bd0ffeb8502e79b69e3bacaf1e6b6bec30')

build(){
  cd "$pkgname/psycopg"

  python -m build --wheel --no-isolation
}

package(){
  cd "$pkgname/psycopg"

  python -m installer --destdir="$pkgdir" dist/*.whl 
}
