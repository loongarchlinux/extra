# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: Xyne <ca dot archlinux at xyne, backwards>
# Contributor: nikor

pkgname=opencsg
pkgver=1.6.0
pkgrel=1
pkgdesc="Library for image-based CSG rendering using OpenGL."
url="https://www.opencsg.org"
arch=('loong64' 'x86_64')
license=('GPL2')
depends=('glew' 'glut' 'libgl')
makedepends=('qt5-base')
source=("https://www.opencsg.org/OpenCSG-${pkgver}.tar.gz")
sha256sums=('bf8fb80e3e0ce11d87dd78dd15a0de872dbb8972d87f5f89cffc461efad47be8')

build() {
  cd "$srcdir/OpenCSG-$pkgver"
  sed -i 's|-rpath,../lib|-rpath,/usr/lib|' src/Makefile
  cd src
  make QMAKE=/usr/bin/qmake-qt5
}

package() {
  cd "$srcdir/OpenCSG-$pkgver"
  install -D lib/libopencsg.so.$pkgver "$pkgdir/usr/lib/libopencsg.so.$pkgver"
  install -D include/opencsg.h "$pkgdir/usr/include/opencsg.h"
  cd "$pkgdir/usr/lib/"
  ln -s libopencsg.so.$pkgver libopencsg.so.1.4
  ln -s libopencsg.so.$pkgver libopencsg.so.1
  ln -s libopencsg.so.$pkgver libopencsg.so
}

