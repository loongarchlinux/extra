# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lev Lybin <lev.lybin@gmail.com>
# Contributor: lagrange <flkazemakase@gmail.com>
# Contributor: mid-kid <esteve.varela@gmail.com>

pkgname=python-kivy
pkgver=2.2.1
pkgrel=3
pkgdesc="A software library for rapid development of hardware-accelerated multitouch applications."
arch=('loong64' 'x86_64')
url="https://kivy.org/"
license=('MIT')
depends=('python-docutils' 'gstreamer' 'sdl2_ttf' 'sdl2_mixer' 'sdl2_image' 'python-requests')
makedepends=('python-setuptools' 'cython0')
optdepends=('python-oscpy: for TUIO provider')
checkdepends=('python-pytest' 'python-pytest-benchmark' 'python-coverage' 'python-gobject' 'xorg-server-xvfb' 'opencv' 'python-pyenchant'
              'git' 'mtdev' 'xclip' 'xsel' 'gtk3' 'gst-plugins-base'
              'gst-plugins-good' 'gst-libav' 'dbus-python' 'python-responses')
source=("https://pypi.io/packages/source/K/Kivy/Kivy-$pkgver.tar.gz")
sha512sums=('64f517a10c5681a556954057f1ae35bfab9a71fb3808e395ae9a59a8fbe505260c554851f2a89db0cb1fee1bf0c8d8ae14d3cb72877b60b0d9ba7c9f9aacb227')

prepare() {
  cd Kivy-$pkgver
  # Do not use certifi
  sed -e '/import certifi/d;s|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' -i kivy/network/urlrequest.py -i kivy/loader.py \
    -i kivy/tests/test_urlrequest/test_urlrequest_urllib.py -i kivy/tests/test_urlrequest/test_urlrequest_requests.py
}

build() {
  cd Kivy-$pkgver
  python setup.py build
}

check() {
  cd Kivy-$pkgver
  python setup.py build_ext --inplace
  xvfb-run -s "-screen 0 1280x720x24 -ac +extension GLX" pytest kivy/tests \
    -k 'not test_urlrequest_urllib.py::test_auth_header and not test_urlrequest_urllib.py::test_auth_auto' # flaky tests that depend on network access
}

package() {
  cd Kivy-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
