# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lev Lybin <lev.lybin@gmail.com>
# Contributor: lagrange <flkazemakase@gmail.com>
# Contributor: mid-kid <esteve.varela@gmail.com>

pkgname=python-kivy
pkgver=2.3.0
pkgrel=1
pkgdesc="A software library for rapid development of hardware-accelerated multitouch applications."
arch=('loong64' 'x86_64')
url="https://kivy.org/"
license=('MIT')
depends=('python-docutils' 'gstreamer' 'sdl2_ttf' 'sdl2_mixer' 'sdl2_image' 'python-requests')
makedepends=('python-setuptools' 'cython')
optdepends=('python-oscpy: for TUIO provider')
checkdepends=('python-pytest' 'python-pytest-benchmark' 'python-coverage' 'python-gobject' 'xorg-server-xvfb' 'opencv' 'python-pyenchant'
              'git' 'mtdev' 'xclip' 'xsel' 'gtk3' 'gst-plugins-base'
              'gst-plugins-good' 'gst-libav' 'dbus-python' 'python-responses')
source=("https://pypi.io/packages/source/K/Kivy/Kivy-$pkgver.tar.gz")
sha512sums=('9d3244dc5942659ea95cf5200074b01f41e3d0ee26da91c715c89572b2d878854208a010ea3013633344ff7f4f9857fd0817b1f1ab0e98e34363c65d8c57f40b')

prepare() {
  cd Kivy-$pkgver
  # Do not use certifi
  sed -e '/import certifi/d;s|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' -i kivy/network/urlrequest.py -i kivy/loader.py \
    -i kivy/tests/test_urlrequest/test_urlrequest_urllib.py -i kivy/tests/test_urlrequest/test_urlrequest_requests.py
}

build() {
  cd Kivy-$pkgver
  CFLAGS+=" -Wno-incompatible-pointer-types" \
  python setup.py build
}

check() {
  cd Kivy-$pkgver
  python setup.py build_ext --inplace
  xvfb-run -s "-screen 0 1280x720x24 -ac +extension GLX" pytest kivy/tests \
    -k 'not test_urlrequest_urllib.py::test_auth_header and not test_urlrequest_urllib.py::test_auth_auto' # flaky tests that depend on network access
}

package() {
  cd Kivy-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
