# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: "Amhairghin" Oscar Garcia Amor (https://ogarcia.me)
# Contributor: Franklyn Tackitt <franklyn@tackitt.net>
# Contributor: Christian HÃ¶ppner <chris@mkaito.com>

pkgname=drone-cli
pkgver=1.8.0
pkgrel=1
pkgdesc='Command line interface for Drone CI'
arch=('loong64' 'x86_64')
url='https://docs.drone.io/cli'
license=('Apache-2.0')
depends=('glibc')
makedepends=(
  'git'
  'go'
)
options=('!lto')
source=("$pkgname::git+https://github.com/harness/drone-cli.git#tag=v$pkgver")
b2sums=('0decf4e0779027e1235fc4d5e1bdcadaa00d4fa3ee44ce55d7b9c8438535037f732bbf76dd7b746c01d1ec880563da0ea2bcd903a12cb82d4f912076a11117f5')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  go mod download -x
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -mod=readonly -modcacherw"
  export GOPATH="${srcdir}"

  local ld_flags="-compressdwarf=false -linkmode=external -X main.version=$pkgver"
  go build -v -ldflags "$ld_flags" -o build ./...
}

check() {
  cd "$pkgname"

  go test -v ./...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/drone

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
}
