# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thibaut PÃ©rami <thibaut.perami@cl.cam.ac.uk>

pkgname=zydis
pkgver=4.1.0
pkgrel=1
pkgdesc='Fast and lightweight x86/x86-64 disassembler and code generation library'
arch=('loong64' 'x86_64')
url='https://zydis.re'
license=('MIT')
depends=('gcc-libs' 'zycore-c')
makedepends=('git' 'cmake' 'ruby-ronn-ng' 'doxygen')
checkdepends=('python')
provides=('libZydis.so')
source=("git+https://github.com/zyantific/zydis.git#tag=v$pkgver")
sha512sums=('49fa2280b2673067630631d436cfecc841fe683640c21db9e8942d59bb6661385df26ad0d2e53fdc3f8a8a97c51bc652cdc413eb83aa9bb30a387b3e46c15e1e')
b2sums=('6c1f646f95951177ab27297398a5bed7e4ada1b1159177bb508ad12871728a63a6bf1e8a5b94df8b17a45cd6e6b7d7154fb84f5c9f7a750dab7081539d943365')

build() {
  # https://archlinux.org/todo/lto-fat-objects/
  export CFLAGS+=" -ffat-lto-objects"

  # https://github.com/zyantific/zydis#-fpic-for-shared-library-builds
  cmake \
    -S "$pkgname" \
    -B build \
    -W no-dev \
    -D CMAKE_BUILD_TYPE='None' \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_POSITION_INDEPENDENT_CODE=ON \
    -D ZYDIS_BUILD_SHARED_LIB=ON \
    -D ZYDIS_BUILD_TESTS=ON \
    -D ZYAN_SYSTEM_ZYCORE=ON \
    -D ZYDIS_BUILD_MAN=ON

  cmake --build build
}

check() {
  # test suite extracted from .github/workflows/main.yml
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd "$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/Zydis" README.md
  cp -vr examples "$pkgdir/usr/share/doc/Zydis"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
