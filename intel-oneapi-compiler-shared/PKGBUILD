# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Contributor: Konstantin Gizdov <arch at kge dot pw>

pkgname=intel-oneapi-compiler-shared
_pkgver=2024.1
pkgver=2024.1.0
_debpkgrel=963
pkgrel=1
pkgdesc='Intel oneAPI compiler runtime libraries'
arch=('loong64' 'x86_64')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("LicenseRef-Intel-EULA-Developer-Tools")
source=("https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb")
b2sums=('9d0c250eee38f42edaf60b624f7dfccae32179609f12c7a2014376350d6742d8ff2a760b6c539c724aa0c0a3c32fab4b4205704acb676aed984cba9313479393')
depends=('intel-oneapi-compiler-shared-common' 'intel-oneapi-compiler-shared-runtime'
         'intel-oneapi-dpcpp-debugger' 'intel-oneapi-common' 'intel-oneapi-compiler-shared-runtime-libs'
         'level-zero-loader' 'glibc' 'gcc-libs' 'zlib')
provides=("${pkgname}-common")
conflicts=('intel-oneapi-basekit')

noextract=(
    "${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
)
_lib_path='/opt/intel/oneapi/compiler'

package() {
    cd "${srcdir}"
    ar x "${srcdir}/${pkgname}-${_pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
    tar xvf data.tar.xz -C "${pkgdir}"
    rm data.tar.xz

    find "${pkgdir}" -type f -iname "*.a" -exec chmod 644 '{}' \;

    install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
    ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}
