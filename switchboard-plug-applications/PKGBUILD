# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-applications
pkgver=7.0.1.r247.92346d65
pkgrel=1
pkgdesc='Switchboard Applications Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-applications
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  flatpak
  glib2
  glibc
  gtk4
  hicolor-icon-theme
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  vala
)
_commit=92346d650e861dd6a4260aa90c684adc7fe00373
source=(git+https://github.com/elementary/switchboard-plug-applications.git#commit=${_commit})
b2sums=(SKIP)

pkgver() {
  cd switchboard-plug-applications
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-applications build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
