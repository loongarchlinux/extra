# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=clblast
pkgver=1.6.3
pkgrel=1
pkgdesc='Tuned OpenCL BLAS library'
url='https://github.com/CNugteren/CLBlast'
arch=('loong64' 'x86_64')
license=('Apache-2.0')
depends=(
  gcc-libs
  glibc
  ocl-icd
  opencl-headers
)
makedepends=(
  cmake
)
provides=('libclblast.so')
source=(https://github.com/CNugteren/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c05668c7461e8440fce48c9f7a8966a6f9e0923421acd7c0357ece9b1d83f20e')

prepare() {
  cd "CLBlast-${pkgver}"
  mkdir -p build
}

build() {
  cd "CLBlast-${pkgver}/build"
  cmake .. -DCMAKE_INSTALL_PREFIX="/usr"
  make
}

package() {
  cd "CLBlast-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
