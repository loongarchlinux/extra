# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=clblast
pkgver=1.6.2
pkgrel=1
pkgdesc='Tuned OpenCL BLAS library'
url='https://github.com/CNugteren/CLBlast'
arch=('loong64' 'x86_64')
license=('Apache-2.0')
depends=(
  gcc-libs
  glibc
  ocl-icd
  opencl-headers
)
makedepends=(
  cmake
)
provides=('libclblast.so')
source=(https://github.com/CNugteren/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('d7c1fb61162a6e2fa4eb6e95fafacbe22ee8460cd82371478f794f195aad267f')

prepare() {
  cd "CLBlast-${pkgver}"
  mkdir -p build
}

build() {
  cd "CLBlast-${pkgver}/build"
  cmake .. -DCMAKE_INSTALL_PREFIX="/usr"
  make
}

package() {
  cd "CLBlast-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
