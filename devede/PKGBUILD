# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Javier 'Phrodo_00' Aravena <phrodo.00@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=devede
_pkgname=devedeng
pkgver=4.18.0
pkgrel=1
pkgdesc='Program to create VideoDVDs and CDs'
arch=('any')
url='https://rastersoft.com/programas/devede.html'
license=('GPL-3.0-only')
depends=(
  'cdrtools'
  'dvdauthor'
  'ffmpeg'
  'gdk-pixbuf2'
  'glib2'
  'gtk3'
  'hicolor-icon-theme'
  'python'
  'python-cairo'
  'python-gobject'
  'python-setuptools'
  'vcdimager'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
)
source=("git+https://gitlab.com/rastersoft/$_pkgname.git#tag=$pkgver")
b2sums=('238b35e2f01e90ab436c1f0eb8f196fcaf4d5a30663c0045cae5ff26d676e210b78aff89b34be4eed85b2f9583988a91e8c2a405fc2760d48d2c2f98d30d7543')

prepare() {
  cd $_pkgname
  sed -i "/share', 'pixmaps/d;s|/usr', 'share|share|" setup.py
}

build() {
  cd $_pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $_pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}
