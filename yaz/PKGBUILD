# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>

pkgname=yaz
pkgver=5.34.0
pkgrel=4
pkgdesc="A toolkit supporting the development of Z39.50/SRW/SRU clients and servers"
arch=('loong64' 'x86_64')
url="https://www.indexdata.dk/yaz"
license=('BSD')
depends=('gnutls' 'libxslt' 'icu')
makedepends=('zsh')
changelog=$pkgname.changelog
source=(http://ftp.indexdata.dk/pub/$pkgname/$pkgname-$pkgver.tar.gz
        https://github.com/indexdata/yaz/commit/3c61afce.patch)
sha256sums=('bcbea894599a13342910003401c17576f0fb910092aecb51cb54065d0cd2d613'
            'c3f560c7adf30e61f5aab7daf2f60392b087d8ac461d248dda928b8d85a5ae12')

prepare() {
  patch -d $pkgname-$pkgver -p1 < 3c61afce.patch # Fix build with glibc 2.39
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --enable-shared=yaz
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

#License
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
