# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Max Mehl <aur at mehl dot mx>
# SPDX-FileCopyrightText: 2019 Max Mehl
# SPDX-License-Identifier: CC0-1.0

pkgname=reuse
pkgver=4.0.3
pkgrel=1
pkgdesc='Helper tool for providing and confirming copyright and licensing information'
arch=('any')
url='https://reuse.software/'
license=(
  'Apache-2.0'
  'CC-BY-SA-4.0'
  'CC0-1.0'
  'GPL-3.0-or-later'
)
depends=(
  'python'
  'python-debian'
  'python-license-expression'
  'python-boolean.py'
  'python-jinja'
  'python-binaryornot'
  'python-attrs'
  'python-tomlkit'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-poetry-core'
)
checkdepends=('python-pytest' 'python-freezegun')
optdepends=(
  'git: git repository support'
  'mercurial: mercurial repository support'
)
source=("$pkgname::git+https://github.com/fsfe/reuse-tool#tag=v$pkgver")
sha512sums=('e0923fdba1da62fe263a402e2cebc36fd99a46d326dfa7a63284668a1ebb229eb4f76d1df689551a34dccb90a7bfa73530502a3a03e82875725a0526aa8b4c18')
b2sums=('d6279d3df2571754450eefdcf17a144ab28c38ae1af352c19d733aecbb1ad525f61e88c33cde4d2363d13597ba6231d36dc4a59813c9b96d3b79e13dd913316f')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.{rst,md}

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSES/*

  sed -n '/## License/,$ p' README.md > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
