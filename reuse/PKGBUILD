# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Max Mehl <aur at mehl dot mx>
# SPDX-FileCopyrightText: 2019 Max Mehl
# SPDX-License-Identifier: CC0-1.0

pkgname=reuse
pkgver=3.0.2
pkgrel=1
pkgdesc='Helper tool for providing and confirming copyright and licensing information'
arch=('any')
url='https://reuse.software/'
license=(
  'Apache-2.0'
  'CC-BY-SA-4.0'
  'CC0-1.0'
  'GPL-3.0-or-later'
)
depends=(
  'python'
  'python-debian'
  'python-license-expression'
  'python-boolean.py'
  'python-jinja'
  'python-binaryornot'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-poetry-core'
)
checkdepends=('python-pytest')
optdepends=(
  'git: git repository support'
  'mercurial: mercurial repository support'
)
source=("$pkgname::git+https://github.com/fsfe/reuse-tool#tag=v$pkgver")
sha512sums=('a53bfcd5a2a223b904f0e7033895e19af5c6bbd97ac3bef0f65616b273db7a6123d2090629fdc9759f7214dafcf38ba424435e5f6f13f61b9fc57a3f5451b34d')
b2sums=('1a80984e026fe8f7e64357ef3a1d8cbf9ecc9766c0a2d1f158d9605bc07fa8ec759f477d893baa7eac42a3eb1fe1862bf36e4376424ec53b166f0934760db83d')

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.{rst,md}

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSES/*

  sed -n '/## License/,$ p' README.md > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
