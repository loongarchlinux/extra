# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

_pyname=json-stream-rs-tokenizer
pkgname=python-${_pyname}
pkgver=0.4.26
pkgrel=2
pkgdesc='Faster tokenizer for the json-stream Python library'
arch=(loong64 x86_64)
url="https://github.com/smheidrich/py-json-stream-rs-tokenizer"
license=('MIT')
depends=(
  gcc-libs
  glibc
  python
)
makedepends=(
  python-build
  python-installer
  python-setuptools-rust
  python-wheel
)
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('19333ccda9121b1224598d67178665f77e142862800cb1edd7797a62936f7a1c')
b2sums=('fc81a8764fc525a141aa4b52e239afa7cea8862da74228f48314e8712aad9f70d8632fba0ae1afd3772eed241ad5468799d918ae88e55fa98cdf547295d3ba26')

build() {
  cd "${_pyname}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd ${_pyname}-${pkgver}
  python -m installer --destdir=test_dir dist/*.whl

  PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH" python -c 'import json_stream_rs_tokenizer'
}

package() {
  cd "${_pyname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
