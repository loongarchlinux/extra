# Maintainer: Campbell Jones <arch at serebit dot com>
# Co-Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Ranieri Althoff <ranisalt+aur at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
_pkgname=MangoHud
pkgname=mangohud
_pkgver=0.7.2
pkgver="${_pkgver%-*}"
pkgrel=1
pkgdesc="A Vulkan overlay layer for monitoring FPS, temperatures, CPU/GPU load and more."
arch=('loong64' 'x86_64')
url="https://github.com/flightlessmango/MangoHud"
license=('MIT')
makedepends=('appstream' 'cmocka' 'git' 'glslang' 'libxnvctrl' 'libxrandr' 'meson' 'nlohmann-json' 'python-mako' 'vulkan-headers')
depends=('dbus' 'fmt' 'gcc-libs' 'glew' 'glfw' 'hicolor-icon-theme' 'libglvnd' 'libx11' 'libxkbcommon' 'python' 'python-matplotlib' 'python-numpy' 'vulkan-icd-loader' 'wayland')
optdepends=('libxnvctrl: NVIDIA GPU stats by XNVCtrl'
            'gamescope: Use MangoApp as an overlay within gamescope')
replaces=("$pkgname-common" "$pkgname-common-wayland" "$pkgname-common-x11" "mangoapp" "$pkgname-x11" "$pkgname-wayland")
conflicts=("$pkgname-common" "$pkgname-common-wayland" "$pkgname-common-x11" "mangoapp" "$pkgname-x11" "$pkgname-wayland")
source=("$pkgname-$pkgver.tar.xz::https://github.com/flightlessmango/MangoHud/releases/download/v$pkgver/$_pkgname-v$_pkgver-Source.tar.xz")
b2sums=('774bdc33a7d58fadf1323da16c81f22eea39c1f101a6753be3cbf888e18b45fc1060e39c3f5191f5a3e71aa8ad11cde3f0c86eed50d7164afa518c282701996d')

prepare() {
    cd "$_pkgname-v$pkgver"

    # Use system cmocka instead of subproject
    sed -i "s/  cmocka = subproject('cmocka')//g" meson.build
    sed -i "s/cmocka_dep = cmocka.get_variable('cmocka_dep')/cmocka_dep = dependency('cmocka')/g" meson.build
}

build() {
    arch-meson "$_pkgname-v$pkgver" build \
        -Dmangoapp=true \
        -Dmangohudctl=true \
        -Dmangoapp_layer=true

    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"

    install -Dm 0644 "$srcdir/$_pkgname-v$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}

check() {
    meson test -C build --print-errorlogs || :
}
