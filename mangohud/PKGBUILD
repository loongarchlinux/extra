# Maintainer: Campbell Jones <arch at serebit dot com>
# Co-Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Ranieri Althoff <ranisalt+aur at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
_pkgname=MangoHud
pkgname=mangohud
_pkgver=0.7.1-1
pkgver="${_pkgver%-*}"
pkgrel=1
pkgdesc="A Vulkan overlay layer for monitoring FPS, temperatures, CPU/GPU load and more."
arch=('loong64' 'x86_64')
url="https://github.com/flightlessmango/MangoHud"
license=('MIT')
makedepends=('appstream' 'cmocka' 'git' 'glfw-x11' 'glslang' 'libxnvctrl' 'meson' 'nlohmann-json' 'python-mako' 'vulkan-headers')
depends=('dbus' 'fmt' 'gcc-libs' 'glew' 'hicolor-icon-theme' 'libglvnd' 'libx11' 'python' 'python-matplotlib' 'python-numpy' 'vulkan-icd-loader')
optdepends=('libxnvctrl: NVIDIA GPU stats by XNVCtrl'
            'glfw-x11: Required for MangoApp'
            'gamescope: Use MangoApp as an overlay within gamescope')
replaces=("$pkgname-common" "$pkgname-common-wayland" "$pkgname-common-x11" "mangoapp" "$pkgname-x11" "$pkgname-wayland")
conflicts=("$pkgname-common" "$pkgname-common-wayland" "$pkgname-common-x11" "mangoapp" "$pkgname-x11" "$pkgname-wayland")
source=("$pkgname-$pkgver.tar.xz::https://github.com/flightlessmango/MangoHud/releases/download/v$pkgver/$_pkgname-v$_pkgver-Source.tar.xz")
b2sums=('d7f7103d180c6e48f2ef36969dae5e04c46ba4cc9dcbcfae27c24029ae97753b4b5af8554df29f70510e24285a7c11583482697bc8f9ef82fb7550308f5e54d0')

prepare() {
    cd "$_pkgname-v$pkgver"

    # Use system cmocka instead of subproject
    sed -i "s/  cmocka = subproject('cmocka')//g" meson.build
    sed -i "s/cmocka_dep = cmocka.get_variable('cmocka_dep')/cmocka_dep = dependency('cmocka')/g" meson.build
}

build() {
    arch-meson "$_pkgname-v$pkgver" build \
        -Dmangoapp=true \
        -Dmangohudctl=true \
        -Dmangoapp_layer=true

    meson compile -C build
}

package() {
    depends+=(
        vulkan-icd-loader libvulkan.so
    )
    meson install -C build --destdir "$pkgdir"

    install -Dm 0644 "$srcdir/$_pkgname-v$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}

check() {
    meson test -C build --print-errorlogs || :
}
