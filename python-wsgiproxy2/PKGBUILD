# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-wsgiproxy2
pkgver=0.5.1
pkgrel=7
pkgdesc="A WSGI Proxy with various http client backends"
arch=('any')
license=('MIT')
url="https://github.com/gawel/WSGIProxy2/"
depends=('python-webob')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'python-requests' 'python-webtest' 'python-waitress')
source=("https://github.com/gawel/WSGIProxy2/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f72e151fedafae535fae3708220586b4baf137b66980b4b395be2895ae2c91e4a8c2a0473cb76a80de6ce057cdf9990620184f726235c1bb780bfb59d0cf54c0')

build() {
  cd WSGIProxy2-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd WSGIProxy2-$pkgver
  pytest
}

package() {
  cd WSGIProxy2-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
