# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=glide
pkgver=0.6.3
pkgrel=1
pkgdesc='Simple and minimalistic media player based on GStreamer and GTK'
arch=('loong64' 'x86_64')
url='https://github.com/philn/glide'
license=('MIT')
depends=('gst-plugins-bad' 'gst-plugins-base' 'gst-plugins-good' 'gtk4' 'libadwaita')
makedepends=('meson' 'rust')
optdepends=('gst-libav: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs')
replaces=('glide-player')
source=("https://github.com/philn/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('94cbc61f52e2e46967dce1046ee5cec6269dea0323f1d7ffc4ec71f3a01960aa')

build() {
  cd $pkgname-$pkgver
  arch-meson . build
  meson compile -C build
}

package() {
  cd $pkgname-$pkgver
  meson install -C build --destdir "$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
