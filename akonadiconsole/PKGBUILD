# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=akonadiconsole
pkgver=24.02.2
pkgrel=1
pkgdesc='Akonadi management and debugging console'
arch=(loong64 x86_64)
url='https://kontact.kde.org'
license=(GPL-2.0-or-later)
depends=(akonadi-contacts
         akonadi-search
         calendarsupport
         gcc-libs
         glibc
         kcalendarcore
         kcolorscheme
         kcompletion
         kconfig
         kconfigwidgets
         kcontacts
         kcoreaddons
         kcrash
         kdbusaddons
         ki18n
         kitemmodels
         kmime
         ktextwidgets
         kwidgetsaddons
         kxmlgui
         libakonadi
         libkdepim
         messagelib
         qt6-base
         xapian-core)
makedepends=(extra-cmake-modules
             kdoctools)
groups=(kde-applications
        kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('e8a5e5bb57d13245fca4bc6bf1bddff192436ffefe82351f4956f7d7b97c39b4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
