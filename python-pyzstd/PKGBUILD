# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=python-pyzstd
_pyname=${pkgname#python-}
pkgver=0.16.1
pkgrel=1
pkgdesc='Python bindings to Zstandard (zstd) compression library'
arch=(loong64 x86_64)
url="https://pypi.org/project/$_pyname"
license=(BSD-3-Clause)
depends=(python
         zstd)
makedepends=(python-{build,installer,wheel}
             python-setuptools)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('ed50c08233878c155c73ab2622e115cd9e46c0f1c2e2ddd76f2e7ca24933f195')

build() {
	cd "$_archive"
	python -m build -wn -C=--build-option=--dynamic-link-zstd
}

check() {
	cd "$_archive"
	local _pyver=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
	export PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-$_pyver"
	python -m unittest discover tests
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
}
