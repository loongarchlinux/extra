# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-magnumclient
pkgver=4.5.0
pkgrel=1
pkgdesc="Client library for Magnum API"
arch=('any')
url="https://docs.openstack.org/python-magnumclient/latest/"
license=('Apache-2.0')
depends=('python' 'python-pbr' 'python-keystoneauth1' 'python-stevedore' 'python-requests'
         'python-oslo-i18n' 'python-oslo-log' 'python-oslo-serialization' 'python-oslo-utils'
         'python-os-client-config' 'python-osc-lib' 'python-prettytable' 'python-cryptography'
         'python-decorator')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-openstackclient' 'python-oslotest' 'python-osprofiler' 'python-stestr'
              'python-testscenarios' 'python-testtools')
source=("git+https://github.com/openstack/python-magnumclient.git#tag=$pkgver")
sha512sums=('b81c60ba8dc011d268573049ed2831c245d3289d06b2310a82910c4c5589d5f5e672f2ab34d8bec5ee892cf0c3fc4954fefab4b64e9c4c68f8e3ef3a45675620')
b2sums=('5b809cda80cbff442454c81e74cde7513f7df0c232e1619c20b53148f2d89d98453609ca932002473f83b8d2030c8d1b97bff647e910c9206cbbcce754bd16f9')

build() {
  cd python-magnumclient
  python -m build --wheel --no-isolation
}

check() {
  local stestr_run_options=(
    # breaks with Python 3.12
    -E 'magnumclient.tests.test_httpclient.HttpClientTest.test_get_connection'
  )

  cd python-magnumclient
  stestr run "${stestr_run_options[@]}"
}

package() {
  cd python-magnumclient
  python -m installer --destdir="$pkgdir" dist/*.whl
}
