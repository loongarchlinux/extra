# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libwpe
pkgver=1.16.0
pkgrel=1
pkgdesc="General-purpose library for WPE WebKit"
url="https://wpewebkit.org"
arch=(loong64 x86_64)
license=(BSD-2-Clause)
depends=(
  gcc-libs
  libegl
  libxkbcommon
)
makedepends=(
  git
  mesa
  meson
)
provides=(libwpe-1.0.so)
source=("git+https://github.com/WebPlatformForEmbedded/libwpe?signed#tag=$pkgver")
b2sums=('51a8265ad9f3a025973d256d66a3fb572f180f7adcfb2b1d931ee3630803c3fd9e8dab32f06e93544a468721ab147bb6d39e35ff654b5c96ccd3557400222bde')
validpgpkeys=(
  5AA3BC334FD7E3369E7C77B291C559DBE4C9123B  # Adrián Pérez de Castro <aperez@igalia.com>
)

prepare() {
  cd libwpe
}

build() {
  arch-meson libwpe build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libwpe/COPYING
}

# vim:set sw=2 sts=-1 et:
