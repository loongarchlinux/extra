# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: yaroslav <proninyaroslav@mail.ru>
# Contributor: Askhat Bakarov <sirocco{at}ngs{dot}ru>

pkgname=android-file-transfer
_pkgname="${pkgname}-linux"
pkgver=4.3
pkgrel=1
pkgdesc="Android MTP client with minimalistic UI"
arch=('loong64' 'x86_64')
url="https://whoozle.github.io/android-file-transfer-linux"
license=('LGPL-2.1-or-later')
depends=('qt5-base' 'fuse2' 'libxkbcommon-x11' 'hicolor-icon-theme' 'file' 'android-udev')
makedepends=('cmake' 'qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/whoozle/android-file-transfer-linux/archive/v${pkgver}.tar.gz")
sha256sums=('8ff658630fc820a7ca0b70025aa47d235b7fb64f5cb6a72ca76a7acbf3435128')

build() {
	cmake -B build -S "${_pkgname}-${pkgver}" \
	  -DCMAKE_BUILD_TYPE='None' \
	  -DCMAKE_INSTALL_PREFIX='/usr' \
	  -DCMAKE_CXX_FLAGS="${CXXFLAGS} -ffat-lto-objects" \
	  -Wno-dev
	cmake --build build

	make -C build
}

package() {
	make DESTDIR="${pkgdir}" -C build install
}
