# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=staticcheck
pkgver=2024.1
pkgrel=1
pkgdesc="The advanced Go linter"
arch=('loong64' 'x86_64')
url="https://staticcheck.io"
license=("MIT")
depends=('glibc')
makedepends=('go' 'git')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dominikh/go-tools/archive/$pkgver.tar.gz")
sha512sums=('7a110c3e21b4cae103caed6ad52153105ad58a45cf33d6b7ca16830ae2d7402de231a51a5a8c26ae76093f10b8fd1c482d1acae60a1f1fe8f30187f93eeb99b9')
b2sums=('3b69c92b24c6542e0d86e86f99154905329148cea0150e080876fc7e14c14e4ad727a3c8cf5b7227cedf43f4dbed8805134904429e9cf3b2c204487d7535f676')

prepare(){
  cd "go-tools-$pkgver"
  mkdir -p bin
}

build(){
  cd "go-tools-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags \"${LDFLAGS}\"" \
    -o bin\
    ./cmd/staticcheck
}

check(){
  cd "go-tools-$pkgver"
  GOROOT="/usr/lib/go" go test -v ./...
}

package(){
  cd "go-tools-$pkgver"
  install -p -Dm755 bin/staticcheck "${pkgdir}/usr/bin/staticcheck"
  install -Dm644 "LICENSE"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
  install -Dm644 "LICENSE-THIRD-PARTY"  "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE-THIRD-PARTY"
}
