# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname=nio4r
pkgname=ruby-${_gemname}
pkgver=2.7.3
pkgrel=1
pkgdesc='Abstract, cross-platform stateful I/O selector API for Ruby'
url='https://github.com/celluloid/nio4r'
arch=('loong64' 'x86_64')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc' 'git')
checkdepends=('ruby-bundler' 'ruby-rake' 'ruby-rake-compiler' 'ruby-rspec')
options=('!emptydirs')
source=("https://github.com/celluloid/nio4r/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('185e0cbe146495d657e8954eef2208e61766c30f5cf132078ce9c00b5b6d678eafaa108102dc79bb8d42d90fd5809a606344d35b03bfc41d7a1f3f75a7c2820f')
b2sums=('283e3f7ea278936a15efe8f8e7219ac3191b4be72f04ab63e64d6ca2125f6be7164353707a7418be31bcbed240317d51912e5c2a2dea0042b1ca2b3a39d56993')

prepare() {
  cd "${_gemname}-${pkgver}"

  sed --in-place 's|git ls-files|find|' "${_gemname}.gemspec"
  sed --in-place '/signing_key/d' "${_gemname}.gemspec"
  sed --in-place '/coveralls/Id' spec/spec_helper.rb

  rm rakelib/rubocop.rake
}

build() {
  cd "${_gemname}-${pkgver}"

  rake compile build
}

check() {
  cd "${_gemname}-${pkgver}"

  rake spec
}

package() {
  cd ${_gemname}-${pkgver}

  local _gemdir="$(gem env gemdir)"

  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin" "pkg/${_gemname}-${pkgver}.gem"

  install -Dm 644 readme.md changes.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 license.md -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache \
    "gems/${_gemname}-${pkgver}/tmp/" \
    "gems/${_gemname}-${pkgver}/ext/${_gemname}/Makefile" \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out} \
    "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"
}

# vim: ts=2 sw=2 et:
