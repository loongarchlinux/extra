# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=klipperscreen
pkgver=0.3.9
pkgrel=2
pkgdesc='GUI for Klipper'
arch=(any)
url=https://github.com/KlipperScreen/KlipperScreen
license=(AGPL-3.0-only)
depends=(
  bash
  cairo
  dbus-glib
  glib2
  gnu-free-fonts
  gdk-pixbuf2
  gtk3
  librsvg
  mpv
  openjpeg2
  pango
  python
  python-cairo
  python-gobject
  python-jinja
  python-mpv
  python-netifaces
  python-numpy
  python-requests
  python-six
  python-websocket-client
  xorg-xinit
  xorg-server
)
makedepends=(git)
_tag=ce884154d63c5d4c9a0a803b123d211449733a01
source=(
  git+https://github.com/jordanruthe/KlipperScreen.git#tag=${_tag}
  klipperscreen.service
)
b2sums=('f64bfc410c9e87b1db5964201871b01e7c98b0f3ea416af9828b76d5080855173339e2eca0284f60945f0cd12a032e7d2c0fa7d8cf2cf5e9b935550f9f24e5ad'
        '1657a79da1e37a0494e021b19e3b95cd1979a3d8465de7f38b95bda77a3a59a382706bbcb61c60e9d1bb3c84590e74fcb58f36b2e835e0ef4df50d07beab01be')

pkgver() {
  cd KlipperScreen
  git describe --tags | sed 's/^v//'
}

build() {
  python -m compileall KlipperScreen
}

package() {
  install -dm 755 "${pkgdir}"/opt/klipperscreen
  cp -dr --no-preserve=ownership KlipperScreen/{docs,ks_includes,panels,scripts,styles,screen.py} "${pkgdir}"/opt/klipperscreen/
  install -Dm 644 klipperscreen.service -t "${pkgdir}"/usr/lib/systemd/system/
}

# vim: ts=2 sw=2 et:
