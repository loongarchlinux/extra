# Maintainer: Carl Smedstad <carsme@archlinux.org>
# Contributor: NOGISAKA Sadata <ngsksdt@gmail.com>
# Contributor: Kohei Suzuki <eagletmt@gmail.com>

pkgname=ghq
pkgver=1.6.2
pkgrel=1
pkgdesc="Remote repository management made easy"
arch=(loong64 x86_64)
url="https://github.com/x-motemen/ghq"
license=(MIT)
depends=(glibc)
makedepends=(go)
checkdepends=(git)
options=(!lto)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('97b2ad74b4ed0879ba60e3d94d5a539481cdf60cfac8f3426aad5b3820a1b830')

prepare() {
  cd $pkgname-$pkgver

  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd $pkgname-$pkgver

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  # shellcheck disable=SC2153
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw -buildvcs=false"
  export GOPATH="$srcdir"

  local ldflags="-compressdwarf=false -linkmode=external"
  go build -v -ldflags="$ldflags" .
}

check() {
  cd $pkgname-$pkgver

  go test -v .
}

package() {
  cd $pkgname-$pkgver

  install -vDm755 -t "$pkgdir/usr/bin" ghq
  install -vDm644 misc/bash/_ghq "$pkgdir/usr/share/bash-completion/completions/ghq"
  install -vDm644 misc/zsh/_ghq "$pkgdir/usr/share/zsh/site-functions/_ghq"
  install -vDm644 misc/fish/ghq.fish "$pkgdir/usr/share/fish/completions/ghq.fish"
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
