# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tangler
pkgver=0.3.0
pkgrel=1
pkgdesc="Extracts (tangles) code fragments from Markdown documents"
arch=(loong64 x86_64)
url="https://github.com/wiktor-k/tangler/"
license=(Apache-2.0 MIT)
depends=(
  gcc-libs
  glibc
)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('477ea87e62dc5d9235f16e8eadeb82ed3fcb84e0a66b5ae3aa77723f5177399a544a9f0c3e0c50f204979c5d61106a8e0e3836d49a0f0f6e732bc8e7cb51137f')
b2sums=('cfa0aaaff07ff63b7242b1508a28b9b6319f8582b0f117e74758b44a0f01a6df3d60f322c95993473ff197e53bad6e414221a201cc35d8742ec09befaed949db')

prepare() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --locked --target "$(rustc -vV | sed -n 's/host: //p')"
}

build() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd $pkgname-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 LICENSES/MIT.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
}
