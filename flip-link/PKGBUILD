# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=flip-link
pkgver=0.1.8
pkgrel=1
pkgdesc="Flips the memory layout of embedded programs to protect against stack overflows"
url='https://github.com/knurling-rs/flip-link'
arch=('loong64' 'x86_64')
license=('MIT' 'Apache-2.0')
makedepends=('cargo')
source=(https://github.com/knurling-rs/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('1b146aa8c8606e8b7c1aad029a972bf5d279aa22b9b3d691753c19d5e68c8372')
b2sums=('ad6170373ec55540493eebafd082f257d521aa1d98467a504a7a0cbf23024aa3b72500328882acbaa967d50324a5839ddd24f09505208fd65d011d02619e6ff8')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/flip-link

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
