# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-desktop
pkgver=6.5.0.r127.7552ae62
pkgrel=1
pkgdesc='Switchboard Desktop Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-pantheon-shell
license=(GPL-3.0-only)
groups=(pantheon)
depends=(
  gdk-pixbuf2
  glib2
  glibc
  gtk4
  libgee
  libgexiv2
  libgranite-7.so
  libswitchboard-3.so
)
makedepends=(
  git
  meson
  ninja
  vala
)
_commit=7552ae6279e509abdf4f2c6e42772601ba3d7b99
source=(switchboard-plug-desktop::git+https://github.com/elementary/switchboard-plug-pantheon-shell.git#commit=${_commit})
b2sums=(SKIP)

pkgver() {
  cd switchboard-plug-desktop
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-desktop build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
