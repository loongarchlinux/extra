# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-minitest-rg
pkgver=5.3.0
pkgrel=1
pkgdesc='Colored red/green output for Minitest'
arch=(any)
url='https://github.com/minitest/minitest-rg'
license=(MIT)
depends=(ruby-minitest)
checkdepends=(ruby-hoe ruby-rake ruby-rubocop)
options=(!emptydirs)
source=(https://github.com/minitest/minitest-rg/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('cbc990d9ea8d7917def2efa98140c9c8da2188db9f6dd9707a7855dca1ea9e9a')

prepare() {
  cd minitest-rg-$pkgver
  sed -i "s/s\\.version = .*/s\\.version = \"$pkgver\"/" minitest-rg.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd minitest-rg-$pkgver
  gem build minitest-rg.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    minitest-rg-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd minitest-rg-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake || echo "Tests failed"
}

package() {
  cd minitest-rg-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
