# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: zhuqin <zhuqin83@gmail.com>
# Contributor: tri1976 <trile7@gmail.com>
# Contributor: snoopy33 <snoopy33@no-log.org>

pkgname=cairo-dock-plug-ins
pkgver=3.5.0
pkgrel=3
pkgdesc='Plugins for Cairo-Dock'
arch=('loong64' 'x86_64')
url='https://glx-dock.org'
license=('GPL')
depends=('cairo-dock')
makedepends=('alsa-lib' 'cmake' 'dbus-sharp-glib' 'fftw' 'gnome-menus'
             'gtk-sharp-3' 'gvfs' 'libetpan' 'libexif' 'libical' 'libpulse'
             'libxklavier' 'lm_sensors' 'python' 'ruby' 'upower'
             'vala' 'vte3' 'zeitgeist' 'python-setuptools')
optdepends=('alsa-lib: Sound Control, Sound Effects applets'
            'dbus-sharp-glib: Mono API'
            'fftw: Impulse applet'
            'gnome-menus: Applications Menu applet'
            'gtk-sharp-3: Mono API'
            'gvfs: GVFS integration'
            'libetpan: Mail applet'
            'libexif: Slider applet'
            'libical: Clock applet'
            'libpulse: Impulse applet'
            'libxklavier: Keyboard Indicator applet'
            'lm_sensors: System Monitor applet'
            'python: Python 3 API'
            'ruby: Ruby API'
            'upower: Power Manager applet'
            'vte3: Terminal applet'
            'wireless_tools: Wifi applet'
            'zeitgeist: Recent Events applet')
replaces=('cairo-dock-plugins')
source=("cairo-dock-plug-ins-${pkgver}.tar.gz::https://github.com/Cairo-Dock/cairo-dock-plug-ins/archive/${pkgver}.tar.gz")
sha256sums=('cf1ff262ee75ef69caa846a447de1738c8c5f3afa9fdbbdf9c51d623692f5636')

prepare() {
  cd cairo-dock-plug-ins-${pkgver}

  sed 's/gmcs/mcs/' -i CMakeLists.txt
}

build() {
  cmake -B build -S cairo-dock-plug-ins-${pkgver} \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DWITH_VALA_SRC='yes'
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}

# vim: ts=2 sw=2 et:
