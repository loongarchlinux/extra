# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=filezilla
pkgver=3.67.1
pkgrel=1
pkgdesc='Fast and reliable FTP, FTPS and SFTP client'
url='https://filezilla-project.org/'
arch=('loong64' 'x86_64')
license=('GPL')
depends=(
  dbus libdbus-1.so
  gtk3 libgtk-3.so libgdk-3.so
  hicolor-icon-theme
  libfilezilla libfilezilla.so
  nettle libnettle.so
  sqlite libsqlite3.so
  wxwidgets-gtk3
  xdg-utils
  )
makedepends=(
  boost
  gnutls
  libidn
)
source=(https://download.filezilla-project.org/client/FileZilla_${pkgver}_src.tar.xz)
sha256sums=('10468e6ef623ad9789996df61f588ca7417d39353678313611d54f2d8131a1db')
sha512sums=('630820a0c67ee8b1d48a8ff4193e066d35bbf9048526c91b8804d786defc28a635dac252b298de518b506506607abd96ce35c455c21a50503844a57c47ea78e7')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -vif
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --with-pugixml=builtin
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
