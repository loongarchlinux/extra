# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-power
pkgver=2.7.0.r213.19e47af7
pkgrel=1
pkgdesc='Switchboard Power Plug'
arch=(loong64 x86_64)
url=https://github.com/elementary/switchboard-plug-power
license=(GPL-2.0-only)
groups=(pantheon)
depends=(
  dbus
  glib2
  glibc
  gtk4
  libadwaita-1.so
  libgee
  libgranite-7.so
  libswitchboard-3.so
  polkit
)
makedepends=(
  git
  meson
  vala
)
_commit=19e47af74fd2bf3989c2fe544ded2981d4fff88d
source=(git+https://github.com/elementary/switchboard-plug-power.git#commit=${_commit})
b2sums=('71aee95282edb50d7cf1b8609722b96d2d6b36e1f69dd41110bdca16ede8bd10146a74cd49842805dd88bfde86fa5b965158728f20580ec29954ccb56529193f')

pkgver() {
  cd switchboard-plug-power
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson switchboard-plug-power build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
