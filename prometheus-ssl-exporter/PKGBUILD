# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=prometheus-ssl-exporter
_pkgname=ssl_exporter
pkgver=2.4.3
pkgrel=1
pkgdesc='Prometheus exporter for TLS certificates'
arch=('loong64' 'x86_64')
url='https://github.com/ribbybibby/ssl_exporter'
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
backup=('etc/prometheus/ssl-exporter.env')
options=('!lto')
source=(
  "$pkgname::git+$url.git#tag=v$pkgver"
  'systemd.service'
  'sysusers.conf'
  'config.env'
)
sha512sums=('b1d3c75a483dfee934cad722dcf020cf679dfd4cee693da354360f4e850803e53bd81edac3832ded4f73e997653758c22d0af63b3d7371815514f26594befa21'
            'fdcc4b416783520fc2358527ab13e37ef280cd7353a8eb910c280f4053d8cf5c2beebd2005f9c79a099d4fe457ee6936114692ce47c8401caa0a920bc08575e9'
            '65ac0aecb8c1585b7a08af81bbbc93e05b993bca00afe00eb74f0fe3ea201bd0a5c2792d417418efc8b453142282be73d073cc7e3b4dde0c107b01f6edd7d6ae'
            '0521eb34976c971dd990283e399a0c6b0e5d84608203fb3e220631bdae4bd004fd52fd02a134236e94a873fe538d6a2bdd0810c527a08b21edd9756b7eb9ef8d')
b2sums=('eef313e3a4a172199f73c8c892d0ec269bdbbf912f9653816d53f3a863358f2494d6030541fd1e21be168b1c096e25e7ac7256cb636a5e04f60313d686f757ed'
        '3661a506027f361b24d4d9832e1fcfc3ec1a116f4e52cf04779b700fa97161075adebc8790981575e68b94a42289221834b20e01f98a20c9ae364ef01e45d373'
        'cd77da1f9c3c6919306124ef36ab7dcec96a338ba667fcd226b66c8ea54e448b14600783a6289091e9776be96e92c0ad98c007019160aa48c68df31f2eae8403'
        '4cd5f8df6d1d4e8dcb5775de96d91f6087dcd7002a9cd910ec06e4bbb855e0f6567fd07a596e0c36dcdab610d467011dfadc65bcd2f466afcad9d7561f6eb51c')

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  export GOPATH="${srcdir}"
  go mod download
}


build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPATH="${srcdir}"

  # reproducible: use commit date for build date
  local _commit_date=$(git show --no-patch --format=%cd --date=format:%Y-%m-%d-%H:%M:%S)

  go build -v \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-compressdwarf=false \
    -linkmode external \
    -extldflags '${LDFLAGS}' \
    -X github.com/prometheus/common/version.Version=$pkgver \
    -X github.com/prometheus/common/version.Revision=$(git rev-parse HEAD) \
    -X github.com/prometheus/common/version.Branch=main \
    -X github.com/prometheus/common/version.BuildUser=archlinux \
    -X github.com/prometheus/common/version.BuildDate=$_commit_date" \
    -o build \
    ./...
}

check() {
  cd "$pkgname"

  go test -v ./...
}

package() {
  # systemd integration
  install -vDm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm640 config.env "$pkgdir/etc/prometheus/ssl-exporter.env"

  cd "$pkgname"

  # binary
  install -vDm755 "build/$_pkgname" "$pkgdir/usr/bin/$pkgname"

  # license
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
