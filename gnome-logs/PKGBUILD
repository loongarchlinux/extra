# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-logs
pkgver=45.0
pkgrel=1
pkgdesc="A log viewer for the systemd journal"
url="https://wiki.gnome.org/Apps/Logs"
arch=(loong64 x86_64)
license=(GPL)
depends=(
  dconf
  glib2
  gsettings-desktop-schemas
  gtk4
  hicolor-icon-theme
  libadwaita
  pango
  systemd
)
makedepends=(
  appstream
  git
  meson
  yelp-tools
)
groups=(gnome)
_commit=83ad0156ed4a80d35babcdd155e217cdea4af597  # tags/gnome-logs-45.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-logs.git#commit=$_commit")
b2sums=('e6aae0af3137c6c96014b00da25fe27b32d90b0b227fb36c376e583f17ce1e9faebc1baeaa51e7ce62c79620f9c16b1831c9c20be55f09d9de9b700a802c9f35')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^gnome-logs-//;s/\.\([a-z]\)/\1/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D man=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
